!$CHBMTAX
      SUBROUTINE CHBMTAX(SCHB1,SCHB2,SCHBV1,SCHBV2,SCR1,ICB,ISUPE)
!********1*********2*********3*********4*********5*********6*********7**
! CHBMTX           88/11/17            8901.0    PGMR - A. MARSHALL
!                                                       D. PAVLIS
!
! FUNCTION:  CALCULATE CHEBYCHEV POLYNOMIAL & MATRICIES USED TO COMPUTE
!            NEW CHEBYCHEV COEFFICIENTS (IN CHCOMP) FOR PLANET POSITIONS
!            (CALLED ONCE PER RUN) - APPLIES TO PRESENCE OF SUPPLEMENTAR
!            PLANETARY EPHEMERIS)
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   SCHB1    O    A    CHEBYCHEV POLYNOMIALS AT TIME 1 (SINGLE
!                      PRECISION)
!   SCHB2    O    A    CHEBYCHEV POLYNOMIALS AT TIME 2 (SINGLE
!                      PRECISION)
!   SCHBV1   O    A    DERIVATIVES OF POLYNOMIALS (SINGLE PRECISION)
!   SCHBV2   O    A    DERIVATIVES OF POLYNOMIALS (SINGLE PRECISION)
!   SCR1     O    A    POLYNOMIAL MATRIX FOR LEAST SQUARES COMPUTATION
!                      (ATA)-1*AT  (SINGLE PRECISION)
!
! COMMENTS:  CODE CANNOT BE VECTORIZED BECAUSE OF DOUBLE PRECISION
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/EPHMPT/NTOT,NTOTS,NNPDPR,NUTORD,IBODDG(8),IGRPDG(4),       &
     &              NENPD,INRCD(2),KPLAN(11),NINEP,NEPHMR,NEPHEM,       &
     &              IBHINT,NINTBH,NXEPHP
      COMMON/IBODPT/IBDCF(999),IBDSF(999),IBDGM(999),IBDAE(999),    &
     &              IBDPF(999),                                     &
     &              ICBDCF,ICBDSF,ICBDGM,ICBDAE,ICBDPF,             &
     &              ITBDCF,ITBDSF,ITBDGM,ITBDAE,ITBDPF,NXBDPT
      COMMON/IEPHM2/ISUPL,ICBODY,ISEQB(2),MAXDEG,ITOTSE,IREPL(988), &
     &              NXEPH2
      COMMON/IORCHB/ IORM,IORJ,IORSA,IORSUN,IOREM,IORMO,IORCB,IORTB,    &
     &               IGRP,ICHBOG(2,14),IORSCB(988),NXORCH
      COMMON/LDUAL/LASTSN,LSTSNX,LSTSNY
      COMMON/PLNETI/IPLNET(999),IPLNIN(999),MPLNGD(999),MPLNGO(999),   &
     &              IPLNZ(999),NXPLNI
!
      DIMENSION CHB1(25),CHB2(25),CHBV1(25),CHBV2(25),CHBVV1(50,25),    &
     &          CHBVV2(50,25),UPT(325),CHEB(200,25),CTC(25,25),         &
     &          TWOT(1)
!    &          TWOT(1),VSEC(14)
      DIMENSION SCHB1(25,50),SCHB2(25,50),SCHBV1(25,50),SCHBV2(25,50),  &
     &          SCR1(200,25,ICB),FNSEC(4)
!     DIMENSION IGRPP(37),IORD(ICB)

      INTEGER, DIMENSION(:), ALLOCATABLE ::  IGRPP,IORD
      DOUBLE PRECISION, DIMENSION(:), ALLOCATABLE ::  VSEC

      DIMENSION ISUPE(ICBODY,5)
      DATA ONE/1.D00/,ZERO/0.D00/,TWO/2.D00/
      DATA FNSEC/2764800.D0,1382400.D0,691200.D0,345600.D0/
      DATA DELTT/2.D-2/
      DATA IOUT6/6/
      DATA LSYMSW/.FALSE./,LMATP/.TRUE./
      DATA DAYSEC/86400.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
       ALLOCATE (IGRPP(ICB+7),IORD(ICB+7))
       ALLOCATE (VSEC(ICB+7))
! INTIALIZE
       ZEROD = ZERO
       ONED  = ONE
       TWOD  = TWO
       VSEC(1) = TWOD/FNSEC(1)
       VSEC(2) = TWOD/FNSEC(2)
       VSEC(3) = TWOD/FNSEC(3)
       VSEC(4) = TWOD/FNSEC(4)
       DELT = DELTT
!
! SET POLYNOMIAL DEGREES (AS DICTATED BY JPL PLANETARY EPHEM TAPES)
!     IORM  = 10
      IORM  = ICHBOG(1,4)
!     IORJ  = 9
      IORJ  = ICHBOG(1,5)
!     IORSA = 8
      IORSA = ICHBOG(1,6)
!     IORSUN= 15
      IORSUN= ICHBOG(1,11)
!     IOREM = 15
      IOREM = ICHBOG(1,3)
!     IORMO = 12
      IORMO = ICHBOG(1,10)
! ASSUME EARTH TRACKING BODY
      IORCB = ICHBOG(1,14)
      IORTB = IOREM
      IGRP  = ICHBOG(2,14)
! FILL IORSCB FOR CELESTIAL BODIES IN THE SUPPLEMENTARY PLANETARY EPHEME
!
! SET UP TABLE OF PLANETARY GROUP AND CHEBYCHEV POLYNOMIAL ORDERS
!  SATURN
      IGRPP(1) = ICHBOG(2,6)
      IORD(1)  = ICHBOG(1,6)
!  JUPITER
      IGRPP(2) = ICHBOG(2,5)
      IORD(2)  = ICHBOG(1,5)
!  SUN
      IGRPP(3) = ICHBOG(2,11)
      IORD(3)  = ICHBOG(1,11)
!  EARTH-MOON
      IGRPP(4) = ICHBOG(2,3)
      IORD(4)  = ICHBOG(1,3)
!  MOON
      IGRPP(5) = ICHBOG(2,10)
      IORD(5)  = ICHBOG(1,10)
!  CENTRAL BODY
      IGRPP(6) = ICHBOG(2,14)
      IORD(6)  = ICHBOG(1,14)
!  MARS
      IGRPP(7) = ICHBOG(2,4)
      IORD(7)  = ICHBOG(1,4)
!
! OTHER CELESTIAL BODIES FROM SUPPLEMENTARY EPHEMERIS
      DO IJ=1,ICBODY
      IORD(IJ+7)=(ISUPE(IJ,2)-2)/3
      IORSCB(IJ)=(ISUPE(IJ,2)-2)/3
      IGRPP(IJ+7)=IJ+7
!     RLOV=(ISUPE(IJ,3)/100)*DAYSEC
      RLOV=( DBLE(ISUPE(IJ,3))/100.D0)*DAYSEC
      VSEC(IJ+7)=TWOD/RLOV
      ENDDO
! INTIALIZE
      DT1 =ZEROD
      DT2 =-ONED
!
! COMPUTE CHEBYCHEV POLYNOMIALS
      DO 100 I=1,50
      DT1=DT1+DELT
      DT2=DT2+DELT
      CHB1(1) = ONED
      CHB2(1) = ONED
      CHBV1(1)= ZEROD
      CHBV2(1)= ZEROD
      CHBV1(2)= ONED
      CHBV2(2)= ONED
      CALL CHEBDP(CHB1,CHBV1,DT1,25,1,TWOT,.TRUE.)
      CALL CHEBDP(CHB2,CHBV2,DT2,25,1,TWOT,.TRUE.)
!
      DO 100 J=1,25
       SCHB1(J,I)  =CHB1(J)
       SCHB2(J,I)  =CHB2(J)
       SCHBV1(J,I) =CHBV1(J)
       SCHBV2(J,I) =CHBV2(J)
      CHEB(I,J)=CHB2(J)
      CHEB(I+50,J)=CHB1(J)
      CHBVV2(I,J)=CHBV2(J)
      CHBVV1(I,J)=CHBV1(J)
  100 CONTINUE
!
! LOOP THROUGH ALL REQUIRED PLANETS
      DO 1000 N=1,ICB
      IF(LASTSN.AND.N.EQ.6) GO TO 1000
! FILL BODY SPECIFIC ELEMENTS (VELOCITY) OF CHEB ARRAY
      DO 200 I=1,50
      DO 200 J=1,IORD(N)
      CHEB(I+100,J)=CHBVV2(I,J)*VSEC(IGRPP(N))
      CHEB(I+150,J)=CHBVV1(I,J)*VSEC(IGRPP(N))
  200 CONTINUE
!
!     CT TIMES C
      DO 300 I=1,IORD(N)
      DO 300 J=1,IORD(N)
      VAL=ZEROD
      DO 400 K=1,200
  400 VAL=VAL+CHEB(K,I)*CHEB(K,J)
      CTC(I,J)=VAL
  300 CONTINUE
!
! CONVERT FROM SYMMETRIC SQUARE TO UPPER TRIANGULAR MATRIX
      K=1
      DO 500 I=1,IORD(N)
      DO 500 J=I,IORD(N)
      UPT(K)=CTC(I,J)
      K=K+1
  500 CONTINUE
!
! INVERT CTC
      CALL DPSINV(UPT,IORD(N),IORD(N),CHB1)
!
! CONVERT FROM UPPER TRIANGULAR TO SYMMETRIC SQUARE MATRIX
      K=1
      DO 600 I=1,IORD(N)
      DO 600 J=I,IORD(N)
      CTC(I,J) = UPT(K)
      CTC(J,I) = UPT(K)
      K=K+1
  600 CONTINUE
!
! CTC INVERSE TIMES CT
      DO 900 I=1,IORD(N)
      DO 900 J=1,200
      VAL=ZEROD
      DO 800 K=1,IORD(N)
  800 VAL=VAL+CTC(I,K)*CHEB(J,K)
       SCR1(J,I,N)=VAL
  900 CONTINUE
 1000 END DO
!
       DEALLOCATE (IGRPP,IORD)
       DEALLOCATE (VSEC)

      RETURN
      END
