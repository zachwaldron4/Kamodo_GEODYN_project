!$ALTOTD
      SUBROUTINE ALTOTD(MJDSEC,FSEC,COSLAT,SINLAT,SATLON,THETAG,AT,BT,  &
     &                  FI,GE,MM,KK,HH,JJ,                              &
     &                  IBDY,SIGN1,LL,QQ,XQQ,IPT,XIEXP,                 &
     &                  XMM,XSIGN1,X2M2HH,X2M2HJ,XKK,ICENTR,MAINNN,     &
     &                  XSIGN2,IIPPTT,IQNDEX,JJBDY,ITIDE,               &
     &                  AB,BA,AJ,BJ,AAA,BBB,ZOMEGA,ZMEANA,ZNODE,        &
     &                  ANGARG,SINARG,COSARG,SINANG,COSANG,QXLONG,      &
     &                  SINQXL,COSQXL,SINQLN,COSQLN,SINTOT,COSTOT,      &
     &                  ACPBS,ASMBC,PLEGDR,P,ATPART,BTPART,ALPHA,       &
     &                  RADTHT,PMPA,NM,NDIM1,NDIM2,NPTIDE,NBB,NBB2,     &
     &                  LNPNM,UNORM,AA,II)
!********1*********2*********3*********4*********5*********6*********7**
! ALTOTD           89/05/23            0000.0    PGMR - A. MARSHALL
!
! FUNCTION:  CALCULATE THE CORRECTIONS TO THE ALTIMETRY
!            MEASUREMENTS DUE TO THE OCEAN TIDES AND/OR
!            CALCULATE THE PARTIALS OF AN ALTIMETRY
!            MEASUREMENT TO THE GEOID WRT THE AT&BT TIDE
!            COEFFICIENTS
!
!  I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I         INTEGER EPHEMERIS SECONDS SINCE GEODYN REF. TIME
!   FSEC     I         FRACTIONAL REMAINING SEDCONDS
!   COSLAT   I         COS OF LATITUDE OF SUB SATELLITE PT
!   SINLAT   I         SIN OF LATITUDE OF SUB SATELLITE PT
!   SATLON   I         LONGITUDE OF SUB SATELLITE PT (DEGREES)
!   THETAG   I         GREENWICH HR ANGLE
!   AT       I         ARRAY CONTAINING CURRENT VALUES OF A
!                      COEFFICIENTS FOR THE EXPANDED TIDAL MODEL
!   BT       I         ARRAY CONTAINING CURRENT VALUES OF B
!                      COEFFICIENTS FOR THE EXPANDED TIDAL MODEL
!   FI                 ARRAY CONTAINING KAULA'S INCLINATION FUNCTION
!                      FOR EARTH TIDE MODEL (AT 12 HR INTERP PERIODS)
!   GE                 ARRAY CONTAINING KAULA'S ECCENTRICITY FUNCTION
!                      FOR EARTH TIDE MODEL (AT 12 HR INTERP PERIODS)
!   MM       I         TIDAL EXPANSION ARGUMENT
!   KK       I         TIDAL EXPANSION ARGUMENT
!   HH       I         TIDAL EXPANSION ARGUMENT
!   JJ       I         TIDAL EXPANSION ARGUMENT
!   IBDY     I         INDICATES TO WHICH BODY EXPANSION ARGUMENT
!                      APPLIES
!                      1 - MOON AND SUN OR JUST MOON
!                      2 - SUN
!                      NOTE THAT IF MOON& SUN,THEN X2M2HH,X2M2HJ,XKK
!                      HAVE ALL BEEN SET TO ZERO
!   SIGN1    I         SIGN ASSOCIATED WITH EXPANSION ARGUMENTS
!   LL       I         DEGREE OF TIDAL HARMONIC (2 FOR ETIDE)
!   QQ       I         ORDER OF TIDAL HARMONIC (MM TOE ETIDE)
!   XQQ      I         DFLOAT OF ORDER OF TIDAL HARMONIC
!   IPT      I         POINTER TO LGENDR POLYNOMIAL OF LL,QQ
!   XIEXP    I         +1 IF MM EVEN;-1 IF MM ODD
!   XMM      I         DFLOAT OF EXPANSION ARGUMENT
!   XSIGN1   I         DFLOAT OF SIGN ASSOCIATED WITH EXPANSION
!                      ARGUMENTS. COLS 16-17 OF THE OTIDES/ETIDES CARD
!                      MULTIPLIES -(LINEAR COMB NODE, PER, MA)
!   X2M2HH   I         2-2HH WHERE HH IS A TIDAL EXPAMSION TERM
!   X2M2HJ   I         X2M2HH+JJ WHERE JJ IS AN EXPANSION ARGUMENT
!   XKK      I         DFLOAT OF EXPANSION ARGUMENT
!   ICENTR   I         POINTER INDICATING MAIN LINE TIDE CONSTITUENT
!   MAINNN   I         DEGREE OF MAIN TIDAL LINE
!   XSIGN2   I         DFLOAT OF SIGN ASSOCIATED WITH EXPANSION
!                      ARGUMENTS FOR OTIDES.
!                      PROGRADE/RETROGRADE (COLS 7-8 OF OTIDES)
!   IIPPTT   I         POINTER FOR MULTIPLE SINES AND COSINES
!   IQNDEX   I         POINTER FOR QQ*LAMBDA TERM
!   JJBDY    I         INDICATES DISTURBING BODY (LIKE JBDY, BUT IF
!                      JBDY=0, THEN JJBDY=1)
!   ITIDE    I         POINTER FOR AMPLITUDE INTERPOLATION
!   AB            A    STORAGE FOR INTERPOLATION
!   BA            A    STORAGE FOR INTERPOLATION
!   AJ            A    INTERPOLATION END POINTS FOR TIDAL AMPLITUDE
!   BJ            A    INTERPOLATION END POINTS FOR TIDAL AMPLITUDE
!   AAA           A    INTERPOLATED TIDAL AMPLITUDES
!   BBB           A    INTERPOLATED TIDAL AMPLITUDES
!   ZOMEGA        A    ANGLE OF PERIGEE
!   ZMEANA        A    MEAN ANOMALY
!   ZNODE         A    ANGLE OF THE NODE
!   ANGARG        A    ANGULAR ARGUMENT OF OTIDE (CHRISTODOULIDIS A5)
!   SINARG        A    SINE OF ANGULAR ARGUMENT  (DISCREET FREQ)
!   COSARG        A    COSINE FO ANGULAR ARGUMENT(DISCREET FREQ)
!   SINANG        A    SINE OF ANGULAR ARGUMENT  (ALL INPUT FREQ)
!   COSANG        A    COSINE FO ANGULAR ARGUMENT(ALL INPUT FREQ)
!   QXLONG        A    QQ TIMES THE LONGITUDE OF SUB-SATELLITE PT
!   SINQXL        A    SINE(QXLONG)      (SPECIFIC VALUES)
!   COSQXL        A    COSINE(QXLONG)    (SPECIFIC VALUES)
!   SINQLN        A    SINE(QXLONG)      (ALL INPUT VALUES)
!   COSQLN        A    COSINE(QXLONG)    (ALL INPUT VALUES)
!   SINTOT        A    SINE TOTAL ANGLE (COMBINE ANGARG & QXLONG)
!   COSTOT        A    COSINE TOAL ANGLE
!   ACPBS         A    AAA*COSTOT+BB*SINTOT
!   ASMBC         A    AAA*SINTOT-BB*COSTOT
!   PLEGDR        A    LGENDRE POLY   (SPECIFIC DEGREES)
!   P             A    LGENDER POLYNOMIALS FOR SATELLITE LATITUDE
!   ATPART   O         AT MEASUREMENT PARTIAL
!   BTPART   O         BT MEASUREMENT PARTIAL
!   ALPHA
!   RADTHT   O         RADIAL OCEAN TIDE HEIGHT
!   PMPA    I/O        D MEASUREMENT/D PARTIAL STORAGE ARRAY
!   NM       I         NUMBER OF MEASUREMENTS IN BLOCK
!   NDIM1    I         FIRST DIMENSION OF PMPA ARRAY
!   NDIM2    I         SECOND DIMENSION OF PMPA ARRAY
!   NPTIDE   I         NUMBER OF LEGENDRE POLY TO BE COMPUTED
!   NBB      I         NUMBER OF ETIDES + NUMBER OF DISTINCT FREQ.
!                      OTIDES
!   NBB2     I         NBB*2
!   LNPNM    I         .TRUE. IF NP IS THE FIRST DIMENSION
!                      IN THE PMPA  ARRAY (AS OPPOSED TO NM)
!   AA      I/O        REAL DYNAMIC ARRAY
!   UNORM    I         DENORMALIZING FACTOR FOR LEGENDRE POLY.
!
! COMMENTS:
!
! REFERENCES:
!            "OBSERVED TIDAL BRAKING IN THE EARTH/MOON/SUN SYSTEM"
!             BY CHRISTODOULIDIS, ET AL., JGR, VOL 93, P. 6216-6236,
!             JUNE 10,1988.
!
!             NASA TECH MEMO 86180, "ALTIMETRY, ORBITS AND TIDES",
!             OSCAR L. COLOMBO
!
!             "THE EFFECTS OF SOLID EARTH DEFORMATION ON GLOBAL
!              OCEAN TIDES", BY M.C. HENDERSHOTT, GEOPHYS. J. R.,
!              VOL 29, P. 389-402, 1972.
!******************************************************************
      IMPLICIT DOUBLE PRECISION(A-H,O-Z),LOGICAL(L)
      SAVE
      INTEGER HH,LL,QQ,SIGN1
      COMMON/CBDSTA/BDSTAT(7,999),XBDSTA
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/CORA01/KFSEC0,KFSECB,KFSEC ,KFSECV,KH    ,KHV   ,KCTOL ,   &
     &              KRSQ  ,KVMATX,KCPP  ,KCPV  ,KCCP  ,KCCV  ,KCCPV ,   &
     &              KCCVV ,KXPPPP,KX    ,KPX   ,KSUMX ,KXDDOT,KSUMPX,   &
     &              KPXDDT,KAB   ,KPN   ,KAORN ,KSINLM,KCOSLM,KTANPS,   &
     &              KCRPAR,KVRARY,KXM   ,KXNP1 ,KXPRFL,KXM2  ,KXNNP1,   &
     &              KWRK  ,KFI   ,KGE   ,KB0DRG,KBDRAG,KAPGM ,KAPLM ,   &
     &              KCN   ,KSN   ,KSTID ,KTIDE ,KSTDRG,KSTSRD,KSTACC,   &
     &              KLGRAV,KGM   ,KAE   ,KFPL  ,KFEQ  ,KPLNPO,KPLNVL,   &
     &              KXEPOC,KCD   ,KCDDOT,KCR   ,KGENAC,KACN  ,KASN  ,   &
     &              KTHDRG,KCKEP ,KCKEPN,KXNRMZ,KXNRMC,KFSCEP,KFSCND,   &
     &              KAREA ,KXMASS,KRMSPO,KTCOEF,KTXQQ ,KTIEXP,KTXMM ,   &
     &              KTXLL1,KTXSN1,KTS2QQ,KT2M2H,KT2MHJ,KTXKK ,KTSCRH,   &
     &              KPXPK ,KAESHD,KCSAVE,KSSAVE,KCGRVT,KSGRVT,KXDTMC,   &
     &              KDNLT ,KTXSN2,KTNORM,KTWRK1,KTWRK2,KUNORM,KAERLG,   &
     &              KSINCO,KPARLG,KCONST,KBFNRM,KTDNRM,KCSTHT,KTPSTR,   &
     &              KTPSTP,KTPFYW,KPLMGM,KTPXAT,KEAQAT,KEAFSS,KEAINS,   &
     &              KACS  ,KECS  ,KSOLNA,KSOLNE,KSVECT,KSFLUX,KFACTX,   &
     &              KFACTY,KADIST,KGEOAN,KPALB ,KALBCO,KEMMCO,KCNAUX,   &
     &              KSNAUX,KPPER ,KACOSW,KBSINW,KACOFW,KBCOFW,KANGWT,   &
     &              KWT   ,KPLNDX,KPLANC,KTGACC,KTGDRG,KTGSLR,KWTACC,   &
     &              KWTDRG,KWTSLR,KTMACC,KTMDRG,KTMSLR,KATTUD,KDYACT,   &
     &              KACCBT,KACPER,KXDDNC,KXDDAO,KXNC  ,KXPPNC,KSMXNC,   &
     &              KXDDTH,KPDDTH,KXSSBS,KCPPNC,KEXACT,KXACIN,KXACOB,   &
     &              KPXHDT,KTPXTH,KPACCL,KTXSTA,KDELXS,KSMRNC,KPRX  ,   &
     &              KSMRNP,KDSROT,KXUGRD,KYUGRD,KZUGRD,KSUMRC,KXDDRC,   &
     &              KTMOS0,KTMOS, KTMOSP,KSMXOS,KSGTM1,KSGTM2,KSMPNS,   &
     &              KXGGRD,KYGGRD,KZGGRD,KXEGRD,KYEGRD,KZEGRD,KSSDST,   &
     &              KSDINS,KSDIND,KSSDSR,KSSDDG,KTATHM,KTAINS,KTAFSS,   &
     &              KSRAT ,KTRAT ,KHLDV ,KHLDA1,KHLDA4,KHLDA7,KQAST1,   &
     &              KQAST2,KQAST3,KQAST4,KQAST5,KQAST6,NXCA01
      COMMON/CORA03/KRFMT ,KROTMT,KSRTCH,KCFSC ,KTHG  ,KCOSTG,KSINTG,   &
     &              KDPSI ,KEPST ,KEPSM ,KETA  ,KZTA  ,KTHTA ,KEQN  ,   &
     &              KDPSR ,KSL   ,KH2   ,KL2   ,KXPUT ,KYPUT ,KUT   ,   &
     &              KSPCRD,KSPSIG,KSTAIN,KSXYZ ,KSPWT ,KDXSDP,KDPSDP,   &
     &              KXLOCV,KSTNAM,KA1UT ,KPLTIN,KPLTVL,KPLTDV,KPUTBH,   &
     &              KABCOF,KSPEED,KANGFC,KSCROL,KSTVEL,KSTVDV,KTIMVL,   &
     &              KSTEL2,KSTEH2,KSL2DV,KSH2DV,                        &
     &              KAPRES, KASCAL, KASOFF,KXDOTP,KSAVST,               &
     &              KANGT , KSPDT , KCONAM,KGRDAN,KUANG ,KFAMP ,        &
     &              KOFACT, KOTSGN,KWRKRS,KANFSD,KSPDSD,KPRMFS,KSCRFR,  &
     &              KCOSAS,KSINAS,KDXTID,KALCOF,KSTANF,KNUTIN,KNUTMD,   &
     &              KPDPSI,KPEPST,KDXDNU,KDPSIE,KEPSTE,NXCA03
      COMMON/CRDDIM/NDCRD2,NDCRD3,NDCRD4,NDCRD5,NDCRD6,NDCRD7,          &
     &              NDCRD8,NDCRD9,NXCRDM
      COMMON/CTIDES/NTIDE,NSTADJ,NET,NETADJ,NETUN,NOT,NOTADJ,           &
     &   NOTUN ,NETUNU,NETADU,NOTUNU,NOTADU,NBSTEP,KTIDA(3),            &
     &   ILLMAX,NUNPLY,NNMPLY,NDOODN,MXTMRT,NRESP ,NXCTID
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      COMMON/EPHSET/EMFACT,DTENPD,DTINPD,FSC1EN,FSCENP(2),FSCINP(4),    &
     &   FSDINP(4),XEPHST
      COMMON/GRANGL/GHRANG(4),COSGHR(4),SINGHR(4)
      COMMON/IBODPT/IBDCF(999),IBDSF(999),IBDGM(999),IBDAE(999),    &
     &              IBDPF(999),                                     &
     &              ICBDCF,ICBDSF,ICBDGM,ICBDAE,ICBDPF,             &
     &              ITBDCF,ITBDSF,ITBDGM,ITBDAE,ITBDPF,NXBDPT
      COMMON/LSTRT/LSTART
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/QQTIDE/IQMAX,IQMIN,IQDIF,NUMFRQ,NBAND
      COMMON/SETADJ/LSADRG(4),LSASRD(4),LSAGA(4),LSAGP,LSATID,LSAGM,    &
     &              LSADPM,LSAKF,LSADNX,LSADJ2,LSAPGM
      COMMON/SETAPT/                                                    &
     &       JSADRG(1),JSASRD(1),JSAGA(1),JFAADJ,JTHDRG,JACBIA,JATITD,  &
     &       JDSTAT,JDTUM ,                                             &
     &       JSACS, JSATID,JSALG,JSAGM,JSAKF,JSAXYP,JSADJ2,JSAPGM,      &
     &       JFGADJ,JLTPMU,JLTPAL,JL2CCO,JL2SCO,JL2GM,JLRELT,           &
     &       JLJ2SN,JLGMSN,JLPLFM,JL2AE,JSAXTO,JSABRN
      COMMON/STARTT/ESSTRT,FSSTRT
      COMMON/TDELEM/XMEAN(2),XNODE(2),OMEGA(2),A(2),XINCL(2),E(2),      &
     &   EPSLON,RMASS,XLONG,TANPSI,THETG,RHO3,CC(3,3),B(2)
      COMMON/TIDTIM/FSCFR1,FSCFR2,FSCMS1,FSCMS2
      COMMON/VRBLOK/SINPSI,COSPSI,XLAMDA,GMR,AOR
      DIMENSION AA(1),II(1)
      DIMENSION FSEC(NM),THETAG(NM),RADTHT(NM),PMPA(NDIM1,NDIM2)
      DIMENSION GE(2,1),FI(2,1),SINLAT(NM),COSLAT(NM),SATLON(NM)
      DIMENSION MM(1),KK(1),HH(1),JJ(1),IBDY(1),SIGN1(1),               &
     &          LL(1),QQ(1),AT(1),BT(1)
      DIMENSION XQQ(1),IPT(1),XIEXP(1),XMM(1),                          &
     &          XSIGN1(1),X2M2HH(1),X2M2HJ(1),                          &
     &          XKK(1)
      DIMENSION SINLAM(5),COSLAM(5)
      DIMENSION XMOON(6),XSUN(6),A1(2),ANM(2),E1(2),ENM(2),             &
     &          XMEAN1(2),XMEANN(2),XNODE1(2),XNODEN(2),                &
     &          OMEGA1(2),OMEGAN(2),XINCL1(2),XINCLN(2),                &
     &          OMGDIF(2),XNDDIF(2),XMNDIF(2)
!
      DIMENSION IIPPTT(NTIDE),IQNDEX(NTIDE),JJBDY(NTIDE),ITIDE(NTIDE),  &
     &          XSIGN2(NTIDE),ICENTR(NTIDE),MAINNN(NTIDE)
      DIMENSION AJ(NBB2),BJ(NBB2),AAA(NOT),BBB(NOT),                    &
     &          AB(0:NBB),BA(0:NBB),ZOMEGA(NUMFRQ),ZMEANA(NUMFRQ),      &
     &          ZNODE(NUMFRQ),ANGARG(NUMFRQ),SINARG(NUMFRQ),            &
     &          COSARG(NUMFRQ),SINANG(NOT),COSANG(NOT),                 &
     &          QXLONG(IQDIF),SINQXL(IQDIF),COSQXL(IQDIF),              &
     &          SINQLN(NOT),COSQLN(NOT),SINTOT(NOT),                    &
     &          COSTOT(NOT),ACPBS(NOT),ASMBC(NOT),P(NOT),               &
     &          PLEGDR(NPTIDE),ALPHA(NOT)
      DIMENSION ATPART(NOT),BTPART(NOT)
      DIMENSION ALPHAH(25)
      DIMENSION UNORM(NTIDE)
!
      DATA HALF/.5D0/,ONE/1.D0/,TWO/2.D0/,                              &
     &     THREE/3.D0/,SIX/6.D0/
      DATA ZERO /0.D0/
      DATA LN1ST/.FALSE./
!
! SPECIFY (1+ALPHA*HPRIME) FROM HENDERSHOTT PAPER
      DATA ALPHAH/1.0D+00, 0.891244D+00, 0.918457D+00, 0.936460D+00,    &
     &       0.946443D+00, 0.951616D+00, 0.955972D+00, 0.958528D+00,    &
     &       0.961668D+00, 0.962586D+00, 0.965362D+00, 0.965416D+00,    &
     &       0.967380D+00, 0.967871D+00, 0.970301D+00, 0.971232D+00,    &
     &       0.972220D+00, 0.971470D+00, 0.972714D+00, 0.974078D+00,    &
     &       0.973519D+00, 0.975064D+00, 0.976713D+00, 0.976284D+00,    &
     &       0.975866D+00/
!
!
!********1*********2*********3*********4*********5*********6*********7**
! START OF EXECUTABLE CODE
!********1*********2*********3*********4*********5*********6*********7**
!
       IF(ILLMAX .GT. 25) THEN
         WRITE(IOUT6,*) ' ALPHAH SIZE(25) EXCEEDED IN ALTOTD',ILLMAX
         STOP
       ENDIF
! IF ON THE FIRST CALL TO TIDCON INITIALIZE
      IF(LN1ST) GO TO 50
! INITIALIZE
      LN1ST=.TRUE.
      RMASS=RATIOM(2)/RATIOM(1)
      B(1)=ONE
      AB(0)=ONE
      BA(0)=ZERO
      CALL FACBIN
   50 CONTINUE
!
! COMPUTE SUN & MOON POSITIONS AT BEGINNING OF BLOCK
      CALL PLANPO(MJDSEC,FSEC(1),.TRUE.,.FALSE.,AA,II)
!
!     WRITE(6,*)'** MJDSEC *** ', MJDSEC, FSEC(1)
! GET ROTATION MATRIX FROM MEAN OF 2000 (50) TO TRUE OF DATE
      ROT1=AA(KROTMT)
      ROT2=AA(KROTMT+NDCRD2)
      ROT3=AA(KROTMT+NDCRD3)
      ROT4=AA(KROTMT+NDCRD4)
      ROT5=AA(KROTMT+NDCRD5)
      ROT6=AA(KROTMT+NDCRD6)
      ROT7=AA(KROTMT+NDCRD7)
      ROT8=AA(KROTMT+NDCRD8)
      ROT9=AA(KROTMT+NDCRD9)
!  ROTATE FROM MEAN OF 2000 (50) TO TRUE OF DATE
      XMOON(1)=ROT1*BDSTAT(1,11)+ROT4*BDSTAT(2,11)                      &
     &        +ROT7*BDSTAT(3,11)
      XMOON(2)=ROT2*BDSTAT(1,11)+ROT5*BDSTAT(2,11)                      &
     &        +ROT8*BDSTAT(3,11)
      XMOON(3)=ROT3*BDSTAT(1,11)+ROT6*BDSTAT(2,11)                      &
     &        +ROT9*BDSTAT(3,11)
      XMOON(4)=ROT1*BDSTAT(4,11)+ROT4*BDSTAT(5,11)                      &
     &        +ROT7*BDSTAT(6,11)
      XMOON(5)=ROT2*BDSTAT(4,11)+ROT5*BDSTAT(5,11)                      &
     &        +ROT8*BDSTAT(6,11)
      XMOON(6)=ROT3*BDSTAT(4,11)+ROT6*BDSTAT(5,11)                      &
     &        +ROT9*BDSTAT(6,11)
      XSUN(1)=ROT1*BDSTAT(1,8)+ROT4*BDSTAT(2,8)                         &
     &        +ROT7*BDSTAT(3,8)
      XSUN(2)=ROT2*BDSTAT(1,8)+ROT5*BDSTAT(2,8)                         &
     &        +ROT8*BDSTAT(3,8)
      XSUN(3)=ROT3*BDSTAT(1,8)+ROT6*BDSTAT(2,8)                         &
     &        +ROT9*BDSTAT(3,8)
      XSUN(4)=ROT1*BDSTAT(4,8)+ROT4*BDSTAT(5,8)                         &
     &        +ROT7*BDSTAT(6,8)
      XSUN(5)=ROT2*BDSTAT(4,8)+ROT5*BDSTAT(5,8)                         &
     &        +ROT8*BDSTAT(6,8)
      XSUN(6)=ROT3*BDSTAT(4,8)+ROT6*BDSTAT(5,8)                         &
     &        +ROT9*BDSTAT(6,8)
!
      CALL TDEELM( XSUN,RATIOM(2),A1(2),EPS,XNODE1(2),OMEGA1(2),        &
     &            XMEAN1(2),E1(2))
      XM2=XMOON(2)
      XM3=XMOON(3)
      XMD2=XMOON(5)
      XMD3=XMOON(6)
!
      XMOON(2)=COS(EPS)*XM2+SIN(EPS)*XM3
      XMOON(3)=-SIN(EPS)*XM2+COS(EPS)*XM3
      XMOON(5)=COS(EPS)*XMD2+SIN(EPS)*XMD3
      XMOON(6)=-SIN(EPS)*XMD2+COS(EPS)*XMD3
!
! CONVERT FROM CARTESIAN TO KEPLERIAN ELEMENTS
!  A1    =SUN&MOON SEMI-MAJOR AXIS  AT 1ST PT IN BLOCK
!  E1    =SUN&MOON ECCENTRICITY AT 1ST PT IN BLOCK
!  XINCL1=SUN&MOON INCLINATION AT 1ST PT IN BLOCK
!  OMEGA1=SUN&MOON ANGLE OF PERIGEE AT 1ST PT IN BLOCK
!  XMEAN1=SUN&MOON MEAN ANOMALY AT 1ST PT IN BLOCK
!  XNODE1=SUN&MOON NODE AT 1ST PT IN BLOCK
!
      CALL TDEELM(XMOON,RATIOM(1),A1(1),XINCL1(1),XNODE1(1),OMEGA1(1),  &
     &            XMEAN1(1),E1(1))
!     CALL TDEELM( XSUN,RATIOM(2),A1(2),XINCL1(2),XNODE1(2),OMEGA1(2),
!    .            XMEAN1(2),E1(2))
!
! DONT INTERPOLATE IF ONLY ONE MEASUREMENT IN BLOCK
      IF(NM .EQ. 1) GOTO 100
! COMPUTE SUN & MOON POSITIONS AT END OF BLOCK
      CALL PLANPO(MJDSEC,FSEC(NM),.TRUE.,.FALSE.,AA,II)
!
! GET ROTATION MATRIX FROM MEAN OF 2000 (50) TO TRUE OF DATE
      ROT1=AA(KROTMT+NM-1)
      ROT2=AA(KROTMT+NDCRD2+NM-1)
      ROT3=AA(KROTMT+NDCRD3+NM-1)
      ROT4=AA(KROTMT+NDCRD4+NM-1)
      ROT5=AA(KROTMT+NDCRD5+NM-1)
      ROT6=AA(KROTMT+NDCRD6+NM-1)
      ROT7=AA(KROTMT+NDCRD7+NM-1)
      ROT8=AA(KROTMT+NDCRD8+NM-1)
      ROT9=AA(KROTMT+NDCRD9+NM-1)
!  ROTATE FROM MEAN OF 2000 (50) TO TRUE OF DATE
      XMOON(1)=ROT1*BDSTAT(1,11)+ROT4*BDSTAT(2,11)                      &
     &        +ROT7*BDSTAT(3,11)
      XMOON(2)=ROT2*BDSTAT(1,11)+ROT5*BDSTAT(2,11)                      &
     &        +ROT8*BDSTAT(3,11)
      XMOON(3)=ROT3*BDSTAT(1,11)+ROT6*BDSTAT(2,11)                      &
     &        +ROT9*BDSTAT(3,11)
      XMOON(4)=ROT1*BDSTAT(4,11)+ROT4*BDSTAT(5,11)                      &
     &        +ROT7*BDSTAT(6,11)
      XMOON(5)=ROT2*BDSTAT(4,11)+ROT5*BDSTAT(5,11)                      &
     &        +ROT8*BDSTAT(6,11)
      XMOON(6)=ROT3*BDSTAT(4,11)+ROT6*BDSTAT(5,11)                      &
     &        +ROT9*BDSTAT(6,11)
      XSUN(1)=ROT1*BDSTAT(1,8)+ROT4*BDSTAT(2,8)                         &
     &        +ROT7*BDSTAT(3,8)
      XSUN(2)=ROT2*BDSTAT(1,8)+ROT5*BDSTAT(2,8)                         &
     &        +ROT8*BDSTAT(3,8)
      XSUN(3)=ROT3*BDSTAT(1,8)+ROT6*BDSTAT(2,8)                         &
     &        +ROT9*BDSTAT(3,8)
      XSUN(4)=ROT1*BDSTAT(4,8)+ROT4*BDSTAT(5,8)                         &
     &        +ROT7*BDSTAT(6,8)
      XSUN(5)=ROT2*BDSTAT(4,8)+ROT5*BDSTAT(5,8)                         &
     &        +ROT8*BDSTAT(6,8)
      XSUN(6)=ROT3*BDSTAT(4,8)+ROT6*BDSTAT(5,8)                         &
     &        +ROT9*BDSTAT(6,8)
! At the end of the block rotate the coordinates of the moon from the
! equatorial system to the ecliptic
      XM2=XMOON(2)
      XM3=XMOON(3)
      XMD2=XMOON(5)
      XMD3=XMOON(6)
      CALL TDEELM( XSUN,RATIOM(2),ANM(2),EPS,XNODEN(2),OMEGAN(2),       &
     &            XMEANN(2),ENM(2))
      XMOON(2)=COS(EPS)*XM2+SIN(EPS)*XM3
      XMOON(3)=-SIN(EPS)*XM2+COS(EPS)*XM3
      XMOON(5)=COS(EPS)*XMD2+SIN(EPS)*XMD3
      XMOON(6)=-SIN(EPS)*XMD2+COS(EPS)*XMD3
!
! CONVERT FROM CARTESIAN TO KEPLERIAN ELEMENTS
!   ANM   =SUN&MOON SEMI-MAJOR AXIS  AT LAST PT IN BLOCK
!   ENM   =SUN&MOON ECCENTRICITY AT LAST PT IN BLOCK
!   XINCLN=SUN&MOON INCLINATION AT LAST PT IN BLOCK
!   OMEGAN=SUN&MOON ANGLE OF PERIGEE AT LAST PT IN BLOCK
!   XMEANN=SUN&MOON MEAN ANOMALY AT LAST PT IN BLOCK
!   XNODEN=SUN&MOON NODE AT LAST PT IN BLOCK
      CALL TDEELM(XMOON,RATIOM(1),ANM(1),XINCLN(1),XNODEN(1),OMEGAN(1), &
     &            XMEANN(1),ENM(1))
!     CALL TDEELM( XSUN,RATIOM(2),ANM(2),XINCLN(2),XNODEN(2),OMEGAN(2),
!    .            XMEANN(2),ENM(2))
!     WRITE(6,115) XMEANN(1),XMEANN(2),XNODEN(1)
!
! COMPUTE VALUES FOR INTERPOLATION
!  OMGDIF=SUN&MOON PERIGEE DIFF. BETWEEN 1ST & LAST PTS
!  XNDDIF=SUN&MOON NODE    DIFF. BETWEEN 1ST & LAST PTS
!  XMNDIF=SUN&MOON MEAN A. DIFF. BETWEEN 1ST & LAST PTS
      TMDIFF = FSEC(NM)-FSEC(1)
      DO 90 K=1,2
      OMGDIF(K) = OMEGAN(K)-OMEGA1(K)
      XNDDIF(K) = XNODEN(K)-XNODE1(K)
      XMNDIF(K) = XMEANN(K)-XMEAN1(K)
! CORRECT FOR THE ZERO,TWOPI BOUNDARY
      IF(ABS(OMGDIF(K)) .GT. PI) THEN
         IF(OMGDIF(K) .GT. ZERO) THEN
              OMGDIF(K)=OMGDIF(K)-TWOPI
         ELSE
              OMGDIF(K)=OMGDIF(K)+TWOPI
         ENDIF
      ENDIF
      IF(ABS(XNDDIF(K)) .GT. PI) THEN
         IF(XNDDIF(K) .GT. ZERO) THEN
              XNDDIF(K)=XNDDIF(K)-TWOPI
         ELSE
              XNDDIF(K)=XNDDIF(K)+TWOPI
         ENDIF
      ENDIF
      IF(ABS(XMNDIF(K)) .GT. PI) THEN
         IF(XMNDIF(K) .GT. ZERO) THEN
              XMNDIF(K)=XMNDIF(K)-TWOPI
         ELSE
              XMNDIF(K)=XMNDIF(K)+TWOPI
         ENDIF
      ENDIF
   90 END DO
!
  100 CONTINUE
!
! CONVERT SAT LONGITUDE TO RADIANS
      DO 110 K=1,NM
      SATLON(K)=DEGRAD*SATLON(K)
  110 END DO
!
! INITIALIZE PARTIAL POINTERS
      IFRSTA = IPVAL0(IXOTDE)
      IFRSTB = IPVAL0(IXOTDE)+NOTADJ
      NUNADJ= NETADJ+NOTADJ+NETUN+1
!
! LOOP OVER ALL MEASUREMENTS IN BLOCK
      DO 1000 NN=1,NM
! BEGIN INTERPOLATION OF DOODSON COEFF, INCLN& ECCENT. FUNC, ETC.
      FJDSEC=DBLE(MJDSEC)+FSEC(NN)
! IF IN THE STARTER AND GOING BACK IN TIME, DON'T RECOMPUTE ENDPTS
! JUST EXTRAPOLATE FROM EXISTING POINTERS FOR COMPUATIONAL EFFICIENCY
      IF(FJDSEC .LT. FSCMS1 .AND. LSTART) THEN
      XFRSTP=(FJDSEC-FSCMS1)/DTINPD
      GOTO 190
      ENDIF
!
! COMPUTE INTERPOLATION POINT (XFRSTP) IN THE INTERPOLATION
! INTERVAL(DTINPD)
      XFRSTP=(FJDSEC-FSCINP(1))/DTINPD
      INPT=INT(XFRSTP)
      IFRSTP=INPT+1
      ISECDP=IFRSTP+1
      XFRSTP=XFRSTP+DBLE(1-IFRSTP)
!
! IF INTERP POINT IS OUTSIDE EXISTING ENDPTS, COMPUTE NEW ENDPTS
      IF(FJDSEC.GT.FSCMS1.AND.FJDSEC.LT.FSCMS2) GOTO 190
      FSCBG2=FSCMS1-DTINPD
      FSCED2=FSCMS2+DTINPD
!
! COMPUTE POINTERS FOR ENDPOINT INFORMATION
      IF(FJDSEC.GT.FSCMS2.AND.FJDSEC.LT.FSCED2)GOTO 150
      IF(FJDSEC.GT.FSCBG2.AND.FJDSEC.LT.FSCMS1)GOTO 155
! COMPUTE BOTH ENDPOINTS
      JT=2
! IT IS OCEAN TIDE POINTER
      IT=NET
      IPD=IFRSTP
      GO TO 170
!
! NEED NEW RIGHT-HAND ENDPOINT
  150 CONTINUE
      JT=1
      IT=NBAND+NET+NET
! IGOTN AND IGOTO ARE THE POINTERS FOR SAVING OLD INFORMATION
      IGOTN=1
      IGOTO=NBB+1
      IPD=ISECDP
      GO TO 160
!
! NEED NEW LEFT-HAND ENDPOINT
  155 CONTINUE
      JT=1
      IT=NET
      IGOTN=NBB+1
      IGOTO=1
      IPD=IFRSTP
  160 CONTINUE
!
! SHIFT PRECOMPUTED ENDPOINTS FROM RIGHT TO LEFT OR VISE-VERSA
      DO 165 K=1,NBB
      AJ(IGOTN+K-1  )=AJ(IGOTO+K-1  )
      BJ(IGOTN+K-1  )=BJ(IGOTO+K-1  )
  165 END DO
  170 CONTINUE
!
! UPDATE ENDPOINT TIMES
      FSCMS1=FSCINP(1)+DBLE(INPT)*DTINPD
      FSCMS2=FSCMS1+DTINPD
! CALCULATE ENDPOINT VALUES
      DO 180 J=1,JT
! EVALUATE SUN & MOON POSITIONS AT ENDPOINT TIME
      CALL TIDEL(IPD)
      COSEPH=COS(HALF*EPSLON)
      SINEPH=SIN(HALF*EPSLON)
      RHO3=(A(1)/A(2))**3
      B(2)=RHO3*RMASS
      THGPI2=GHRANG(IPD)+PI*HALF
!
! CALCULATE DOODSON CONSTANT(GD), INC FUNC, ECC FUNC
      GD=0.25D0*GM*RATIOM(1)*AESQ/(A(1)**3)
!
! CALCULATE ALL DOODSON COEFS AND SIDE BAND INFO FOR OTIDES
      DO 75 I=1,NBAND
      I2=I+NET
      CALL AANDBO (AJ(IT+I),BJ(IT+I),MAINNN(I2),ICENTR(I2),             &
     &             COSEPH,SINEPH,THGPI2)
      AJ(IT+I)=AJ(IT+I)*GD
      BJ(IT+I)=BJ(IT+I)*GD
   75 END DO
      IT=IT+NBAND+NET
  180 IPD=IPD+1
!
  190 CONTINUE
! BEGIN ACTUAL INTERPOLATION
!
! INTERPOLATE A AND B
      DO 200 K=1,NBAND
      AB(NET+K)=(AJ(NBB+NET+K)-AJ(NET+K))                               &
     &               *XFRSTP+AJ(NET+K)
      BA(NET+K)=(BJ(NBB+NET+K)-BJ(NET+K))                               &
     &               *XFRSTP+BJ(NET+K)
  200 END DO
!
! PUT THE A'S AND B'S INTO THE PROPER LOCATION
!  ADJUSTED OTIDES
      IF(NOTADJ .EQ. 0) GOTO 275
      DO 250 K=1,NOTADJ
      AAA(K)=AB(ITIDE(NETADJ+K)-1)
      BBB(K)=BA(ITIDE(NETADJ+K)-1)
  250 END DO
  275 CONTINUE
!
!  UNADJUSTED OTIDES
      IF(NOTUN .EQ. 0) GOTO 325
      DO 300 K=1,NOTUN
      AAA(NOTADJ+K)=AB(ITIDE(NUNADJ+K-1)-1)
      BBB(NOTADJ+K)=BA(ITIDE(NUNADJ+K-1)-1)
  300 END DO
  325 CONTINUE
!
!
! STORE VALUES AT MEASUREMENT TIME
      THGPI2=THETAG(NN)+PI*HALF
      XLONG=SATLON(NN)
! INTERPOLATE (LINEARLY) FOR PERIGEE, MEAN ANOMALY, AND NODE OF
! SUN & MOON (USE VALUES ALREADY CALCULATED FOR 1ST&LAST PTS IN BLOCK)
      IF(NM .EQ. 1 .OR. NN .EQ. 1) THEN
         OMEGA(1)=OMEGA1(1)
         OMEGA(2)=OMEGA1(2)
         XNODE(1)=XNODE1(1)
         XNODE(2)=XNODE1(2)
         XMEAN(1)=XMEAN1(1)
         XMEAN(2)=XMEAN1(2)
      ELSE IF(NN .EQ. NM) THEN
         OMEGA(1)=OMEGAN(1)
         OMEGA(2)=OMEGAN(2)
         XNODE(1)=XNODEN(1)
         XNODE(2)=XNODEN(2)
         XMEAN(1)=XMEANN(1)
         XMEAN(2)=XMEANN(2)
      ELSE
         FSCDIF=FSEC(NN)-FSEC(1)
         TSTEP =FSCDIF/TMDIFF
         OMEGA(1) = OMGDIF(1)*TSTEP+OMEGA1(1)
         OMEGA(2) = OMGDIF(2)*TSTEP+OMEGA1(2)
         XNODE(1) = XNDDIF(1)*TSTEP+XNODE1(1)
         XNODE(2) = XNDDIF(2)*TSTEP+XNODE1(2)
         XMEAN(1) = XMNDIF(1)*TSTEP+XMEAN1(1)
         XMEAN(2) = XMNDIF(2)*TSTEP+XMEAN1(2)
      ENDIF
!
! COMPUTE ANGULAR ARGUMENT FOR DISTINCT FREQ ONLY (NUMFREQ)
      DO 350 K=1,NUMFRQ
      ZOMEGA(K)=OMEGA(JJBDY(K))
      ZMEANA(K)=XMEAN(JJBDY(K))
      ZNODE(K) =XNODE(JJBDY(K))
  350 END DO
!
!OMPUTE ANGULAR ARGUMENT
      DO 400 K=1,NUMFRQ
      ANGARG(K)=-(X2M2HH(K)*ZOMEGA(K)                                   &
     &           +X2M2HJ(K)*ZMEANA(K)                                   &
     &           +XKK(K)*ZNODE(K))                                      &
     &           *XSIGN1(K)+XMM(K)*THGPI2
!     WRITE(6,*) ' ANGARG(k) * ', ANGARG(K),XKK(K)
!     WRITE(6,*) ' ANGARG(k) * ', THGP12,XMM(K)
!     WRITE(6,*) ' ANGARG(k) * ', X2M2HH(K),ZOMEGA(k)
!     WRITE(6,*) ' ANGARG(k) * ', X2M2HJ(K),ZMEANA(k)
  400 END DO
!
! FIND SIN AND COSINE OF ANGULAR ARGUMENT (DISTINCT FREQ ONLY)
      DO 450 K=1,NUMFRQ
      SINARG(K)=SIN(ANGARG(K))
      COSARG(K)=COS(ANGARG(K))
  450 END DO
!
! MATCH ANGULAR ARGUMENT SIN AND COS WITH CORRESPONDING TIDE CARDS
!  ADJUSTED OTIDES
      IF(NOTADJ .EQ. 0) GOTO 540
      DO 525 K=1,NOTADJ
      SINANG(K)=SINARG(IIPPTT(NETADJ+K))
      COSANG(K)=COSARG(IIPPTT(NETADJ+K))
  525 END DO
  540 CONTINUE
!
!  UNADJUSTED OTIDES
      IF(NOTUN .EQ. 0) GOTO 575
      DO 550 K=1,NOTUN
      SINANG(NOTADJ+K)=SINARG(IIPPTT(NUNADJ+K-1))
      COSANG(NOTADJ+K)=COSARG(IIPPTT(NUNADJ+K-1))
  550 END DO
  575 CONTINUE
!
! COMPUTE Q*XLONG FOR EVERY Q BETWEEN QMAX & QMIN
      DO 600 K=1,IQDIF
      QXLONG(K)=(IQMIN+K-1)*XLONG
  600 END DO
!
! STORE SIN AND COS OF QXLONG
      DO 650 K=1,IQDIF
      SINQXL(K)=SIN(QXLONG(K))
      COSQXL(K)=COS(QXLONG(K))
  650 END DO
!
! MATCH SIN AND COS OF QXLONG WITH CORRESPONDING TIDE CARD
!  ADJUSTED OTIDES
      IF(NOTADJ .EQ. 0) GOTO 680
      DO 675 K=1,NOTADJ
      SINQLN(K)=SINQXL(IQNDEX(NETADJ+K))*XSIGN2(NETADJ+K)
      COSQLN(K)=COSQXL(IQNDEX(NETADJ+K))
  675 END DO
  680 CONTINUE
!
!  UNADJUSTED OTIDES
      IF(NOTUN .EQ. 0) GOTO 710
      DO 700 K=1,NOTUN
      SINQLN(NOTADJ+K)=SINQXL(IQNDEX(NUNADJ+K-1))*XSIGN2(NUNADJ+K-1)
      COSQLN(NOTADJ+K)=COSQXL(IQNDEX(NUNADJ+K-1))
  700 END DO
!
  710 CONTINUE
!
!
! USING TRIG IDENTITY FOR ANGLE SUMMATION, COMPUTE SIN AND COS OF
! ANGULAR ARGUMENT +- QXLONG
! INPUT:
!        SIN(ANGARG) COS(ANGARG)
!        SIN(QXLONG) COS(QXLONG)
! OUTPUT:
!        SINTOT = SIN(ANGARG + QXLONG)
!        COSTOT = COS(ANGARG + QXLONG)
      DO 725 K=1,NOT
      SINTOT(K)=SINANG(K)*COSQLN(K)+                                    &
     &          COSANG(K)*SINQLN(K)
      COSTOT(K)=COSANG(K)*COSQLN(K)-                                    &
     &          SINANG(K)*SINQLN(K)
  725 END DO
!
! MULTIPLY TOTAL ANGLE BY SIDEBAND COEFFICIENTS
      DO 750 K=1,NOT
      ACPBS(K)=AAA(K)*COSTOT(K)+                                        &
     &         BBB(K)*SINTOT(K)
      ASMBC(K)=AAA(K)*SINTOT(K)-                                        &
     &         BBB(K)*COSTOT(K)
  750 END DO
!
! GET LGENDR POLYNOMIALS
!     WRITE(6,*)'** SINLAT,COSLAT***  ', SINLAT(NN),COSLAT(NN)
!.................................................
      SINPSI=SINLAT(NN)
      COSPSI=COSLAT(NN)
      CALL LGENDR(.TRUE.,AA(KXNRMZ),AA(KXNRMC),AA(KVRARY),PLEGDR,       &
     &            AA(KAORN),SINLAM,COSLAM,AA(KTANPS),ILLMAX)
! SPLIT CODE BETWEEN ADJUSTED AND UNADJUSTED TO REDUCE IF CHECKS
! FOR RANGE ZERO LOOPS
!
!  ADJUSTED OTIDES
      IF(NOTADJ .EQ. 0) GOTO 810
      DO 775 K=1,NOTADJ
      P(K)=PLEGDR(IPT(NETADJ+K))*XIEXP(NETADJ+K)
  775 END DO
! GET P*(-1)**M
!
! DETERMINE IF UNNORMALIZED POLYNOMIALS SHOULD BE USED
      IF(NUNPLY .EQ. 1) THEN
      DO 780 K=1,NOTADJ
      P(K)=P(K)/UNORM(NETADJ+K)
  780 END DO
      ENDIF
!
! MATCH ALPHAH TERM WITH APPROPRIATE TIDE DEGREE
!  ADJUSTED OTIDES
      DO 800 K=1,NOTADJ
      ALPHA(K)=ALPHAH(LL(NETADJ+K))
  800 END DO
  810 CONTINUE
!
!  UNADJUSTED OTIDES
      IF(NOTUN .EQ. 0) GOTO 860
      DO 825 K=1,NOTUN
      P(NOTADJ+K)=PLEGDR(IPT(NUNADJ+K-1))*XIEXP(NUNADJ+K-1)
  825 END DO
!
! GET P*(-1)**M
!  UNADJUSTED OTIDES
!
! DETERMINE IF UNNORMALIZED POLYNOMIALS SHOULD BE USED
      IF(NUNPLY .EQ. 1) THEN
      DO 830 K=1,NOTUN
      P(NOTADJ+K)=P(NOTADJ+K)/UNORM(NUNADJ+K-1)
  830 END DO
      ENDIF
!
! MATCH ALPHAH TERM WITH APPROPRIATE TIDE DEGREE
!  UNADJUSTED OTIDES
      DO 850 K=1,NOTUN
      ALPHA(NOTADJ+K)=ALPHAH(LL(NUNADJ+K-1))
  850 END DO
  860 CONTINUE
!
! COMPUTE MEASUREMENT PARTIALS WRT A&B TIDE COEFFICIENTS
      DO 875 K=1,NOT
      ATPART(K)=-P(K)*ACPBS(K)*ALPHA(K)
      BTPART(K)=-P(K)*ASMBC(K)*ALPHA(K)
  875 END DO
!
! COMPUTE RADIAL OCEAN TIDAL HEIGHT (AT&BT FROM OT INPUT CARDS)
      RADTHT(NN)=ZERO
! ADJUSTED OTIDES
      IF(NOTADJ .EQ. 0) GOTO 925
      DO 900 K=1,NOTADJ
      RADTHT(NN)=RADTHT(NN)+AT(NETADJ+K)*ATPART(K)                      &
     &                     +BT(NETADJ+K)*BTPART(K)
  900 END DO
  925 CONTINUE
!
! UNADJUSTED OTIDES
      IF(NOTUN .EQ. 0) GOTO 975
      DO 950 K=1,NOTUN
      RADTHT(NN)=RADTHT(NN)+AT(NUNADJ+K-1)*ATPART(NOTADJ+K)             &
     &                     +BT(NUNADJ+K-1)*BTPART(NOTADJ+K)
  950 END DO
  975 CONTINUE
!
!
! STORE PARTIALS IN PMPA ARRAY ON LAST INNER ITERATION
      IF(.NOT. LSTINR .OR. NOTADJ .EQ. 0) GOTO 1000
      IF(LNPNM) THEN
      DO 977 I=1,NOTADJ
      PMPA(IFRSTA+I-1,NN)=-ATPART(I)
      PMPA(IFRSTB+I-1,NN)=-BTPART(I)
  977 END DO
      ELSE
         DO 980 I=1,NOTADJ
         PMPA(NN,IFRSTA+I-1)=-ATPART(I)
         PMPA(NN,IFRSTB+I-1)=-BTPART(I)
  980 END DO
      ENDIF
!
 1000 END DO
      RETURN
      END
