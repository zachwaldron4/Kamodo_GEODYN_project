!$COORDT
      SUBROUTINE COORDT(MJDSEC,FSEC,IORJX,IORSAX,IOREMX,IORMOX,IORSNX,  &
     &                  IORMX,COEFJ,COEFSA,COEFEM,COEFMO,COEFS,COEFM,   &
     &                  NM,TREFC,DELET,PSTAT,PSUN,PMARS,                &
     &                  TT1,TT2,TT4,CHEB1,CHEBV1,CHEB2,CHEBV2,          &
     &                  CHEB4,CHEBV4,PJUP,PSAT,PMOON,PEM,               &
     &                  RDIFF1,RDIFF2,TERM,TWOT)
!********1*********2*********3*********4*********5*********6*********7**
! COORDT           00/00/00            8709.0    PGMR - D. ROWLANDS
!                                      8901.0    PGMR - A. MARSHALL
!
!  FUNCTION:  GET THE TIME&POSITION DEPENDENT COORECTION
!             TO THE AVERAGE (ET-TAI) DIFFERENCE. THIS
!             CORRECTION GIVES THE TRUE (ET-TAI) DIFFERENCE
!             WHEN ADDED TO THE AVERAGE DIFFERENCE.
!             FROM MOYER'S FORMULATION.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I    S    INTEGER SECONDS SINCE GEODYN REFERENCE TIME
!   FSEC     I    A    FRACTIONAL REMAINING SECONDS (COORDINATE ET)
!   IORJ     I    S    ORDER OF CHEBYSHEV POLYNOMIALS (JUPITER)
!   IORSA    I    S    ORDER OF CHEBYSHEV POLYNOMIALS (SATURN)
!   IOREM    I    S    ORDER OF CHEBYSHEV POLYNOMIALS (EARTH-MOON)
!   IORMO    I    S    ORDER OF CHEBYSHEV POLYNOMIALS (MOON)
!   IORSUN   I    S    ORDER OF CHEBYSHEV POLYNOMIALS (SUN)
!   IORM     I    S    ORDER OF CHEBYSHEV POLYNOMIALS (MARS)
!   COEFJ    I    A    CHEBYSHEV POLYNOMIAL COEFFICIENTS (JUPITER)
!   COEFSA   I    A    CHEBYSHEV POLYNOMIAL COEFFICIENTS (SATURN)
!   COEFEM   I    A    CHEBYSHEV POLYNOMIAL COEFFICIENTS (EARTH-MOON)
!   COEFMO   I    A    CHEBYSHEV POLYNOMIAL COEFFICIENTS (MOON)
!   COEFS    I    A    CHEBYSHEV POLYNOMIAL COEFFICIENTS (SUN)
!   COEFM    I    A    CHEBYSHEV POLYNOMIAL COEFFICIENTS (MARS)
!   NM       I    S    NUMBER OF TIMES
!   TREFC    I    A    TRUE OF REF EARTH CENTERD COORDINATES OF THE
!                      PLACE WHERE THE TIME CORRECTION IS REQUESTED.
!   DELET    O    A    THE TIME CORRECTION.
!   PSTAT    O    A    PLANET STATE IN BARYCENTRIC TRUE SAT REF SYSTEM
!   PSUN     O    A    SUN    STATE IN BARYCENTRIC TRUE SAT REF SYSTEM
!   PMARS    O    A    MARS   STATE IN BARYCENTRIC TRUE SAT REF SYSTEM
!   TT1      I    A    NORMALIZED TIME GROUP 1
!   TT2      I    A    NORMALIZED TIME GROUP 2
!   TT4      I    A    NORMALIZED TIME GROUP 4
!   CHEB1    O    A    CHEBYSHEV POLYNOMIAL GROUP 1
!   CHEBV1   O    A    CHEBYSHEV VELOCITY POLY. GROUP 1
!   CHEB2    O    A    CHEBYSHEV POLYNOMIAL GROUP 2
!   CHEBV2   O    A    CHEBYSHEV VELOCITY POLY. GROUP 2
!   CHEB4    O    A    CHEBYSHEV POLYNOMIAL GROUP 4
!   CHEBV4   O    A    CHEBYSHEV VELOCITY POLY. GROUP 4
!   PJUP     O    A    JUPITER STATE IN BARYCENTRIC TRUE SAT REF SYSTEM
!   PSAT     O    A    SATURN  STATE IN BARYCENTRIC TRUE SAT REF SYSTEM
!   PMOON    O    A    MOON    STATE IN BARYCENTRIC TRUE SAT REF SYSTEM
!   PEM      O    A    ER-MOON STATE IN BARYCENTRIC TRUE SAT REF SYSTEM
!   RDIFF1   I    A    SCRATCH DIFFERENCES
!   RDIFF2   I    A    SCRATCH DIFFERENCES
!   TERM     I    A    SCRATCH SUMS
!   TWOT     I    A    SCRATCH SUM
!
! COMMENTS:
!
! RESTRICTIONS:  (1)THIS ROUTINE WILL WORK CORRECTLY ONLY
!                FOR POSITIONS ON THE EARTH (LIMITATIONS
!                OF THE DERIVATION). IT WILL ONLY WORK IF
!                THE TRACKING BODY OF THE RUN IS THE EARTH
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CBDSTA/BDSTAT(7,999),XBDSTA
      COMMON/CLIGHT/VLIGHT,ERRLIM,XCLITE
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/EPHM/FSECXY(1),XP(261),YP(261),A1UT(261),EP(261),EC(261),  &
     &            FSCFLX(1),FLUXS(36),AVGFLX(36),FLUXM(288),FSECEP(1),  &
     &            EPHP(816),EPHN(96),ELIB(120),BUFT(2700),FSECNP(4)
      COMMON/EPHMPT/NTOT,NTOTS,NNPDPR,NUTORD,IBODDG(8),IGRPDG(4),       &
     &              NENPD,INRCD(2),KPLAN(11),NINEP,NEPHMR,NEPHEM,       &
     &              IBHINT,NINTBH,NXEPHP
      COMMON/EPHSET/EMFACT,DTENPD,DTINPD,FSC1EN,FSCENP(2),FSCINP(4),    &
     &   FSDINP(4),XEPHST
      COMMON/IBODPT/IBDCF(999),IBDSF(999),IBDGM(999),IBDAE(999),    &
     &              IBDPF(999),                                     &
     &              ICBDCF,ICBDSF,ICBDGM,ICBDAE,ICBDPF,             &
     &              ITBDCF,ITBDSF,ITBDGM,ITBDAE,ITBDPF,NXBDPT
      COMMON/IORCHB/ IORM,IORJ,IORSA,IORSUN,IOREM,IORMO,IORCB,IORTB,    &
     &               IGRP,ICHBOG(2,14),IORSCB(988),NXORCH
      COMMON/PLNETI/IPLNET(999),IPLNIN(999),MPLNGD(999),MPLNGO(999),   &
     &              IPLNZ(999),NXPLNI
      DIMENSION FSEC(NM),COEFMO(3,IORMO),COEFS(3,IORSUN),COEFM(3,IORM), &
     &          COEFJ(3,IORJ),COEFSA(3,IORSA),COEFEM(3,IOREM),          &
     &          TREFC(MINTIM,3)
      DIMENSION CHEB1(NM,15),CHEBV1(NM,15),                             &
     &          CHEB2(NM,15),CHEBV2(NM,15),                             &
     &          CHEB4(NM,15),CHEBV4(NM,15),                             &
     &          PSTAT(NM,6),PMOON(NM,6),PSUN(NM,6),PMARS(NM,6),         &
     &          PJUP(NM,6),PSAT(NM,6),PEM(NM,6),DELET(NM)
      DIMENSION TT1(NM),TT2(NM),TT4(NM),FNSEC(4),VSEC(4),               &
     &          RDIFF1(NM,6),RDIFF2(NM,3),TERM(NM,6),TWOT(NM)
      DATA ZERO/0.0D0/,ONE/1.0D0/,TWO/2.0D0/
      DATA FNSEC/2764800.D0,1382400.D0,691200.D0,345600.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! INITIALIZE
      VSEC(1)=TWO/FNSEC(1)
      VSEC(2)=TWO/FNSEC(2)
      VSEC(3)=TWO/FNSEC(3)
      VSEC(4)=TWO/FNSEC(4)
      DO 100 I=1,NM
      DO 100 J=1,6
      PMARS(I,J) = ZERO
      PSUN(I,J)  = ZERO
      PJUP(I,J)  = ZERO
      PSAT(I,J)  = ZERO
  100 CONTINUE
!
!
      DO 1900 IGROUP=1,4
!
      DO 200 I=1,NM
      CHEB1(I,1)  = ONE
      CHEBV1(I,1) = ZERO
      CHEBV1(I,2) = ONE
  200 END DO
!
! MAP MJDSEC,FSEC INTO INTERVAL  -1,1  FOR EACH PERIOD GROUP
! FIGURE POINTER WHITHIN 32 DAY RECORD FOR EACH PERIOD GROUP
      FSECC1=FSECNP(IGROUP)
      RNSEC=ONE/FNSEC(IGROUP)
      DO 300 I=1,NM
      TT1(I)=((MJDSEC-FSECC1)+FSEC(I))*RNSEC
      TT1(I)=TT1(I)*TWO-ONE
  300 END DO
!
! COMPUTE CHEBYSHEV POLYNOMIALS AT APPROPRIATE TIMES
      CALL CHEBPV(CHEB1,CHEBV1,TT1,15,NM,TWOT,.TRUE.)
      DO 400 I=1,NM
      DO 400 J=1,15
      CHEBV1(I,J) = CHEBV1(I,J)*VSEC(IGROUP)
  400 CONTINUE
!
! COMPUTE POSITION AND VELOCITY
!
! SUN POSITION
      IF(IGROUP.NE.ICHBOG(2,11)) GO TO 610
      DO 600 J=1,IORSUN
      DO 600 JL=1,NM
      PSUN(JL,1) = PSUN(JL,1)+COEFS(1,J)*CHEB1(JL,J)
      PSUN(JL,2) = PSUN(JL,2)+COEFS(2,J)*CHEB1(JL,J)
      PSUN(JL,3) = PSUN(JL,3)+COEFS(3,J)*CHEB1(JL,J)
      PSUN(JL,4) = PSUN(JL,4)+COEFS(1,J)*CHEBV1(JL,J)
      PSUN(JL,5) = PSUN(JL,5)+COEFS(2,J)*CHEBV1(JL,J)
      PSUN(JL,6) = PSUN(JL,6)+COEFS(3,J)*CHEBV1(JL,J)
  600 CONTINUE
  610 CONTINUE
!
! JUPITER POSITION
      IF(IGROUP.NE.ICHBOG(2,5)) GO TO 710
      DO 700 J=1,IORJ
      DO 700 JL=1,NM
      PJUP(JL,1) = PJUP(JL,1)+COEFJ(1,J)*CHEB1(JL,J)
      PJUP(JL,2) = PJUP(JL,2)+COEFJ(2,J)*CHEB1(JL,J)
      PJUP(JL,3) = PJUP(JL,3)+COEFJ(3,J)*CHEB1(JL,J)
      PJUP(JL,4) = PJUP(JL,4)+COEFJ(1,J)*CHEBV1(JL,J)
      PJUP(JL,5) = PJUP(JL,5)+COEFJ(2,J)*CHEBV1(JL,J)
      PJUP(JL,6) = PJUP(JL,6)+COEFJ(3,J)*CHEBV1(JL,J)
  700 CONTINUE
  710 CONTINUE
!
! SATURN POSITION
      IF(IGROUP.NE.ICHBOG(2,6)) GO TO 810
      DO 800 J=1,IORSA
      DO 800 JL=1,NM
      PSAT(JL,1) = PSAT(JL,1)+COEFSA(1,J)*CHEB1(JL,J)
      PSAT(JL,2) = PSAT(JL,2)+COEFSA(2,J)*CHEB1(JL,J)
      PSAT(JL,3) = PSAT(JL,3)+COEFSA(3,J)*CHEB1(JL,J)
      PSAT(JL,4) = PSAT(JL,4)+COEFSA(1,J)*CHEBV1(JL,J)
      PSAT(JL,5) = PSAT(JL,5)+COEFSA(2,J)*CHEBV1(JL,J)
      PSAT(JL,6) = PSAT(JL,6)+COEFSA(3,J)*CHEBV1(JL,J)
  800 CONTINUE
  810 CONTINUE
!
! MARS POSITION
      IF(IGROUP.NE.ICHBOG(2,4)) GO TO 910
      DO 900 J=1,IORM
      DO 900 JL=1,NM
      PMARS(JL,1) = PMARS(JL,1)+COEFM(1,J)*CHEB1(JL,J)
      PMARS(JL,2) = PMARS(JL,2)+COEFM(2,J)*CHEB1(JL,J)
      PMARS(JL,3) = PMARS(JL,3)+COEFM(3,J)*CHEB1(JL,J)
      PMARS(JL,4) = PMARS(JL,4)+COEFM(1,J)*CHEBV1(JL,J)
      PMARS(JL,5) = PMARS(JL,5)+COEFM(2,J)*CHEBV1(JL,J)
      PMARS(JL,6) = PMARS(JL,6)+COEFM(3,J)*CHEBV1(JL,J)
  900 CONTINUE
  910 CONTINUE
!
! OLD GROUP 2
      IF(IGROUP.NE.ICHBOG(2,3)) GO TO 1410
      DO 950 I=1,NM
      DO 950 J=1,6
      PEM(I,J) = ZERO
  950 CONTINUE
!
! COMPUTE POSITION AND VELOCITY
! EARTH POSITION
      DO 1400 J=1,IOREM
      DO 1400 JL=1,NM
      PEM(JL,1) = PEM(JL,1)+COEFEM(1,J)*CHEB1(JL,J)
      PEM(JL,2) = PEM(JL,2)+COEFEM(2,J)*CHEB1(JL,J)
      PEM(JL,3) = PEM(JL,3)+COEFEM(3,J)*CHEB1(JL,J)
      PEM(JL,4) = PEM(JL,4)+COEFEM(1,J)*CHEBV1(JL,J)
      PEM(JL,5) = PEM(JL,5)+COEFEM(2,J)*CHEBV1(JL,J)
      PEM(JL,6) = PEM(JL,6)+COEFEM(3,J)*CHEBV1(JL,J)
 1400 CONTINUE
 1410 CONTINUE
!
! OLD GROUP 4
      IF(IGROUP.NE.ICHBOG(2,10)) GO TO 1760
      DO 1450 I=1,NM
      DO 1450 J=1,6
      PMOON(I,J) = ZERO
 1450 CONTINUE
!
! COMPUTE POSITION AND VELOCITY
! MOON
      DO 1750 J=1,IORMO
      DO 1750 JL=1,NM
      PMOON(JL,1) = PMOON(JL,1)+COEFMO(1,J)*CHEB1(JL,J)
      PMOON(JL,2) = PMOON(JL,2)+COEFMO(2,J)*CHEB1(JL,J)
      PMOON(JL,3) = PMOON(JL,3)+COEFMO(3,J)*CHEB1(JL,J)
      PMOON(JL,4) = PMOON(JL,4)+COEFMO(1,J)*CHEBV1(JL,J)
      PMOON(JL,5) = PMOON(JL,5)+COEFMO(2,J)*CHEBV1(JL,J)
      PMOON(JL,6) = PMOON(JL,6)+COEFMO(3,J)*CHEBV1(JL,J)
 1750 CONTINUE
 1760 CONTINUE
!
!
 1900 END DO
!
!
!
!
      DO 1950 JL=1,NM
      PSTAT(JL,1) = PEM(JL,1)-PMOON(JL,1)
      PSTAT(JL,2) = PEM(JL,2)-PMOON(JL,2)
      PSTAT(JL,3) = PEM(JL,3)-PMOON(JL,3)
      PSTAT(JL,4) = PEM(JL,4)-PMOON(JL,4)
      PSTAT(JL,5) = PEM(JL,5)-PMOON(JL,5)
      PSTAT(JL,6) = PEM(JL,6)-PMOON(JL,6)
 1950 END DO
!
! THE FOLLOWING QUANTITIES SHOULD BE CALCULATED ONCE PER GLOBAL ITER
      VLIT2R=ONE/(VLIGHT*VLIGHT)
      RATJUS=BDSTAT(7,3)/(BDSTAT(7,3)+BDSTAT(7,8))
      RATSAS=BDSTAT(7,4)/(BDSTAT(7,4)+BDSTAT(7,8))
!
! COMPUTE VECTOR DIFFERENCES
      DO 2000 I=1,NM
      DO 2000 J=1,6
      RDIFF1(I,J)=PEM(I,J)-PSUN(I,J)
 2000 CONTINUE
      DO 2100 I=1,NM
      TERM(I,1)=TWO*(RDIFF1(I,1)*RDIFF1(I,4)+                           &
     &             RDIFF1(I,2)*RDIFF1(I,5)+                             &
     &             RDIFF1(I,3)*RDIFF1(I,6))
 2100 END DO
!
      DO 2200 I=1,NM
      DO 2200 J=1,3
      RDIFF2(I,J)=PSTAT(I,J)-PEM(I,J)
 2200 CONTINUE
      DO 2300 I=1,NM
      TERM(I,2)=PEM(I,4)*RDIFF2(I,1)+                                   &
     &             PEM(I,5)*RDIFF2(I,2)+                                &
     &             PEM(I,6)*RDIFF2(I,3)
 2300 END DO
!
      DO 2400 I=1,NM
      TERM(I,3)=PSTAT(I,4)*TREFC(I,1)+                                  &
     &             PSTAT(I,5)*TREFC(I,2)+                               &
     &             PSTAT(I,6)*TREFC(I,3)
 2400 END DO
!
      DO 2500 I=1,NM
      DO 2500 J=1,6
      RDIFF1(I,J)=PJUP(I,J)-PSUN(I,J)
 2500 CONTINUE
      DO 2600 I=1,NM
      TERM(I,4)=RATJUS*(RDIFF1(I,1)*RDIFF1(I,4)+                        &
     &             RDIFF1(I,2)*RDIFF1(I,5)+                             &
     &             RDIFF1(I,3)*RDIFF1(I,6))
 2600 END DO
!
      DO 2700 I=1,NM
      DO 2700 J=1,6
      RDIFF1(I,J)=PSAT(I,J)-PSUN(I,J)
 2700 CONTINUE
      DO 2800 I=1,NM
      TERM(I,5)=RATSAS*(RDIFF1(I,1)*RDIFF1(I,4)+                        &
     &             RDIFF1(I,2)*RDIFF1(I,5)+                             &
     &             RDIFF1(I,3)*RDIFF1(I,6))
 2800 END DO
!
      DO 2900 I=1,NM
      DO 2900 J=1,3
      RDIFF2(I,J)=PEM(I,J)-PSUN(I,J)
 2900 CONTINUE
      DO 3000 I=1,NM
      TERM(I,6)=PSUN(I,4)*RDIFF2(I,1)+                                  &
     &             PSUN(I,5)*RDIFF2(I,2)+                               &
     &             PSUN(I,6)*RDIFF2(I,3)
 3000 END DO
!
      DO 3100 I=1,NM
      DELET(I)=VLIT2R*(TERM(I,1)+TERM(I,2)+TERM(I,3)                    &
     &                     +TERM(I,4)+TERM(I,5)+TERM(I,6))
 3100 END DO
!
      RETURN
      END
