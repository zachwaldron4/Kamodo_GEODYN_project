!$OBSVLB
      SUBROUTINE OBSVLB (AA,II,LL,OBSBUF)
!********1*********2*********3*********4*********5*********6*********7**
! OBSRD            00/00/00            0000.0    PGMR - T. MARTIN
!
! FUNCTION: CONTROLS THE READING AND REWRITING OF OBSERVATION DATA
!           AND THE COMPUTATION OF MEMORY POINTERS NECESSARY FOR
!           DATA PROCESSING
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   AA      I/O   A    REAL DYNAMIC ARRAY
!   II      I/O   A    INTEGER DYNAMIC ARRAY
!   LL      I/O   A    LOGICAL DYNAMIC ARRAY
!   OBSBUF  I/O   A    OBSERVATION BUFFER CONSISTING OF THE LOGICAL
!                      OBSERVATION RECORDS THAT MAKE UP ONE BLOCK
!                      OF DATA
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/COBBUF/IOBSBF,IBUF1 ,IBUF2 ,IWORD1,NHEADR,NWORDM
      COMMON/COBDIR/DWORD(100),CDIREC,TDIREC
      COMMON/COBGLO/MABIAS,MCBIAS,MPVECT,MINTPL,MPARAM,MSTA,MOBBUF,     &
     &       MNPITR,MWIDTH,MSIZE ,MPART ,MBUF  ,NXCOBG
      COMMON/VLBTM/VLMJD1,VLSEC1,VLMJD2,VLSEC2
      DIMENSION AA(*),II(*),LL(*),OBSBUF(MOBBUF)
!
!**********************************************************************
! START OF EXECUTABLE CODE ********************************************
!**********************************************************************
!
      VLMJD1=-99.D0
      VLSEC1=0.D0
      VLMJD2=-99.D0
      VLSEC2=0.D0
!
      VSEC1= 9.D12
      VSEC2=-9.D12
      LEOF=.FALSE.
   10 CONTINUE
! READ OBSERVATION DIRECTORY RECORD
      CALL OBSDIR(AA,II,LL,LEOF)
! EXIT IF DIRECTORY EMPTIED
      IF(LEOF) GO TO 500
! OBTAIN OBSERVATION BLOCK START DATE AND TIME
      MJDSBL=INT(DWORD(1))
      FSECBL=DWORD(2)
! OBTAIN OBSERVATION BLOCK POINTERS
      IBUF1 =INT(DWORD(3))
      IBUF2 =INT(DWORD(4))
      IWORD1=INT(DWORD(5))
! READ OBSERVATION BLOCK
      CALL OBSIN (AA,OBSBUF,IOBSBF,IBUF1,IBUF2)
!
! VERIFY THAT OBSERVATION BLOCK START TIME AGREES WITH DIRECTORY
!
      MJDSOB=INT(OBSBUF(IWORD1))
      IF(MJDSOB.NE.MJDSBL) THEN
           WRITE(6,6000)
           !original WRITE(6,6001) MJDOBS,MJDSBL
           ! jjm 20120531 changed MJDOBS below to MJDSOB
           WRITE(6,6001) MJDSOB,MJDSBL
           STOP
      ENDIF
      MTYPE =INT(OBSBUF(IWORD1+4))
      IF(MTYPE.NE.31) GO TO 10
!
! BLOCK START & END TIME IN ELAPSED SECONDS FROM MJDSBL
      SECSTR=OBSBUF(IWORD1)+OBSBUF(IWORD1+1)
      SECEND=OBSBUF(IWORD1)+OBSBUF(IWORD1+2)
      IF(SECSTR.LE.VSEC1) VSEC1=SECSTR
      IF(SECEND.GE.VSEC2) VSEC2=SECEND
      GO TO 10
  500 CONTINUE
      IF(VSEC2.LE.0.D0) RETURN
!
      VLMJD1=VSEC1/86400.D0
      MJD1=VLMJD1
      VLSEC1=VSEC1-DBLE(MJD1)*86400.D0
      MJD1=MJD1+30000
      VLMJD1=DBLE(MJD1)
!
      VLMJD2=VSEC2/86400.D0
      MJD2=VLMJD2
      VLSEC2=VSEC2-DBLE(MJD2)*86400.D0
      MJD2=MJD2+30000
      VLMJD2=DBLE(MJD2)
      RETURN
 6000 FORMAT(' EXECUTION TERMINATING IN OBSVLB')
 6001 FORMAT(' TDD TIM TAG ',I12,' DOES NOT MATCH OBS FILE TM TG',I12)
      END
