!$ZROADJ
      SUBROUTINE ZROADJ(SUM1,SUM2,XYZ,LADJEL,IELMTP)
!********1*********2*********3*********4*********5*********6*********7**
! ZROADJ           85/02/28            0000.0    PGMR - B. EDDY
!
!
! FUNCTION:   TRANSFORMS THE PARTS OF THE NORMAL MATRIX PERTAINING
!             TO SATELLITE ELEMENTS FROM CARTESIAN TO KEPLERIAN OR
!             NON-SINGULAR KEPLERIAN SPACE, AND ZEROES OUT THE ROWS
!             AND COLUMNS OF THE NORMAL MATRIX ASSOCIATED WITH ANY
!             SATELLITE ELEMENTS NOT BEING ADJUSTED
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   SUM1    I/O        ATWA PART OF NORMAL MATRIX AS INPUT
!                      ATWA PART OF NORMAL MATRIX PERTAINING TO
!                      SATELLITE ELEMENTS TRANSFORMED TO SYSTEM
!                      INDICATED BY IELMTP AS OUTPUT
!   SUM2    I/O        ATWR PART OF NORMAL MATRIX AS INPUT
!                      ATWR PART OF NORMAL MATRIX PERTAINING TO
!                      SATELLITE ELEMENTS TRANSFORMED TO SYSTEM
!                      INDICATED BY IELMTP AS OUTPUT
!   XYZ      I         CARTESIAN SATELLITE ELEMENTS
!   LADJEL   I         ARRAY USED TO INDICATE WHICH SATELLITE
!                      ELEMENTS ARE BEING ADJUSTED(T) OR FIXED(F)
!   IELMTP   I         ARRAY INDICATING WHICH SYSTEM ELEMENTS ARE
!                      TO BE ADJUSTED IN
!                      1-CARTESIAN; 2-KEPLERIAN; 3-NON SING KEP
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/LDUAL/LASTSN,LSTSNX,LSTSNY
      COMMON/CBDSTA/BDSTAT(7,999),XBDSTA
      COMMON/CESTIM/MPARM,MAPARM,MAXDIM,MAXFMG,ICLINK,IPROCS,NXCEST
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CNIARC/NSATA,NSATG,NSETA,NEQNG,NMORDR,NMORDV,NSORDR,       &
     &              NSORDV,NSUMX,NXDDOT,NSUMPX,NPXDDT,NXBACK,NXI,       &
     &              NXILIM,NPXPF,NINTIM,NSATOB,NXBCKP,NXTIMB,           &
     &              NOBSBK,NXTPMS,NXCNIA
!
      DIMENSION A(6,6),XYZ(1)
      DIMENSION IELMTP(1),LADJEL(1)
      DIMENSION PXPK(6,6)
      DIMENSION SUM1(1),SUM2(1)
      DIMENSION VECTOR(6),VECTR2(6)
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
      NS6=NSATA*6
      IFRST=0
      INDEX=0
! SKIP TRANSFORMATION CODE IF ALL SATELLITES ARE IN CARTESIAN SPACE
      DO 20 I=1,NSATA
      IF(IELMTP(I).GT.1) GO TO 30
   20 END DO
      GO TO 1010
! NORMAL MATRIX TRANSFORMATION
   30 DO 1000 ISATNO=1,NSATA
      GMX=GM
      IF(ISATNO.EQ.1.AND.LASTSN) GMX=BDSTAT(7,8)
!  DETERMINE PARTIALS NECESSARY FOR TRANSFORMATION OF NORMAL MATRIX TO
!  PROPER SPACE
      IF(IELMTP(ISATNO).GT.1) GO TO 50
!  CARTESIAN SPACE  FORM IDENTITY MATRIX
      DO 40 I=1,6
      DO 35 J=1,6
   35 PXPK(I,J)=0.0D0
   40 PXPK(I,I)=1.0D0
      GO TO 60
   50 IDRAD=(IELMTP(ISATNO)-2)*8+2
      IREF=(ISATNO-1)*6+1
      CALL ELEM(XYZ(IREF),XYZ(IREF+1),XYZ(IREF+2),XYZ(IREF+3),          &
     &   XYZ(IREF+4),XYZ(IREF+5),VECTOR,IDRAD,PXPK,GMX)
      CALL DNVERT(6,PXPK,6,VECTOR)
   60 NROW=6*(ISATNO-1)
      IF(ISATNO.EQ.1) GO TO 200
!
! FOR SATELLITES 2 THRU NSATA POST MULTIPLY BY PXPK
!
      IROWNO=0
      INDEX=NROW
  110 DO 160 ILOOP=1,6
      DO 130 ICOL=1,6
      SUM=0.0D0
      DO 120 IROW=1,6
  120 SUM=SUM+SUM1(INDEX+IROW)*PXPK(IROW,ICOL)
  130 VECTOR(ICOL)=SUM
      DO 140 ISTORE=1,6
  140 SUM1(INDEX+ISTORE)=VECTOR(ISTORE)
      INDEX=INDEX+MAPARM-ILOOP-IROWNO
  160 END DO
! DETERMINE IF FINISHED WITH POST MULTIPLICATION
      IROWNO=IROWNO+6
      IF(IROWNO.LT.NROW) GO TO 110
      IFRST=INDEX
!
! LOAD TRIANGULAR PART OF MATRIX( FOR SATELLITE ISATNO) INTO
! DUMMY 6 BY 6 ARRAY
!
  200 NROWM1=NROW-1
      DO 250 I1=1,6
      K=7-I1
      DO 230 I2=1,K
      IL=I1+I2-1
      A(I1,IL)=SUM1(INDEX+I2)
  230 A(IL,I1)=A(I1,IL)
      INDEX=INDEX+MAPARM-I1-NROWM1
  250 END DO
!
! PRE MULTIPLY SUM1 BY PXPK TRANSPOSE AND POSTMULTIPLY BY PXPK
!
      IST=IFRST
      DO 400 ILOOP2=1,6
! FORM PXPK TRANSPOSE TIMES SUM1
      DO 360 ICOL=1,6
      SUM=0.0D0
      DO 350 IROW=1,6
  350 SUM=SUM+PXPK(IROW,ILOOP2)*A(IROW,ICOL)
  360 VECTOR(ICOL) =SUM
! FORM PXPK TRANSPOSE*SUM1*PXPK
      DO 380 ICOL=1,6
      SUM=0.0D0
      DO 370 IROW=1,6
  370 SUM=SUM+VECTOR(IROW)*PXPK(IROW,ICOL)
  380 VECTR2(ICOL)=SUM
! STORE RESULTS BACK IN SUM1 ARRAY
      ISTOP=7-ILOOP2
      DO 390 I=1,ISTOP
      IND=ILOOP2-1+I
  390 SUM1(IST+I)=VECTR2(IND)
      IST=IST+MAPARM-ILOOP2-NROWM1
  400 END DO
!
! PRE MULTIPLY REMAINING ELEMENTS FOR SATELLITE ISATNO BY PXPK TRANSPOSE
!
! CALCULATE INDEX OF FIRST ELEMENT AFTER TRIANGULAR PORTION OF MATRIX
      INDEX=IFRST+7
      INDEX2=IFRST+MAPARM-(ISATNO-1)*6
      IF(INDEX.GT.INDEX2) GO TO 510
      DO 500 ILOOP3=INDEX,INDEX2
! LOAD ONE 6BY 1COLUMN OFSUM1 INTO SCRATCH ARRAY
      IND=ILOOP3
      DO 410 K=1,6
      VECTOR(K)=SUM1(IND)
      IND=IND+MAPARM-K-NROW
  410 END DO
      IND=ILOOP3
! FORM PXPK TRANSPOSE * SUM1
      DO 430 ICOL=1,6
      SUM=0.D0
      DO 420 IROW=1,6
  420 SUM=SUM+PXPK(IROW,ICOL)*VECTOR(IROW)
      SUM1(IND)=SUM
      IND=IND+MAPARM-ICOL-NROW
  430 END DO
  500 END DO
  510 CONTINUE
!
! FORM PXPK TRANSPOSE TIMES SUM2 ARRAY
!
      IND=(ISATNO-1)*6
      DO 580 ICOL=1,6
      SUM=0.0D0
      DO 570 IROW=1,6
  570 SUM=SUM+PXPK(IROW,ICOL)*SUM2(IND+IROW)
  580 VECTOR(ICOL)=SUM
      DO 590 IL=1,6
  590 SUM2(IND+IL)= VECTOR(IL)
 1000 END DO
 1010 CONTINUE
!
! DETERMINE IF ANY ELEMENTS ARE FIXED (LADJEL=F MEANS NOT ADJUSTED)
!
      DO 1012 J=1,NS6
      IF(.NOT. LADJEL(J) ) GO TO 1015
 1012 END DO
      GO TO 1200
!
! ZERO OUT THE APPOPRIATE ROWS ANDCOLUMNS OF THE SUM1 AND SUM2 ARRAYS
! FOR ANY ELEMENTS NOT BEING ADJUSTED
!
 1015 ISTRT=1
      MAXCOL=NSATA*6
      DO 1100 ICOL=1,MAXCOL
      ILAST=ISTRT+MAPARM-ICOL
      IF(LADJEL(ICOL)) GO TO 1090
! ZERO OUT ROWS FOR SUM1 ARRAY
      DO 1020 IZERO=ISTRT,ILAST
 1020 SUM1(IZERO)=0.D0
! CALCULATE NUMBER OF ELEMENTS IN COLUMN AND ZERO THEM OUT
      IND=ICOL
      NELEMS=MAPARM-(ILAST-ISTRT+1)
      DO 1040 I=1,NELEMS
      SUM1(IND)=0.0D0
 1040 IND=IND+MAPARM-I
!
! ZERO OUT APPROPRIATE ELEMENT IN SUM2 ARRAY
!
      SUM2(ICOL)=0.0D0
 1090 ISTRT=ILAST+1
 1100 END DO
 1200 RETURN
      END
