!$LITDEL
      SUBROUTINE LITDEL(MJDSEC,FSEC,XP1,XP2,NDXP,RANGE,DELJUP,DELSAT,   &
     &                  RJUPP,RSATP,PJUP,PSAT,CHEB1,CHEBV1,NM,TT1,TWOT)
!********1*********2*********3*********4*********5*********6*********7**
!  ROUTINE NAME:    LITDEL   DATE:  05/08/91     PGMR: SBL
!
! FUNCTION          COMPUTES CONTRIBUTION OF SATURN AND JUPITER TO THE
!                   RELATIVISTIC LIGHT TIME DELAY.  CONTRIBUTION OF THE
!                   SUN IS COMPUTED IN LITEUP AND LITEDN.
! I/O PARAMETERS:
!  NAME   I/O   DESCRIPTION OF I/O PARAMETERS IN ARGUMENT LIST ORDER
!  ----   ---   ------------------------------------------------------
!  MJDSEC  I    INTEGER SECONDS SINCE GEODYN REFERENCE TIME
!  FSEC    I    FRACTIONAL REMAINING SECONDS (COORDINATE ET)
!  XP1     I    BARYCENTERED TRUE OF REFERENCE POINT 1 POSITION
!  XP2     I    BARYCENTERED TRUE OF REFERENCE POINT 2 POSITION
!  NDXP    I    FIRST DIMENSION OF XP1 AND XP2
!  RANGE   I    DISTANCE BETWEEN POINT 1 AND POINT 2
!  DELJUP  O    CONTRIBUTION OF JUPITER TO RELATIVISTIC LIGHT TIME DELAY
!               CORRECTION
!  DELSAT  O    CONTRIBUTION OF SATURN TO RELATIVISTIC LIGHT TIME DELAY
!               CORRECTION
!  RJUPP   O    THE DISTANCE FROM JUPITER TO POINT#1 PLUS THE DISTANCE
!               FROM JUPITER TO POINT#2
!  RSATP   O    THE DISTANCE FROM SATURN TO POINT#1 PLUS THE DISTANCE
!               FROM SATURN TO POINT#2
!  PJUP    O    BARYCENTRIC POSITION OF JUPITER
!  PSAT    O    BARYCENTRIC POSITION OF SATURN
!  CHEB1   O    CHEBYSHEV POLYNOMIALS
!  CHEBV1  O    CHEBYSHEV VELOCITY POLYNOMIALS
!  NM      I    NUMBER OF TIMES
!  TT1    WORK  NORMALIZED TIMES
!  TWOT   WORK  SCRATCH SUM
!
!
! REFERENCES: " MATHEMATICAL FORMULATION OF THE DPODP ", T.D.MOYER,
!             JPL TECHNICAL REPORT 32-1527, MAY 15,1971.
!
! COMMENT: SOME OF THE INFORMATION COMPUTED IN THIS ROUTINE IS AVAILABLE
!          FROM OTHER ROUTINES.  IN ORDER TO AVOID ADDITIONAL MEMORY
!          ALLOCATION AND TO PRESERVE SOME SORT OF CODE MODULATION THIS
!          ROUTINE RECALCULATES THIS INFORMATION.
!
!***********************************************************************
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CHCOEF/ COEFM(3,15),COEFJ(3,15),COEFSA(3,15),COEFS(3,15),  &
     &               COEFEM(3,15),COEFMO(3,15),COEF(3,15)
      COMMON/IORCHB/ IORM,IORJ,IORSA,IORSUN,IOREM,IORMO,IORCB,IORTB,    &
     &               IGRP,ICHBOG(2,14),IORSCB(988),NXORCH
      COMMON/EPHM/FSECXY(1),XP(261),YP(261),A1UT(261),EP(261),EC(261),  &
     &            FSCFLX(1),FLUXS(36),AVGFLX(36),FLUXM(288),FSECEP(1),  &
     &            EPHP(816),EPHN(96),ELIB(120),BUFT(2700),FSECNP(4)
      COMMON/XLITSD/DELULT,DDLULT,UNVLTU(3),DELDLT,DDLDLT,UNVLTD(3),    &
     &              GRELLT,TOLLTX,GRLFCT,FCTJUP,FCTSAT
!
      DIMENSION FSEC(NM),RANGE(NM),XP1(NDXP,3),XP2(NDXP,3)
      DIMENSION RJUPP(NM),RSATP(NM),DELJUP(NM),DELSAT(NM)
      DIMENSION CHEB1(NM,15),CHEBV1(NM,15),                             &
     &          PJUP(NM,3),PSAT(NM,3)
      DIMENSION TT1(NM),TWOT(NM),FNSEC(4)
      DATA ZERO/0.0D0/,ONE/1.0D0/,TWO/2.0D0/
      DATA FNSEC/2764800.D0,1382400.D0,691200.D0,345600.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! INITIALIZE
!
      FSECC1=FSECNP(1)
      RNSEC=ONE/FNSEC(ICHBOG(2,5))
!
      DO 100 I=1,NM
      PJUP(I,1)  = ZERO
      PSAT(I,1)  = ZERO
      PJUP(I,2)  = ZERO
      PSAT(I,2)  = ZERO
      PJUP(I,3)  = ZERO
      PSAT(I,3)  = ZERO
      CHEB1(I,1)  = ONE
! MAP MJDSEC,FSEC INTO INTERVAL  -1,1  FOR EACH PERIOD GROUP
! FIGURE POINTER WHITHIN 32 DAY RECORD FOR EACH PERIOD GROUP
      TT1(I)=((MJDSEC-FSECC1)+FSEC(I))*RNSEC
      TT1(I)=TT1(I)*TWO-ONE
  100 END DO
!
! COMPUTE BARYCENTERED POSITIONS OF JUPITER AND SATURN
!
! COMPUTE CHEBYSHEV POLYNOMIALS AT APPROPRIATE TIMES
      CALL CHEBPV(CHEB1,CHEBV1,TT1,15,NM,TWOT,.FALSE.)
! JUPITER POSITION
      DO 500 J=1,IORJ
      DO 500 JL=1,NM
      PJUP(JL,1) = PJUP(JL,1)+COEFJ(1,J)*CHEB1(JL,J)
      PJUP(JL,2) = PJUP(JL,2)+COEFJ(2,J)*CHEB1(JL,J)
      PJUP(JL,3) = PJUP(JL,3)+COEFJ(3,J)*CHEB1(JL,J)
  500 CONTINUE
      IF(ICHBOG(2,5).NE.ICHBOG(2,6)) THEN
        RNSEC=ONE/FNSEC(ICHBOG(2,5))
        DO 550 I=1,NM
        CHEB1(I,1)  = ONE
! MAP MJDSEC,FSEC INTO INTERVAL  -1,1  FOR EACH PERIOD GROUP
! FIGURE POINTER WHITHIN 32 DAY RECORD FOR EACH PERIOD GROUP
        TT1(I)=((MJDSEC-FSECC1)+FSEC(I))*RNSEC
        TT1(I)=TT1(I)*TWO-ONE
  550 END DO
        CALL CHEBPV(CHEB1,CHEBV1,TT1,IORSA,NM,TWOT,.FALSE.)
      ENDIF
!
! SATURN POSITION
      DO 600 J=1,IORSA
      DO 600 JL=1,NM
      PSAT(JL,1) = PSAT(JL,1)+COEFSA(1,J)*CHEB1(JL,J)
      PSAT(JL,2) = PSAT(JL,2)+COEFSA(2,J)*CHEB1(JL,J)
      PSAT(JL,3) = PSAT(JL,3)+COEFSA(3,J)*CHEB1(JL,J)
  600 CONTINUE
!
! COMPUTE LIGHT TIME DELAY FROM JUPITER AND SATURN
!
      DO 1000 I=1,NM
      RJUPP(I)=SQRT((XP1(I,1)-PJUP(I,1))**2 +                           &
     &              (XP1(I,2)-PJUP(I,2))**2 +                           &
     &              (XP1(I,3)-PJUP(I,3))**2 )                           &
     & +       SQRT((XP2(I,1)-PJUP(I,1))**2 +                           &
     &              (XP2(I,2)-PJUP(I,2))**2 +                           &
     &              (XP2(I,3)-PJUP(I,3))**2 )
      RSATP(I)=SQRT((XP1(I,1)-PSAT(I,1))**2 +                           &
     &              (XP1(I,2)-PSAT(I,2))**2 +                           &
     &              (XP1(I,3)-PSAT(I,3))**2 )                           &
     & +       SQRT((XP2(I,1)-PSAT(I,1))**2 +                           &
     &              (XP2(I,2)-PSAT(I,2))**2 +                           &
     &              (XP2(I,3)-PSAT(I,3))**2 )
      DELJUP(I)=FCTJUP*(LOG((RJUPP(I)+RANGE(I))/(RJUPP(I)-RANGE(I))))
      DELSAT(I)=FCTSAT*(LOG((RSATP(I)+RANGE(I))/(RSATP(I)-RANGE(I))))
 1000 END DO
      RETURN
      END
