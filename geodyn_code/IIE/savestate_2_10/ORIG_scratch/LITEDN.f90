!$LITEDN
      SUBROUTINE LITEDN(AA,X1,X1PC,XT,X2,X2D,COSTHG,SINTHG,R,WORK,WORK2,&
     &                  WORK3,WORK4,MJDS1,FSECR,FSEC1,FSEC2,SIGNL,      &
     &                  DELCTR,DELCET,RSS,GRLLT,TBSTAT,NM,SCRTM,II,     &
     &                  UTDT)
!********1*********2*********3*********4*********5*********6*********7**
! LITEDN           83/04/06            8304.0    PGMR - TOM MARTIN
!                  89/02/17            8901.0    PGMR - A. MARSHALL
!
! FUNCTION:  ITERATIVELY COMPUTE THE LIGHT TIME BETWEEN POINTS
!            #1 AND #2 ASSUMING THAT THE EPOCH OF POINT #1 IS
!            KNOWN AND THAT POINT #2 IS AN EARTH LOCATED
!            TRACKING STATION
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   AA      I/O   A    REAL DYNAMIC ARRAY
!   II      I/O   A    INTEGER DYNAMIC ARRAY
!   X1       I    A    INERTIAL TRUE OF DATE POSITION VECTORS FOR
!                      POINT #1
!   X1PC     I    A    INERTIAL TRUE OF DATE POSITION VECTORS FOR
!                      POINT #1 PLANET CENTERED FOR BARYCENTRIC CASE
!   XT       I    A    TRUE POLE COORDINATES OF POINTS #2
!   X2       O    A    INERTIAL TRUE OF DATE POSITION VECTORS FOR
!                      POINT #2
!   X2D      O    A    INERTIAL TRUE OF DATE VELOCITY VECTORS FOR
!                      POINT #2
!   COSTHG   O    A    COSINES OF THE RIGHT ASCENSION OF GREENWICH
!                      FOR THE TIMES MJDS1+FSEC2
!   SINTHG   O    A    SINES  OF THE RIGHT ASCENSION OF GREENWICH
!                      FOR THE TIMES MJDS1+FSEC2
!   R        O    A    SLANT RANGE FROM POINT #1 TO POINT #2
!   WORK          A    USED FOR PREVIOUS VALUES OF FSEC2 AND
!                      FOR ITERATIVE DIFFERENCES OF FSEC2
!   WORK2         A
!   WORK3         A
!   WORK4         A
!   MJDS1    I    S    MODIFIED JULIAN DATE SECONDS OF THE FIRST
!                      TIME POINT
!   FSECR    I    A    ELAPSED SECONDS SINCE MJDS1 FOR AT ORG R TM
!   FSEC1    I    A    ELAPSED SECONDS SINCE MJDS1 FOR POINTS #1
!   FSEC2   I/O   A    FIRST ESTIMATE OF ELAPSED SECONDS SINCE
!                      MJDS1 FOR POINTS #2 AS INPUT
!                      ELAPSED SECONDS SINCE MJDS1 FOR POINTS #2
!                      THIS WILL BE IN EPHEMERIS TIME WITHOUT
!                      COORDINATE TIME CORRECTIONS AS OUTPUT.
!   SIGNL    I    S    SIGN OF THE LIGHT TIME (+1. OR -1.)
!   DELCTR   I    A    COORDINATE TIME CORRECTION TO AVERAGE
!                      (EPHEMERIS-TAI) DIFFERENCE AT RECEIVE TIME
!   DELCET   O    A    COORDINATE TIME CORRECTION TO AVERAGE
!                      (EPHEMERIS-TAI) DIFFERENCE AT TRANSMIT TIME
!   RSS      I    A    SUN SAT RANGE AT SAT TIME
!   GRLLT    O    A    GEN REL LIGHT TIME CORRECTION
!   TBSTAT   I    A    TRACKING BODY STATE AT CENTER OF MASS
!   NM       I    S    NUMBER OF LIGHT TIMES TO BE COMPUTED
!   SCRTM         A
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CBARYC/CBODY(6),TBODY(6),SUNRF(6)
      COMMON/CEGREL/LGRELE,LRLCLK,LGRELR,LXPCLK,LBPCLK,NXEGRL
      COMMON/CHCOEF/ COEFM(3,15),COEFJ(3,15),COEFSA(3,15),COEFS(3,15),  &
     &               COEFEM(3,15),COEFMO(3,15),COEF(3,15)
      COMMON/CLIGHT/VLIGHT,ERRLIM,XCLITE
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/CORA03/KRFMT ,KROTMT,KSRTCH,KCFSC ,KTHG  ,KCOSTG,KSINTG,   &
     &              KDPSI ,KEPST ,KEPSM ,KETA  ,KZTA  ,KTHTA ,KEQN  ,   &
     &              KDPSR ,KSL   ,KH2   ,KL2   ,KXPUT ,KYPUT ,KUT   ,   &
     &              KSPCRD,KSPSIG,KSTAIN,KSXYZ ,KSPWT ,KDXSDP,KDPSDP,   &
     &              KXLOCV,KSTNAM,KA1UT ,KPLTIN,KPLTVL,KPLTDV,KPUTBH,   &
     &              KABCOF,KSPEED,KANGFC,KSCROL,KSTVEL,KSTVDV,KTIMVL,   &
     &              KSTEL2,KSTEH2,KSL2DV,KSH2DV,                        &
     &              KAPRES, KASCAL, KASOFF,KXDOTP,KSAVST,               &
     &              KANGT , KSPDT , KCONAM,KGRDAN,KUANG ,KFAMP ,        &
     &              KOFACT, KOTSGN,KWRKRS,KANFSD,KSPDSD,KPRMFS,KSCRFR,  &
     &              KCOSAS,KSINAS,KDXTID,KALCOF,KSTANF,KNUTIN,KNUTMD,   &
     &              KPDPSI,KPEPST,KDXDNU,KDPSIE,KEPSTE,NXCA03
      COMMON/CORA07/KELEVT,KSRFEL,KTINT,KFSCSD,KXTIM,KFSDM,             &
     & KSGM1,KSGM2,KNOISE,KEPHMS,NXCA07
      COMMON/CORI07/KSMSA1,KSMSA2,KSMSA3,KSMST1,KSMST2,KSMST3,KSMTYP,   &
     &              KSMT1,KSMT2,KMSDM,KIOUT,KISUPE,NXCI07
      COMMON/CRELAT/AER(11),GRLFC2(11),GRLFC3(11),SECCOR
      COMMON/CSBSAT/KR,KRT,KZT,KXY2,KUZ,KUZSQ,KTEMP,KC3,KTHETG
      COMMON/IBODPT/IBDCF(999),IBDSF(999),IBDGM(999),IBDAE(999),    &
     &              IBDPF(999),                                     &
     &              ICBDCF,ICBDSF,ICBDGM,ICBDAE,ICBDPF,             &
     &              ITBDCF,ITBDSF,ITBDGM,ITBDAE,ITBDPF,NXBDPT
      COMMON/IORCHB/ IORM,IORJ,IORSA,IORSUN,IOREM,IORMO,IORCB,IORTB,    &
     &               IGRP,ICHBOG(2,14),IORSCB(988),NXORCH
      COMMON/IEPHM2/ISUPL,ICBODY,ISEQB(2),MAXDEG,ITOTSE,IREPL(988), &
     &              NXEPH2
      COMMON/LEPHM2/LXEPHM
      COMMON/NLIGHT/KNTLIM,NXNLIT
      COMMON/XLITSD/DELULT,DDLULT,UNVLTU(3),DELDLT,DDLDLT,UNVLTD(3),    &
     &              GRELLT,TOLLTX,GRLFCT,FCTJUP,FCTSAT
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! THE S/C ARRAY BEING DIMENSIONED BY (MINTIM,3,6,2) ALLOWS YOU TO
! TEMPORARILY ACCESS ARRAY SPACE SET ASIDE FOR OTHER S/C AND STATION
! POSITION AND VELOCITY ARRAYS DIMENSIONED IN MTSTST.
!
      DIMENSION UTDT(NM)
      DIMENSION DUM(3),DUM1(3,2,1)
      DIMENSION AA(1),II(1),X2(MINTIM,3),X2D(MINTIM,3),XT(NM,3),        &
     &   X1(MINTIM,3,6,2),COSTHG(NM),SINTHG(NM),R(NM),WORK(NM),         &
     &   WORK2(NM),WORK3(NM),WORK4(NM),FSEC1(NM),FSEC2(NM),DELCTR(NM),  &
     &   DELCET(NM),FSECR(NM),RSS(NM),GRLLT(NM),TBSTAT(NM,6),           &
     &   SCRTM(NM,68),X1PC(MINTIM,3)
      DATA ZERO/0.0D0/
! GRLFC3 IS 2*GM/(C**3) ; BHN IS A SCALE FACTOR BETWEEN CLOCKS ON GEOID
! VS CLOCKS AT EARTH'S CENTER
!      DATA GRLFC3/.29587322D-10/ NOW IN COMMON BLOCK CRELAT
      DATA BHN/6.9693D-10/
      CINVRS=SIGNL/VLIGHT
      KOUNT=0
      LCOUNT=.FALSE.
      IF(ICBDGM.NE.ITBDGM) GO TO 10000
!
! CASE WHEN TRACKING BODY THE SAME AS THE CENTER OF INTEGRATION
      LFIRST=.TRUE.
 1000 CONTINUE
! COMPUTE COSINES AND SINES OF RIGHT ASCENSION OF GREENWICH
!LST  IF(KOUNT.GT.0) CALL GRHRAN(MJDS1,FSEC2,.TRUE.,.FALSE.,AA(KEQN),
      IF(KOUNT.GT.0) CALL GRHRAN(MJDS1,FSEC2,.TRUE.,.TRUE. ,AA(KEQN),   &
     &    AA(KSRTCH),AA(KTHETG),COSTHG,SINTHG,NM,AA,II,UTDT,.FALSE.,    &
     &    DUM,DUM1)
! ROTATE TRUE POLE STATION COORDINATES TO TRUE OF DATE
      CALL INERTP(XT,COSTHG,SINTHG,X2,NM,MINTIM,NM)
      CALL TDORTR(MJDS1,FSEC2,X2,X2,AA,NM,MINTIM,LFIRST,.TRUE.,II)
      LFIRST=.FALSE.
! COMPUTE RANGES FROM STATION TO SATELLITE
      CALL RANGE(X2,X1,WORK,R,WORK4,NM,.FALSE.)
      IF(KOUNT.GT.0) GO TO 1500
      DO 1300 N=1,NM
      GRLLT(N)=ZERO
      DELCET(N)=ZERO
 1300 END DO
      IF(.NOT.LGRELE) GO TO 1500
      DO 1400 N=1,NM
      WORK2(N)=X1(N,1,1,1)*X1(N,1,1,1)+X1(N,2,1,1)*X1(N,2,1,1)          &
     &        +X1(N,3,1,1)*X1(N,3,1,1)
      WORK(N)=SQRT(WORK2(N))
      WORK3(N)=X2(N,1)*X2(N,1)+X2(N,2)*X2(N,2)                          &
     &           +X2(N,3)*X2(N,3)
      WORK2(N)=SQRT(WORK3(N))
      WORK3(N)=WORK(N)+WORK2(N)
      WORK(N)=WORK3(N)+R(N)
      WORK2(N)=WORK3(N)-R(N)
      WORK3(N)=WORK(N)/WORK2(N)
      WORK(N)=LOG(WORK3(N))
      GRLLT(N)=GRLFC3(ICBDGM)*WORK(N)
!     GRLLT(N)=GRLLT(N)-BHN*R(N)*CINVRS
 1400 END DO
 1500 CONTINUE
! STORE PREVIOUS ESTIMATED EPOCHS INTO WORKING ARRAY
      DO 2000 N=1,NM
      WORK(N)=FSEC2(N)
 2000 END DO
! COMPUTE IMPROVED EPOCHS
      DO 3000 N=1,NM
      FSEC2(N)=FSEC1(N)+R(N)*CINVRS-GRLLT(N)
 3000 END DO
! DIFFERENCE PREVIOUS AND IMPROVED EPOCHS
      DO 4000 N=1,NM
      WORK(N)=ABS(WORK(N)-FSEC2(N))
 4000 END DO
! COMPUTE MAXIMUM ERROR IN PREVIOUS EPOCHS
      ERRMAX=ZERO
      DO 5000 N=1,NM
      ERRMAX=MAX(ERRMAX,WORK(N))
 5000 END DO
! IF MAXIMUM ERROR IS TOLERABLE, RETURN
      IF(ERRMAX.LE.ERRLIM) RETURN
! RETURN IF ITERATIONS EXCEED MAXIMUM
      KOUNT=KOUNT+1
      LCOUNT=KOUNT.GT.KNTLIM
      IF(LCOUNT) RETURN
      GO TO 1000
!
! CASE WHEN TRACKING BODY NOT THE SAME AS THE CENTER OF INTEGRATION
10000 CONTINUE
      CINVR=SIGNL*CINVRS
      FACT1=SIGNL*DELDLT
      FACT2=SIGNL*DDLDLT
      DO 10100 N=1,NM
      FSEC2(N)=FSEC1(N)+FACT1+FACT2*(FSECR(N)+DELCTR(N))
10100 END DO
      DO 10101 N=1,NM
      WORK(N)=FSEC2(N)
10101 END DO
! ROTATE TRUE POLE STATION COORDINATES TO TRUE OF DATE
!LST  CALL GRHRAN(MJDS1,FSEC2,.TRUE.,.FALSE.,AA(KEQN),AA(KSRTCH),
      CALL GRHRAN(MJDS1,FSEC2,.TRUE.,.TRUE. ,AA(KEQN),AA(KSRTCH),       &
     &   AA(KTHETG),COSTHG,SINTHG,NM,AA,II,UTDT,.FALSE.,DUM,DUM1)
      CALL INERTP(XT,COSTHG,SINTHG,X2,NM,MINTIM,NM)
! ROTATE POSITION AND VELOCITY TO TRUE OF REF
      CALL TDORTR(MJDS1,FSEC2,X2,X2,AA,NM,MINTIM,.TRUE.,.TRUE.,II)
!
! ADJUST THE LIGHT TIME FOR TRACKING BODY STATION POSITIONS
      FACT1=CINVRS*UNVLTD(1)
      FACT2=CINVRS*UNVLTD(2)
      FACT3=CINVRS*UNVLTD(3)
      DO 10200 N=1,NM
      FSEC2(N)=FSEC2(N)-FACT1*X2(N,1)-FACT2*X2(N,2)-FACT3*X2(N,3)
10200 END DO
!
! ADJUST THE LIGHT TIME FOR PLANET CENTERED  SATELLITE POSITION
!
      DO 10500 N=1,NM
      FSEC2(N)=FSEC2(N)+FACT1*X1PC(N,1)+FACT2*X1PC(N,2)                 &
     &                 +FACT3*X1PC(N,3)
10500 END DO
! AT THIS POINT FSEC2 HAS CET AND WORK WILL APPROXIMATE ET BY
! SUBTRACTING THE CET-ET CORRECTION FROM GROUND RECEIVE TIME.
! THE COORRCTION SOULD REMAIN CONSTANT AT THE LEVEL WHICH WOULD
! AFFECT EARTH ROTATION
      DO 10700 N=1,NM
      WORK(N)=FSEC2(N)-DELCTR(N)
10700 END DO
      CALL BUFXTR(MJDS1,FSEC2(1),MJDS1,FSEC2(NM),1,MJDR,FSECZ,LRECAL,AA)
!
      IF(LXEPHM) THEN
      DO IJ=1,ICBODY
!     write(6,*)' dbg LITEDN calls BUFSUP '
      CALL BUFSUP(AA,MJDS1,FSEC2(1),MJDS1,FSEC2(NM),                    &
     &II(KISUPE),AA(KEPHMS),IJ,2)
      ENDDO
      ENDIF
!
!
! BEGIN ITERATIVE LOOP TO GET FSEC2
11000 CONTINUE
      KOUNT=KOUNT+1
!LST  CALL GRHRAN(MJDS1,WORK,.TRUE.,.FALSE.,AA(KEQN),AA(KSRTCH),
      CALL GRHRAN(MJDS1,WORK,.TRUE.,.TRUE. ,AA(KEQN),AA(KSRTCH),        &
     &   AA(KTHETG),COSTHG,SINTHG,NM,AA,II,UTDT,.FALSE.,DUM,DUM1)
! ROTATE TRUE POLE STATION COORDINATES TO TRUE OF DATE
      CALL INERTP(XT,COSTHG,SINTHG,X2,NM,MINTIM,NM)
! CALULATE VELOCITY TOD
      CALL INRTSV(COSTHG,SINTHG,X2,X2D,NM,MINTIM)
! ROTATE POSITION AND VELOCITY TO TRUE OF REF
      CALL TDORTR(MJDS1,FSEC2,X2,X2,AA,NM,MINTIM,.FALSE.,.TRUE.,II)
      CALL TDORTR(MJDS1,FSEC2,X2D,X2D,AA,NM,MINTIM,.FALSE.,.TRUE.,II)
      CALL COORDT(MJDS1,FSEC2,IORJ,IORSA,IOREM,IORMO,                   &
     &               IORSUN,IORM,COEFJ,COEFSA,COEFEM,COEFMO,COEFS,COEFM,&
     &               NM,X2,DELCET,SCRTM(1,56),SCRTM(1,62),SCRTM(1,44),  &
     &               SCRTM(1,1),SCRTM(1,1),SCRTM(1,1),                  &
     &               SCRTM(1,2),SCRTM(1,17),SCRTM(1,2),SCRTM(1,17),     &
     &               SCRTM(1,2),SCRTM(1,17),SCRTM(1,32),SCRTM(1,38),    &
     &               SCRTM(1,44),SCRTM(1,50),SCRTM(1,2),SCRTM(1,8),     &
     &               SCRTM(1,11),WORK)
      DO 11100 N=1,NM
      X2(N,1)=SCRTM(N,56)+X2(N,1)
      X2(N,2)=SCRTM(N,57)+X2(N,2)
      X2(N,3)=SCRTM(N,58)+X2(N,3)
      X2D(N,1)=SCRTM(N,59)+X2D(N,1)
      X2D(N,2)=SCRTM(N,60)+X2D(N,2)
      X2D(N,3)=SCRTM(N,61)+X2D(N,3)
      TBSTAT(N,1)=SCRTM(N,56)
      TBSTAT(N,2)=SCRTM(N,57)
      TBSTAT(N,3)=SCRTM(N,58)
      TBSTAT(N,4)=SCRTM(N,59)
      TBSTAT(N,5)=SCRTM(N,60)
      TBSTAT(N,6)=SCRTM(N,61)
      IF(KOUNT.GT.1) GO TO 11100
! SOME QUANTITIES NECESSARY FOR GEN REL LIGHT TIME
      WORK(N)=SCRTM(N,62)-X2(N,1)
      WORK2(N)=SCRTM(N,63)-X2(N,2)
      WORK3(N)=SCRTM(N,64)-X2(N,3)
      WORK4(N)=WORK(N)*WORK(N)+WORK2(N)*WORK2(N)+WORK3(N)*WORK3(N)
      GRLLT(N)=RSS(N)+SQRT(WORK4(N))
11100 END DO
!   GET RANGE
      DO 11115 N=1,NM
      WORK(N) =X2(N,1)-X1(N,1,1,1)
      WORK2(N)=X2(N,2)-X1(N,2,1,1)
      WORK3(N)=X2(N,3)-X1(N,3,1,1)
      WORK4(N)=WORK(N)*WORK(N)+WORK2(N)*WORK2(N)                        &
     &        +WORK3(N)*WORK3(N)
      R(N)=SQRT(WORK4(N))
!   GET DERIV OF RANGE USED IN LIGHT TIME SOLUTION. DIVIDE BY VLIGHT
      WORK4(N)= (WORK(N)*X2D(N,1)                                       &
     &          +WORK2(N)*X2D(N,2)                                      &
     &          +WORK3(N)*X2D(N,3))*CINVR
      WORK4(N)=WORK4(N)/R(N)
11115 END DO
      IF(KOUNT.GT.1) GO TO 11120
! FINISH CALCULATING THE GEN REL LIGHT TIME
!
! .... CALCULATE JUPITER AND SAPURN CONTRIBUTION TO LIGHT TIME DELAY
! .... AND STORE IT IN SCRTM(1-NM,1) AND SCRTM(1-NM,2)
      CALL LITDEL(MJDS1,FSEC2,X1,X2,MINTIM,R,SCRTM(1,1),SCRTM(1,2),     &
     &            SCRTM(1,3),SCRTM(1,4),SCRTM(1,5),SCRTM(1,8),          &
     &            SCRTM(1,11),SCRTM(1,26),NM,SCRTM(1,41),SCRTM(1,42))
!
      DO 11118 N=1,NM
      WORK(N)=(GRLLT(N)+R(N))/(GRLLT(N)-R(N))
      WORK2(N)=LOG(WORK(N))
      GRLLT(N)=(WORK2(N)*GRLFCT) + SCRTM(N,1) + SCRTM(N,2)
11118 END DO
11120 CONTINUE
!   USE DERIV/VLIGHT TO GET A BETTER ESTIMATE OF RANGE & LIGHT TIME
      ERRMAX=ZERO
      DO 11130 N=1,NM
      WORK(N)=WORK4(N)*WORK4(N)-WORK4(N)
      WORK3(N)=SIGNL*(FSEC2(N)-FSEC1(N))
      WORK2(N)=R(N)*CINVR-WORK3(N)+GRLLT(N)
      WORK2(N)=WORK(N)*WORK2(N)+GRLLT(N)
      WORK(N)=R(N)*CINVR+WORK2(N)
! WORK CONTAINS NEW LIGHT TIME & WORK3 CONTAINS THE PREVIOUS
      WORK2(N)=WORK(N)-WORK3(N)
      WORK4(N)=ABS(WORK2(N))
      ERRMAX=MAX(WORK4(N),ERRMAX)
      FSEC2(N)=SIGNL*WORK(N)+FSEC1(N)
      WORK(N)=FSEC2(N)-DELCET(N)
11130 END DO
      LAST=ERRMAX.LT.TOLLTX
      IF(KOUNT.LT.KNTLIM.AND..NOT.LAST) GO TO 11000
      DO 11150 N=1,NM
      FSEC2(N)=FSEC2(N)-DELCET(N)
11150 END DO
      RETURN
      END
