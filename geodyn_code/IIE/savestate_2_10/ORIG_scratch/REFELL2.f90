!$REFELL2
      SUBROUTINE REFELL2 (XBF, AE, FLAT, XLAT, XLONG, HT)
!
!...PURPOSE:  COMPUTE THE HEIGHT ABOVE REFERENCE ELLIPSOID, GEODETIC
!             LATITUDE AND LONGITUDE OF A POINT IN BODY-FIXED COORDINATE
!             COORDINATES
!
!
!...FORMAL PARAMETERS:
!   INPUT:    XBF      POSITION VECTOR IN BODY-FIXED COORDINATES
!             AE       EQUATORIAL RADIUS
!             FLAT     FLATTENING
!
!   OUTPUT:   XLAT     GEODETIC LATITUDE OF SUB-SATELLITE POIINT
!             XLONG    LONGITUDE OF SUB-SATELLITE POINT
!             HT       HEIGHT ABOVE REFERENCE ELLIPSOID
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION XBF(3)
!
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
!
!
!...COMPUTE HEIGHT, LATITUDE AND LONGITUDE
      E2=FLAT*(2.0D00-FLAT)
      XTILD2=XBF(1)**2+XBF(2)**2
      I=0
      T=E2*XBF(3)
    1 ZT=XBF(3)+T
      ENPH=SQRT(XTILD2+ZT**2)
      SINE=ZT/ENPH
      COSINE=1.0D+0-E2*SINE**2
      IF (COSINE.LE.0.0D+0 .OR. I.GT.20) THEN
           WRITE(6,*)' ERROR IN REFELL2: COSINE LE 0.0 '
           WRITE(6,*)'  OR I GT 20 '
           STOP
      ENDIF
      EN=AE/SQRT(COSINE)
      TNEW=EN*E2*SINE
      IF (ABS(TNEW-T).LT.1.0D-6) GO TO 2
      T=TNEW
      I=I+1
      GO TO 1
    2 ZT=XBF(3)+TNEW
      ENPH=SQRT(XTILD2+ZT**2)
      SINE=ZT/ENPH
      SINE2=SINE**2
      EN=AE/SQRT(1.0D+0-E2*SINE2)
      HT=ENPH-EN
      XLAT=ASIN(SINE)
      XLONG=ATAN2(XBF(2),XBF(1))
      RETURN
      END
