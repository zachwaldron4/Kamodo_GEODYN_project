!$SUMNM
      SUBROUTINE SUMNM(PMPP,RESID,WT,NP,NRMTOT,NM,ATPA,ATPL,SCRTCH,     &
     &                 LAVOID)
!********1*********2*********3*********4*********5*********6*********7**
! SUMNM            84/02/27            0000.0    PGMR - D. ROWLAND
!
! FUNCTION:  ACCUMULATE PARTIALS & RESIDUALS INTO THE
!            NORMAL MATRIX AND RIGHT HAND SIDE OF THE NORMAL EQ.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   PMPP     I         FULL ARRAY OF PARTIALS(INCLUDING ZEROS WHERE
!                      NECCESSARY) FOR EACH MEASUREMENT.SOMETIMES A
!                      INCLUDES ONLY ARC,SOMTIMES ARC&COMMON.
!   RESID    I    A    RESIDUAL ARRAY
!   WT       I    A    MEASUREMENT WEIGHT ARRAY
!   NP       I         NUMBER OF PARAMETERS
!   NRMTOT   I         NORMAL MATRIX MAXIMUM DIMENSION
!   NM       I         NUMBER OF MEASUREMENTS
!   ATPA     O         NORMAL MATRIX
!   ATPL     O         RIGHT HAND SIDE OF NORMAL EQUATIONS
!   SCRATCH       A    SCRATCH ARRAY
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      PARAMETER(NAM=1000)
      COMMON/BIAOR/NAMBB,NCYCB,NAMBBH
      COMMON/CGLBAR/LGDRAG,NXGLAR
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/EMAT  /EMTNUM,EMTPRT,EMTCNT,VMATRT,VMATS,VMATFR,FSCVMA,    &
     &              XEMAT
!
      DIMENSION PMPP(NM,NP),RESID(NM),WT(NM),ATPA(1),ATPL(NP),SCRTCH(NP)
      DIMENSION IS1(NAM)
      DIMENSION LAVOID(1)
!
      DATA ZERO/0.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
!***** BEGIN DEBUG *****
!     PRINT 12347,NM,NP,NRMTOT
!     PRINT 12345,RESID
!     PRINT 12346,WT
!2345 FORMAT(' ** SUMNM **  RESID='/(1X,3G25.16))
!2346 FORMAT(' ** SUMNM **  WT='/(1X,3G25.16))
!2347 FORMAT(' ** SUMNM **  NM,NP,NRMTOT=',3I12)
!***** END DEBUG *****
!
! IF GLOBAL DRAG WAS REQUESTED AND THIS IS THE LAST INNER ITERATION
! OF AN EMAT RUN PUT THE DRAG PARTIALS AT THE END OF THE ARC PARTIALS
      IF(LGDRAG.AND.LSTINR.AND.(EMTNUM.GT.ZERO)) THEN
      IF(NPVAL0(IXDRAG).EQ.0) GOTO 80
! SET INDEX EQUAL TO THE POSITION OF THE FIRST DRAG PARAMETER
      INDEX=IPVAL0(IXDRAG)
      DO 70 INM=1,NM
! RUN THROUGH ALL OF THE DRAG PARAMETERS FOR A PARTICULAR MEASUREMENT
      DO 60 I=1,NPVAL0(IXDRAG)
! SET DRAG PARTIAL TO TEMP SPACE AND SHIFT OVER THE REST OF THE ARC
! PARTIALS.  THIS IS DONE FOR EACH DRAG PARAMETER.
      TEMP=PMPP(INM,INDEX)
      DO 50 J=INDEX,NPVAL0(IXARC)-1
      PMPP(INM,J)=PMPP(INM,J+1)
   50 END DO
! PUT TEMP (ARC DRAG PARTIAL) AT END OF ARC PARTIALS
      PMPP(INM,NPVAL0(IXARC))=TEMP
   60 END DO
   70 END DO
      ENDIF
   80 CONTINUE
!
!
!
      NA=0
      NAX=0
      DO 85 I=1,NP
      IF(LAVOID(I)) GO TO 85
      NA=NA+1
      IF(NA.GT.NAM) THEN
         WRITE(6,6000)
         WRITE(6,6001) NAM
 6000 FORMAT(' EXECUTION TERMINATING IN SUMNM. NUMBER OF PARAMETERS')
 6001 FORMAT(' FOR ONE BLOCK OF OBS GREATER THAN ',I5)
       STOP
      ENDIF
      IF(I.LE.NAMBB) NAX=NAX+1
      IS1(NA)=I
   85 END DO
      IF(NAX.GT.1) THEN
         WRITE(6,6010) NAX
         WRITE(6,6011)
 6010 FORMAT(' EXECUTION TERMINATING IN SUMNMX. NUMBER OF AMBIGUIITY')
 6011 FORMAT(' PARAMETERS FOR ONE BLOCK = ',I5,' (GREATER THAN 1)')
         STOP
      ENDIF
      IF(NAX.EQ.1) THEN
         DO 100 I=1,NM
         SCRTCH(I)=WT(I)*PMPP(I,IS1(1))
  100    CONTINUE
         IP=1+(IS1(1)-1)*(NRMTOT-NAMBB+1)
         DO 110 I=1,NM
         ATPA(IP)=ATPA(IP)+SCRTCH(I)*PMPP(I,IS1(1))
         ATPL(IS1(1))=ATPL(IS1(1))+RESID(I)*SCRTCH(I)
  110    CONTINUE
         IP0=IP
         DO 150 JPARM=2,NA
         IP=IP0+IS1(JPARM)-NAMBB
         DO 120 I=1,NM
         ATPA(IP)=ATPA(IP)+SCRTCH(I)*PMPP(I,IS1(JPARM))
  120    CONTINUE
  150    CONTINUE
      ENDIF
      N1ST=NAMBB*(NRMTOT-NAMBB+1)
      IPS=1+NAX
      DO 1000 IPARM=IPS,NA
      DO 190 I=1,NM
      SCRTCH(I)=WT(I)*PMPP(I,IS1(IPARM))
  190 END DO
      ISX=IS1(IPARM)-NAMBB
      ISTR2=(ISX-1)*(NRMTOT-NAMBB)-(ISX-1)*(ISX-2)/2
      ISTR2=ISTR2-ISX+1+N1ST
      DO 500 JPARM=IPARM,NA
      IP=ISTR2+IS1(JPARM)-NAMBB
      DO 200 I=1,NM
      ATPA(IP)=ATPA(IP)+SCRTCH(I)*PMPP(I,IS1(JPARM))
  200 END DO
  500 END DO
      DO 600 I=1,NM
      ATPL(IS1(IPARM))=ATPL(IS1(IPARM))+RESID(I)*SCRTCH(I)
  600 END DO
 1000 END DO
      RETURN
      END
