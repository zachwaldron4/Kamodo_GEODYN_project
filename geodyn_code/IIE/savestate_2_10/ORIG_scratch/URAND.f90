!$URAND
      DOUBLE PRECISION FUNCTION URAND(IY)
!********1*********2*********3*********4*********5*********6*********7**
! URAND            00/00/00            0000.0    PGMR -
!
! FUNCTION:        GENERATES RANDOM NUMBERS BETWEEN 0 AND +1
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   IY      I/O   S    ANY INTEGER NUMBER  (NEEDED FOR THE CALCULATIONS)
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z), LOGICAL (L)
      SAVE
!
!    URAND IS A UNIFORM RANDOM NUMBER GENERATOR BASED ON THEORY AND
!  SUGGESTIONS GIVEN IN D.E. KNUTH (1969). VOL 2.  THE INTEGER IY
!  SHOULD BE INITIALIZED TO AN ARBITRARY INTEGER PRIOR TO THE FIRST CALL
!  TO URAND.  THE CALLING PROGRAM SHOULD NOT ALTER THE VALUE OF IY
!  BETWEEN SUBSEQUENT CALLS TO URAND.  VALUES OF URAND WILL BE RETURNED
!  IN THE INTERVAL (0.1).
!
!     SAVE M2, IA, MIC, IC, S
!
      INTEGER IA,IC,ITWO,M2,M,MIC,IY
!
      DATA M2/0/,ITWO/2/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      IF (M2 .NE. 0) GO TO 20
!
!  IF FIRST ENTRY, COMPUTE MACHINE INTEGER WORD LENGTH
!
!      M = 1
!  10  M2 = M
!      M = ITWO*M2
!      IF (M .GT. M2) GO TO 10
!
      M = 1
  10  M2 = M
      M = ITWO*M2
!      write(6,*) 'urand: m, m2 ', m, m2
      IF (M .GT. M2) GO TO 10
      HALFM = DBLE(M2)
!
!  COMPUTE MULTIPLIER AND INCREMENT FOR LINEAR CONGRUENTIAL METHOD
!
      IA = 8*NINT(HALFM*ATAN(1.D+0)/8.D+0)+5
      IC = 2*NINT(HALFM*(0.5D+0-SQRT(3.D+0)/6.D+0))+1
      MIC = (M2-IC) + M2
!
!  S IS THE SCALE FACTOR FOR CONVERTING TO FLOATING POINT
!
      S = 0.5D+0 / HALFM
!
!  COMPUTE NEXT RANDOM NUMBER
!
   20 IY = IY*IA
!
!  THE FOLLOWING STATEMENT IS FOR COMPUTERS WHICH DO NOT ALLOW
!  INTEGER OVERFLOW ON ADDITION
!
      IF(IY .GT. MIC) IY = (IY - M2) - M2
!
      IY = IY + IC
!
!  THE FOLLOWING STATEMENT IS FOR COMPUTERS WHERE THE
!  WORD LENGTH FOR ADDITION IS GREATER THAN FOR MULTIPLICATION
!
      IF (IY/2 .GT. M2) IY = (IY - M2) - M2
!
!  THE FOLLOWING STATEMENT IS FOR COMPUTERS WHERE INTEGER
!  OVERFLOW AFFECTS THE SIGN BIT
!
      IF (IY .LT. 0) IY = (IY + M2) + M2
      URAND = DBLE(IY)*S
      RETURN
      END
