!$SUMNPF
      SUBROUTINE SUMNPF(PMPP,RESID,WT,NP,NRMTOT,NM,ATPA,ATPL,SCRTCH,    &
     &           LAVOID)
!********1*********2*********3*********4*********5*********6*********7**
! SUMNP            85/10/08            0000.0    PGMR - TOM MARTI
!                  84/02/27            0000.0    PGMR - D.ROWLAND
!
! FUNCTION:  ACCUMULATE PARTIALS & RESIDUALS INTO THE
!            NORMAL MATRIX AND RIGHT HAND SIDE OF THE NORMAL EQ.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   PMPP     I         FULL ARRAY OF PARTIALS(INCLUDING ZEROS WHERE
!                      NECCESSARY) FOR EACH MEASUREMENT.SOMETIMES A
!                      INCLUDES ONLY ARC,SOMTIMES ARC&COMMON.
!   RESID    I         RESIDUAL ARRAY
!   WT       I         MEASUREMENT WEIGHT ARRAY
!   NP       I         NUMPER OF PARAMETERS
!   NRMTOT   I         MAXIMUM DIMENSION OF NORMAL MATRIX
!   NM       I         NUMBER OF MEASUREMENTS
!   ATPA     O         NORMAL MATRIX
!   ATPL     O         RIGHT HAND SIDE OF NORMAL EQUATIONS
!   SCRTCH       A     SCRATCH ARRAY
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/BIAOR/NAMBB,NCYCB,NAMBBH
      COMMON/CGLBAR/LGDRAG,NXGLAR
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/EMAT  /EMTNUM,EMTPRT,EMTCNT,VMATRT,VMATS,VMATFR,FSCVMA,    &
     &              XEMAT
!
      DIMENSION PMPP(NP,NM),RESID(NM),WT(NM),ATPA(1),ATPL(NP),SCRTCH(NP)
      DIMENSION LAVOID(1)
!
      DATA ZERO/0.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! IF GLOBAL DRAG WAS REQUESTED AND THIS IS THE LAST INNER ITERATION
! OF AN EMAT RUN PUT THE DRAG PARTIALS AT THE END OF THE ARC PARTIALS
      IF(LGDRAG.AND.LSTINR.AND.(EMTNUM.GT.ZERO)) THEN
      IF(NPVAL0(IXDRAG).EQ.0) GOTO 80
! SET INDEX EQUAL TO THE POSITION OF THE FIRST DRAG PARAMETER
      INDEX=IPVAL0(IXDRAG)
      DO 70 INM=1,NM
! RUN THROUGH ALL OF THE DRAG PARAMETERS FOR A PARTICULAR MEASUREMENT
      DO 60 I=1,NPVAL0(IXDRAG)
! SET DRAG PARTIAL TO TEMP SPACE AND SHIFT OVER THE REST OF THE ARC
! PARTIALS.  THIS IS DONE FOR EACH DRAG PARAMETER.
      TEMP=PMPP(INDEX,INM)
      DO 50 J=INDEX,NPVAL0(IXARC)-1
      PMPP(J,INM)=PMPP(J+1,INM)
   50 END DO
! PUT TEMP (ARC DRAG PARTIAL) AT END OF ARC PARTIALS
      PMPP(NPVAL0(IXARC),INM)=TEMP
   60 END DO
   70 END DO
      ENDIF
   80 CONTINUE
!
!
!
      NA=0
      NAX=0
      DO 85 I=1,NAMBB
      IF(LAVOID(I)) GO TO 85
      NA=I
      NAX=NAX+1
   85 END DO
      IF(NAX.GT.1) THEN
         WRITE(6,6010) NAX
         WRITE(6,6011)
 6010 FORMAT(' EXECUTION TERMINATING IN SUMNPF. NUMBER OF AMBIGUIITY')
 6011 FORMAT(' PARAMETERS FOR ONE BLOCK = ',I5,' (GREATER THAN 1)')
         STOP
      ENDIF
      IS=NAMBB+1
!
!
!
      NPX=NP-NAMBB
      NL=NRMTOT-NP
      ISROW1=(NA-1)*(NRMTOT-NAMBB+1)
      IL1=NP-NAMBB+1
      IP1=NAMBB-1
!
      DO 1000 IOBS=1,NM
      IF(WT(IOBS).LE.ZERO) GO TO 1000
!     SCRATCH IS ATP (IN ATPA)
      DO 100 I=IS,NP
      SCRTCH(I)=WT(IOBS)*PMPP(I,IOBS)
  100 END DO
      IL=NPX
      ISROW=NAMBB*(NRMTOT-NAMBB+1)
      ISP=NAMBB
      DO 500 I=IS,NP
      IF(LAVOID(I)) GO TO 201
      DO 200 II=1,IL
      ATPA(ISROW+II)=ATPA(ISROW+II)+SCRTCH(I)*PMPP(ISP+II,IOBS)
  200 END DO
  201 CONTINUE
      ISROW=ISROW+NL+IL
      ISP=ISP+1
      IL=IL-1
  500 END DO
      DO 600 I=IS,NP
      ATPL(I)=ATPL(I)+RESID(IOBS)*SCRTCH(I)
  600 END DO
      IF(NAX.LT.1) GO TO 1000
      SCRTCH(1)=WT(IOBS)*PMPP(NA,IOBS)
      ATPL(NA)=ATPL(NA)+RESID(IOBS)*SCRTCH(1)
      ATPA(ISROW1+1)=ATPA(ISROW1+1)+SCRTCH(1)*PMPP(NA,IOBS)
      DO 800 II=2,IL1
      ATPA(ISROW1+II)=ATPA(ISROW1+II)+SCRTCH(1)*PMPP(IP1+II,IOBS)
  800 END DO
 1000 END DO
      RETURN
      END
