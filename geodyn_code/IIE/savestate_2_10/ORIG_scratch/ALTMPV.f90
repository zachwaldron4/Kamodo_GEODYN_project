!$ALTMPV
      SUBROUTINE ALTMPV(S,S1,NMP,INDP,RPOLE,PXPOLE,LPOLAD,XM,XT,PXSPXP, &
     &   PXSPLV,NM,NINTPL,NINTP2,MJDSEC,FSEC,COSTG,SINTG,LCET,SCRTCH,   &
     &   STAINF,PLTDRV,PLTVEL,ISTA,STAV,STAVT,PMPXE,PMPXI,              &
     &   ICNL2,ICNH2,LVLBI,AA,LETAOF,XHOLD,II,LSET,ETIDES)
!********1*********2*********3*********4*********5*********6*********7**
! TRUEPV           83/03/29            8303.0    PGMR - TOM MARTIN
!
! FUNCTION:  ROTATE THE TRUE POLE COORDINATES OF AN ALTIMETER
!            SPOT TO MEAN POLE COORDINATES FOR A SET OF
!            MEASUREMENTS OF VECTOR LENGTH "NM".
!            TO ACCOMPLISH THIS, LINEARLY INTERPOLATE THE TRUE
!            COORDINATES OVER A TIME SPAN
!            AS LONG AS ONE PASS OF DATA. IF DISCRETE POLAR
!            MOTION VALUES SWITCH OVER DURING THIS INTERVAL OF
!            TIME THEN SUBDIVIDE THE INTERPOLATION INTERVAL AS
!            NECESSARY TO AVOID INTERPOLATING ACROSS DISCRETE
!            BOUNDARIES.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   S        I         INTERPOLATION FRACTIONS
!   S1            A    1.0-S
!   NMP      I         NUMBER OF MEASUREMENTS IN EACH INTERPOLATION
!                      INTERVAL
!   INDP     I         POLAR MOTION INTERVAL->ADJUSTED PARAMETER
!                      POINTER
!   RPOLE    I         POLAR MOTION ROTATION MATRICES AT EACH END
!                      OF EACH INTERPOLATION INTERVAL
!   PXPOLE   I         PARTIALS OF THE TRUE POLE STATION LOCATION
!                      W.R.T. THE POLAR MOTION X & Y PARAMETERS AT
!                      EACH END OF EACH INTERPOLATION INTERVAL
!   LPOLAD   I         SWITCH INDICATING THAT POLAR MOTION IS
!                      ADJUSTED IN THIS RUN
!   XT       I         MEAN POLE STATION COORDINATES
!   XM       O         TRUE POLE STATION COORDINATES FOR EACH OF "NM"
!                      MEASUREMENT TIMES
!   PXSPXP   O         PARTIALS OF TRUE POLE STATION COORDINATES
!                      W.R.T. POLAR MOTION FOR EACH OF "NM"
!                      MEASUREMENT TIMES
!   PXSPLV
!   NM       I         NUMBER OF MEASUREMENT TIMES
!   NINTPL   I         NUMBER OF INTERPOLATION INTERVALS
!   NINTP2   I         NINTPL*2
!   MJDSEC   I         INTEGER SECONDS SINCE GEODYN REFERENCE TIME
!   FSEC     I         FRACTIONAL REMAINING SECONDS
!   COSTG    I         COSINE OF GREENWICH HR ANGLE
!   SINTG    I         SINE OF GREENWICH HR ANGLE
!   LCET     I         COMPUTE SOLID EARTH TIDES IF TRUE
!   SCRTCH        A    WORK ARRAY 14*NM
!   STAINF   I         ARRAY OF STATION INFORMATION
!   PLTDRV   I         ARRAY CONTAING DERIVATIVES OF STATION
!                      VELOCITIES WRT PLATE MOTION PARAMETERS
!   PLTVEL   I         STATION VELOCITIES DUE TO PLATE MOTION
!   PMPXE   I/O   A    STATION PARTIALS
!   ISTA
!   STAV
!   STAVT
!   LSET     I         CORRECT COORDINATES FOR EARTH TIDES IF TRUE
!   ETIDES  I/O        RADIAL VALUE OF EARTH TIDES
!
! COMMENTS: MODIFICATIONS                LUCIA TSAOUSSI
!
! 02.20.91: THE POLAR MOTION VALUES XP & YP INTERPOLATED ARE STORED
!           IN SCRTCH(NM,1) & SCRTCH(NM,2) RESPECTIVELY
!           THESE ARE USED IN SOLIDT FOR COMPUTING THE POLE TIDE
! 06.28.91:COMPUTATION OF A  PART OF THE STATION PARTIALS FOR THE
!          VLBI DELAY : POL.MOT.MATRIX * EHAT = PMPXE
!   IF(LSET) ETIDES ARRAY USED TO CORRECT COORDINATES
!   IF(LCET) ETIDES ARRAY IS COMPUTED (ETIDES CAN BE FILED FROM DATA
!            RECORDS)
!   IF LCET IS TRUE THEN LSET IS TRUE BUT NOT NECCESARILY THE OTHER
!   WAY AROUND
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CBLOKV/QUANO(3),EHAT(3),DEDA(3),DEDD(3),EFLG
      COMMON/CESTML/LNPNM ,NXCSTL
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/PLATE /NPLATE,MJDPLR,NPLTMS,NXPLAT
      COMMON/STATN /NSTA,NSTAE,NMSTA,NCSTA,NSTAV,NSTLV,NSTEL2,          &
     &              NSTEH2,                                             &
     &              NPH2L2,                                             &
     &              NSTNUM, NSTIME, NALLST,KCONAD, NXSTAT
      COMMON/CEARTH/AEG,AEGSQ,FGINV,FG,EGSQ,EGSQP1,C1,C2,FOURC1,TWOC2,  &
     &              XH2,XL2,WREF,RMEAN,REFC20,REFC40,REFC60,BEG,CBLTC,  &
     &              WAE2,GAMMAA,FSTAR,F4
      COMMON/CORA03/KRFMT ,KROTMT,KSRTCH,KCFSC ,KTHG  ,KCOSTG,KSINTG,   &
     &              KDPSI ,KEPST ,KEPSM ,KETA  ,KZTA  ,KTHTA ,KEQN  ,   &
     &              KDPSR ,KSL   ,KH2   ,KL2   ,KXPUT ,KYPUT ,KUT   ,   &
     &              KSPCRD,KSPSIG,KSTAIN,KSXYZ ,KSPWT ,KDXSDP,KDPSDP,   &
     &              KXLOCV,KSTNAM,KA1UT ,KPLTIN,KPLTVL,KPLTDV,KPUTBH,   &
     &              KABCOF,KSPEED,KANGFC,KSCROL,KSTVEL,KSTVDV,KTIMVL,   &
     &              KSTEL2,KSTEH2,KSL2DV,KSH2DV,                        &
     &              KAPRES, KASCAL, KASOFF,KXDOTP,KSAVST,               &
     &              KANGT , KSPDT , KCONAM,KGRDAN,KUANG ,KFAMP ,        &
     &              KOFACT, KOTSGN,KWRKRS,KANFSD,KSPDSD,KPRMFS,KSCRFR,  &
     &              KCOSAS,KSINAS,KDXTID,KALCOF,KSTANF,KNUTIN,KNUTMD,   &
     &              KPDPSI,KPEPST,KDXDNU,KDPSIE,KEPSTE,NXCA03
      COMMON/CORA06/KPRMV ,KPNAME,KPRMV0,KPRMVC,KPRMVP,KPRMSG,KPARVR,   &
     &              KPRML0,KPDLTA,KSATCV,KSTACV,KPOLCV,KTIDCV,KSUM1 ,   &
     &              KSUM2 ,KGPNRA,KGPNRM,KPRSG0,KCONDN,KVELCV,          &
     &              KSL2CV,KSH2CV,KTIEOU,KPRMDF,NXCA06
      COMMON/CPMPA /KTMPAR(3)    ,KMBPAR(2)    ,KRFPAR(2)    ,          &
     &              KAEPAR,KFEPAR,KFPPAR,KVLPAR,KETPAR(2,2)  ,          &
     &              KPMPAR,KUTPAR,KSTPAR(3,4)  ,NADJST       ,          &
     &              NDIM1 ,NDIM2 ,NXDIM1,NXDIM2
      DIMENSION S(NM),S1(NM),RPOLE(3,3,NINTP2),NMP(NINTPL),             &
     &   PXPOLE(3,2,NINTP2),INDP(NINTPL),XM(MINTIM,3),XT(MINTIM,3),     &
     &   PXSPXP(NM,3,2),PXSPLV(NM,3,2),FSEC(NM),COSTG(NM),SINTG(NM),    &
     &   STAINF(NSTAIN,*),PLTDRV(3,3,*),PLTVEL(3,*),SCRTCH(NM,21),      &
     &   XMPLTC(3),STAV(3,1),STAVT(*),PMPXE(NM,3),PMPXI(NM,3),          &
     &   ICNL2(NSTA),ICNH2(NSTA),AA(*),II(*),ETIDES(NM)
!
! PATCH
!
      DIMENSION XHOLD(MINTIM,6)
!
! PATCH
!
!
      DATA ZERO/0.0D0/,ONE/1.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! COMPUTE COMPLEMENT OF INTERPOLATION FRACTIONS
      DO 100 N=1,NM
      S1(N)=ONE-S(N)
  100 END DO
! FIRST INTERPOLATION INTERVAL ALWAYS BEGINS WITH 1
      NM1=1
      INDEX=1
! LOOP THRU ALL INTERVALS
      DO 2000 INTVL=1,NINTPL
! LAST MEASUREMENT IN THE INTERVAL
      NM2=NMP(INTVL)+NM1-1
      INDEX1=INDEX+1
!   GET TRUE POLE COORDINATES AT ENDPOINT TIMES
      DO 1100 N=NM1,NM2
      SCRTCH(N,1)=S(N)*RPOLE(3,1,INDEX)+S1(N)*RPOLE(3,1,INDEX1)
      SCRTCH(N,2)=S(N)*RPOLE(2,3,INDEX)+S1(N)*RPOLE(2,3,INDEX1)
      XM(N,1)=XT(N,1)+SCRTCH(N,1)*XT(N,3)
      XM(N,2)=XT(N,2)-SCRTCH(N,2)*XT(N,3)
      XM(N,3)=XT(N,3)-SCRTCH(N,1)*XT(N,1)+SCRTCH(N,2)*XT(N,2)
!
! PATCH
!
      XHOLD(N,1)=XT(N,1)
      XHOLD(N,2)=XT(N,2)
      XHOLD(N,3)=XT(N,3)
!
! PATCH
!
 1100 END DO
!
! BYPASS PARTIAL DERIVATIVE COMPUTATIONS UNLESS ADJUSTMENT REQUESTED
!
! NOTE THAT FOR THE TIME BEING, ALTIMETRY WILL NOT BE USED TO SOLVE FOR
! POLAR MOTION
!
      IF(.NOT.LPOLAD) GO TO 1950
      IF(INDP(INTVL).LE.0) GO TO 1950
      DO 1900 I=1,3
! LOOP THRU X & Y OF THE POLE
      DO 1800 J=1,2
      DO 1600 N=NM1,NM2
      PXSPXP(N,I,J)=0.D0
 1600 END DO
 1800 END DO
 1900 END DO
 1950 CONTINUE
! INCREMENT THE MEASUREMENT POINTER AND THE MATRIX INDEX
      NM1=NM2+1
      INDEX=INDEX1+1
 2000 END DO
!
!
!
      IF(.NOT.LSET) RETURN
      IF(.NOT.LCET) THEN
!
!  SUM VALUEOF ETIDES THAT IS ALREADY CONTAINED IN THE ARRAY
      DO I=1,NM
      SCRTCH(I,14)=1.D0-ETIDES(I)/SQRT(XM(I,1)*XM(I,1)     &
     &                                         +XM(I,2)*XM(I,2)   &
     &                                         +XM(I,3)*XM(I,3))
        XM(I,1)=XM(I,1)*SCRTCH(I,14)
        XM(I,2)=XM(I,2)*SCRTCH(I,14)
        XM(I,3)=XM(I,3)*SCRTCH(I,14)
        ENDDO
        RETURN
      ENDIF
!
!  COMPUTE ETIDES AND SUM IT INTO BOUNCE POINT

      XL2 = AA(KL2)
      XH2 = AA(KH2)
      DO 2100 I=1,NM
      SCRTCH(I,14)=XM(I,1)*XM(I,1)+XM(I,2)*XM(I,2)
      SCRTCH(I,15)=SQRT(SCRTCH(I,14)+XM(I,3)*XM(I,3))
      SCRTCH(I,14)=SQRT(SCRTCH(I,14))
      SCRTCH(I,20)=XM(I,1)/SCRTCH(1,14)
      SCRTCH(I,21)=XM(I,2)/SCRTCH(1,14)
      SCRTCH(I,18)=SCRTCH(I,14)/SCRTCH(1,15)
      SCRTCH(I,19)=XM(I,3)/SCRTCH(1,15)
 2100 END DO
      CALL SOLIDT(XM,MJDSEC,FSEC,COSTG,SINTG,NM,SCRTCH,                 &
     &            XT,PXSPLV,SCRTCH(1,18),SCRTCH(1,19),SCRTCH(1,20),     &
     &            SCRTCH(1,21),MINTIM,.TRUE.,LETAOF,.TRUE.,XHOLD(1,4),  &
     &            AA(KDXTID),AA(KANFSD),AA(KSPDSD),AA(KPRMFS),          &
     &            AA(KSCRFR),AA(KCOSAS),AA(KSINAS),AA,II)
!
! PATCH
!
      DO 3000 N=1,NM
      ETIDES(N)=-XHOLD(N,4)
      XM(N,1)=XT(N,1)
      XM(N,2)=XT(N,2)
      XM(N,3)=XT(N,3)
      XT(N,1)=XHOLD(N,1)
      XT(N,2)=XHOLD(N,2)
      XT(N,3)=XHOLD(N,3)
 3000 END DO
!
! PATCH
!
      RETURN
      END
