!$TROPRF
      FUNCTION TROPRF(MODELT,MODE,P,TKELVN,E0,SINEL,COSEL,EDOT,RNM,WETT)
!********1*********2*********3*********4*********5*********6*********7**
! TROPRF           00/00/00            0000.0    PGMR - ?
!
!
! FUNCTION:
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MODELT
!                      0=ALTIMETER RANGE
!                      1=RANGE
!                      2=RANGE RATE
!                      3=ELEVATION
!   MODE
!                      1=RADIO FREQUENCY (RADAR)
!                      2=OPTICAL (LASER)
!   P                  PRESSURE IN MB
!   TKELVN             INVERSE OF TEMP IN DEG KELVIN
!   E0                 WATER VAPOR PRESSURE IN MB
!   SINEL              SINE OF THE ELEVATION
!   COSEL              COSINE OF THE ELEVATION
!   EDOT               TIME RATE OF CHANGE OF ELEVATION
!   RNM                THE RANGE FROM THE STATION TO THE SATELLITE
!   WETT               WET TERM OF TROPOSPHERIC CORRECTION
!   TROPRF             DRY TERM OF TROPOSPHERIC CORRECTION
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
!   MO@ELT
!    0=ALTIMETER RANGE
!    1=RANGE
!    2=RANGE RATE
!    3=ELEVATION
!   MODE
!    1=RADIO FREQUENCY (RADAR)
!    2=OPTICAL (LASER)
!   P IS PRESSURE IN MB
!   TKELVN IS INVERSE OF TEMP IN DEG KELVIN
!   E0 IS WATER VAPOR PRESSURE IN MB
!   SINEL=SINE OF THE ELEVATION
!   COSEL=COSINE OF THE ELEVATION
!   EDOT=TIME RATE OF CHANGE OF ELEVATION
!   RNM=THE RANGE FROM THE STATION TO THE SATELLITE
!   WETT=WET TERM OF TROPOSPHERIC CORRECTION
!   TROPRF=DRY TERM OF TROPOSPHERIC CORRECTION
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      LOGICAL LASER,TEST
      DIMENSION ABC(9),GAMA(9),RN(9),DISQPI(7),DELR(2)
! B0=1.D0/(2.D0*AE)
      DATA B0/7.839257591D-05/
      DATA B02/1.567851518D-4/
      DATA AE/6378.155D0/
      DATA THIRD/.333333333333333D0/
      DATA GAMA/2*0.D0,1.333333333333333D0,6*0.D0/
      DATA DISQPI/2.0D0,6.0D0,12.0D0,20.0D0,30.0D0,42.0D0,56.0D0/
      DATA ONE/1.0D0/,ZERO/0.0D0/
      DATA DREFDT/77.624D-6/
      DATA SCALE/1.1385D-5/
      DATA WET1/371900.0D-6/,WET2/-12.92D-6/,WET3/1255.0D0/,            &
     &     WET4/0.05D0/
      DATA P2/0.2D0/,TWOP4/2.4D0/,ONEP2/1.2D0/,SIX/6.0D0/,              &
     &     THOUS/1.0D3/
      DATA SEVNTH/1.4285714285742857D-1/,HALF/0.5D0/
      DATA THREE/3.0D0/,FOUR/4.0D0/,P8/0.8D0/,TWELVE/12.0D0/
      DATA ONEM30/1.0D-30/
      DATA SEV8TH/0.875D0/,C8D9/8.888888888888889D-1/
      DATA FACTL2/1.052446421D0/,FACTL3/1.016216634D0/,CM2KM/1.0D-3/
      DATA VERTC/2.277D-3/
      DATA ROOTMN/1.0D-35/
      DATA SELCUT/-8.715574D-02/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      IF(MODELT.EQ.0) GO TO 333
      TROPRF =ZERO
      WETT   =ZERO
! IF ELEVATION IS BELOW -5 DEGREES, SKIP MODEL AND RETURN ZERO.
      IF(SINEL.LT.SELCUT) RETURN
      DELR(1)=ZERO
      DELR(2)=ZERO
      LASER=MODE.EQ.2
      LWET =E0.GT.ZERO.AND..NOT.LASER
      FACTL=ONE
!  COMPUTE DRY PARAMETERS
      REF=DREFDT*TKELVN
! COMPUTE HEIGHT OF TROPOSPHERE USING SAASTAMOINEN 90 DEGREE CORRECTION
      H=SCALE/REF
      REF=REF*P
      COS2=COSEL**2
      NDX=1
      GO TO 8888
 7777 CONTINUE
      NDX=2
      LWET =.FALSE.
!   COMPUTE WET PARAMETERS
      REF=TKELVN*(WET1*TKELVN+WET2)
      H=SCALE*(WET3*TKELVN+WET4)/REF
      REF=REF*E0
 8888 CONTINUE
! COMPUTE SLANT RANGE TO TOP OF TROPOSPHERE
      RTOP=SQRT((AE+H)**2-(AE*COSEL)**2)-AE*SINEL
      A=-SINEL/H
      B=-B0*COS2/H
      A2=A**2
      B2=B**2
      AB=A*B
      TWOB=B+B
      FOURB=TWOB+TWOB
      RN(1)=RTOP
      DO 1 I=1,8
      RN(I+1)=RN(I)*RTOP
    1 END DO
      IF(MODELT-2) 9300,9400,9500
 9300 CONTINUE
      IF(LASER )     FACTL=FACTL2
      ABC (2)=A+A
      ABC(3) =A2+A2+FOURB*THIRD
      ABC (4)=A*(FOURB-B+A2)
      ABC(5)=P2*A2**2+TWOP4*A2*B+ONEP2*B2
      ABC (6)=TWOB*ABC (4)*THIRD
      ABC (7)=B2*(SIX*A2+FOURB)*SEVNTH
      ABC(8)=ZERO
      ABC(9)=ZERO
      IF(B2.LT.ROOTMN)GO TO 8000
      ABC(8)=AB*B2*HALF
      ABC(9)=(B2*THIRD)**2
 8000 CONTINUE
      DR=RTOP
      DO 2 I=2,9
      DR=DR+ABC(I)*RN(I)
    2 END DO
      DELR(NDX)=DELR(NDX)+DR*REF*THOUS *FACTL
      GO TO 110
 9400 CONTINUE
      PAPTH=-COSEL
      PBPTH=B02*COSEL*SINEL
      ABC(3)=FOUR*A
      ABC(4)=THREE*(B+A2)
      ABC(5)=A*P8*(A2+FOURB+TWOB)
      ABC(6)=TWOB*ABC(4)*THIRD
      ABC(7)=TWELVE*AB*B*SEVNTH
      ABC(8)=ZERO
      ABC(9)=ZERO
      IF(B2.GT.ROOTMN)ABC(8)=B2*B*HALF
      TEST=ABS(PBPTH).LT.ONEM30
      IF(TEST) GO TO 30
      GAMA(4)=ABC(3)-A
      GAMA(5)=2.4D0*(A2+B)
      GAMA (6)=(A+A)*(A2*THIRD+TWOB)
      GAMA (7)=FOUR*B*ABC(5)*SEVNTH
      GAMA(8)=SEV8TH*ABC(7)
      GAMA (9)=ABC(8)*C8D9
   30 CONTINUE
      SBDRN=RN(2)+RN(2)
      SGDRN=ZERO
      DO 33 I=3,9
      SBDRN=SBDRN+ABC(I)*RN(I)
   33 END DO
      IF(TEST) GO TO 303
      DO 3 I=3,9
      SGDRN=SGDRN+GAMA(I)*RN(I)
    3 END DO
  303 CONTINUE
      DELR(NDX)=DELR(NDX)+THOUS*EDOT* REF/H*(SBDRN*PAPTH+SGDRN*PBPTH)
      GO TO 110
 9500 CONTINUE
      IF(LASER )     FACTL=FACTL3
      ABC(2)=THREE*A
      ABC(3)=THREE*(A2+B)
      ABC(4)=A*(TWOB+FOURB+A2)
      ABC(5)=B*ABC(3)
      ABC(6)=AB*(TWOB+B)
      ABC(7)=ZERO
      IF(B2.GT.ROOTMN)ABC(7)=B2*B
      FORCOS=FOUR*COSEL
      RANGE=RNM*CM2KM
      BEND=RN(1)
      DELTA=RN(2)*HALF
      DO 4 I=2,7
      BEND=BEND+ABC(I)*RN(I)/DBLE(I)
      DELTA=DELTA+ABC(I)*RN(I+1)/DISQPI(I)
    4 END DO
      REFDH=REF/H*FACTL
      DELR(NDX)=DELR(NDX)+(DELTA+BEND*(RANGE-RTOP))/RANGE*FORCOS*REFDH
  110 CONTINUE
      IF(LWET) GO TO 7777
      TROPRF=DELR(1)
      WETT  =DELR(2)
      RETURN
  333 CONTINUE
      WETT  =VERTC*(WET3*TKELVN+WET4)*E0
      TROPRF=VERTC* P
      RETURN
      END
