!$BWALBD
      SUBROUTINE BWALBD(TDNRM1,TDNRM2,TDNRM3,                           &
     &                  ECFSAT,SVECT,SFLUX,DIST,APAREA,RAMS,            &
     &                  SCAREA,BWAREA,BWSPEC,                           &
     &                  BWDIFF,DXDD,PXDDT,JARADJ,JSPADJ,JDFADJ,IMAPAR,  &
     &                  IMAPSP,IMAPDF,NFACE,NEQN,SINTHG,COSTHG,NPOINT,  &
     &                  ISATID,SFLUXSW,SFLUXLW,ISPOTS,IPNALB,NPNALB)
!*******************************************************************
!  ROUTINE NAME:   BWALBD   DATE: 09/04/90      PGMR: A. MARSHALL
!
!   FUNCTION - TO COMPUTE THE TOTAL ALBEDO ACCELERATION IN TRUE OF
!              DATE SYSTEM FOR EACH FLATE PLATE IN THE MODEL AND
!              THE ASSOCIATED PARTIALS WITH RESPECT TO PLATE AREA,
!              SPECULAR REFLECTIVITY, AND DIFFUSE REFLECTIVITY.
!
!  I/O PARAMETERS:
!
!   NAME    A/S    I/O   DESCRIPTION OF I/O PARAMETERS IN ARGUMENT LIST
!   -----  ------ -----  -----------------------------------------------
!   TDNRM1   A      I    SPACECRAFT FLAT PLATE NORMAL VECTORS IN TRUE OF
!                        DATE SYSTEM (X COMP.)
!   TDNRM2   A      I    SPACECRAFT FLAT PLATE NORMAL VECTORS IN TRUE OF
!                        DATE SYSTEM (Y COMP.)
!   TDNRM3   A      I    SPACECRAFT FLAT PLATE NORMAL VECTORS IN TRUE OF
!                        DATE SYSTEM (Z COMP.)
!   ECFSAT   A      I    SATELLITE COOR. IN EARTH CENTERED FIXED (ECF)
!   SVECT    A      I    ECF SPOT VECTORS
!   SFLUX    A      I    SUMMATION OF LONG AND SHORT WAVE RADIANCE FOR
!                        EACH AREA SPOT
!   DIST     A      I    DISTANCE FROM SPOT TO SATELLITE
!   APAREA   S      I    ATTENUATED PROJECTED SPOT AREA
!   RAMS     S      I    CR TERM=SOLAR CONSTANT*AREA/MASS*(1+SAT REFLEC)
!   SCAREA   S      I    NOMINAL SATELLITE CROSS-SECTIONAL AREA AS
!                        ENTERED ON THE SATPAR CARD
!   BWAREA   A      I    FLAT PLATE AREAS
!   BWSPEC   A      I    FLATE PLATE SPECULAR REFLECTIVITIES
!   BWDIFF   A      I    FLATE PLATE DIFFUSE  REFLECTIVITIES
!   DXDD     A      O    ECF ACCEL. DUE TO ALBEDO
!   PXDDT    A      O    ACCEL. PARTIAL WRT PLATE PARAMETERS
!   JARADJ   A      O    POINTER TO PLATE AREA PARTIAL IN PXDDT
!   JSPADJ   A      O    POINTER TO PLATE SPEC REF. PARTIAL IN PXDDT
!   JDFADJ   A      O    POINTER TO PLATE SPEC REF. PARTIAL IN PXDDT
!   IMAPAR   A      I    MAP FROM UNADJUSTED TO ADJUSTED POINTERS FOR
!                        PLATE AREA
!   IMAPSP   A      I    MAP FROM UNADJUSTED TO ADJUSTED POINTERS FOR
!                        PLATE SPECUALR REFLECTIVITY
!   IMAPDF   A      I    MAP FROM UNADJUSTED TO ADJUSTED POINTERS FOR
!                        PLATE DIFFUSE REFLECTIVITY
!   NFACE    S      I    NUMBER OF FLAT PLATES MODELING SATELLITE SHAPE
!   COSTHG   I      S    COSINE OF THE GREENWICH HOUR ANGLE
!   SINTHG   I      S    SINE OF THE GREENWICH HOUR ANGLE
!
!************ BWALBD LOCAL VARIABLE DEFFINITIONS************************
!   VECT     A      W    SATELLITE-SPOT VECTOR ECF SYSTEM
!   CTHETA   S      W    COSINE OF ANGLE BETWEEN SATELLITE-SPOT VECTOR
!                        AND PLATE NORMAL VECTOR
!   PLAREA   S      W    PROJECTED PLATE AREA
!*******************************************************************
!
!
! NOTES:
!        1)
!
!
!
!
! REFERENCES:
!
!        "NON-GRAVITATIONAL PERTURBATIONS AND SATELLITE GEODESY,"
!         MILANI ET AL., 1987, PP. 49.
!*******************************************************************
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/BWLOGC/LVAREA,LTOPEX,LSPOT2,LGPSVA,LERS1,LMO,LMOC,LTDRSA,  &
     &              LMAGNA,LGFO,LTRMM,LEUVE,LVCL,LENVS,LCRYO,LGRAIL,    &
     &              LHY2A,LSARAL
      COMMON/CRMB/RMB(9), rmb0(9)
      COMMON/CRMBI/RMBI(9)
!
      DIMENSION TDNRM1(NFACE),TDNRM2(NFACE),TDNRM3(NFACE)
      DIMENSION ECFSAT(3),BWAREA(NFACE),BWSPEC(NFACE),                  &
     &          BWDIFF(NFACE),DXDD(3),ACCEL(3),ECFNRM(3)
      DIMENSION PXDDT(NEQN,3)
      DIMENSION IMAPAR(NFACE),IMAPSP(NFACE),IMAPDF(NFACE)
      DIMENSION SVECT(1),SFLUX(1),DIST(1),VECT(3)
      DIMENSION SFLUXSW(ISPOTS),SFLUXLW(ISPOTS)
      DIMENSION IPNALB(NPNALB)
!
      DATA ZERO/0.0D0/,ONE/1.0D0/,TWO/2.0D0/,THREE/3.0D0/
!
!********1*********2*********3*********4*********5*********6*********7**
! START OF EXECUTABLE CODE
!********1*********2*********3*********4*********5*********6*********7**
!
! INITIALIZE VARIABLES
      JCOUNT = JARADJ-1
      MCOUNT = JSPADJ-1
      NCOUNT = JDFADJ-1
!
! REMOVE SPACECRAFT AREA AS DEFINED ON THE SATPAR CARD FROM RAMS
      RAMS = RAMS/SCAREA
!
! LOOP THROUGH EACH PLATE, COMPUTING ACCELERATION AND PARTIALS
      DO 400 I=1,NFACE
! ROTATE PLATE NORMAL VECTORS FROM TOD TO ECF
         ECFNRM(1)=COSTHG*TDNRM1(I) + SINTHG*TDNRM2(I)
         ECFNRM(2)=-SINTHG*TDNRM1(I) + COSTHG*TDNRM2(I)
         ECFNRM(3)=TDNRM3(I)
         ECFMAG = SQRT(ECFNRM(1)**2+ECFNRM(2)**2+ECFNRM(3)**2)
         ECFNRM(1)=ECFNRM(1)/ECFMAG
         ECFNRM(2)=ECFNRM(2)/ECFMAG
         ECFNRM(3)=ECFNRM(3)/ECFMAG
! INITIALIZE ACCELS
         ACCEL(1) =ZERO
         ACCEL(2) =ZERO
         ACCEL(3) =ZERO
! INITIALIZE PARTIAL POINTERS
         IF(IMAPAR(I).GT.0) JCOUNT=JCOUNT+1
         IF(IMAPSP(I).GT.0) MCOUNT=MCOUNT+1
         IF(IMAPDF(I).GT.0) NCOUNT=NCOUNT+1
         DO 300 J=1,NPOINT
! FORM UNIT SAT-SPOT VECTOR
!          IINDEX=(J-1)/6+1
!          IF(IINDEX.GT.2) IINDEX=IINDEX-1
      JJ=0
           DO 310 IJ=1,3
           VECT(IJ) = (SVECT(J+JJ)-ECFSAT(IJ))/DIST(J)
           JJ=JJ+NPOINT
  310      CONTINUE
!
! COMPUTE DOT PRODUCT OF EACH PLATE NORMAL VECTOR WITH SAT-SPOT VECTOR
           CTHETA = ECFNRM(1)*VECT(1)+ECFNRM(2)*VECT(2)                 &
     &             +ECFNRM(3)*VECT(3)
!
! DETERMINE IF THIS PLATE IS ILLUMINATED BY EACH SPOT
            IF(CTHETA .LE. ZERO) GOTO 300
!
! COMPUTE ILLUMINATED PLATE CROSS-SECTIONAL AREA
            PLAREA = BWAREA(I)*CTHETA
!
! COMPUTE ALBEDO/IR ACCELERATION ON THIS PLATE IN
! ECF SYSTEM
!
! ... If TDRSS then compute 1-transmissivity of single access antennae
! .... PANEL cards must have the single access antennae as 1st four
! .... for TDRSS macro-model
!
            OMTRNS=ONE
            IF(LTDRSA.AND.(I.LE.4).AND.ISATID.NE.9501700)               &
     &       CALL TDRSTN(I,CTHETA,OMTRNS)
!
!           TERM1 = -PLAREA*RAMS*OMTRNS*SFLUX(J)*APAREA

       IF (IPNALB(J).EQ.1) THEN ! just short-wave radiation applies
            TERM1 = -PLAREA*RAMS*OMTRNS*SFLUXSW(J)*APAREA
       ELSEIF (IPNALB(J).EQ.2) THEN ! just long-wave radiation applies
            TERM1 = -PLAREA*RAMS*OMTRNS*SFLUXLW(J)*APAREA
       ELSE ! default: both
            TERM1 = -PLAREA*RAMS*OMTRNS*SFLUX(J)*APAREA
       ENDIF

            TERM2 = ONE - BWSPEC(I)
            TERM3 = TWO*(BWDIFF(I)/THREE + BWSPEC(I)*CTHETA)
            PART1 = TERM1*(TERM2*VECT(1) + TERM3*ECFNRM(1))
            PART2 = TERM1*(TERM2*VECT(2) + TERM3*ECFNRM(2))
            PART3 = TERM1*(TERM2*VECT(3) + TERM3*ECFNRM(3))
            ACCEL(1) = ACCEL(1) + PART1
            ACCEL(2) = ACCEL(2) + PART2
            ACCEL(3) = ACCEL(3) + PART3
!
! COMPUTE PARTIAL OF ACCELERATION WRT AREA, ADDING THEM TO THE SAME PART
! PREVIOUSLY COMPUTED FOR THE OTHER NON-CONSERVATIVE FORCES
            IF(IMAPAR(I).GT.0) THEN
               VAL1 = PART1/BWAREA(I)
               VAL2 = PART2/BWAREA(I)
               VAL3 = PART3/BWAREA(I)
! ROTATE TO TOD
               TOD1 = VAL1*COSTHG-VAL2*SINTHG
               TOD2 = VAL1*SINTHG+VAL2*COSTHG
!
               PXDDT(JCOUNT,1) = PXDDT(JCOUNT,1)+TOD1
               PXDDT(JCOUNT,2) = PXDDT(JCOUNT,2)+TOD2
               PXDDT(JCOUNT,3) = PXDDT(JCOUNT,3)+VAL3
            ENDIF
!
! COMPUTE PARTIAL OF ACCELERATION WRT SPECULAR REFLECTIVITY,
! ADDING THEM TO THE SAME PARTIALS PREVIOUSLY COMPUTED FOR THE OTHER
! NON-CONSERVATIVE FORCES
           IF(IMAPSP(I).GT.0) THEN
              VAL1 = TERM1*(TWO*CTHETA*ECFNRM(1)-VECT(1))
              VAL2 = TERM1*(TWO*CTHETA*ECFNRM(2)-VECT(2))
              VAL3 = TERM1*(TWO*CTHETA*ECFNRM(3)-VECT(3))
! ROTATE TO TOD
              TOD1 = VAL1*COSTHG-VAL2*SINTHG
              TOD2 = VAL1*SINTHG+VAL2*COSTHG
!
               PXDDT(MCOUNT,1) = PXDDT(MCOUNT,1)+TOD1
               PXDDT(MCOUNT,2) = PXDDT(MCOUNT,2)+TOD2
               PXDDT(MCOUNT,3) = PXDDT(MCOUNT,3)+VAL3
           ENDIF
!
! COMPUTE PARTIAL OF ACCELERATION WRT DIFFUSE REFLECTIVITY,
! ADDING THEM TO THE SAME PARTIALS PREVIOUSLY COMPUTED FOR THE OTHER
! NON-CONSERVATIVE FORCES
            IF(IMAPDF(I).GT.0) THEN
               VAL1=TERM1*TWO/THREE*ECFNRM(1)
               VAL2=TERM1*TWO/THREE*ECFNRM(2)
               VAL3=TERM1*TWO/THREE*ECFNRM(3)
! ROTATE TO TOD
              TOD1 = VAL1*COSTHG-VAL2*SINTHG
              TOD2 = VAL1*SINTHG+VAL2*COSTHG
!
               PXDDT(NCOUNT,1)=PXDDT(NCOUNT,1)+TOD1
               PXDDT(NCOUNT,2)=PXDDT(NCOUNT,2)+TOD2
               PXDDT(NCOUNT,3)=PXDDT(NCOUNT,3)+VAL3
            ENDIF
!
  300 END DO
! ADD PLATE ACCELERATIONS TO TOTAL
         DXDD(1) = DXDD(1)+ACCEL(1)
         DXDD(2) = DXDD(2)+ACCEL(2)
         DXDD(3) = DXDD(3)+ACCEL(3)
!
  400 END DO
!
      RETURN
      END
