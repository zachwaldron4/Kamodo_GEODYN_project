      SUBROUTINE UPDLG(IMAPLG,PARMVC,VAL,CSM)
!********1*********2*********3*********4*********5*********6*********7**
! UPDLG
!
!
! FUNCTION:  LOAD INFORMATION ABOUT LOCAL GRAVITY INTO WORKING ARRAYS
!
! I/O PARAMETERS:
!
!   IMAPLG   I    A    FOR EACH LOCAL GRAVITY SUB BLOCK MAPS LOCALTION
!                      IN THE ADJUSTED PARAMETER ARRAY (PARMVC)
!   PARMVC   I    A    CURRENT VALUES OF ADJUSTED PARAMETERS
!                      FOR THE CURRENT SATELLITE
!   VAL     I/O   A    GRAVITY ANOMALY VALUE FOR EACH SUB BLOCK
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      COMMON/CESTIM/MPARM,MAPARM,MAXDIM,MAXFMG,ICLINK,IPROCS,NXCEST
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/GEODEG/NMAX,NMAXP1,NP,NTOLD,NTOLO,NADJC,NADJS,NADJCS,      &
     &              NPMAX,NXGDEG
      COMMON/GRANI/NSUBLG,NXGRAN
      COMMON/GRANR/RGRAN
      COMMON/MASCNI/NMASSC,NMASSS,NPMASS,ICPNTM(11475),ISPNTM(11325)
      COMMON/MASCNR/CMASS(11775),SMASS(11775)
      COMMON/NLOCGR/INTEGG,IOPTLG,ICUTLG,MAXDO,NLOCAL,NLATLG,NLONLG,    &
     &              nlocgu, nlocga, nxlocg
      DIMENSION IMAPLG(NSUBLG),PARMVC(MAPARM),VAL(NSUBLG)
      DIMENSION CSM(NSUBLG,2)
!
      NMASSC=0
      IF(NSUBLG.LE.0) RETURN
      IF(IOPTLG.EQ.3) GO TO 50
!
!  GRAVITY ANAMOALY OPTION
!
      BE=AE*(1.D0-FE)
      RGRAN=(BE*AE*AE)**.33333333333333D0
!
   50 CONTINUE
      DO 100 I=1,NSUBLG
      IF(IMAPLG(I).LE.0) GO TO 100
      VAL(I)=PARMVC(IMAPLG(I))
  100 END DO
      IF(IOPTLG.LT.3) RETURN
!
!  MASCON OPTION
!
      NMASSC=NSUBLG/NLOCAL
      XDEG=(-3.0D0+SQRT(9.D0+8.D0*DBLE(NMASSC)))/2.D0
      IDEG=XDEG+.001D0
      IF(IDEG.GT.150) THEN
        WRITE(6,6000)
        WRITE(6,6001) IDEG
        STOP
      ENDIF
      NMASSS=IDEG*(IDEG+1)/2
      NPMASS=NMASSS+3*IDEG
      DO I=1,NP
        CMASS(I)=0.D0
        SMASS(I)=0.D0
      ENDDO
      IPTF=0
      DO I=1,IDEG
      I1=I+1
      DO J=1,I1
      IPTF=IPTF+1
      IPTT=(I-1)*I/2+(I-1)*3+J
      ICPNTM(IPTF)=IPTT
      DO K=1,NLOCAL
      IPPV=1+(K-1)*NMASSC
      CMASS(IPTT)=CMASS(IPTT)+VAL(IPPV)*CSM(IPTF+IPPV-1,1)
      ENDDO
      ENDDO
      ENDDO
!
      IPTF=0
      DO I=1,IDEG
      DO J=1,I
      IPTF=IPTF+1
      IPTT=(I-1)*I/2+(I-1)*3+J+1
      ISPNTM(IPTF)=IPTT
      DO K=1,NLOCAL
      IPPV=1+(K-1)*NMASSC
      SMASS(IPTT)=SMASS(IPTT)+VAL(IPPV)*CSM(IPTF+IPPV-1,2)
      ENDDO
      ENDDO
      ENDDO
!
      RETURN
 6000 FORMAT(' EXECUTION TERMINATING IN SUBROUTINE UPDLG')
 6001 FORMAT(' DEGREE USED TO MODEL MASCONS:',I5,' EXCEEDS 150')
      END
