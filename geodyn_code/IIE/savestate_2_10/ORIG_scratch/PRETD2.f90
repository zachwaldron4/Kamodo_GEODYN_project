      SUBROUTINE PRETD2(IDOODN,CONNAM,P,IRDEGT,IRORDT,IPRESP,IMP2RS,    &
     &                  IPTRUA,PARMV,TIDES,OFACT,IA,LRAY,ISIDE,IMP2FR)
!********1*********2*********3*********4*********5*********6*********7**
! PRETD2           00/00/00            8604.0    PGMR - ?
!
! FUNCTION:  LOADS WORKING ARRAYS WHEN NON-CHRISTODOULIDIS TIDES
!            ARE BEING USED
!            (PRETID IS USED IN CHRISTODOULIDIS CASE)
!
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   IDOODN   I    A    NON DUPLICATE LIST OF ALL DOODSON NUMBERS
!                      BEING USED.
!   CONNAM   O    A    NON DUPLICATE LIST OF TIDE NAMES BASES ON IDOODN
!   P        I    A    LGENDR POLYNOMIAL ARRAY USED AS SCRATCH SPACE
!   IRDEGT   I    A    RESPONSE DEGREE FOR EACH TIDE PARAMETER
!   IRORDT   I    A    RESPONSE ORDER  FOR EACH TIDE PARAMETER
!   IPRESP   O    A    NON DUPLICATE LIST OF LOCATIONS IN THE
!                      GEOPOTENTIAL ARRAYS THAT CORRESPOND TO RESPONSE
!                      DEGREE AND ORDERS
!   IMP2RS   O    A    MAP FROM THE NTIDE PARAMETER PAIRS TO THE
!                      NON DUPLICATE LIST OF RESPONSE DEGREES AND ORDERS
!   IPTRUA   I    A    POINTER FROM COMPLETE LIST OF PARAMETERS TO LIST
!                      OF ADJUSTED PARTAMETERS
!   PARMV    I    A    LIST OF CURRENT VALUES OF ALL PARAMETERS
!   TIDES    O    A    TIDE A & B PARAMETERS REORDERED FROM PARMV
!                      ARRAY. NOW As ARE ALL TOGETHER NETADJ,NOTADJ,
!                      NETUN AND NOTUN FOLLOWED BY ALL Bs. FURTHERMORE
!                      THE As AND Bs CORRESPONDING TO UNADJUSTED
!                      OCEAN TIDES HAVE BEEN MULTIPLIED BY THE XKL
!                      FACTOR (LOAD COEFFICIENTS AND DENSITY) SEE
!                      OFACT BELOW
!   OFACT    O    A    THIS ARRAY'S LENGTH IS THE NUMBER OF
!                      ADJUSTED OTIDE PAIRS. FOR EACH OF THESE PAIRS
!                      OFACT CORRESPONDS TO THE COEFFICIENT
!                      4*PI*R*G*RHO*(1-KL)/(2L+1)
!
!
! COMMENTS:  MOST ARRAYS ARE ORDERED NETADJ,NOTADJ,NETUN,NOTUN
!            PARMV ARRAY HAS ALL As FOLLOWED BY ALL Bs IN EACH CATEGORY
!            TIDES ARRAY HAS ALL As ACROSE NTIDE FOLLOWED BY ALL Bs
!
!
!********1*********2*********3*********4*********5*********6*********7**
      USE LOAD_COEFS_MOD, ONLY : OLOAD_DEF_COEFS
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      CHARACTER*4 TIDEN,CONSTITUENT
      COMMON/CITER /NINNER,NARC,NGLOBL
      COMMON/CTIDES/NTIDE,NSTADJ,NET,NETADJ,NETUN,NOT,NOTADJ,           &
     &   NOTUN ,NETUNU,NETADU,NOTUNU,NOTADU,NBSTEP,KTIDA(3),            &
     &   ILLMAX,NUNPLY,NNMPLY,NDOODN,MXTMRT,NRESP ,NXCTID
      COMMON/GEODEG/NMAX,NMAXP1,NP,NTOLD,NTOLO,NADJC,NADJS,NADJCS,      &
     &              NPMAX,NXGDEG
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/TIDE2I/ISCRT(5),INTRT,NTMRT,JMP2RS(10)
      COMMON/TIDE2R/TIDESS(20)
      DIMENSION IDOODN(NDOODN,3),IDOOD(6),CONNAM(NDOODN)
      DIMENSION P(NP)
      DIMENSION IRDEGT(NTIDE),IRORDT(NTIDE),ISIDE(NTIDE),IMP2FR(NTIDE)
      DIMENSION IPRESP(1),IMP2RS(NTIDE)
      DIMENSION IPTRUA(1),PARMV(1),TIDES(NTIDE,2)
      DIMENSION OFACT(1)
      DIMENSION IA(NTIDE)
      DOUBLE PRECISION, ALLOCATABLE :: XKL(:)
! GRAV CONST ; SEAWATER DENSITY ; GRAVITY AT SURFACE ;
      DATA GCONST/6.672D-11/,RHO/1035.D0/,GL/9.820D0/
!
!
!  GEODYN's input air tides are in pressure units of dbar.  This is
!  roughly equivalent to an equilibrium ocean tide height in
!  units of meters -- which is the unit GEODYN uses for ocean tides.
!  Converting to MKS units (i.e. Pascals) and scaling the air pressure
!  by (1/g) converts to surface mass anomaly (per area), which is
!  what is needed in our expression for computing Stokes coefficients.
!  Thus, GEODYN's "seawater density" for air tides must be set
!  to 10,000/g = 1018.
!  [ Note: 1 dbar = 100 mbar = 10,000 Pa ]
!
      EQUIVALENCE(CONHLD,TIDEN)
      RHOAIR=1018.D0

!     IF(NGLOBL.GT.1) GO TO 500
      PI=ACOS(-1.D0)
      ALLOCATE(XKL(SIZE(OLOAD_DEF_COEFS)))
      DO I=1,SIZE(OLOAD_DEF_COEFS)
      IDEG=I-1
      XKL(I)=((1.D0-OLOAD_DEF_COEFS(I))/(2*DBLE(IDEG)+1.D0))          &
     &       *(4.D0*PI*GCONST*RHO/GL)
      ENDDO
      SCLAIR=RHOAIR/RHO
!
      IF(NDOODN.LE.0) GO TO 110
      DO 100 I=1,NDOODN
!!! INITIALIZE SIDE BAMD INDICATORS
      IDOODN(I,2)=0
      IDOODN(I,3)=0
!!!
! BREAK THE FREQUENCY NUMBER DOWN INTO SIX ARGUMENTS
      JDOODN=IDOODN(I,1)*10
      IDOOD(1)=JDOODN/1000000
      IF(IDOOD(1).GT.5) IDOOD(1)=IDOOD(1)-6
      IDOOD(2)=MOD(JDOODN,1000000)/100000-5
      IDOOD(3)=MOD(JDOODN,100000)/10000-5
      IDOOD(4)=MOD(JDOODN,10000)/1000-5
      IDOOD(5)=MOD(JDOODN,1000)/100-5
      IDOOD(6)=MOD(JDOODN,100)/10-5
      TIDEN=CONSTITUENT(IDOOD)
      CONNAM(I)=CONHLD
  100 END DO
  110 CONTINUE
      INTRT=600
      INTRT=86400
      NTMRT=0
      IF(.NOT.LRAY.OR.NTIDE.LE.0) RETURN
!
      DO I=1,NP
         P(I)=-99.D0
      ENDDO
! FREQUENCY INDEPENDENT EARTH TIDE TERMS
      P(5)=99.D0
      P(6)=99.D0
      P(7)=99.D0
      P(10)=99.D0
      P(11)=99.D0
      P(12)=99.D0
      P(13)=99.D0
      P(16)=99.D0
      P(17)=99.D0
      P(18)=99.D0
!
      IF(NTIDE.GT.0) THEN
         DO 200 I=1,NTIDE
         IF(IRDEGT(I).GT.NMAX) THEN
           WRITE(6,6000)
           WRITE(6,6001) IRDEGT(I)
           WRITE(6,6002) NMAX
           STOP
         ENDIF
         IPT=1+IRORDT(I)+(IRDEGT(I)-1)*3+(IRDEGT(I)-1)*IRDEGT(I)/2
         P(IPT)=99.D0
         IF(ISIDE(I).EQ.1) IDOODN(IMP2FR(I),2)=1
         IF(ISIDE(I).EQ.2) IDOODN(IMP2FR(I),3)=1
  200    CONTINUE
      ELSE
         N4=4
         IF(N4.GT.NMAX) THEN
           WRITE(6,6000)
           WRITE(6,6001) N4
           WRITE(6,6002) NMAX
           STOP
         ENDIF
      ENDIF
      NNRESP=0
      DO 300 I=1,NP
      IF(P(I).LT.0.D0) GO TO 300
      NNRESP=NNRESP+1
      IF(NNRESP.GT.NRESP) THEN
        WRITE(6,6000)
        WRITE(6,6003)
        STOP
      ENDIF
      IPRESP(NNRESP)=I
  300 END DO
      NRESP=NNRESP
      IF(NTIDE.LE.0) GO TO 410
      DO 400 I=1,NTIDE
      IPT=1+IRORDT(I)+(IRDEGT(I)-1)*3+(IRDEGT(I)-1)*IRDEGT(I)/2
      NC=0
      DO 350 J=1,IPT
      IF(P(J).GT.0.D0) NC=NC+1
  350 END DO
      IMP2RS(I)=NC
  400 END DO
  410 CONTINUE
!
      NXT=0
      IF(P(1).GT.0.D0) NXT=NXT+1
      IF(P(2).GT.0.D0) NXT=NXT+1
      IF(P(3).GT.0.D0) NXT=NXT+1
      JMP2RS(1)=1+NXT
      JMP2RS(2)=2+NXT
      JMP2RS(3)=3+NXT
      JMP2RS(4)=8+NXT
      JMP2RS(5)=9+NXT
      JMP2RS(6)=10+NXT
      JMP2RS(7)=4+NXT
      JMP2RS(8)=5+NXT
      JMP2RS(9)=6+NXT
      JMP2RS(10)=7+NXT
      DO I=1,NP
        P(I)=0.D0
      ENDDO
!
      INTRT=600
      INTRT=86400
      NTMRT=0
      DO I=1,5
        ISCRT(I)=0
      ENDDO
!
!
!
  500 CONTINUE
!
!  FILL TIDESS ARRAYS
      IPTQ=IPVAL(IXTIDE)-1
      DO I=1,10
!
!   NOT EMAT ORDER
!
        TIDESS(2*I-1)=PARMV(IPTQ+I)
        TIDESS(2*I)=PARMV(IPTQ+I+10)
      ENDDO
!
!  FILL THE TIDES  ARRAYS
!  PARMV ARRAY IS ORDERED NETADJ,NOTADJ,NETUN,NOTUN
!  WITHIN EACH FOUR CATEGORIES ALL As THEN ALL Bs
!  MAKE TIDES ARRAY ALL As FOLLOWED BY ALL Bs
!
      NC=0
      IPTQ=IPVAL(IXETDE)-1
!
      IF(NETADJ.LE.0) GO TO 610
      DO 600 I=1,NETADJ
      IF(IPTRUA(IPTQ+I).LE.0) THEN
         WRITE(6,6000)
         WRITE(6,6004)
         STOP
      ENDIF
      NC=NC+1
      TIDES(NC,1)=PARMV(IPTQ+I)
      TIDES(NC,2)=PARMV(IPTQ+I+NETADJ)
  600 END DO
  610 CONTINUE
!
      IF(NOTADJ.LE.0) GO TO 710
      DO 700 I=1,NOTADJ
      IF(IPTRUA(IPTQ+I+2*NETADJ).LE.0) THEN
         WRITE(6,6000)
         WRITE(6,6005)
         STOP
      ENDIF
      NC=NC+1
      IF(IRDEGT(NC).GT.360) THEN
        WRITE(6,6000)
        WRITE(6,6001) IRDEGT(NC)
        WRITE(6,6008)
        STOP
      ENDIF
      XDFACT=XKL(IRDEGT(NC)+1)
! FOR AIR TIDES RESET XDFACR
      IF(IA(NC).EQ.1) XDFACT=XDFACT*SCLAIR
      OFACT(I)=XDFACT
      TIDES(NC,1)=PARMV(IPTQ+I+2*NETADJ)
      TIDES(NC,2)=PARMV(IPTQ+I+2*NETADJ+NOTADJ)
  700 END DO
  710 CONTINUE
!
      IF(NETUN.LE.0) GO TO 810
      DO 800 I=1,NETUN
      IF(IPTRUA(IPTQ+I+2*NETADJ+2*NOTADJ).GT.0) THEN
         WRITE(6,6000)
         WRITE(6,6006)
         STOP
      ENDIF
      NC=NC+1
      TIDES(NC,1)=PARMV(IPTQ+I+2*NETADJ+2*NOTADJ)
      TIDES(NC,2)=PARMV(IPTQ+I+2*NETADJ+2*NOTADJ+NETUN)
  800 END DO
  810 CONTINUE
!
      IF(NOTUN.LE.0) GO TO 910
      DO 900 I=1,NOTUN
      IF(IPTRUA(IPTQ+I+2*NETADJ+2*NOTADJ+2*NETUN).GT.0) THEN
         WRITE(6,6000)
         WRITE(6,6007)
         STOP
      ENDIF
      NC=NC+1
      IF(IRDEGT(NC).GT.360) THEN
        WRITE(6,6000)
        WRITE(6,6001) IRDEGT(NC)
        WRITE(6,6008)
        STOP
      ENDIF
      XDFACT=XKL(IRDEGT(NC)+1)
      IF(IA(NC).EQ.1) XDFACT=XDFACT*SCLAIR
      TIDES(NC,1)=PARMV(IPTQ+I+2*NETADJ+2*NOTADJ+2*NETUN)*XDFACT
      TIDES(NC,2)=PARMV(IPTQ+I+2*NETADJ+2*NOTADJ+2*NETUN+NOTUN)*XDFACT
  900 END DO
  910 CONTINUE

      DEALLOCATE(XKL)

      RETURN
 6000 FORMAT(' EXECUTION TERMINATING IN PRETD2')
 6001 FORMAT(' A TIDAL RESONSE DEGREE: ',I3,' IS LARGER THAN')
 6002 FORMAT(' MAXIMUM DEGREE OF GEOPOTENTIAL: ',I3)
 6003 FORMAT(' NNRESP IN IIE: ',I12,&
             ' IS BIGGER THAN NRESP FROM IIS:',I12)
 6004 FORMAT(' ADJ ET  ARE NOT PACKED CONTIGUOUSLY')
 6005 FORMAT(' ADJ OT ARE NOT PACKED CONTIGUOUSLY')
 6006 FORMAT(' UNADJ ET  ARE NOT PACKED CONTIGUOUSLY')
 6007 FORMAT(' UNADJ OT ARE NOT PACKED CONTIGUOUSLY')
 6008 FORMAT(' 360 (MAX DEGREE OF LOAD INFORMATION)')
      END
