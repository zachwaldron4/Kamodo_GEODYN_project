!$WTADDX
      SUBROUTINE WTADDX(JDIM,NSATA,NRMTOT,PARRSD,PARVAR,SATCOV,ATPA,    &
     &                  ATPL)
!********1*********2*********3*********4*********5*********6*********7**
! WTADDA           00/00/00            0000.0    PGMR - ?
!
! FUNCTION:       ADD ARC PARAMETER VARIANCE-COVARIANCE INFORMATION
!                 TO THE NORMAL MATRIX AND RHS OF THE NORMAL EQUATIONS
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   JDIM     I         NUMBER OF PARAMETERS
!   NSATA    I         NUMBER OF SATELLITES IN ARC
!   NRMTOT   I         ACTUAL LENGTH OF FIRST ROW IN NORMAL MATRIX
!   PARRSD   I         DIFFERENCE BETWEEN LATEST ESTIMATE OF
!                      PARAMETERS AND APRIORI ESTIMATE
!   PARVAR   I         DIAGONAL POTION OF THE INVERTED APRIORI
!                      PARAMETER VARIANCE COVARIANCE MATRIX
!   SATCOV   I         SATELLITE COVARIANCE INFORMATION
!   ATPA    I/O        NORMAL EQUATION MATRIX WITHOUT APRIORI
!                      PARAMETER INFORMATION AS INPUT
!                      NORMAL EQUATION MATRIX WITH APRIORI
!                      PARAMETER INFORMATION AS OUTPUT
!   ATPL    I/O        RHS OF NORMAL EQUATIONS WITHOUT APRIORI
!                      PARAMETER INFORMATION AS INPUT
!                      RHS OF NORMAL EQUATIONS WITH APRIORI
!                      PARAMETER INFORMATION AS OUTPUT
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/BIAOR/NAMBB,NCYCB,NAMBBH
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      DIMENSION PARRSD(1),PARVAR(1),SATCOV(15,NSATA),ATPA(1),ATPL(1)
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! ADD IN LEFT HAND SIDE DIAGONAL FIRST
      IPT=1
      IL=NRMTOT-NAMBB+1
      IPT1=IPVAL0(IXBISA)-1
      DO 100 I=1,NAMBB
      ATPA(IPT)=ATPA(IPT)+PARVAR(I+IPT1)
      IPT=IPT+IL
  100 END DO
!
      IL=NRMTOT-NAMBB
      KDIM=IPVAL0(IXBISA)-1
      DO 110 I=1,KDIM
      ATPA(IPT)=ATPA(IPT)+PARVAR(I)
      IPT=IPT+IL
      IL=IL-1
  110 END DO
!
!     KDIM=NPVAL0(IXBISA)-NAMBB
      KDIM=NPVAL0(IXARC)-(IPVAL0(IXBISA)-1)-NAMBB
      IF(KDIM.LE.0) GO TO 130
      KDIM1=IPVAL0(IXBISA)+NAMBB
!     KDIM2=IPVAL0(IXBISA)+NPVAL0(IXBISA)-1
      KDIM2=KDIM1+KDIM-1
      DO 120 I=KDIM1,KDIM2
      ATPA(IPT)=ATPA(IPT)+PARVAR(I)
      IPT=IPT+IL
      IL=IL-1
  120 END DO
!
  130 CONTINUE
!
! ADD IN INFORMATION TO RIGHT HAND SIDE
      IPT1=IPVAL0(IXBISA)-1
      DO 200 I=1,NAMBB
      ATPL(I)=ATPL(I)+PARRSD(I+IPT1)*PARVAR(I+IPT1)
  200 END DO
!
      KDIM=IPVAL0(IXBISA)-1
      DO 205 I=1,KDIM
      ATPL(NAMBB+I)=ATPL(NAMBB+I)+PARRSD(I)*PARVAR(I)
  205 END DO
!
!     KDIM=NPVAL0(IXBISA)-NAMBB
      KDIM=NPVAL0(IXARC)-(IPVAL0(IXBISA)-1)-NAMBB
      IF(KDIM.LE.0) GO TO 215
      KDIM1=IPVAL0(IXBISA)+NAMBB
!     KDIM2=IPVAL0(IXBISA)+NPVAL0(IXBISA)-1
      KDIM2=KDIM1+KDIM-1
      DO 210 I=KDIM1,KDIM2
      ATPL(I)=ATPL(I)+PARRSD(I)*PARVAR(I)
  210 END DO
!
  215 CONTINUE
!
! ADD IN COVARIANCE INFORMATION TO LEFT HAND SIDE
      I=NAMBB
      IPTA=1+NAMBB*(NRMTOT-NAMBB+1)
      ILA=NRMTOT-NAMBB
!
      DO 300 I=1,NSATA
      IPTS=0
      ILS=5
      DO 250 J=1,5
      DO 220 JJ=1,ILS
      ATPA(IPTA+JJ)=ATPA(IPTA+JJ)+SATCOV(IPTS+JJ,I)
  220 END DO
      IPTS=IPTS+ILS
      ILS=ILS-1
      IPTA=IPTA+ILA
      ILA=ILA-1
  250 END DO
      IPTA=IPTA+ILA
      ILA=ILA-1
  300 END DO
! ADD IN COVARIANCE INFO TO RIGHT HAND SIDE
      DO 400 I=1,NSATA
      IPTA=NAMBB+1+(I-1)*6
! FIRST ROW OF COVAR MATRIX
      ATPL(IPTA)=ATPL(IPTA)+SATCOV(1,I)*PARRSD(IPTA+1)                  &
     &          +SATCOV(2,I)*PARRSD(IPTA+2)+SATCOV(3,I)*PARRSD(IPTA+3)  &
     &          +SATCOV(4,I)*PARRSD(IPTA+4)+SATCOV(5,I)*PARRSD(IPTA+5)
! SECOND ROW
      ATPL(IPTA+1)=ATPL(IPTA+1)+SATCOV(1,I)*PARRSD(IPTA)                &
     &          +SATCOV(6,I)*PARRSD(IPTA+2)+SATCOV(7,I)*PARRSD(IPTA+3)  &
     &          +SATCOV(8,I)*PARRSD(IPTA+4)+SATCOV(9,I)*PARRSD(IPTA+5)
! THIRD ROW
      ATPL(IPTA+2)=ATPL(IPTA+2)+SATCOV(2,I)*PARRSD(IPTA)                &
     &          +SATCOV(6,I)*PARRSD(IPTA+1)+SATCOV(10,I)*PARRSD(IPTA+3) &
     &          +SATCOV(11,I)*PARRSD(IPTA+4)+SATCOV(12,I)*PARRSD(IPTA+5)
! FOURTH ROW
      ATPL(IPTA+3)=ATPL(IPTA+3)+SATCOV(3,I)*PARRSD(IPTA)                &
     &          +SATCOV(7,I)*PARRSD(IPTA+1)+SATCOV(10,I)*PARRSD(IPTA+2) &
     &          +SATCOV(13,I)*PARRSD(IPTA+4)+SATCOV(14,I)*PARRSD(IPTA+5)
! FIFTH ROW
      ATPL(IPTA+4)=ATPL(IPTA+4)+SATCOV(4,I)*PARRSD(IPTA)                &
     &          +SATCOV(8,I)*PARRSD(IPTA+1)+SATCOV(11,I)*PARRSD(IPTA+2) &
     &          +SATCOV(13,I)*PARRSD(IPTA+3)+SATCOV(15,I)*PARRSD(IPTA+5)
! SIXTH ROW
      ATPL(IPTA+5)=ATPL(IPTA+5)+SATCOV(5,I)*PARRSD(IPTA)                &
     &          +SATCOV(9,I)*PARRSD(IPTA+1)+SATCOV(12,I)*PARRSD(IPTA+2) &
     &          +SATCOV(14,I)*PARRSD(IPTA+3)+SATCOV(15,I)*PARRSD(IPTA+4)
  400 END DO
      RETURN
      END
