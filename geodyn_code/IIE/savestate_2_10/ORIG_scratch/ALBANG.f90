!$ALBANG
      SUBROUTINE ALBANG(ECFSAT,GEOANG,R,APAREA,NSEG)
!********1*********2*********3*********4*********5*********6*********7**
!  ALBANG          89/05/23            0000.0    PGMR: SBL,FGL,DEP
!
!  FUNCTION: CALCULATE GEOCENTRIC ANGLES TO RINGS THAT DEFINE
!            LOCATION OF ALBEDO SPOT AREAS
!
!  I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   ECFSAT   I    A    INPUT ARRAY CONTAINING X,Y,AND Z COMPONENTS
!                      OF THE SATELLITES POSITION IN EARTH CENTERED
!                      FIXED REFERENCE FRAME.
!   GEOANG   O    A    GEOCENTRIC ANGLE TO THE RINGS THAT DEFINE AREA
!                      ELEMENTS
!   R        I    S    DISTANCE FROM CENTER OF EARTH TO SATELLITE
!   APAREA   O    S    ATTENUATED PROJECTED AREA, THE SAME FOR ALL
!                      ALBEDO SPOTS
!   NSEG     I    A    CONTAINS THE TOTAL NUMBER OF RING ELEMENTS PER
!                      VARIABLE RING FOR EXAMPLE FOR 6 RINGS
!                      NSEG={1,7,19,43,91,187,379}
!
! COMMENTS:
!
! REFERENCES: KNOCKE, P.,AND RIES, J.,"EARTH RADIATION PRESSURE EFFECTS
!                   ON SATELLITES"; CENTER FOR SPACE RESEARCH,
!                   UNIVERSITY OF TEXAS AT AUSTIN, SEPTEMBER, 1987.
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/ALBINT/IRINGS,ISPOTS,IMAXA,IMAXE,MMAXA,MMAXE,KMAXA,KMAXE,  &
     &              KALB,KEMM,KLMOD,IMODEL,NPNALB,NXALBI
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
!
! %%%%%% GEOANG(1) -> GEOANG(*)
      DIMENSION ECFSAT(3),GEOANG(*)
      DIMENSION NSEG(1)
!
      DATA TWO/2.0D0/,ONE/1.0D0/,C360D0/360.0D0/,C180D0/180.0D0/
      DATA C90D0/90.0D0/,ZERO/0.0D0/,AN/19.0D0/,ANRI/6.0D0/
!
!    AN     - IS THE TOTAL NUMBER OF AREA ELEMENTS
!    ANRI   - IS THE NUMBER OF AREA ELEMENTS IN THE FIRST RING
!
!**********************************************************************
! START OF EXECUTABLE CODE ********************************************
!**********************************************************************
!
! NUMBER OF RINGS FOR THE ALBEDO MODEL FROM COMMON ALBINT = IRINGS
! COMPUTE NUMBER OF RING ELEMENTS
      AN = DBLE(NSEG(IRINGS+1))
!
! CALCULATE MAXIMUM VIEWING ANGLE
      VIEWAG = ASIN(AE/R)
! CALCULATE FIRST RING ANGLE REFERENCED FROM SUB-SAT POINT
      GEOANG(1)=ZERO
      CV=COS(VIEWAG)
      GEOANG(2)=ACOS((AN-ONE+CV)/AN)
! CALCULATION OF ATTENUATED PROJECTED AREA
      APAREA=TWO*(ONE-COS(GEOANG(2)))
! CALCULATION OF NEXT RING ANGLE
! COMPUTE RING BOUNDARIES USING A DO LOOP
         K = 1
      DO 80 J=2,IRINGS
         K = K + 6*(J-1)
         DK = DBLE(K)
         COSANG1 = DK*COS(GEOANG(2)) - DK + ONE
! reverse
         COSANG2 = COS(GEOANG(J)) -                                     &
     &     ((APAREA*((2.0D0**(J-1))*3.0D0))/2.0D0)
!        COSANG2 = COS(GEOANG(J-1)) -
         COSANG1 = ACOS(COSANG1)
         COSANG2 = ACOS(COSANG2)
         GEOANG(J+1) = COSANG2
   80 END DO
         GEOANG(IRINGS+2) = VIEWAG
! CALCULATE GEOCENTRIC ANGLES TO RINGS
      GEOANG(1)=ZERO
! CALCULATE GEOCENTRIC ANGLES
      DO 90 J=2,IRINGS+2
        ARG = R*(SIN(GEOANG(J)))/AE
        ARG = MIN(ARG,ONE)
        GEOANG(J) = (ASIN(ARG)) - GEOANG(J)
   90 END DO
! CALCULATE GEOCENTRIC ANGLES TO CENTER OF RINGS
      DO 200 J1=2,IRINGS+1
      J2=J1+1
      GEOANG(J1)=((GEOANG(J2)-GEOANG(J1))/TWO)+GEOANG(J1)
  200 END DO
      RETURN
      END
