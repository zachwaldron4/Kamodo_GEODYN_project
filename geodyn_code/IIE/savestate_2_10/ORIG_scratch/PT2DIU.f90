      SUBROUTINE PT2DIU(NM,NTOLFR,COSLAT,SINLAT,COSLON,SINLON,          &
     &                  COSANG,SINANG,PXSPHL)
!********1*********2*********3*********4*********5*********6*********7**
! FUNCTION: THIS IS THE COMPANION ROUTINE TO ST2DIU. IT COMPUTES
!           THE PARTIALS FOR THE PARAMETERS IN ST2DIU. THESE ARE
!           THE DELTA R(IP), DELTA R(OP), DELTA T(IP) AND DELTA T(OP)
!           PARAMETERS FOR EACH OF THE DIURNAL FREQUENCIES
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   NM       I    S    NUMBER OF OBSERVATIONS IN THE BLOCK
!   NTOLFR   I    S    NUMBER OF FREQUENCIES
!   COSLAT   I    A    COSINES OF LATITUDES WHERE SET DESIRED
!   SINLAT   I    A    SINES OF LATITUDES WHERE SET DESIRED
!   COSLON   I    A    COSINES OF LONGITUDES WHERE SET DESIRED
!   SINLON   I    A    SINES OF LONGITUDES WHERE SET DESIRED
!   COSANG   I    A    COSINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   SINANG   I    A    SINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   PXSPHL   O    A    THE PARTIAL DERIVATIVES OF EARTH FIXED POSITION
!                      WRT DELTA R(IP), DELTA R(OP), DELTA T(IP)
!                      AND DELTA T (OP) FOR DIURNAL FREQUENCIES
!
! COMMENTS:  THIS SUBROUTINE WAS MODIFIED FROM SUBROUTINE STEP2DIU
!            FROM DEHANT.
!
!
!********1*********2*********3*********4*********5*********6*********7**
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      DIMENSION COSLAT(NM),SINLAT(NM),COSLON(NM),SINLON(NM)
      DIMENSION COSANG(NM,NTOLFR),SINANG(NM,NTOLFR)
      DIMENSION PXSPHL(NM,3,4,NTOLFR)
!
!
!
      DO 500 K=1,NM
!
      SC2=2.D0*SINLAT(K)*COSLAT(K)
      C2MS2=COSLAT(K)*COSLAT(K)-SINLAT(K)*SINLAT(K)
!
! NOTE PARMS SCALED TO M (ORIGINAL CODE MM)
!
      DO 100 J=1,NTOLFR

      COSA=COSANG(K,J)*COSLON(K)-SINANG(K,J)*SINLON(K)
      SINA=SINANG(K,J)*COSLON(K)+COSANG(K,J)*SINLON(K)


      DR1=SC2*SINA
      DR2=SC2*COSA

      DN3=C2MS2*SINA
      DN4=C2MS2*COSA

      DE3=SINLAT(K)*COSA
      DE4=SINLAT(K)*SINA

!
      PXSPHL(K,1,1,J)=DR1*COSLON(K)*COSLAT(K)
      PXSPHL(K,1,2,J)=DR2*COSLON(K)*COSLAT(K)
      PXSPHL(K,2,1,J)=DR1*SINLON(K)*COSLAT(K)
      PXSPHL(K,2,2,J)=DR2*SINLON(K)*COSLAT(K)
      PXSPHL(K,3,1,J)=DR1*SINLAT(K)
      PXSPHL(K,3,2,J)=DR2*SINLAT(K)
!
      PXSPHL(K,1,3,J)=-DE3*SINLON(K)-DN3*SINLAT(K)*COSLON(K)
      PXSPHL(K,1,4,J)=-DE4*SINLON(K)-DN4*SINLAT(K)*COSLON(K)
      PXSPHL(K,2,3,J)= DE3*COSLON(K)-DN3*SINLAT(K)*SINLON(K)
      PXSPHL(K,2,4,J)= DE4*COSLON(K)-DN4*SINLAT(K)*SINLON(K)
      PXSPHL(K,3,3,J)=DN3*COSLAT(K)
      PXSPHL(K,3,4,J)=DN4*COSLAT(K)
  100 END DO
  500 END DO
      RETURN
      END
