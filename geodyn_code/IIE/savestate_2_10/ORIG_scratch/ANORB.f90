      SUBROUTINE ANORB(AA,INDSAT,ETIME,XSAT)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! FUNCTION:    GET THE APPROXIMATE STATE VECTOR OF ONE OF GEODYN'S ARC
!              SATELLITES AT AN ELAPSED TIME FROM ITS INITIAL STATE.
!              DO THIS FROM A SIMPLE TWO BODY KEPLER ORBIT
!
! AA     - GEODYN'S DYNAMIC FLOATING POINT ARRAY
! INDSAT - SATELLITE NUMBER WITHIN THE ARC
! ETIME  - ELAPSED TIME IN SECONDS FROM INTIAL EPOCH OF SATELLITE
! XSAT   - APPROXIMATE STATE VECTOR
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      COMMON/CORA01/KFSEC0,KFSECB,KFSEC ,KFSECV,KH    ,KHV   ,KCTOL ,   &
     &              KRSQ  ,KVMATX,KCPP  ,KCPV  ,KCCP  ,KCCV  ,KCCPV ,   &
     &              KCCVV ,KXPPPP,KX    ,KPX   ,KSUMX ,KXDDOT,KSUMPX,   &
     &              KPXDDT,KAB   ,KPN   ,KAORN ,KSINLM,KCOSLM,KTANPS,   &
     &              KCRPAR,KVRARY,KXM   ,KXNP1 ,KXPRFL,KXM2  ,KXNNP1,   &
     &              KWRK  ,KFI   ,KGE   ,KB0DRG,KBDRAG,KAPGM ,KAPLM ,   &
     &              KCN   ,KSN   ,KSTID ,KTIDE ,KSTDRG,KSTSRD,KSTACC,   &
     &              KLGRAV,KGM   ,KAE   ,KFPL  ,KFEQ  ,KPLNPO,KPLNVL,   &
     &              KXEPOC,KCD   ,KCDDOT,KCR   ,KGENAC,KACN  ,KASN  ,   &
     &              KTHDRG,KCKEP ,KCKEPN,KXNRMZ,KXNRMC,KFSCEP,KFSCND,   &
     &              KAREA ,KXMASS,KRMSPO,KTCOEF,KTXQQ ,KTIEXP,KTXMM ,   &
     &              KTXLL1,KTXSN1,KTS2QQ,KT2M2H,KT2MHJ,KTXKK ,KTSCRH,   &
     &              KPXPK ,KAESHD,KCSAVE,KSSAVE,KCGRVT,KSGRVT,KXDTMC,   &
     &              KDNLT ,KTXSN2,KTNORM,KTWRK1,KTWRK2,KUNORM,KAERLG,   &
     &              KSINCO,KPARLG,KCONST,KBFNRM,KTDNRM,KCSTHT,KTPSTR,   &
     &              KTPSTP,KTPFYW,KPLMGM,KTPXAT,KEAQAT,KEAFSS,KEAINS,   &
     &              KACS  ,KECS  ,KSOLNA,KSOLNE,KSVECT,KSFLUX,KFACTX,   &
     &              KFACTY,KADIST,KGEOAN,KPALB ,KALBCO,KEMMCO,KCNAUX,   &
     &              KSNAUX,KPPER ,KACOSW,KBSINW,KACOFW,KBCOFW,KANGWT,   &
     &              KWT   ,KPLNDX,KPLANC,KTGACC,KTGDRG,KTGSLR,KWTACC,   &
     &              KWTDRG,KWTSLR,KTMACC,KTMDRG,KTMSLR,KATTUD,KDYACT,   &
     &              KACCBT,KACPER,KXDDNC,KXDDAO,KXNC  ,KXPPNC,KSMXNC,   &
     &              KXDDTH,KPDDTH,KXSSBS,KCPPNC,KEXACT,KXACIN,KXACOB,   &
     &              KPXHDT,KTPXTH,KPACCL,KTXSTA,KDELXS,KSMRNC,KPRX  ,   &
     &              KSMRNP,KDSROT,KXUGRD,KYUGRD,KZUGRD,KSUMRC,KXDDRC,   &
     &              KTMOS0,KTMOS, KTMOSP,KSMXOS,KSGTM1,KSGTM2,KSMPNS,   &
     &              KXGGRD,KYGGRD,KZGGRD,KXEGRD,KYEGRD,KZEGRD,KSSDST,   &
     &              KSDINS,KSDIND,KSSDSR,KSSDDG,KTATHM,KTAINS,KTAFSS,   &
     &              KSRAT ,KTRAT ,KHLDV ,KHLDA1,KHLDA4,KHLDA7,KQAST1,   &
     &              KQAST2,KQAST3,KQAST4,KQAST5,KQAST6,NXCA01
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      DIMENSION AA(1),XSAT(6)
      DIMENSION AEI(6),PKPX(6,6)
!
! GET SATELLITE'S INITIAL CARTESIAN STATE VECTOR
      XI=AA(KXEPOC+(INDSAT-1)*6)
      YI=AA(KXEPOC+(INDSAT-1)*6+1)
      ZI=AA(KXEPOC+(INDSAT-1)*6+2)
      XDOTI=AA(KXEPOC+(INDSAT-1)*6+3)
      YDOTI=AA(KXEPOC+(INDSAT-1)*6+4)
      ZDOTI=AA(KXEPOC+(INDSAT-1)*6+5)
!
! GET SATELLITE'S INITIAL KEPLER STATE VECTOR IN RADIANS
      IDRAD=1
      CALL ELEM(XI,YI,ZI,XDOTI,YDOTI,ZDOTI,AEI,IDRAD,PKPX,GM)
!
! UPDATE THE MEAN ANAMOLY FOR THE ELAPSED TIME FROM EPOCH
      XN=SQRT(GM/(AEI(1)*AEI(1)*AEI(1)))
      XM=AEI(6)+ETIME*XN
      XM=MOD(XM,TWOPI)
      AEI(6)=XM
!
! GET SATELLITE'S CARTESIAN VECTOR AT THE ELAPSED TIME
      IDRAD=2
      CALL POSVEL(XSAT,AEI,IDRAD,GM)
!
      RETURN
      END
