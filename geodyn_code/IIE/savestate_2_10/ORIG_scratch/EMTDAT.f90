!$EMTDAT
      SUBROUTINE EMTDAT(SUM1,SUM2,PARML0,NRECS,IUNT,LPRTD)
!********1*********2*********3*********4*********5*********6*********7**
! EMTDAT           84/07/02            0000.0    PGMR - WFE
!
! FUNCTION:  TO OUTPUT THE NORMAL MATRIX. EACH ROW OF THE MATRIX
!            IS OUTPUT AS A SEPARATE RECORD. IF PARTITIONING IS
!            BEING DONE THEN THE EMATRX IS WRITTEN OUT IN EMTPDT
!            WHICH IS CALLED FROM SMPART
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   SUM1     I    A    ATWA PART OF THE NORMAL MATRIX
!   SUM2     I    A    ATWR PART OF THE NORMAL MATRIX
!   PARML0   I    A    ADJUSTED PARAMETERS LABEL ARRAY
!   NRECS    I    S    NUMBER OF RECORDS OUTPUT TO IUNT SO FAR
!   IUNT     I    S    UNIT NUMBER FOR OUTPUT OF NORMAL MATRIX
!   LPRTD    I    S    T THEN PRINT NORMAL MATRIX ON UNIT 6
!
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      PARAMETER(MAXZER=20000)
      SAVE
!
      COMMON/BIAOR/NAMBB,NCYCB,NAMBBH
      COMMON/CESTIM/MPARM,MAPARM,MAXDIM,MAXFMG,ICLINK,IPROCS,NXCEST
      COMMON/CPARTL/LPART ,LPARTI,NXPARL
      COMMON/EMAT  /EMTNUM,EMTPRT,EMTCNT,VMATRT,VMATS,VMATFR,FSCVMA,    &
     &              XEMAT
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
!
      DIMENSION PARML0(3,1)
      DIMENSION SUM1(1),SUM2(1)
      DIMENSION ZER(MAXZER)
!
      DATA ZER/MAXZER*0.D0/
!
!
!**********************************************************************
!* START OF EXECUTABLE CODE *******************************************
!**********************************************************************
      INDXNO(I)=NAMBB*(MAPARM-NAMBB+1)+(MAPARM-NAMBB)*(I-1)-(I*(I-1))/2
!
! DEFINITION OF IMPORTANT VARIABLES IN THIS ROUTINE
!
! MAPARM - TOTAL NUMBER OF ADJUSTED PARAMETERS
!          (= MAX FOR ANY ARC+NO. OF GLOBAL PARM.)
! NP     - ACTUAL NUMBER OF ADJUSTED PARAMETERS FOR THIS ARC
!          (= NO. OF ADJ ARC + NO. OF ADJ GLOBAL PARM)
! NARCP  - NO. OF ADJUSTED ARC PARAMETERS FOR THIS ARC
! NGLBP  - NO. OF ADJUSTED GLOBAL PARAMETERS
      NARCP =NPVAL0(IXARC)
      NGLBP =NPVAL0(IXGLBL)
      NP    =NPVAL0(IXARC)+NPVAL0(IXGLBL)
      IGLBAL=IPVAL0(IXGLBL)
!
! ON OPTION WRITE E-MATRIX LABELS OUT
!
      IF(.NOT.LPRTD) GO TO 600
      IARCB=1
      IARCE=NARCP
      IGLBB=IGLBAL
      IGLBE=IGLBB+NGLBP-1
      WRITE(IOUT6,80000) (PARML0(1,J),J=IARCB,IARCE),                   &
     &                   (PARML0(1,K),K=IGLBB,IGLBE)
      WRITE(IOUT6,80100)
  600 CONTINUE
      IF(LPARTI) RETURN
!
! OUTPUT ROWS CONTAINING ARC AND GLOBAL INFORMATION
!
      IARCB =1
      IGLBB =IGLBAL
!
      IF(NAMBB.GT.0) GO TO 1100
      DO 1000 NROW=1,NARCP
      ROW   =NROW
      RSIZE =NP-NROW+1
      NRECS =NRECS+1
      IARCE =IARCB+NARCP-NROW
      IGLBE =IGLBB+NGLBP-1
      WRITE(IUNT) ROW,RSIZE,(SUM1(I),I=IARCB,IARCE),                    &
     &          (SUM1(J),J=IGLBB,IGLBE),SUM2(NROW)
      IF(LPRTD)  WRITE(IOUT6,80200) PARML0(1,NROW),SUM2(NROW),          &
     &          (SUM1(I),I=IARCB,IARCE),(SUM1(J),J=IGLBB,IGLBE)
      IARCB =IARCB +(MAPARM-NAMBB)-NROW+1
      IGLBB =IGLBB +(MAPARM-NAMBB)-NROW
 1000 END DO
      GO TO 1900
!
 1100 CONTINUE
      IF(NAMBB.GT.MAXZER) THEN
        WRITE(6,70000)
        WRITE(6,70001) NAMBB,MAXZER
        STOP
      ENDIF
!     NARCPA=NPVAL0(IXARC)-NPVAL0(IXBISA)
      NARCPA=IPVAL0(IXBISA)-1
      IARCB1=1+NAMBB*(MAPARM-NAMBB+1)
      IARCB2=2
      IARCB3=IPVAL0(IXBISA)+NAMBB*(MAPARM-NAMBB+1)
      IINC2=MAPARM-NAMBB+1
      IGLBB=(IGLBAL-NAMBB)+NAMBB*(MAPARM-NAMBB+1)
      DO 1200 NROW=1,NARCPA
      ROW   =NROW
      RSIZE =NP-NROW+1
      NRECS =NRECS+1
!     IARCE1 =IARCB1+NARCP-NPVAL0(IXBISA)-NROW
      IARCE1 =IARCB1+NARCPA-NROW
      IARCE2 =IARCB2+(NAMBB-1)*(MAPARM-NAMBB+1)
!     IARCE3 =IARCB3+NPVAL0(IXBISA)-NAMBB-1
      IARCE3 =(IARCB3-1)+NPVAL0(IXARC)-(IPVAL0(IXBISA)-1)-NAMBB
      IGLBE =IGLBB+NGLBP-1
      WRITE(IUNT) ROW,RSIZE,(SUM1(I),I=IARCB1,IARCE1),                  &
     &                      (SUM1(I),I=IARCB2,IARCE2,IINC2),            &
     &                      (SUM1(I),I=IARCB3,IARCE3),                  &
     &          (SUM1(J),J=IGLBB,IGLBE),SUM2(NROW+NAMBB)
      IF(LPRTD)  WRITE(IOUT6,80200) PARML0(1,NROW),SUM2(NROW+NAMBB),    &
     &                      (SUM1(I),I=IARCB1,IARCE1),                  &
     &                      (SUM1(I),I=IARCB2,IARCE2,IINC2),            &
     &                      (SUM1(I),I=IARCB3,IARCE3),                  &
     &          (SUM1(J),J=IGLBB,IGLBE)
      IARCB1 =IARCB1 +(MAPARM-NAMBB)-NROW+1
      IARCB2 =IARCB2 +1
      IARCB3 =IARCB3 +(MAPARM-NAMBB)-NROW
      IGLBB =IGLBB +(MAPARM-NAMBB)-NROW
 1200 END DO
      NARCB1=NARCPA+1
      NARCB2=NARCPA+NAMBB
      JARCB1=1
!     JARCB2=2+NPVAL0(IXARC)-NPVAL0(IXBISA)
      JARCB2=2+IPVAL0(IXBISA)-1
      JARCE2=MAPARM-NAMBB+1
      NZER=NAMBB-1
      DO 1300 NROW=NARCB1,NARCB2
      ROW   =NROW
      RSIZE =NP-NROW+1
      NRECS =NRECS+1
      WRITE(IUNT) ROW,RSIZE,SUM1(JARCB1),(ZER(I),I=1,NZER),             &
     &                  (SUM1(I),I=JARCB2,JARCE2),SUM2(NROW-NARCPA)
      IF(LPRTD)  WRITE(IOUT6,80200) PARML0(1,NROW),SUM2(NROW-NARCPA),   &
     &                SUM1(JARCB1),(ZER(I),I=1,NZER),                   &
     &               (SUM1(I),I=JARCB2,JARCE2)
      JARCB1=JARCB1+(MAPARM-NAMBB+1)
      JARCB2=JARCB2+(MAPARM-NAMBB+1)
      JARCE2=JARCE2+(MAPARM-NAMBB+1)
      NZER=NZER-1
 1300 END DO
      NARCC1=NARCB2+1
      NARCC2=NARCP
      DO 1400 NROW=NARCC1,NARCC2
      ROW   =NROW
      RSIZE =NP-NROW+1
      NRECS =NRECS+1
      IARCE1 =IARCB1+NARCP-NROW
      IGLBE =IGLBB+NGLBP-1
      WRITE(IUNT) ROW,RSIZE,(SUM1(I),I=IARCB1,IARCE1),                  &
     &          (SUM1(J),J=IGLBB,IGLBE),SUM2(NROW)
      IF(LPRTD)  WRITE(IOUT6,80200) PARML0(1,NROW),SUM2(NROW),          &
     &          (SUM1(I),I=IARCB1,IARCE1),(SUM1(J),J=IGLBB,IGLBE)
      IARCB1 =IARCB1+MAPARM-NROW+1
      IGLBB =IGLBB+MAPARM-NROW
 1400 END DO
!
! OUTPUT ROWS CONTAINING ONLY GLOBAL INFORMATION
!
 1900 CONTINUE
      NARCP1=NARCP+1
      IF(NARCP1.GT.NP) GO TO 3000
      IGLBB=INDXNO(IGLBAL-NAMBB)+IGLBAL-NAMBB
      DO 2000 NROW=NARCP1,NP
      ROW   =NROW
      RSIZE =NP-NROW+1
      NRECS =NRECS+1
      IGLBE =IGLBB+NP-NROW
      WRITE(IUNT) ROW,RSIZE,(SUM1(I),I=IGLBB,IGLBE),SUM2(NROW)
      IF(LPRTD)  WRITE(IOUT6,80200) PARML0(1,NROW),SUM2(NROW),          &
     &          (SUM1(J),J=IGLBB,IGLBE)
      IGLBB =IGLBB+MAPARM-NROW+1
 2000 END DO
 3000 CONTINUE
      RETURN
70000 FORMAT(' EXECUTION TERMINATING IN EMTDAT. NUMBER OF AMBIGUITY')
70001 FORMAT(' BIASES (',I7,') IS GREATER THAN MAX ALLOWABLE (',I7,')')
80000 FORMAT('1',72X,'MATRIX LABELS'/73X,13('-')/('0',29X,6F18.1))
80100 FORMAT('0',2X,'LABELS',3X,'RIGHT HAND SIDE',1X,45('*'),           &
     &   ' MATRIX DATA ',45('*')/' ')
80200 FORMAT(1X,F16.0,1PD14.6,(T32,1P,6D17.9))
      END
