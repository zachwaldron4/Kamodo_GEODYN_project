!$GPMODA
      SUBROUTINE GPMODA(ATPAA,ATPAAC,DXG,DXA,SCRTCH,IAPARM,IGPARM,      &
     &                  NRMTOT)
!********1*********2*********3*********4*********5*********6*********7**
! GPMODA           00/00/00            0000.0    PGMR - ?
!
! FUNCTION:  MODIFY THE ARC CORRECTION VECTOR AFTER A GLOBAL
!            ITERATION HAS TAKEN PLACE.CALCULATE THE PRODUCT
!            OF THE ARC NORMAL INVERSE AND ARC-GLOBAL CROSS BLOCK
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   ATPAA    I    A    ARC INVERTED  NORMAL MATRIX
!   ATPAAC  I/O   A    ARC-COMMON CROSS BLOCK IN NORMAL MATRIX FOR INPUT
!                      ARC NORMAL INVERSE TIMES ARC GLOBAL CROSS BLOCK
!                      FOR OUTPUT
!   DXG      I    A    GLOBAL PARAMETER CORRECTION VECTOR
!   DXA      O    A    ARC PARAMETER CORRECTION FOR GLOBAL SOLUTION
!   SCRTCH  I/O   A    SCRATCH VECTOR IAPARM IN LENGTH
!   IAPARM   I    S    NUMBER OF ARC PARAMETERS
!   IGPARM   I    S    NUMBER OF GLOBAL PARAMETERS
!   NRMTOT   I    S    LENGTH OF FIRST ROW OF ENTIRE NORMAL MATRIX
!
! COMMENTS:
!
! NOTE:  ATPAAC,THE CROSS BLOCK MATRIX IS MODIFIED
!
!*********1*********2*********3*********4*********5*********6*********7*
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION DXA(1),ATPAA(1),ATPAAC(1),DXG(1),SCRTCH(1)
      DATA ZERO/0.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
!    REMAKE THE ATPAAC MATRIX INTO THE PRODUCT ATPAA*ATPACC.
! DO THIS ONE COLUMN AT A TIME.
!    TO GET THE EFFECT OF THE ICOL COLUMN OF THE ATPACC MATRIX,
! OPERATE ON THE LOWER PORTION OF THE ATPAA MATRIX FIRST (ICLD DIAG).
! DO THIS COLUMNWISE.THEN,FINALLY GET THE UPPER TRIANGULAR PORTION
! ROWISE.
!
      IAPRM1=IAPARM-1
      DO 100 ICOL=1,IGPARM
! PUT INTO SCRATCH THE EFFECT OF THE ICOL COLUMN OF ATPACC
!
!   THRU 50 - LOWER TRIANGULAR PORTION OF ATPAA COLUMNWISE
!
!   FIRST,THE EFFECT OF THE FIRST ELEMENT OF THE ICOL COLUMN TOGETHER
! WITH THE FIRST COLUMN OF THE ATPAA MATRIX
      DO 10 J=1,IAPARM
      SCRTCH(J)=ATPAA(J)*ATPAAC(ICOL)
   10 END DO
      IF(IAPARM.LE.1) GO TO 90
! NOW SUM INTO SCRATCH THE EFFECTS OF THE SECOND THRU IAPARM ELEMENTS
! OF ICOL COLUMN.MATCH THESE WITH THE TRIANGULATED PORTIONS OF THE
! SECOND THRU IAPARM ROWS OF THE ATPAA MATRIX.
      ICOLSR=ICOL+NRMTOT-1
      IROWSR=NRMTOT
      IRCL=NRMTOT-1
      IROWL=IAPRM1
      DO 50 IROW=2,IAPARM
      IROW1=IROW-1
      DO 30 J=1,IROWL
      SCRTCH(IROW1+J)=SCRTCH(IROW1+J)+ATPAA(IROWSR+J)*ATPAAC(ICOLSR)
   30 END DO
      IROWSR=IROWSR+IRCL
      IRCL=IRCL-1
      ICOLSR=ICOLSR+IRCL
      IROWL=IROWL-1
   50 END DO
!
!   THRU 100 UPPER TRIANGULAR PORTION ROWISE
!
      IROWSR=1
      IRL=NRMTOT
      IROWL=IAPRM1
      ICOLSR=ICOL+NRMTOT-1
!
      DO 80 IROW=1,IAPRM1
      ICOLS=ICOLSR
      ICLS=IRL-2
!
      DO 60 J=1,IROWL
      SCRTCH(IROW)=SCRTCH(IROW)+ATPAA(IROWSR+J)*ATPAAC(ICOLS)
      ICOLS=ICOLS+ICLS
      ICLS=ICLS-1
   60 END DO
      IROWSR=IROWSR+IRL
      ICOLSR=ICOLSR+IRL-2
      IRL=IRL-1
      IROWL=IROWL-1
   80 END DO
! LOAD SCRATCH INTO THE ICOL COLUMN OF ATPAAC
   90 CONTINUE
      ICL=NRMTOT-1
      ICOLSR=ICOL
      DO 95 J=1,IAPARM
      ATPAAC(ICOLSR)=SCRTCH(J)
      ICOLSR=ICOLSR+ICL
      ICL=ICL-1
   95 END DO
  100 END DO
! GET THE ARC CORRECTION
      ICOLSR=0
      ICL=NRMTOT-1
      DO 200 IROW=1,IAPARM
      DXA(IROW)=ZERO
      DO 150 J=1,IGPARM
      DXA(IROW)=DXA(IROW)+ATPAAC(ICOLSR+J)*DXG(J)
  150 END DO
      ICOLSR=ICOLSR+ICL
      ICL=ICL-1
  200 END DO
      RETURN
      END
