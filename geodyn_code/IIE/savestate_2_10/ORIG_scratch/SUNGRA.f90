!$SUNGRA
      SUBROUTINE SUNGRA(ISAT,X,LORBIT,DXDD,VMATRX)
!********1*********2*********3*********4*********5*********6*********7**
! SUNGRV        83/08/08            8308.0    PGMR - D. ROWLANDS
!
! FUNCTION:   (1) CALCULATE THIRD BODY PT MASS EFFECTS ON AN ASTEROID
!                 WHEN THE ASTEROID IS THE CENTRAL BODY AND THE CENTRAL
!                 BODY ORBIT IS BEING COMPUTED
!             (2) IN THE CASE OF DATA REDUCTION RUNS,CALCULATE THE EFFEC
!                 OF (1) ON THE VMATRX
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   ISAT     I         INDICATES THAT THE CALL IS BEING MADE FOR THE
!                      ISAT TIME WITHIN A CALL TO F
!   X        I         POSITION OF THE ISAT SATELLITE IN TRUE OF
!                      INTEGRATION STEP TIME
!   LORBIT   I         .TRUE. IF COMPUTATIONS ARE BEING MADE FOR AN
!                      ORBIT GENERATOR RUN
!   DXDD     O         ACCELERATION DUE TO THIRD BODIES IN TRUE OF
!                      INTEGRATION
!   VMATRX  I/O        TRUE OF INTEGRATION STEP VMATRX WITHOUT THIRD
!                      BODY EFFECTS AS INPUT
!                      TRUE OF INTEGRATION STEP VMATRX WITH EFFECTS OF
!                      THIRD BODIES SUMMED IN AS OUTPUT
!
! COMMENTS:
!
! RESTRICTIONS:  ROUTINE USES INFORMATION FROM ACALL TO SUNGRAV THAT
!                HAS BEEN MADE AT THE SAME TIME STEP FOR THE BODY
!                THAT ORBITS THE ASTEROID
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION BDSAT(3),XLT(3,999),VMATRX(3,3),VM(6),KGPT(999)
      DIMENSION DXDD(3),DXDDIO(3),X(3)
      DIMENSION QDTRUE(7,999)
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CRM5OE/RM5OE(9)
      COMMON/EPHMPT/NTOT,NTOTS,NNPDPR,NUTORD,IBODDG(8),IGRPDG(4),       &
     &              NENPD,INRCD(2),KPLAN(11),NINEP,NEPHMR,NEPHEM,       &
     &              IBHINT,NINTBH,NXEPHP
      COMMON/EPHSET/EMFACT,DTENPD,DTINPD,FSC1EN,FSCENP(2),FSCINP(4),    &
     &   FSDINP(4),XEPHST
      COMMON/CBDTRU/BDTRUE(7,999)
      COMMON/IBODPT/IBDCF(999),IBDSF(999),IBDGM(999),IBDAE(999),    &
     &              IBDPF(999),                                     &
     &              ICBDCF,ICBDSF,ICBDGM,ICBDAE,ICBDPF,             &
     &              ITBDCF,ITBDSF,ITBDGM,ITBDAE,ITBDPF,NXBDPT
      COMMON/IDUAL/IASTSN
      COMMON/IEPHM2/ISUPL,ICBODY,ISEQB(2),MAXDEG,ITOTSE,IREPL(988), &
     &              NXEPH2
      COMMON/LCBODY/LEARTH,LMOON,LMARS
      COMMON/LEPHM2/LXEPHM
      COMMON/PLNETI/IPLNET(999),IPLNIN(999),MPLNGD(999),MPLNGO(999),   &
     &              IPLNZ(999),NXPLNI
      DATA ZERO/0.D0/,ONE/1.D0/,ONEP5/1.5D0/,THREE/3.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      DXDD(1)=ZERO
      DXDD(2)=ZERO
      DXDD(3)=ZERO
      IF(ISAT.GT.1) GO TO 350
!  CALCULATE  QUANTITIES NOT DEPENDENT UPON SATELLITE POSITION
!   UPDATE THE REQUIRED PLANET POSITIONS TO TRUE OF STEP FROM MEAN 50
      NBDG=0
!     DO 100 I=1,8
!  FOR THIS CALL THE SUN IS THE CENTRAL BODY; LIMIT I TO 7
      DO 100 I=1,7
      IF(IBODDG(I).LT.1) GO TO 100
      NBDG=NBDG+1
      KGPT(NBDG)=I
! THIS TAKES ADVANTAGE OF THE FACT THAT SUNGRV WAS RECENTLY CALLED FOR THE ASTER
! AS THE CENTRAL BODY
      QDTRUE(1,I)=BDTRUE(1,I)-BDTRUE(1,8)
      QDTRUE(2,I)=BDTRUE(2,I)-BDTRUE(2,8)
      QDTRUE(3,I)=BDTRUE(3,I)-BDTRUE(3,8)
      QDTRUE(4,I)=BDTRUE(4,I)
      RK2=QDTRUE(1,I)*QDTRUE(1,I)+QDTRUE(2,I)*QDTRUE(2,I)               &
     &   +QDTRUE(3,I)*QDTRUE(3,I)
      RK=SQRT(RK2)
      RK3=RK2*RK
      XLT(1,NBDG)=QDTRUE(1,I)/RK3
      XLT(2,NBDG)=QDTRUE(2,I)/RK3
      XLT(3,NBDG)=QDTRUE(3,I)/RK3
  100 END DO
! SINCE SUN WAS  CALCULATED LAST
      RS=RK
      IM=11
      IF(LMOON) IM=10
      DO 200 I=9,IM
      IF(IGRPDG(I-7).LT.1) GO TO 200
      QDTRUE(1,I)=BDTRUE(1,I)-BDTRUE(1,8)
      QDTRUE(2,I)=BDTRUE(2,I)-BDTRUE(2,8)
      QDTRUE(3,I)=BDTRUE(3,I)-BDTRUE(3,8)
      QDTRUE(4,I)=BDTRUE(4,I)
      NBDG=NBDG+1
      KGPT(NBDG)=I
      RK2=QDTRUE(1,I)*QDTRUE(1,I)+QDTRUE(2,I)*QDTRUE(2,I)               &
     &   +QDTRUE(3,I)*QDTRUE(3,I)
      RK=SQRT(RK2)
      RK3=RK2*RK
      XLT(1,NBDG)=QDTRUE(1,I)/RK3
      XLT(2,NBDG)=QDTRUE(2,I)/RK3
      XLT(3,NBDG)=QDTRUE(3,I)/RK3
  200 END DO
!
! SUPPLEMENTARY EPHEMERIS SECTION
        IF(LXEPHM) THEN
        KBD=11+IASTSN+ICBODY
      DO 250 I=12+IASTSN,KBD
      NBDG=NBDG+1
      KGPT(NBDG)=I
      QDTRUE(1,I)=BDTRUE(1,I)-BDTRUE(1,8)
      QDTRUE(2,I)=BDTRUE(2,I)-BDTRUE(2,8)
      QDTRUE(3,I)=BDTRUE(3,I)-BDTRUE(3,8)
      QDTRUE(4,I)=BDTRUE(4,I)
      RKB=QDTRUE(1,I)*QDTRUE(1,I)+QDTRUE(2,I)*QDTRUE(2,I)               &
     &   +QDTRUE(3,I)*QDTRUE(3,I)
      RB=SQRT(RKB)
      RKB3=RKB*RB
      XLT(1,NBDG)=QDTRUE(1,I)/RKB3
      XLT(2,NBDG)=QDTRUE(2,I)/RKB3
      XLT(3,NBDG)=QDTRUE(3,I)/RKB3
  250 END DO
       ENDIF
!
! SINCE MOON WAS  CALCULATED LAST
      RM2=RK2
      RM=RK
      RM3=RK3
  350 CONTINUE
!   CALCULATE THIRD BODY PT MASS EFFECTS
      DO 500 K=1,NBDG
      BDSAT(1)=QDTRUE(1,KGPT(K))-X(1)
      BDSAT(2)=QDTRUE(2,KGPT(K))-X(2)
      BDSAT(3)=QDTRUE(3,KGPT(K))-X(3)
      RKR2=BDSAT(1)*BDSAT(1)+BDSAT(2)*BDSAT(2)+BDSAT(3)*BDSAT(3)
      RKR=SQRT(RKR2)
      RKR3=RKR*RKR2
!   CALCULATE ACCELERATION
      DO 400 I=1,3
      DXDD(I)=DXDD(I)+(BDSAT(I)/RKR3-XLT(I,K))*QDTRUE(4,KGPT(K))
  400 END DO
      IF(LORBIT) GO TO 500
!   SUM THIRD BODY EFFECTS INTO VMATRX
!     RKR5=RKR3*RKR2
!     THREEX=THREE*QDTRUE(4,K)/RKR5
! ABOVE CAN OVERFLOW;IFSO,REPLACE WITH
      THREEX=(THREE/RKR2)*(QDTRUE(4,KGPT(K))/RKR3)
      ONEX=QDTRUE(4,KGPT(K))/RKR3
      VM(1)=THREEX*BDSAT(1)*BDSAT(1)-ONEX
      VM(2)=THREEX*BDSAT(1)*BDSAT(2)
      VM(3)=THREEX*BDSAT(1)*BDSAT(3)
      VM(4)=THREEX*BDSAT(2)*BDSAT(2)-ONEX
      VM(5)=THREEX*BDSAT(2)*BDSAT(3)
      VM(6)=THREEX*BDSAT(3)*BDSAT(3)-ONEX
      VMATRX(1,1)=VMATRX(1,1)+VM(1)
      VMATRX(2,1)=VMATRX(2,1)+VM(2)
      VMATRX(3,1)=VMATRX(3,1)+VM(3)
      VMATRX(1,2)=VMATRX(1,2)+VM(2)
      VMATRX(2,2)=VMATRX(2,2)+VM(4)
      VMATRX(3,2)=VMATRX(3,2)+VM(5)
      VMATRX(1,3)=VMATRX(1,3)+VM(3)
      VMATRX(2,3)=VMATRX(2,3)+VM(5)
      VMATRX(3,3)=VMATRX(3,3)+VM(6)
  500 END DO
      RETURN
      END
