      SUBROUTINE FBURN(TIMEE,ISATID,NEQN,NBURN,ISATBN,ITBURN,IBURND,    &
     &                 PARMV,PXDDOT,LSSTEP,IPSBRN,LLSTEP,XDDOT,ETUTC,   &
     &                 STEP)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!  FBURN - COMPUTE THE ACCELERATION DURING A FINITE BURN INTERVAL. ALSO
!          COMPUTE QUANTITITES NEEDED FOR THE VARUATIONAL EQUATIOBS TO
!          COMPUTE PARTIAL DERIVATIVES OF SATELLITE STATE WRT BURN
!          PARAMETERS
!
!  TIMEE  - SCALER INPUT. ET (TDT TIME) OF THIS CALL
!  NEQN   - SCALER INPUT. NUMBER OF FORVE MODEL PARAMTERS FOR THIS SAT
!  NBURN  - SCALER INPUT. NUMBER OF BURNS IN THIS RUN
!  ISATID - SCALER INPUT. SATELLITE ID FOR THIS CALL
!  ISATBN - ARRAY INPUT. SATELLITE IDS ASSOCIATED WITH EACH BURN
!  ITBURN - ARRAY INPUT. BURN TERMINATION TYPE.  1=> LENGTH, 2=>DV
!  IBURND - ARRAY INPUT/OUTPUT. BURN STATUS FOR ALL BURNS.
!           2=> BURN HAS COMPLETED. 0=>BURN HAS NOT YET COMPLETED
!           1=> BURN ALREADY UNDERWAY
!  PARMV  - ARRAY INPUT. ARRAY OF ALL PARAMTERS
!  PXDDOT - ARRAY INPUT/OUTPUT. ARRAY OF EXPLICIT PARTIALS FOR
!           ALL FORCE MODEL PARAMTERS.
!  LSSTEP - SCALER OUTUT. TRUE IFF THHIS CALL INITIATES A BURN
!  IPSBRN - SCALER OUTPUT. POINTER IN THE FORCE MODEL EXPLICIT PARTIAL
!           ARRAY TO THE FIRST PARAMTER IN THE ACTIVE BURN
!  LLSTEP - SCALER OUTUT. TRUE IFF THHIS CALL TERMINATES A BURN
!  XDDOT  - ARRAY OUTPUT. BURN ACCELERATION AT THIS STEP
!  ETUTC  - ARRAY INPUT. TABLE OF ETUTC DIFFERNCES
!  STEP   - SCALER INPUT. INTEGRATION STEP SIZE BRING USED
!
!
!  NOTE  - TWO OF THE BURN PARAMETERS, T0 AND TLEN (START TIME AND
!          LENGTH)  HAVE UNIQUE VARIATIONAL EQUATION INITIALIZATIONS.
!          THESE INITIALIZATIONS AFFECT THE INTEGRATION SUMS AND ARE
!          DONE IN SUBROUTINE SUMBRN. HERE IS A BRIEF DESCRIPTION.
!          AT THE START OF THE ARC, ALL FORCE MODEL PARAMETERS EXCEPT
!          THE SATELLITE STATE START FROM ZERO INITIAL VALUES. AT
!          THE START OF THE BURN, THE PARTIAL OF VELOCITY WRT THE T0
!          BURN PARAMETER IS SET TO THE NEGATIVE OF THE BURN
!          ACCELERATION (THE LATER THE BURN STARTS THE LESS THE
!          VELOCITY IS CHANGED BY THE STARTING ACCELERATION AND
!          dV=BURNACC*dt). AT THE END OF THE BURN,THE PARTIAL OF
!          VELOCITY WRT THE T0 BURN PARAMETER INCREMENTED BY THE
!          ENDING BURN VELOCITY (THE LATER THE BURN IS SHIFTED THE
!          MORE THE VELOCITY IS AFFECTED BY RGE ENDING ACCEL AND
!          dV=BURNACC*dt). AT THE END OF THE BURN THE PARTIAL OF
!          VELOCITY WRT THE TLEN PARAMETER IS SET TO THE ENDING
!          BURN ACCELERATION MUCH AS WAS DONE FOR T0.
!  NOTE  - ALL OF THE BURN PARAMETERS WITH THE ECEPTION OF TLEN
!          HAVE EXPLICIT PARTIALS
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/ARCPAR/MAXSEL,NSEL,MAXDEL,NDEL,MAXMET,NMETDT,NSATID,       &
     &              IATDEN,ISATEQ,ITRMAX,ITRMIN,ITRMX2,                 &
     &              MJDSRF,MREFSY,NTDDR,NBTOTL,IDRAGM,IMRNUT,           &
     &              NXARCP
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/SETAPT/                                                    &
     &       JSADRG(1),JSASRD(1),JSAGA(1),JFAADJ,JTHDRG,JACBIA,JATITD,  &
     &       JDSTAT,JDTUM ,                                             &
     &       JSACS, JSATID,JSALG,JSAGM,JSAKF,JSAXYP,JSADJ2,JSAPGM,      &
     &       JFGADJ,JLTPMU,JLTPAL,JL2CCO,JL2SCO,JL2GM,JLRELT,           &
     &       JLJ2SN,JLGMSN,JLPLFM,JL2AE,JSAXTO,JSABRN
      DIMENSION ISATBN(NBURN),ITBURN(NBURN),IBURND(NBURN)
      DIMENSION PARMV(1)
      DIMENSION PXDDOT(NEQN,3)
      DIMENSION XDDOT(3),XDDOTT(3)
      DIMENSION UVECT(3),UVDOT(3),RAVECT(3),DCVECT(3)
      DIMENSION ETUTC(1)
      DIMENSION FSECE(1),FSECU(1)
      DIMENSION THR(5)
      DIMENSION XMS(5)
      DIMENSION RA(5)
      DIMENSION DC(5)
      DIMENSION TAU(4)
      DIMENSION IYMD(1),IHM(1),SECX(1)
!!!!! DATA TEN6/10.D6/
!!!!! DATA TEN6/1.D6/
      DATA TEN6/1.D0/
      DATA TOL/0.001D0/
      DATA TIMEP/-99.D0/
      DATA ISATP/-99/
!
      LDUP=.FALSE.
      TEST=ABS(TIMEE-TIMEP)
      IF(TEST.LT.TOL.AND.ISATP.EQ.ISATID) LDUP=.TRUE.
      ISATP=ISATID
      TIMEP=TIMEE
!
      XDDOT(1)=0.D0
      XDDOT(2)=0.D0
      XDDOT(3)=0.D0
      LSSTEP=.FALSE.
      LLSTEP=.FALSE.
      IF(NPVAL(IXBURN).LE.0) RETURN
      IF(MREFSY.NE.1) THEN
        WRITE(6,6000)
        WRITE(6,6004)
        STOP
      ENDIF
!
!
      STEPH=STEP/2.D0
    5 CONTINUE
!
!
      NBRNTS=0

! SKIP OVER COMPLETED BURNS
!
      DO 10 I=1,NBURN
      IF(ISATID.NE.ISATBN(I)) GO TO 10
      JPT=I
      IF(IBURND(I).LT.2) GO TO 20
      NBRNTS=NBRNTS+1
  10  CONTINUE
      RETURN
  20  CONTINUE
!
! NOW SEE IF THIS INTEGRATION STEP BELONGS TO A BURN
!
      MJDS=TIMEE
      FSECE(1)=TIMEE-DBLE(MJDS)
      CALL UTCET(.FALSE.,1,MJDS,FSECE,FSECU(1),ETUTC)
      TIME=DBLE(MJDS)+FSECU(1)
      ITPT=IPVAL(IXBURN)+(JPT-1)*25
      DO 30 I=JPT,NBURN
      IPT=I
      IF(ISATID.EQ.ISATBN(I)) THEN
        NBRNTS=NBRNTS+1
      ELSE
        GO TO 25
      ENDIF
      TIMEB=PARMV(ITPT)
      TLEN=PARMV(ITPT+21)
      TDIF=TIME-TIMEB

      IF(TDIF.GT.-STEPH) THEN
        IF(IBURND(I).EQ.0) LSSTEP=.TRUE.
        IBURND(I)=1
        IF(ITBURN(I).NE.1) THEN
          WRITE(6,6000)
          WRITE(6,6001)
          WRITE(6,6002)
          WRITE(6,6003) I
          STOP
        ENDIF
        TDIF2=TIME-TIMEB-TLEN

!!!     CHECKING IF REPEATED TIMESTEP
!!      IF (.NOT.LSSTEP.AND.ABS(TDIF-PREV_TDIF).LT.STEP/100) THEN
!!!!!!     GO TO 25
!!         LDUP=.TRUE.
!!      ENDIF
!!      PREV_TDIF = TDIF

! CHECK IF TIME REMAINING IS LESS THAN OR EQUAL STEP SIZE.
! IF SO THIS IS THE LAST STEP
!
        IF(TDIF2.GE.-STEP) THEN
          IBURND(I)=2
          LLSTEP=.TRUE.
        ENDIF
        GO TO 40
      ENDIF
  25  CONTINUE
      ITPT=ITPT+25
  30  CONTINUE
      RETURN
  40  CONTINUE
!
! THIS INTEGRATION STEP BELONGS TO BURN # IPT
!
!
      TAU(1)=TIME-TIMEB
      IF(TAU(1).LT.0.D0) TAU(1)=0.D0
      IF(TAU(1).GT.TLEN) TAU(1)=TLEN
      TAU(2)=TAU(1)*TAU(1)
      TAU(3)=TAU(2)*TAU(1)
      TAU(4)=TAU(2)*TAU(2)
      DO I=1,5
       THR(I)=PARMV(ITPT+I)
       XMS(I)=PARMV(ITPT+I+5)
       RA(I)=PARMV(ITPT+I+10)
       DC(I)=PARMV(ITPT+I+15)
      ENDDO
      RAT=RA(1)
      DCT=DC(1)
      XMST=XMS(1)
      THRT=THR(1)
      DO I=1,4
       RAT=RAT+TAU(I)*RA(I+1)
       DCT=DCT+TAU(I)*DC(I+1)
       XMST=XMST-TAU(I)*XMS(I+1)/DBLE(I)
       THRT=THRT+TAU(I)*THR(I+1)
      ENDDO
      RAT=RAT*DEGRAD
      DCT=DCT*DEGRAD
      COSRA=COS(RAT)
      SINRA=SIN(RAT)
      COSDC=COS(DCT)
      SINDC=SIN(DCT)
      UVECT(1)=COSRA*COSDC
      UVECT(2)=SINRA*COSDC
      UVECT(3)=SINDC
      A=TEN6*THRT/XMST
      XDDOTT(1)=A*UVECT(1)
      XDDOTT(2)=A*UVECT(2)
      XDDOTT(3)=A*UVECT(3)
! EXPLICIT PARTIALS BELOW
! NOTE THAT IF THE CODE BELOW "30  CONTINUE" IS
! BEING EXECUTED, THEN A BURN IS ACTIVE DURING THIS
! INTEGRATION STEP. ALL 25 PARAMETERS FROM THIS
! BURN PERIOD ARE BEING ADJUSTED. PARAMETERS # 22
! (THE BURN LENGTH) NEVER HAS EXPLICIT PAERIALS.
! PARAMETERS 23-25 ARE NOT BEING USED CURRENTLY
!
      IPARPT=JSABRN+(NBRNTS-1)*25
      IPSBRN=IPARPT
!
      ADOT1=THR(2)+2.D0*TAU(1)*THR(3)+3.D0*TAU(2)*THR(4)                &
     &     +4.D0*TAU(3)*THR(5)
      ADOT1=TEN6*ADOT1/XMST
!
      ADOT2=XMS(2)+TAU(1)*XMS(3)+TAU(2)*XMS(4)+TAU(3)*XMS(5)
      ADOT2=A*ADOT2/XMST
!
      ADOT=ADOT1+ADOT2
!
      RADOT=RA(2)+2.D0*TAU(1)*RA(3)+3.D0*TAU(2)*RA(4)+4.D0*TAU(3)*RA(5)
      DCDOT=DC(2)+2.D0*TAU(1)*DC(3)+3.D0*TAU(2)*DC(4)+4.D0*TAU(3)*DC(5)
!
      UVDOT(1)=-RADOT*COSDC*SINRA-DCDOT*SINDC*COSRA
      UVDOT(2)= RADOT*COSDC*COSRA-DCDOT*SINDC*SINRA
      UVDOT(3)= DCDOT*COSDC
!
! EXPLICIT PARTIAL OF BURN START TIME
      PXDDOT(IPARPT,1)=-(ADOT*UVECT(1)+A*UVDOT(1))
      PXDDOT(IPARPT,2)=-(ADOT*UVECT(2)+A*UVDOT(2))
      PXDDOT(IPARPT,3)=-(ADOT*UVECT(3)+A*UVDOT(3))
!
! EXPLICIT PARTIAL OF THRUST COEFFICENTS
      IPARPT=IPARPT+1
      PXDDOT(IPARPT,1)=(TEN6/XMST)*UVECT(1)
      PXDDOT(IPARPT,2)=(TEN6/XMST)*UVECT(2)
      PXDDOT(IPARPT,3)=(TEN6/XMST)*UVECT(3)
      DO I=1,4
        IPARPT=IPARPT+1
        PXDDOT(IPARPT,1)=TAU(1)*PXDDOT(IPARPT-1,1)
        PXDDOT(IPARPT,2)=TAU(1)*PXDDOT(IPARPT-1,2)
        PXDDOT(IPARPT,3)=TAU(1)*PXDDOT(IPARPT-1,3)
      ENDDO
!
! EXPLICIT PARTIAL OF MASS AT TIME 0
      IPARPT=IPARPT+1
      PXDDOT(IPARPT,1)=-XDDOTT(1)/XMST
      PXDDOT(IPARPT,2)=-XDDOTT(2)/XMST
      PXDDOT(IPARPT,3)=-XDDOTT(3)/XMST
!
! EXPLICIT PARTIALS OF MASS FLOW COEFFICIRNTS
      IPARPT=IPARPT+1
      PXDDOT(IPARPT,1)=TAU(1)*XDDOTT(1)/XMST
      PXDDOT(IPARPT,2)=TAU(1)*XDDOTT(2)/XMST
      PXDDOT(IPARPT,3)=TAU(1)*XDDOTT(3)/XMST
!
      IPARPT=IPARPT+1
      PXDDOT(IPARPT,1)=TAU(2)*XDDOTT(1)/(2.D0*XMST)
      PXDDOT(IPARPT,2)=TAU(2)*XDDOTT(2)/(2.D0*XMST)
      PXDDOT(IPARPT,3)=TAU(2)*XDDOTT(3)/(2.D0*XMST)
!
      IPARPT=IPARPT+1
      PXDDOT(IPARPT,1)=TAU(3)*XDDOTT(1)/(3.D0*XMST)
      PXDDOT(IPARPT,2)=TAU(3)*XDDOTT(2)/(3.D0*XMST)
      PXDDOT(IPARPT,3)=TAU(3)*XDDOTT(3)/(3.D0*XMST)
!
      IPARPT=IPARPT+1
      PXDDOT(IPARPT,1)=TAU(4)*XDDOTT(1)/(4.D0*XMST)
      PXDDOT(IPARPT,2)=TAU(4)*XDDOTT(2)/(4.D0*XMST)
      PXDDOT(IPARPT,3)=TAU(4)*XDDOTT(3)/(4.D0*XMST)
!
! EXPLICIT PARTIALS OF RA COEFFICIENTS
      RAVECT(1)=-A*COSDC*SINRA*DEGRAD
      RAVECT(2)= A*COSDC*COSRA*DEGRAD
      RAVECT(3)= 0.D0
      IPARPT=IPARPT+1
      PXDDOT(IPARPT,1)=RAVECT(1)
      PXDDOT(IPARPT,2)=RAVECT(2)
      PXDDOT(IPARPT,3)=RAVECT(3)
      DO I=1,4
        IPARPT=IPARPT+1
        PXDDOT(IPARPT,1)=RAVECT(1)*TAU(I)
        PXDDOT(IPARPT,2)=RAVECT(2)*TAU(I)
        PXDDOT(IPARPT,3)=RAVECT(3)*TAU(I)
      ENDDO
!
! EXPLICIT PARTIALS OF DC COEFFICIENTS
      DCVECT(1)=-A*SINDC*COSRA*DEGRAD
      DCVECT(2)=-A*SINDC*SINRA*DEGRAD
      DCVECT(3)= A*COSDC*DEGRAD
      IPARPT=IPARPT+1
      PXDDOT(IPARPT,1)=DCVECT(1)
      PXDDOT(IPARPT,2)=DCVECT(2)
      PXDDOT(IPARPT,3)=DCVECT(3)
      DO I=1,4
        IPARPT=IPARPT+1
        PXDDOT(IPARPT,1)=DCVECT(1)*TAU(I)
        PXDDOT(IPARPT,2)=DCVECT(2)*TAU(I)
        PXDDOT(IPARPT,3)=DCVECT(3)*TAU(I)
      ENDDO

!
! MIDDLE OF A BURN
!
      IF(.NOT.LSSTEP.AND..NOT.LLSTEP) THEN
         XDDOT(1)=XDDOTT(1)
         XDDOT(2)=XDDOTT(2)
         XDDOT(3)=XDDOTT(3)
         NSTEP=NSTEP+1
         FACT=2.D0
!
! REDUCE FACT FOR 1ST oR LAST STEP IN THE MIDDLE
         IF(NSTEP.EQ.1) FACT=1.D0
         TEST=-2.D0 * STEP
         IF(TDIF2.GE.TEST) FACT=1.D0
!
         IF(.NOT.LDUP) THEN
            DVX=DVX+FACT*XDDOT(1)
            DVY=DVY+FACT*XDDOT(2)
            DVZ=DVZ+FACT*XDDOT(3)
         ENDIF

         RETURN
      ENDIF


!
! LAST STEP OF A BURN
!
      IF(LLSTEP.AND..NOT.LSSTEP) THEN
!
! CODE TO ACCOUNT FOR PARTIAL STEP AT THE END
!
         XTRA= -TDIF2/STEP
         XDDOT(1)=XDDOTT(1)*XTRA
         XDDOT(2)=XDDOTT(2)*XTRA
         XDDOT(3)=XDDOTT(3)*XTRA
!
         IF(LDUP) GO TO 5
         TLENM=DBLE(NSTEP)*STEP
         DVX=DVX*TLENM/DBLE(2*NSTEP-2)
         DVY=DVY*TLENM/DBLE(2*NSTEP-2)
         DVZ=DVZ*TLENM/DBLE(2*NSTEP-2)
         DVX=DVX+DVXS+(-TDIF2/STEP)*XDDOTT(1)
         DVY=DVY+DVYS+(-TDIF2/STEP)*XDDOTT(2)
         DVZ=DVZ+DVZS+(-TDIF2/STEP)*XDDOTT(3)


         MJDS=TIMEB
!!!!     FSECE(1)=TIMEE-DBLE(MJDS)
!!!!     CALL UTCET(.FALSE.,1,MJDS,FSECE,FSECU(1),ETUTC)
         FSECU(1)=TIMEB-DBLE(MJDS)
         CALL YMDHMS(MJDS,FSECU,IYMD,IHM,SECX,1)
         IF(IYMD(1).GT.1000000)IYMD(1)=IYMD(1)-1000000
         TMDS=DBLE(IYMD(1))*1.D06+DBLE(IHM(1))*1.D02+SECX(1)
         WRITE(6,6009)
         WRITE(6,6010) TMDS,TLEN
         WRITE(6,6011) DVX,DVY,DVZ
         DVM=SQRT(DVX*DVX+DVY*DVY+DVZ*DVZ)
         WRITE(6,6017) DVM
!
         DVX=0.D0
         DVY=0.D0
         DVZ=0.D0
         NSTEP=0

         GO TO 5
      ENDIF
!
!
!
! FIRST STEP OF A BURN
!
      IF(LSSTEP) THEN
!
! CODE TO ACCOUNT FOR PARTIAL STEP AT THE START
!
         XTRA=(STEP-TDIF)/STEP
         XDDOT(1)=XDDOT(1)+XDDOTT(1)*XTRA
         XDDOT(2)=XDDOT(2)+XDDOTT(2)*XTRA
         XDDOT(3)=XDDOT(3)+XDDOTT(3)*XTRA

         DVXS=((STEP-TDIF)/STEP)*XDDOTT(1)
         DVYS=((STEP-TDIF)/STEP)*XDDOTT(2)
         DVZS=((STEP-TDIF)/STEP)*XDDOTT(3)

         DVX=0.D0
         DVY=0.D0
         DVZ=0.D0
         NSTEP=0

      ENDIF
      RETURN
 6000 FORMAT(' EXECUTION TERMINATING IN FBURN')
 6001 FORMAT(' ONLY BURNS TERMINATED BT DELTA T')
 6002 FORMAT(' ARE CURRENTLY SUPPORTED')
 6003 FORMAT(' BURN # ',I6,' NOT A DELTA T')
 6004 FORMAT(' REFERENCE COORDINATE SYSTEM IS NOT J2000')
 6009 FORMAT(' ')
 6010 FORMAT(' FINITE BURN AT: ',F20.2,' WITH LENGTH ',F12.2,' SECONDS')
 6011 FORMAT(' RESULTS IN AN ACCUMULATED DELTA-V (M/S):',3F15.6)
 6017 FORMAT(' MAGNITUDE ',F15.6)
      END
