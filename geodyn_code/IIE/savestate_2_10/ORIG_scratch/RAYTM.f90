!$RAYTM
      SUBROUTINE RAYTM(IGPT,MJDSEC,CONNAM,ANGFAC,SCRTCH,GRDANG,         &
     &                 UANG,FAMP,IDOODN,NTDDOO)
!********1*********2*********3*********4*********5*********6*********7**
! RAYGRD           00/00/00            8604.0    PGMR - ?
!
! FUNCTION:  COMPUTES TIDAL QUANTITIES THAT ARE ONLY TIME DEPENDENT
!            (NO SATELLITE POSITION DEPENDENCY).
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   IGPT     I    S    GRID POINT NUMBER TO BE FILLED
!   MJDSEC   I    S    INTEGER TIME
!   ANGFAC   I    A    FACTORS TO MULTIPLY EACH OF THE 5 ARGUMENTS BY
!                      PLUS A -/+ PHASE FOR CERTAIN DOODSON NUMBERS
!   SCRTCH  I/O   A    SCRATCH ARRAY FOR INTERMEDIATE CALCULATION
!   GRDANG   O    A    ANGLE COMPUTED FROM THE LINEAR COMBINATION
!                      OF ASTRONOMICAL ANGLES (GIVEN AT GRID TIMES)
!   UANG     O    A    ANGLE TO BE ADDED TO GRDANG TO COMPENSATE
!                      FOR SIDE BANDS (GIVEN AT GRID TIMES)
!   FAMP     O    A    FACTOR TO MULTIPLLT AMPLITUDE OF THE TIDE TO
!                      UNIQUE DOODSON NUMBERS
!                      SIDEBANDS OR NOT
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      CHARACTER*4 CONC(1)
      SAVE
      COMMON/CTIDES/NTIDE,NSTADJ,NET,NETADJ,NETUN,NOT,NOTADJ,           &
     &   NOTUN ,NETUNU,NETADU,NOTUNU,NOTADU,NBSTEP,KTIDA(3),            &
     &   ILLMAX,NUNPLY,NNMPLY,NDOODN,MXTMRT,NRESP ,NXCTID
      COMMON/DTMGDN/TGTYMD,TMGDN1,TMGDN2,REPDIF,XTMGN
      DIMENSION ANGFAC(NDOODN,6)
      DIMENSION CONNAM(NDOODN)
      DIMENSION SCRTCH(NTDDOO,4)
      DIMENSION GRDANG(NDOODN,MXTMRT)
      DIMENSION UANG(NDOODN,MXTMRT,3)
      DIMENSION FAMP(NDOODN,MXTMRT,3)
      DIMENSION IDOODN(NDOODN,3)
      DIMENSION UX(1),FX(1)
      EQUIVALENCE(CONC(1),CONR)
!
!
!
      DATA CAPTA/0.4101437397284052D+00/,CAPTB/0.3168808892311202D-09/
      DATA H0A/0.4881627972896973D+01/,H0B/0.6283319510866408D+03/,     &
     &     H0C/0.5288347633542818D-05/
      DATA S0A/0.3316125578789226D-07/,S0B/0.1977458042509575D-04/,     &
     &     S0C/0.8399709144920216D+04/,S0D/0.4719969957617067D+01/
      DATA P0A/-0.2094395102393195D-06/,P0B/0.1802052452684145D-03/,    &
     &     P0C/0.7101804125081532D+02/,P0D/0.5835153231900137D+01/
      DATA XNA/-0.3490658503988659D-07/,XNB/-0.3630284844148205D-04/,   &
     &     XNC/ 0.3375714627564109D+02/,XND/-0.4523601602118369D+01/
      DATA PSA/0.5235987755982988D-07/,PSB/0.7853981633974482D-05/,     &
     &     PSC/0.3000255890763292D-01/,PSD/0.4908229408691244D+01/
      DATA NSDAY/86400/,PI2/1.570796326795D0/,ONE/1.D0/,ZERO/0.D0/
!
!
!
!***********************************************************************
! START OF EXECUPABLE CODE *********************************************
!***********************************************************************
!
!
      XSABD=DBLE(MJDSEC) +REPDIF
!
!   FIRST GET ANGULAR ARGUMENT OF ASTRONOMICAL FORCING FOR EACH TIDE
!
!
      CAPT=CAPTA+CAPTB*XSABD
!  MEAN LONGITUDE OF SUN
      H0=H0A+(H0B+H0C*CAPT)*CAPT
!   MEAN LONGITUDE OF  MOON
      S0=((S0A*CAPT-S0B)*CAPT+S0C)*CAPT+S0D
!   MEAN LONGITUDE OF LUNAR PRIGEE
      P0=((P0A*CAPT-P0B)*CAPT+P0C)*CAPT+P0D
!
      XN0=((XNA*CAPT+XNB)*CAPT+XNC)*CAPT+XND
!
      PS0=((PSA*CAPT-PSB)*CAPT+PSC)*CAPT+PSD
!
!    GET ANGLE FOR EACH FREQ AT GRID TIME
      DO 20 N=1,NDOODN
!!!!  SCRTCH(N,2)=ANGFAC(N,1)*H0+ANGFAC(N,6)
      SCRTCH(N,2)=ANGFAC(N,1)*H0
      SCRTCH(N,1)=SCRTCH(N,2)+ANGFAC(N,2)*S0
      SCRTCH(N,2)=SCRTCH(N,1)+ANGFAC(N,3)*P0
   20 END DO
      DO 60 N=1,NDOODN
      SCRTCH(N,1)=SCRTCH(N,2)+ANGFAC(N,4)*XN0
      GRDANG(N,IGPT)=SCRTCH(N,1)+ANGFAC(N,5)*PS0
   60 END DO
!
      P0DI=P0*90.D0/PI2
      H0D=H0*90.D0/PI2
      PS0D=PS0*90.D0/PI2
!
! OUR DOODSON NUMBERS CORESPOND TO TO THE NEGATIVE OF THE LONGITUDE
! OF THE MOON'S NODE (XN0 IS THE NEGATIVE OF THE LONGITUDE OF
! THE MOON'S NODE IN RADIANS). RDR's ROUTINE WANTS
! THE ACTUAL LONGITUDE (NOT THE NEGATIVE) IN DEGREES
!
      XN0D=-XN0*90.D0/PI2
!
! NOW GET SIDE BAND INFO
      DO 200 I=1,NDOODN
      UANG(I,IGPT,1)=0.D0
      UANG(I,IGPT,2)=0.D0
      UANG(I,IGPT,3)=0.D0
      FAMP(I,IGPT,1)=1.D0
      FAMP(I,IGPT,2)=1.D0
      FAMP(I,IGPT,3)=1.D0
      IF(IDOODN(I,2).EQ.0.AND.IDOODN(I,3).EQ.0) GO TO 200
      CONR=CONNAM(I)
      IF(IDOODN(I,2).EQ.1) THEN
        CALL fu_nodal(XN0D,P0DI,CONC,1,FX,UX)
        UANG(I,IGPT,2)=UX(1)*PI2/90.D0
        FAMP(I,IGPT,2)=FX(1)
      ENDIF
      IF(IDOODN(I,3).EQ.1) THEN
        CALL fu_group(PS0D,XN0D,P0DI,H0D,CONC,1,FX,UX)
        UANG(I,IGPT,3)=UX(1)*PI2/90.D0
        FAMP(I,IGPT,3)=FX(1)
      ENDIF
  200 END DO
      RETURN
      END
