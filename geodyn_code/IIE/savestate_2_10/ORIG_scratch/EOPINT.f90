
!$EOPINT
      SUBROUTINE EOPINT(MJDSC,FSEC,ETUTC,EOPDEP,EOPDPS)
!********1*********2*********3*********4*********5*********6*********7**
! EOPINT           91/03/04            9101.0    PGMR - SBL
!
!   FUNCTION:   COMPUTE THE "BIH" VALUE OF EARTH ORIENTATION
!               PARAMETERS AT THE DESIRED EPOCH BASED ON
!               BIQUADRATIC INTERPOLATION UPON BIH TABULATED
!               VALUES.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSC    I    S    TIME IN INTEGRAL SECONDS FROM GEODYN REF. TIME
!   FSEC     I         REMAINING SECONDS FROM MJDSC
!   ETUTC    I         TABLE OF ETUTC VALUES
!   EOPDEP   O         BIQUADRATICALLY INETRPOLATED EOPDEP VALUE
!                      AT EPOCH MJDSC+FSEC, POLE OFFSETS IN
!                      LONGITUDE
!   EOPDPS   O         BIQUADRATICALLY INTERPOLATED EOPDPS VALUE
!                      AT EPOCH MJDSC+FSEC, POLE OFFSETS IN
!                      OBLIQUITY
!
!   RESTRICTIONS:      EPHEMERIS BUFFER MUST BE POSITIONED CORRECTLY
!                      (CALL BUFPOS)
!   REFERENCE:   THIS ROUTINE IS BASICALLY THE SAME AS BIH WHICH
!                WAS WRITTEN BY DAVE ROWLANDS
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/EPHM/FSECXY(1),XP(261),YP(261),A1UT(261),EP(261),EC(261),  &
     &            FSCFLX(1),FLUXS(36),AVGFLX(36),FLUXM(288),FSECEP(1),  &
     &            EPHP(816),EPHN(96),ELIB(120),BUFT(2700),FSECNP(4)
      COMMON/EPHMPT/NTOT,NTOTS,NNPDPR,NUTORD,IBODDG(8),IGRPDG(4),       &
     &              NENPD,INRCD(2),KPLAN(11),NINEP,NEPHMR,NEPHEM,       &
     &              IBHINT,NINTBH,NXEPHP
      DIMENSION FSECET(1),FSCUTC(1),ETUTC(1)
      DATA SIXTH/.1666666666666667D0/
      DATA ETA1/32.1496183D0/,ONE/1.D0/,THIRD/.3333333333333333D0/
      DATA FTHRD/1.333333333333333D0/
      DATA C3600/3600.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! CALCULATE INTERVAL IN SECONDS
      XINT=IBHINT*C3600
      XINTI=ONE/XINT
      XINTT=(IBHINT*(NINTBH-2))*C3600
      IPOINT=1
!   GET UTC TIME TO BE ABLE TO USE TABLE VALUES OF THE POLE
      FSECET(1)=FSEC
      CALL UTCET(.FALSE.,1,MJDSC,FSECET,FSCUTC,ETUTC)
! TTIME IS TIME OF FIRST POINT IN BUFFER FROM BIH TABLE
      TTIME=FSECXY(1)+XINTT
      TIME=MJDSC+FSCUTC(1)
!   FIGURE WHICH OF 2 BUFFERS WILL BE USED INCOMPUTATIONS
      IF(TIME.GT.TTIME) IPOINT=IPOINT+NTOT
      TTIME=FSECXY(IPOINT)
! FIND TIME BETWEEN FIRST POINT IN BUFFER AND OBS TIME
      DT=TIME-TTIME
! FIND THE NUMBER OF POINTS IN BUFFER IN INTERVAL DT
      IDX=INT(DT*XINTI)
! POINTER TO FIRST BUFFERED PT TO THE LEFT OF OBS VALUE
      IDX2=IDX+IPOINT
! POINTER TWO TO THE LEFT OF OBS VALUE
      IDX1=IDX2-1
! POINTER ONE TO THE RIGHT OF OBS VALUE
      IDX3=IDX2+1
! POINTER TWO TO THE RIGHT OF OBS VALUE
      IDX4=IDX3+1
! MAP TIME BETWEEN OBS AND TABLE PT INTO INTERVAL  0,1
      TTIME=TTIME+IDX*XINT
      DT1=((MJDSC-TTIME)+FSCUTC(1))*XINTI
!   FIGURE QUADRATIC COEFFCIENTS
      F2P= (EP(IDX3)+EP(IDX1))*SIXTH
      V11P= FTHRD*EP(IDX2)-F2P
      V21P=-THIRD*EP(IDX2)+F2P
      F2P= (EP(IDX4)+EP(IDX2))*SIXTH
      V12P= FTHRD*EP(IDX3)-F2P
      V22P=-THIRD*EP(IDX3)+F2P
      F2C= (EC(IDX3)+EC(IDX1))*SIXTH
      V11C= FTHRD*EC(IDX2)-F2C
      V21C=-THIRD*EC(IDX2)+F2C
      F2C= (EC(IDX4)+EC(IDX2))*SIXTH
      V12C= FTHRD*EC(IDX3)-F2C
      V22C=-THIRD*EC(IDX3)+F2C
      DT2=ONE-DT1
! INTERPOLATE FOR POLE VALUES
      EOPDEP=(DT1*(V12P+DT1**2*V22P)+DT2*(V11P+DT2**2*V21P))
      EOPDPS=(DT1*(V12C+DT1**2*V22C)+DT2*(V11C+DT2**2*V21C))
      RETURN
      END
