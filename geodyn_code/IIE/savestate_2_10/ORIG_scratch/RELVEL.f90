!$RELVEL
      SUBROUTINE RELVEL(VSM,COSTHG,SINTHG,UHAT,RNM,THDOTR,RELV,RDOT,    &
     &   NM,LNRELV,LNRDOT)
!********1*********2*********3*********4*********5*********6*********7**
! RELVEL           83/06/16            8306.0    PGMR - TOM MARTIN
!
! FUNCTION:  COMPUTE S/C VELOCITY IN E.C.F. COORD. RELATIVE
!            TO THE TRACKING STATION. ALSO COMPUTE THE
!            RANGE RATE.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   VSM      I    A    INERTIAL S/C VELOCITY
!   COSTHG   I    A    COSINES OF RIGHT ASCENSION OF GREENWICH
!   SINTHG   I    A    SINES  OF RIGHT ASCENSION OF GREENWICH
!   UHAT     I    A    E.C.F. UNIT VECTOR FROM STATION TO S/C
!   RNM      I    A    RANGE FROM STATION TO S/C
!   THDOTR   I    A    RANGE TIMES EARTH ROTATION RATE
!   RELV     O    A    S/C E.C.F. RELATIVE VELOCITY
!   RDOT     O    A    RANGE RATE FROM STATION TO S/C
!   NM       I    S    NUMBER OF TIMES FOR WHICH ELEV. TO BE COMPUTED
!   LNRELV   I    S    .T.-RELATIVE VELOCITY PRECOMPUTED
!                      .F.-COMPUTATION OF REL. VEL. REQUIRED
!   LNRDOT   I    S    .T.-RANGE RATE PRECOMPUTED
!                      .F.-COMPUTATION OF RANGE RATE REQUIRED
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/CTHDOT/THDOT
      DIMENSION VSM(MINTIM,3),COSTHG(NM),SINTHG(NM),UHAT(NM,3),RNM(NM), &
     &   THDOTR(NM),RELV(NM,3),RDOT(NM)
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! BYPASS RELATIVE VELOCITY COMPUTATION UNLESS REQUIRED
      IF(LNRELV) GO TO 10000
! COMPUTE RELATIVE VELOCITY
      CALL ECFIXP(VSM,COSTHG,SINTHG,RELV,MINTIM,NM,NM)
      DO 8800 N=1,NM
      THDOTR(N)=THDOT*RNM(N)
 8800 END DO
      DO 9800 N=1,NM
      RELV(N,1)= RELV(N,1)+UHAT(N,2)*THDOTR(N)
      RELV(N,2)= RELV(N,2)-UHAT(N,1)*THDOTR(N)
 9800 END DO
10000 CONTINUE
! BYPASS RANGE RATE COMPUTATION UNLESS REQUIRED
      IF(LNRDOT) RETURN
! RANGE RATE IS SIMPLY THE DOT PRODUCT OF RELATIVE VELOCITY
!       WITH UNIT RELATIVE POSITION
      CALL DOTPRD(RELV,UHAT,RDOT,NM,NM,NM,3)
      RETURN
      END
