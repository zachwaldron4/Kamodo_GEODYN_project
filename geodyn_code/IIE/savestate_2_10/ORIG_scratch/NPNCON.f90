!$NPNCON
      SUBROUTINE NPNCON
!********1*********2*********3*********4*********5*********6*********7**
! NPNCON           83/04/06            8202.3    PGMR - D. ROWLANDS
!
!  FUNCTION:   DETERMINE ELEMENTS OF PRECESION SINCE 1950.0
!              AND THE MEAN OBLIQUITY.THIS IS DONE ONLY AT
!              12 HR INTERPOLATION ENDPOINT TIMES.IN ADDITION,
!              DETERMINE THE COEFFICIENTS OF THE QUADRATIC
!              POLYNOMIALS NEEDED TO CALCULATE THE ABOVE
!              QUANTITIES AT OTHER TIMES, GIVEN THE NUMBER
!              OF EPHEMERIS SECONDS SINCE THE FIRST 12 HR
!              ENDPOINT IN THE CURRENT BUFFER.
!
!
! COMMENTS:    THIS SUBROUTINE CALCULATES THE ELEMENTS AT
!              THE FIRST TIME IN COMMON BLOCK ENDTRP USING
!              THE STANDARD FORMULATION. AFTER THIS, NEW
!              COEFFICIENTS ARE COMPUTED FOR A QUADRATIC
!              POLYNOMIAL WITH ORIGIN AT THE FIRST TIME IN
!              ENDTRP. THESE COEFFICIENTS ARE PLACED IN
!              COMMONS NUTCON & PRECON. FINALLY, THESE
!              COEFFICIENTS ARE USED TO CALCULATE THE
!              ELEMENTS AT OTHER TIMES IN ENDTRP.
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION PRE20(3,4),OBLQ20(4)
      COMMON/EPHMPT/NTOT,NTOTS,NNPDPR,NUTORD,IBODDG(8),IGRPDG(4),       &
     &              NENPD,INRCD(2),KPLAN(11),NINEP,NEPHMR,NEPHEM,       &
     &              IBHINT,NINTBH,NXEPHP
      COMMON/EPHSET/EMFACT,DTENPD,DTINPD,FSC1EN,FSCENP(2),FSCINP(4),    &
     &   FSDINP(4),XEPHST
      COMMON/NUTCON/CNUT(3)
      COMMON/PRECON/PRECON(3,3)
      COMMON/CNUTAT/SINEM(1),COSEM(1),SINDP(1),COSDP(1),SINET(1),       &
     &   COSET(1),SINZ(1),COSZ(1),SINTH(1),COSTH(1),SINEA(1),COSEA(1),  &
     &   SDSE(1),SDCE(1),CDSE(1),CDCE(1),SESD(1),SECD(1),CESD(1),       &
     &   CECD(1),SESE(1),SECE(1),CESE(1),CECE(1),CECT(1),SEST(1),       &
     &   CEST(1),SECT(1),CECZ(1),CESZ(1),SECZ(1),SESZ(1),STSZ(1),       &
     &   CTCZ(1),STCZ(1),CTSZ(1),CTCZCE(1),STSZSE(1),CTCZSE(1),         &
     &   CTSZCE(1),STCZCE(1),STSZCE(1),STCZSE(1),CTSZSE(1),SESDSE(1),   &
     &   SESDCE(1),SECDSE(1),SECDCE(1),CESDSE(1),CESDCE(1),CECDSE(1),   &
     &   CECDCE(1),EPSM(1),EPST(1),DPSI(1),Z(1),THTA(1),ETA(1),AK1(1),  &
     &   AK2(1),AK3(1),AK4(1),AK5(1),AK6(1),AK7(1),AK8(1),SUMMY(198)
      COMMON/DTMGDN/TGTYMD,TMGDN1,TMGDN2,REPDIF,XTMGN
      DATA PRE20/-.6594609497239083D-02,-.6593272394886178D-02,         &
     &           -.5732413675953063D-02,+.3542482751561867D-11,         &
     &           +.3541046232608777D-11,+.3079892623955878D-11,         &
     &           +.1314561901364292D-24,+.5172291724451325D-24,         &
     &           -.1716636502556173D-24,+.2776433098255333D-35,         &
     &           +.2808057100096779D-35,-.6453301800161981D-35/
      DATA OBLQ20/+.4092266784594708D+00,-.7191705826116799D-13,        &
     &            -.1849047160369022D-26,+.2796795870172751D-36/
      DATA ONE/1.D0/,TWO/2.D0/,THREE/3.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!     XTC=FSCINP(1)
      XTC=FSCINP(1) + ( TMGDN2-30000.0)*86400.D0
      ETA(1)=((PRE20(1,4)*XTC+PRE20(1,3))*XTC+PRE20(1,2))*XTC+PRE20(1,1)
      Z(1)=((PRE20(2,4)*XTC+PRE20(2,3))*XTC+PRE20(2,2))*XTC+PRE20(2,1)
      THTA(1)=((PRE20(3,4)*XTC+PRE20(3,3))*XTC+PRE20(3,2))*XTC          &
     &       +PRE20(3,1)
      EPSM(1)=((OBLQ20(4)*XTC+OBLQ20(3))*XTC+OBLQ20(2))*XTC             &
     &   +OBLQ20(1)
      PRECON(1,1)=ETA(1)
      PRECON(2,1)=Z(1)
      PRECON(3,1)=THTA(1)
      CNUT(1)=EPSM(1)
      DO 100 I=1,3
      PRECON(I,2)=PRE20(I,2)+XTC*(THREE*XTC*PRE20(I,4)+                 &
     &            TWO*PRE20(I,3))
      PRECON(I,3)=PRE20(I,3)+XTC*THREE*PRE20(I,4)
  100 END DO
      CNUT(2)=OBLQ20(2)+XTC*(THREE*XTC*OBLQ20(4)+TWO*OBLQ20(3))
      CNUT(3)=OBLQ20(3)+THREE*XTC*OBLQ20(4)
!    NOW GET ELEMENTS AT OTHER TIMES.
      DO 300 I=2,NINEP
      TM=FSCINP(I)-FSCINP(1)
      INDX=(I-1)*66+1
      ETA(INDX)=(PRECON(1,3)*TM+PRECON(1,2))*TM+PRECON(1,1)
      Z(INDX)=(PRECON(2,3)*TM+PRECON(2,2))*TM+PRECON(2,1)
      THTA(INDX)=(PRECON(3,3)*TM+PRECON(3,2))*TM+PRECON(3,1)
      EPSM(INDX)=(CNUT(3)*TM+CNUT(2))*TM+CNUT(1)
  300 END DO
      RETURN
      END
