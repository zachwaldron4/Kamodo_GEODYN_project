!$SUMNP
      SUBROUTINE SUMNP(PMPP,RESID,WT,NP,NRMTOT,NM,ATPA,ATPL,SCRTCH,     &
     &                 LAVOID)
!********1*********2*********3*********4*********5*********6*********7**
! SUMNP            85/10/08            0000.0    PGMR - TOM MARTI
!                  84/02/27            0000.0    PGMR - D.ROWLAND
!
! FUNCTION:  ACCUMULATE PARTIALS & RESIDUALS INTO THE
!            NORMAL MATRIX AND RIGHT HAND SIDE OF THE NORMAL EQ.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   PMPP     I         FULL ARRAY OF PARTIALS(INCLUDING ZEROS WHERE
!                      NECCESSARY) FOR EACH MEASUREMENT.SOMETIMES A
!                      INCLUDES ONLY ARC,SOMTIMES ARC&COMMON.
!   RESID    I         RESIDUAL ARRAY
!   WT       I         MEASUREMENT WEIGHT ARRAY
!   NP       I         NUMPER OF PARAMETERS
!   NRMTOT   I         MAXIMUM DIMENSION OF NORMAL MATRIX
!   NM       I         NUMBER OF MEASUREMENTS
!   ATPA     O         NORMAL MATRIX
!   ATPL     O         RIGHT HAND SIDE OF NORMAL EQUATIONS
!   SCRTCH       A     SCRATCH ARRAY
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      PARAMETER(NAM=1000)
      COMMON/BIAOR/NAMBB,NCYCB,NAMBBH
      COMMON/CGLBAR/LGDRAG,NXGLAR
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/EMAT  /EMTNUM,EMTPRT,EMTCNT,VMATRT,VMATS,VMATFR,FSCVMA,    &
     &              XEMAT
!
      DIMENSION PMPP(NP,NM),RESID(NM),WT(NM),ATPA(1),ATPL(NP),SCRTCH(NP)
      DIMENSION ATPLX(NAM),IS1(NAM),ATPAX(500500),SCR2(NAM)
      DIMENSION LAVOID(1)
!
      DATA ZERO/0.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! IF GLOBAL DRAG WAS REQUESTED AND THIS IS THE LAST INNER ITERATION
! OF AN EMAT RUN PUT THE DRAG PARTIALS AT THE END OF THE ARC PARTIALS
      IF(LGDRAG.AND.LSTINR.AND.(EMTNUM.GT.ZERO)) THEN
      IF(NPVAL0(IXDRAG).EQ.0) GOTO 80
! SET INDEX EQUAL TO THE POSITION OF THE FIRST DRAG PARAMETER
      INDEX=IPVAL0(IXDRAG)
      DO 70 INM=1,NM
! RUN THROUGH ALL OF THE DRAG PARAMETERS FOR A PARTICULAR MEASUREMENT
      DO 60 I=1,NPVAL0(IXDRAG)
! SET DRAG PARTIAL TO TEMP SPACE AND SHIFT OVER THE REST OF THE ARC
! PARTIALS.  THIS IS DONE FOR EACH DRAG PARAMETER.
      TEMP=PMPP(INDEX,INM)
      DO 50 J=INDEX,NPVAL0(IXARC)-1
      PMPP(J,INM)=PMPP(J+1,INM)
   50 END DO
! PUT TEMP (ARC DRAG PARTIAL) AT END OF ARC PARTIALS
      PMPP(NPVAL0(IXARC),INM)=TEMP
   60 END DO
   70 END DO
      ENDIF
   80 CONTINUE
!
!
!
      NA=0
      NAX=0
      DO 85 I=1,NP
      IF(LAVOID(I)) GO TO 85
      NA=NA+1
      IF(NA.GT.NAM) THEN
         WRITE(6,6000)
         WRITE(6,6001) NAM
 6000 FORMAT(' EXECUTION TERMINATING IN SUMNP. NUMBER OF PARAMETERS')
 6001 FORMAT(' FOR ONE BLOCK OF OBS GREATER THAN ',I5)
       STOP
      ENDIF
      IF(I.LE.NAMBB) NAX=NAX+1
      IS1(NA)=I
      ATPLX(NA)=ZERO
   85 END DO
      IF(NAX.GT.1) THEN
         WRITE(6,6010) NAX
         WRITE(6,6011)
 6010 FORMAT(' EXECUTION TERMINATING IN SUMNPX. NUMBER OF AMBIGUIITY')
 6011 FORMAT(' PARAMETERS FOR ONE BLOCK = ',I5,' (GREATER THAN 1)')
          STOP
      ENDIF
      NDIMP=NA*(NA+1)/2
      DO 90 I=1,NDIMP
      ATPAX(I)=ZERO
   90 END DO
!
!
!     NL=NRMTOT-NP
!     NL=0
!
      DO 1000 IOBS=1,NM
      IF(WT(IOBS).LE.ZERO) GO TO 1000
!     SCRATCH IS ATP (IN ATPA)
      DO 100 I=1,NA
      SCR2(I)=PMPP(IS1(I),IOBS)
      SCRTCH(I)=WT(IOBS)*SCR2(I)
  100 END DO
!$OMP PARALLEL DO DEFAULT (SHARED) PRIVATE(ISROW)
      DO 500 I=1,NA
      IL=NA+1-I
      ISROW=(I-1)*NA-((I-1)*(I-2))/2
!!!!!!!!!!!!!!!!!!DIR$ IVDEP
      DO 200 II=1,IL
      ATPAX(ISROW+II)=ATPAX(ISROW+II)+SCRTCH(I)*SCR2(I-1+II)
  200 END DO
  500 END DO
!$OMP END PARALLEL DO
      DO 600 I=1,NA
      ATPLX(I)=ATPLX(I)+RESID(IOBS)*SCRTCH(I)
  600 END DO
 1000 END DO
!
!
      IL=NA
      IS=1
      IF(NAX.EQ.1) THEN
         IL=NA-1
         IS=2
         IPT=1+(IS1(1)-1)*(NRMTOT-NAMBB+1)
         ATPA(IPT)=ATPA(IPT)+ATPAX(1)
         IPT=IPT-NAMBB
         DO 1100 J=2,NA
         ATPA(IPT+IS1(J))=ATPA(IPT+IS1(J))+ATPAX(J)
 1100    CONTINUE
      ENDIF
!
      N1ST=NAMBB*(NRMTOT-NAMBB+1)
      DO 1500 I=IS,NA
      ISTR1=(I-1)*NA-(I-1)*(I-2)/2
      ISX=IS1(I)-NAMBB
      ISTR2=(ISX-1)*(NRMTOT-NAMBB)-(ISX-1)*(ISX-2)/2
      ISTR2=ISTR2-ISX+1+N1ST-NAMBB
      ISTR3=I-1
      DO 1200 II=1,IL
      ATPA(ISTR2+IS1(ISTR3+II))=ATPA(ISTR2+IS1(ISTR3+II))               &
     &                         +ATPAX(ISTR1+II)
 1200 END DO
      IL=IL-1
 1500 END DO
      DO 1600 I=1,NA
      ATPL(IS1(I))=ATPL(IS1(I))+ATPLX(I)
 1600 END DO
      RETURN
      END
