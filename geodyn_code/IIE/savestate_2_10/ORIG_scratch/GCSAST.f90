!$GCSAST
      SUBROUTINE GCSAST(MJDSEC,FSEC,NM,ANGFAC,SPEED,SCRTCH,NTOLFR,      &
     &                  COSANG,SINANG)
!********1*********2*********3*********4*********5*********6*********7**
! GCSAST
!
! FUNCTION:  COMPUTES COSINES AND SINES OF A SERIES OF FREQUENCIES
!            CORRESPONDING TO DOODSON NUMBERS AT A SERIES OF TIMES
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I    S    START BLOCK SATELLITE INTEGER TIME
!   FSEC     I    A    FRACTIONAL SECONDS OF BLOCK MEASUREMENT TIMES
!   NM       I    S    NUMBER OF OBSERVATIONS IN THE BLOCK
!   ANGFAC   I    A    FACTORS TO MULTIPLY EACH OF THE 5 ARGUMENTS BY
!   SPEED    I    A    RATE OF ANGULAR ARGUMENT FOR EACH FREQUENCY
!   SCRTCH  I/O   A    SCRATCH ARRAY FOR INTERMEDIATE CALCULATION
!   NTOLFR   I    S    NUMBER OF FREQUENCIES
!   COSANG   O    A    COSINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   SINANG   O    A    SINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/EPHM/FSECXY(1),XP(261),YP(261),A1UT(261),EP(261),EC(261),  &
     &            FSCFLX(1),FLUXS(36),AVGFLX(36),FLUXM(288),FSECEP(1),  &
     &            EPHP(816),EPHN(96),ELIB(120),BUFT(2700),FSECNP(4)
      COMMON/DTMGDN/TGTYMD,TMGDN1,TMGDN2,REPDIF,XTMGN
      DIMENSION ANGFAC(NTOLFR,5),SPEED(NTOLFR)
      DIMENSION SCRTCH(NTOLFR,4)
      DIMENSION FSEC(NM)
      DIMENSION COSANG(NM,NTOLFR),SINANG(NM,NTOLFR)
!
      DATA CAPTA/0.4101437397284052D+00/,CAPTB/0.3168808892311202D-09/
      DATA H0A/0.4881627972896973D+01/,H0B/0.6283319510866408D+03/,     &
     &     H0C/0.5288347633542818D-05/
      DATA S0A/0.3316125578789226D-07/,S0B/0.1977458042509575D-04/,     &
     &     S0C/0.8399709144920216D+04/,S0D/0.4719969957617067D+01/
      DATA P0A/-0.2094395102393195D-06/,P0B/0.1802052452684145D-03/,    &
     &     P0C/0.7101804125081532D+02/,P0D/0.5835153231900137D+01/
      DATA XNA/-0.3490658503988659D-07/,XNB/-0.3630284844148205D-04/,   &
     &     XNC/ 0.3375714627564109D+02/,XND/-0.4523601602118369D+01/
      DATA PSA/0.5235987755982988D-07/,PSB/0.7853981633974482D-05/,     &
     &     PSC/0.3000255890763292D-01/,PSD/0.4908229408691244D+01/
      DATA NSDAY/86400/,PI2/1.570796326795D0/,ONE/1.D0/,ZERO/0.D0/
      DATA ETA1/32.1496183D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!  OFFU IS AN APPRX ET-UT1
      OFFU=A1UT(32)+ETA1
!
      NTOLF2=NTOLFR*2
!
!  GET INTEGER SECONDS AT BEGINNING OF DAY
      MM=MJDSEC+INT(REPDIF)
!     ISSBD=MOD(MJDSEC,NSDAY)
      ISSBD=MOD(MM,NSDAY)
!     ISABD=MJDSEC-ISSBD
      ISABD=MM-ISSBD
!  CONVERT TO START OF JULIAN DAY
      IF(ISSBD.GT.43200) GO TO 10
!     ISABD=ISABD-43200
!     ISSBD=ISSBD+43200
      GO TO 15
   10 CONTINUE
!     ISABD=ISABD+43200
!     ISSBD=ISSBD-43200
   15 CONTINUE
      XSABD=DBLE(ISABD)
      XSSBD=DBLE(ISSBD)-OFFU
!
!
      CAPT=CAPTA+CAPTB*XSABD
!  MEAN LONGITUDE OF SUN AT BEGINNING OF DAY
      H0=H0A+(H0B+H0C*CAPT)*CAPT
!   MEAN LONGITUDE OF  MOON AT BEGINNING OF DAY
      S0=((S0A*CAPT-S0B)*CAPT+S0C)*CAPT+S0D
!   MEAN LONGITUDE OF LUNAR PRIGEE AT BEGINNING OF DAY
      P0=((P0A*CAPT-P0B)*CAPT+P0C)*CAPT+P0D
!
      XN0=((XNA*CAPT+XNB)*CAPT+XNC)*CAPT+XND
!
      PS0=((PSA*CAPT-PSB)*CAPT+PSC)*CAPT+PSD
!
!    GET ANGLE FOR EACH FREQ AT START OF BLOCK
      DO 20 N=1,NTOLFR
      SCRTCH(N,2)=ANGFAC(N,1)*H0
      SCRTCH(N,1)=SCRTCH(N,2)+ANGFAC(N,2)*S0
      SCRTCH(N,2)=SCRTCH(N,1)+ANGFAC(N,3)*P0
   20 END DO
      DO 60 N=1,NTOLFR
      SCRTCH(N,1)=SCRTCH(N,2)+ANGFAC(N,4)*XN0
      SCRTCH(N,2)=SCRTCH(N,1)+ANGFAC(N,5)*PS0
   60 END DO
      DO 110 N=1,NTOLFR
      SCRTCH(N,1)=SCRTCH(N,2)+SPEED(N)*XSSBD
  110 END DO
! ALTER ANGLE TO GET SINES FROM COSINE FUNCTION
      DO 120 N=1,NTOLFR
      SCRTCH(N,2)=PI2-SCRTCH(N,1)
  120 END DO
! GET COS AND SIN FOR EACH FREQ AT START OF BLOCK
      DO 125 N=1,NTOLF2
      SCRTCH(N,3)=COS(SCRTCH(N,1))
  125 END DO
!   GET THE DERIVATIVE OF DELTS WRT TIME IN SECONDS
!   FIGURE TERM FOR HT AT OTHER TIMES
      DO 150 N=1,NTOLFR
      SCRTCH(N,1)=-SCRTCH(N,4)*SPEED(N)
      SCRTCH(N,2)=SCRTCH(N,3)*SPEED(N)
      DO 140 J=1,NM
      COSANG(J,N)=SCRTCH(N,3)+SCRTCH(N,1)*FSEC(J)
      SINANG(J,N)=SCRTCH(N,4)+SCRTCH(N,2)*FSEC(J)
  140 END DO
  150 END DO
      RETURN
      END
