!$STRTNC
      SUBROUTINE STRTNC(IORDER,H,NH,                                    &
     &   N3,IOL1,CIPV,                                                  &
     &   CIVV,XPNC,SUMXNC,IDSMXN,                                       &
     &   XDDTNC,IDXDDN,NRAT,IBCKNC,                                     &
     &   X,XDDOTH,XDDOT,SUMX,LORBIT,NEQN3,PX,PDDOTH,PXDDOT,SUMPX,       &
     &   N2,TOPXAT,LTPXAT,TOPXTH,LTPXTH,XDDTAC,PXDDAC,NEQNA)
!********1*********2*********3*********4*********5*********6*********7**
! STRTNC
!
! FUNCTION: THIS ROUTINE SHOULD BE CALLED BY COWLIN ONLY IF MULTIRATE
!           INTEGRATION IS BEING USED. BEFORE THIS ROUTINE IS CALLED
!           A SINGLE RATE INTEGRATION MUST BE STARTED AT THE SMALL
!           STEP SIZE. THAT INTEGRATION MUST HAVE BEEN CONTINUED
!           BACKWARD AND THEN RESTARTED AT A TIME SO THAT THE 1ST
!           ACCELERATION IN THE (SMALL STEP SIZE) RESTART MATCHES
!           THE TIME OF THE FIRST ACCELERATION IN THE BIG STEP
!           STARTING PROCEDURE. AFTER THAT THE INTEGRATION MUST HAVE
!           BEEN CARRIED FORWARD (AT THE SMALL STEPSIZE) TO THE
!           TIME OF THE FINAL STEP (STEP # IOL1) IN THE LARGE
!           STEP STARTER. THIS ROUTINE THEN COMPUTES THE STARTING
!           SUMS FOR THE SMALL STEP FORCES (SUFFIX NC) AS WELL
!           AS FOR THE LARGE STEP FORCES AND FOR THE VARIATIONAL
!           EQUATIONS. FINALLY, THE INTEGRATION SUMS AT ALL OF
!           THE SMALL STEP TIMES LEADING UP TO THE IOL1 LARGE STEP
!           ARE COMPUTED. THIS COMPLETES THE MULTIRATE STARTING
!           PROCEDURE.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   IORDER   I    S    ORDER OF COWELL INTEGRATION FOR ORBIT
!   H        I    S    INTEGRATION (LARGE) STEP SIZE (SECONDS) FOR ORBIT
!   NH       I    S    NUMBER OF STEPS (LAST DIMENDION) OF THE LARGE
!                      STEP SUM ARRAY
!   N3       I    S    NSAT*3
!   IOL1     I    S    IORDER - 1
!   CIPV     I    A    COWELL COEFFICIENTS FOR INTERPOLATING
!                      POSITION
!   CIVV     I    A    COWELL COEFFICIENTS FOR INTERPOLATING
!                      VELOCITY
!   XPNC     I    A    PREDICTED S/C POS. AND VEL. (SMALL STEP)
!   SUMXNC   O    A    INTEGRATION SUMS FOR ORBIT  (SMALL STEP)
!   IDSMXN   I    S    NUMBER OF STEPS COVERED BY SUMXNC
!   XDDTNC   I/O  A    SMALL FORCE ACCELERATIONS (ACCELEROMETRY
!                      COMPUTED AND/OR SEPARATELY INTEGRATED)
!   IDXDDN   I    S    NUMBER OF STEPS COVERED BY XDDTNC
!   NRAT     I    S    RATIO OF LARGE STEPSIZE TO SMALL STEPSIZE
!                      ALWAYS AN INTEGER
!   IBCKNC   O    S    NUMBER OF EXTRA BACK VALUES COMPUTED AT
!                      SMALL STEP SIZE
!   X        I    A    INITIAL STATE OF LARGE STEP
!   XDDOTH   I    A    LARGE STEP ACCELERATIONS THAT HAVE BEEN
!                      CAPTURED FROM SMALL STEP STARTING PROCEDURE
!                      (THESE ARE TOTAL ACCELERATIONS)
!   XDDOT    O    A    WORKING ARRAY OF LARGE STEP ACCELERATIONS
!                      WITH SMALL STEP ACCELERATIONS REMOVED
!   SUMX     O    A    INTEGRATION SUMS FOR LARGE STEP
!   LORBIT   I    S    TRUE IF NO PARTIALS ARE BEING INTEGRATED
!   NEQN3    I    S    N3*(NUMBER OF FORCE PARTIALS PER SAT)
!   PX       O    A    INITIAL STATE OF PARTIALS
!   PDDOTH   I    A    LARGE STEP PARTIAL DERIV ACCELERATIONS THAT HAVE
!                      BEEN CAPTURED FROM SMALL STEP STARTING PROCEDURE
!   PXDDOT   O    A    WORKING ARRAY OF LARGE STEP PARTIAL DERIV ACCELS
!   SUMPX    O    A    INTEGRATION SUMS FOR PARTIAL DERIVATIVES (PARTIAL
!                      DERIVATIVES ARE ONLY INTEGRATED AT LARGE STEP)
!   TOPXTH   I    A    TOPEX ATTITUDE INFORMATION CAPTURED FROM SMALL
!                      STEP STARTING PROCEDURE
!   LTPXTH   I    A    TOPEX ATTITUDE INFORMATION CAPTURED FROM SMALL
!                      STEP STARTING PROCEDURE
!   TOPXAT   O    A    TOPEX ATTITUDE INFORMATION ARRAY
!   LTPXTH   O    A    TOPEX ATTITUDE INFORMATION ARRAY
!   XDDTAC  I/O   A    COMPUTED VERSION OF ACCEREOMRTRY ACEELERATIONS
!   PXDDAC  I/O   A    PARTIAL DERVATIVES OF XDDTAC
!   NEQNA    I    S    NUMBER OF PARTIAL DERIVATIVES OF PXDDAC
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/NFORCE/NHRATE,NSURF,MPXHDT,MXHRG,MXFMG,NFSCAC,NXFORC
      DIMENSION CIPV(IOL1,IORDER),CIVV(IOL1,IORDER),                    &
     &   XPNC(N3,2),SUMXNC(N3,2,IDSMXN),                                &
     &   X(N3,2),XDDTNC(N3,IDXDDN),XDDOT(N3,IOL1),XDDOTH(N3,IOL1),      &
     &   SUMX(N3,2),PXDDOT(NEQN3,IOL1),PDDOTH(NEQN3,IOL1),              &
     &   PX(NEQN3,2),XDDTAC(N3,NFSCAC,IDXDDN),PXDDAC(NEQNA,N3,IDXDDN),  &
     &   SUMPX(NEQN3,2)
!    4   PX(NEQN3,2),XDDTAC(N3,NFSCAC,IDXDDN),PXDDAC(NEQN3,N3,IDXDDN),
      DIMENSION TOPXAT(N2,IOL1),TOPXTH(N2,IOL1)
      DIMENSION LTPXAT(N3,IOL1),LTPXTH(N3,IOL1)
      DIMENSION S(20),B(20)
!
      DATA ZERO/0.0D0/,ONE/1.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      N=N3/3
      N6=N3*2
      IOL2=IOL1-1
      IEVEN=MOD(IOL1,2)
      MF=IOL2/2-IEVEN
      MB=MF+1+IEVEN
      MID=MB+1
      MF1=MF+1
      H2=H**2
      HS=H/DBLE(NRAT)
      HS2=HS*HS
!
! NUMBER OF FREE ACCEL SLOTS FOR LARGE STEP
! INTEGRATOR AT END OF STARTER
      NXL=NH-1
      NXSI=NXL*NRAT
! NUMBER OF SMALL STEPS OCCUPIED BY STARTER
      NSSOS=IOL2*NRAT+1
! NUMBER OF EXTRA SMALL STEPS OCCUPIED BY STARTER
      NXSA=IDXDDN-NSSOS
! NUMBER OF STEPS TO BE SQUEEZED
      NSQZ=NXSI-NXSA
      NSQZ=0
!
! COMPUTE POINTER TO FIRST STEP TO BE SAVED IN XDDTNC
!     IP1=1+MB*NRAT-MB
      IP1=NSQZ+1
! COMPUTE INTERPOLATOR COEFFICIENTS FOR EACH TIME
      IS=-IOL1
      DO 100 I=1,IOL1
      IS=IS+1
      S(I)=DBLE(IS)
  100 END DO
!
! ZERO OUT XPNC
      DO 200 I=1,N6
      XPNC(I,1)=ZERO
  200 END DO
      CALL COEFV(S,B,IOL1,IORDER,CIPV,CIVV)
! COMPUTE POSITION AND VELOCITY SUMS AT STARTING EPOCH
      CALL STARTS(IORDER,IOL1,IOL2,HS,HS2,N3,MF1,MID,                   &
     &   XPNC,SUMXNC(1,1,1),CIPV,CIVV,XDDTNC(1,IP1))
!
!  COMPUTE NUMBER OF SMALL INTEGRATION STEPS NEEDED TO ADVANCE
!
!  NUMBER OF LARGE INTEGTATION STEPS FORWARD -1
!     NFORL1=MF-1
!  NUMBER OF SMALL INTEGRATION STEPS NEEDED TO GET
!  TO 1ST LARGE INTEGRATION STEP PAST ORG EPOCH
!     NS1=NRAT-MF
!
      NSTEP=NSSOS-(IP1+IOL2)
!     NSTEP=NS1+NRAT*NFORL1
      IBCKNC=0
      IPA=IP1+IOL1
  500 CONTINUE
      IF(IBCKNC.EQ.NSTEP) GO TO 700
      DO 600 J=1,N3
      SUMXNC(J,2,IBCKNC+2)=SUMXNC(J,2,IBCKNC+1)+XDDTNC(J,IPA)
      SUMXNC(J,1,IBCKNC+2)=SUMXNC(J,2,IBCKNC+2)+SUMXNC(J,1,IBCKNC+1)
  600 END DO
      IBCKNC=IBCKNC+1
      IPA=IPA+1
      GO TO 500
  700 CONTINUE
!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
!
! CORRECT EPOCH STATE VECTOR OF BIG STEP FOR FOR VALUE
! OF SMALL STATE AT SAME EPOCH
!
      MIDB=1+MB*NRAT
      MIDBS=MIDB-MB
      IC=6
      NK=-5
      CALL STARTC(IORDER,IOL1,IOL2,HS,HS2,N3,NK,IC,                     &
     &  XPNC,SUMXNC(1,1,MIDBS),CIPV,CIVV,XDDTNC(1,MIDBS))
         DO IQP=1,N6
            XPNC(IQP,1)=X(IQP,1)-XPNC(IQP,1)
         ENDDO
!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
!
! NOW TAKE CARE OF THE LARGE STEP SIZE ACCELERATIONS AND SUMS
      IPTR=1
      DO 800 IPTL=1,IOL1
      DO 750 J=1,N3
      XDDOT(J,IPTL)=XDDOTH(J,IPTL)-XDDTNC(J,IPTR)
      LTPXAT(J,IPTL)=LTPXTH(J,IPTL)
  750 END DO
      DO 755 J=1,N2
      TOPXAT(J,IPTL)=TOPXTH(J,IPTL)
  755 END DO
      DO 760 J=1,NEQN3
      PXDDOT(J,IPTL)=PDDOTH(J,IPTL)
  760 END DO
      IPTR=IPTR+NRAT
  800 END DO
      NEQN=NEQN3/N3
      NEQN6=NEQN3*2
      N6=N3*2
      NSAT=N3/3
! INITIALIZE EPOCH STATE AND PARTIAL DERIVATIVE ARRAYS
      CALL START0(LORBIT,N3,N6,NEQN,NEQN6,NSAT,XPNC,XPNC,PX,PX,ONE)
! COMPUTE POSITION AND VELOCITY SUMS
      CALL STARTS(IORDER,IOL1,IOL2,H,H2,N3,MF1,MID,                     &
     &   XPNC,SUMX,CIPV,CIVV,XDDOT)
      IF(.NOT.LORBIT) THEN
! COMPUTE SUMS FOR POSITION AND VELOCITY PARTIALS
         CALL STARTS(IORDER,IOL1,IOL2,H,H2,NEQN3,MF1,MID,               &
     &      PX,SUMPX,CIPV,CIVV,PXDDOT)
      ENDIF
!
! SHIFT OUT OF XDDTNC THE STEPS THAT WILL NOT BE USED
!     NSAVE=MB+1+MF*NRAT
!     DO 900 I=1,NSAVE
!     DO 850 J=1,N3
!     XDDTNC(J,I)=XDDTNC(J,IP1-1+I)
!     DO 840 JJ=1,NFSCAC
! THE FIRST TWO DIMENSIONS OF XDDTAC ARE NOT AS USED
! HERE, BUT THE LUMP SUM IS THE SAME
!     XDDTAC(J,JJ,I)=XDDTAC(J,JJ,IP1-1+I)
! 840 CONTINUE
! 850 CONTINUE
! 900 CONTINUE
!     write(6,*)' dbg STRTNC IDXDDN ',IDXDDN
!     IF(NEQNA.LE.0) RETURN
!     DO 1900 I=1,NSAVE
!     DO 1850 J=1,N3
!     DO 1800 K=1,NEQNA
!     PXDDAC(K,J,I)=PXDDAC(K,J,IP1-1+I)
!     write(6,*)'moving ',IP1-1+I,I,PXDDAC(K,J,IP1-1+I),K,J,IP1-1+I
!1800 CONTINUE
!1850 CONTINUE
!1900 CONTINUE
      RETURN
      END
