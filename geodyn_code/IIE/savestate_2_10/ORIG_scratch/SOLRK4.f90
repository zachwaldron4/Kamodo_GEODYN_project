!$SOLRK4
      SUBROUTINE SOLRK4(MJDSEC,FSEC,ELEM,R,RSQ,APLM,APGM,LORBIT,NEQN,   &
     &                  DXDD,GRPAR,STACC,LAJSP,LSTSUN,NTRANS,NDARKS,    &
     &                  AA,II,LL,ISATID,ALAPGM,VMAT)
!********1*********2*********3*********4*********5*********6*********7**
! SOLRK4           02/20/90            8912.0    PGMR- T. WILLIAMS
!                  07/03/91            9107.0          S. ROWTON
!
! FUNCTION:     COMPUTE ACCELERATION DUE TO SOLAR RADIATION
!               FOR GPS SATELLITES USING THE ROCK4 MODEL
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I         INTEGER EPHEMERIS SECONDS SINCE GEODYN REF.TIME
!   FSEC     I         FRACTIONAL REMAINING SECONDS
!   ELEM     I         TRUE OF DATE ELEMENTS
!   R        I
!   RSQ      I
!   APLM     I         (AREA/MASS)*(RADIATION PRESSURE AT 1 AU)*(AU**2)
!   APGM     I         (1)CR*APLM,(2)CR DOT*APLM,(3)CR DOUBLE DOT*APLM
!   LORBIT   I         .TRUE. IF ORBGEN RUN
!   NEQN     I         NUMBER OF FORCE MODEL PARAMETERS
!   DXDD     O         TRUE OF DATE ACCELERATION DUE TO SOLAR RAD
!   GRPAR    O         TRUE OF REFERENCE PARTIAL OF ACCELERATION
!                      WRT SOLAR COEFFCIENT
!   STACC    I         START TIMES FOR SOLAR RADIATION PERIODS
!   LAJSP    I         LOGICAL TELLING WHETHER A PARTICULAR PERIOD HAS
!                      BEEN ADJUSTED
!   NTRANS             NUMBER OF LIGHT TO DARK TRANSITIONS BEFORE/AFTER
!                        THIS CALL AS INPUT
!   NDARKS             NUMBER OF STEPS THAT HAVE BEEN IN DARKNESS
!                        BEFORE/AFTER THIS CALL
!   AA      I/O        REAL DYNAMIC ARRAY
!   II      I/O        INTEGER DYNAMIC ARRAY
!   LL      I/O        LOGICAL DYNAMIC ARRAY
!   ISATID             SAT ID ARRAY
!   ALAPGM   O         CORRECT CR*APLM SAVED FOR ALBEDO
!
!
! NOTE:  ALL FLAGS NEEDED IN THIS SUBROUTINE DEALING WITH THE
!        SATELLITE ARE SET IN SUBROUTINE ORBIT OR COWLIN
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION(A-H,O-Z),LOGICAL(L)
      SAVE
!
      LOGICAL LSTSUN,LCRSUN
!
      COMMON/GPSBET/BETCUT,BETGPS(200)
      COMMON/CBDTRU/BDTRUE(7,999)
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CIROCK/IGPSBK,IRKSTN
      COMMON/CIMDST/IDRLTM(200),ITRLTM(200),IRELON,IRLONA,NRLTMC,       &
     & IDRCK4(200),IRKONA,NROCK4,IRKRAD,NXIRTM
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/CLVIEW/LNPRNT(18),NXCLVI
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/CORA01/KFSEC0,KFSECB,KFSEC ,KFSECV,KH    ,KHV   ,KCTOL ,   &
     &              KRSQ  ,KVMATX,KCPP  ,KCPV  ,KCCP  ,KCCV  ,KCCPV ,   &
     &              KCCVV ,KXPPPP,KX    ,KPX   ,KSUMX ,KXDDOT,KSUMPX,   &
     &              KPXDDT,KAB   ,KPN   ,KAORN ,KSINLM,KCOSLM,KTANPS,   &
     &              KCRPAR,KVRARY,KXM   ,KXNP1 ,KXPRFL,KXM2  ,KXNNP1,   &
     &              KWRK  ,KFI   ,KGE   ,KB0DRG,KBDRAG,KAPGM ,KAPLM ,   &
     &              KCN   ,KSN   ,KSTID ,KTIDE ,KSTDRG,KSTSRD,KSTACC,   &
     &              KLGRAV,KGM   ,KAE   ,KFPL  ,KFEQ  ,KPLNPO,KPLNVL,   &
     &              KXEPOC,KCD   ,KCDDOT,KCR   ,KGENAC,KACN  ,KASN  ,   &
     &              KTHDRG,KCKEP ,KCKEPN,KXNRMZ,KXNRMC,KFSCEP,KFSCND,   &
     &              KAREA ,KXMASS,KRMSPO,KTCOEF,KTXQQ ,KTIEXP,KTXMM ,   &
     &              KTXLL1,KTXSN1,KTS2QQ,KT2M2H,KT2MHJ,KTXKK ,KTSCRH,   &
     &              KPXPK ,KAESHD,KCSAVE,KSSAVE,KCGRVT,KSGRVT,KXDTMC,   &
     &              KDNLT ,KTXSN2,KTNORM,KTWRK1,KTWRK2,KUNORM,KAERLG,   &
     &              KSINCO,KPARLG,KCONST,KBFNRM,KTDNRM,KCSTHT,KTPSTR,   &
     &              KTPSTP,KTPFYW,KPLMGM,KTPXAT,KEAQAT,KEAFSS,KEAINS,   &
     &              KACS  ,KECS  ,KSOLNA,KSOLNE,KSVECT,KSFLUX,KFACTX,   &
     &              KFACTY,KADIST,KGEOAN,KPALB ,KALBCO,KEMMCO,KCNAUX,   &
     &              KSNAUX,KPPER ,KACOSW,KBSINW,KACOFW,KBCOFW,KANGWT,   &
     &              KWT   ,KPLNDX,KPLANC,KTGACC,KTGDRG,KTGSLR,KWTACC,   &
     &              KWTDRG,KWTSLR,KTMACC,KTMDRG,KTMSLR,KATTUD,KDYACT,   &
     &              KACCBT,KACPER,KXDDNC,KXDDAO,KXNC  ,KXPPNC,KSMXNC,   &
     &              KXDDTH,KPDDTH,KXSSBS,KCPPNC,KEXACT,KXACIN,KXACOB,   &
     &              KPXHDT,KTPXTH,KPACCL,KTXSTA,KDELXS,KSMRNC,KPRX  ,   &
     &              KSMRNP,KDSROT,KXUGRD,KYUGRD,KZUGRD,KSUMRC,KXDDRC,   &
     &              KTMOS0,KTMOS, KTMOSP,KSMXOS,KSGTM1,KSGTM2,KSMPNS,   &
     &              KXGGRD,KYGGRD,KZGGRD,KXEGRD,KYEGRD,KZEGRD,KSSDST,   &
     &              KSDINS,KSDIND,KSSDSR,KSSDDG,KTATHM,KTAINS,KTAFSS,   &
     &              KSRAT ,KTRAT ,KHLDV ,KHLDA1,KHLDA4,KHLDA7,KQAST1,   &
     &              KQAST2,KQAST3,KQAST4,KQAST5,KQAST6,NXCA01
      COMMON/CORA03/KRFMT ,KROTMT,KSRTCH,KCFSC ,KTHG  ,KCOSTG,KSINTG,   &
     &              KDPSI ,KEPST ,KEPSM ,KETA  ,KZTA  ,KTHTA ,KEQN  ,   &
     &              KDPSR ,KSL   ,KH2   ,KL2   ,KXPUT ,KYPUT ,KUT   ,   &
     &              KSPCRD,KSPSIG,KSTAIN,KSXYZ ,KSPWT ,KDXSDP,KDPSDP,   &
     &              KXLOCV,KSTNAM,KA1UT ,KPLTIN,KPLTVL,KPLTDV,KPUTBH,   &
     &              KABCOF,KSPEED,KANGFC,KSCROL,KSTVEL,KSTVDV,KTIMVL,   &
     &              KSTEL2,KSTEH2,KSL2DV,KSH2DV,                        &
     &              KAPRES, KASCAL, KASOFF,KXDOTP,KSAVST,               &
     &              KANGT , KSPDT , KCONAM,KGRDAN,KUANG ,KFAMP ,        &
     &              KOFACT, KOTSGN,KWRKRS,KANFSD,KSPDSD,KPRMFS,KSCRFR,  &
     &              KCOSAS,KSINAS,KDXTID,KALCOF,KSTANF,KNUTIN,KNUTMD,   &
     &              KPDPSI,KPEPST,KDXDNU,KDPSIE,KEPSTE,NXCA03
      COMMON/CORI01/KMJDS0,KMJDSB,KMJDSC,KMJDSV,KIBACK,KIBAKV,KIORDR,   &
     &              KIORDV,KNOSTP,KNOCOR,KNSAT ,KN3   ,KNEQN ,KNEQN3,   &
     &              KNH   ,KNHV  ,KNSTPS,KNSTPV,KICPP ,KICPV ,KICCP ,   &
     &              KICCV ,KICCPV,KICCVV,KISUMX,KIXDDT,KISMPX,KIPXDD,   &
     &              KNMAX ,KNTOLD,KNTOLO,KICNT ,KISNT ,KMM   ,KKK   ,   &
     &              KJJ   ,KHH   ,KIBDY ,KSIGN1,KSIGN2,KLL   ,KQQ   ,   &
     &              KIORFR,KIPDFR,KITACC,KJSAFR,KJSPFR,KMJDEP,KMJDND,   &
     &              KNVSTP,KNSTRN,KNDARK,KTIPPT,KTJBDY,                 &
     &              KICNTA,KISNTA,KISHDP,KIPTC ,KIPTS ,KIGTSR,KIXTMC,   &
     &              KTIPTT,KTNOSD,KTQNDX,KTLL1 ,KTJJBD,KTITDE,KTCNTR,   &
     &              KTNN  ,KITACX,KNMOVE,KPANEL,KPLPTR,KNADAR,KNADSP,   &
     &              KNADDF,KNADEM,KNADTA,KNADTC,KNADTD,KNADTF,KNADTX,   &
     &              KITPMD,KSCATT,KITPAT,KILTPX,KEASBJ,KEANMP,KEANAN,   &
     &              KEAPMP,KEAPAN,KEAMJS,KEAPPP,KEAAAA,KICNTT,KISNTT,   &
     &              KTPGRC,KTPGRS,KTPC  ,KTPS  ,KALCAP,KEMCAP,KNSEG ,   &
     &              KICNTP,KISNTP,KNRDGA,KNRDDR,KNRDSR,KIRDGA,KIRDRG,   &
     &              KIRSLR,KSTRTA,KSTRTD,KSTRTS,KDYNPE,KACCPE,KIBCKN,   &
     &              KNRAT ,KIXDDN,KISMXN,KDXDDN,KDSMXN,KICPPN,KACSID,   &
     &              KNEQNH,KHRFRC,KPTFBS,KPTFSB,KIPXDA,KIACCP,KXSTAT,   &
     &              KPXST ,KSALST,KMAPLG,KNMBUF,KSTEPS,KSGMNT,KSATIN,   &
     &              KMEMST,KNEQNI,KBUFIN,KWEMGA,KWEMDR,KTPATS,KTANMP,   &
     &              KTAPPP,KTAMJS,KTASID,KGPSID,KNSSVA,KPNALB,KBRAX1,   &
     &              KBRAX2,KBRAX3,NXCI01
      COMMON/CRMI/RMI(9)
      COMMON/GPSSHD/LGPSHD,LGPSHO,LSHADS
      COMMON/LSTRT/LSTART
      COMMON/SETADJ/LSADRG(4),LSASRD(4),LSAGA(4),LSAGP,LSATID,LSAGM,    &
     &              LSADPM,LSAKF,LSADNX,LSADJ2,LSAPGM
      COMMON/SETAPT/                                                    &
     &       JSADRG(1),JSASRD(1),JSAGA(1),JFAADJ,JTHDRG,JACBIA,JATITD,  &
     &       JDSTAT,JDTUM ,                                             &
     &       JSACS, JSATID,JSALG,JSAGM,JSAKF,JSAXYP,JSADJ2,JSAPGM,      &
     &       JFGADJ,JLTPMU,JLTPAL,JL2CCO,JL2SCO,JL2GM,JLRELT,           &
     &       JLJ2SN,JLGMSN,JLPLFM,JL2AE,JSAXTO,JSABRN
      COMMON/SETIOR/IORDRG,IORSRD,IORGA,IPDDRG,IPDSRD,IPDGA
      COMMON/SETOPT/LSDRG,LSSRD,LSGA,LSORB,LTHDRG,LROCK4
      COMMON/STARTT/ESSTRT,FSSTRT
      COMMON/YAWNFO/YAWRP(200),YAWR(200),YAWRR(200),YAWBTP(200),        &
     &              YAWBID(200)
      DIMENSION XHELIO(3),DXDD(3),ELEM(6),ZPOS(3),UVECT(3,3),USUN(3)
      DIMENSION GRPAR(NEQN,3),APGM(1),STACC(1),TM(3),PART(3),LAJSP(1)
      DIMENSION AA(1),II(1),LL(1)
      DIMENSION ISATID(*)
      DIMENSION VMAT(3,6)
      DIMENSION XAXIS(3),YAXIS(3),ZAXIS(3)
      DIMENSION DXAXIS(3,3),DYAXIS(3,3),DZAXIS(3,3),DBDX(3)
      DIMENSION OXYZ(3,3),FSECE(1),FSECU(1),IYMD(1),IHM(1)
      DIMENSION SUNXYZ(3)
      DATA ZERO/0.0D0/,ONE/1.0D0/,TWO/2.0D0/,THREE/3.0D0/,FOUR/4.0D0/
      DATA FIVE/5.0D0/,SEVN/7.0D0/
      DATA ONEM5/1.0D-5/
      DATA LFIRST/.TRUE./
!
!
!**********************************************************************
! START OF EXECUTABLE CODE
!**********************************************************************
!
      JGPSBK=IGPSBK
      !IF(IGPSBK.GT.3) JGPSBK=3
      IF(JGPSBK.EQ.3) JGPSBK=2
      IF(JGPSBK.GT.3) JGPSBK=3
      IF( LFIRST ) THEN
         LFIRST = .FALSE.
         IF( IRKRAD .EQ. 0 ) THEN
            WRITE(6,*) 'NEW ROCK4 MODEL  ****'
         ELSE
            WRITE(6,*) 'OLD ROCK4 MODEL  ****'
         ENDIF
      ENDIF
      LCRSUN = .TRUE.
!
! *** COMPUTE VECTOR GOING FROM THE SATELLITE TO THE SUN
! *** NOTE: BOTH ELEM AND BDTRUE ARE IN THE TRUE OF DATE
! *** COORDINATE SYSTEM
      DO 1000 I=1,3
         XHELIO(I) = BDTRUE(I,8)-ELEM(I)
         ZPOS(I)   = -ELEM(I)
 1000 END DO
!CCCCCCCCCC
      RATIO=ONE
      RSSQ=XHELIO(1)**2+XHELIO(2)**2+XHELIO(3)**2
      RS=SQRT(RSSQ)
!
!     ....DETERMINE IF THE SATELLITE IS IN SUN OR SHADOW
!
      CALL SOLRAT(AA(KAESHD),II(KISHDP),FE,RS,RSSQ,R,RSQ,ELEM,RATIO)
      LTEST=.FALSE.
      IF(RATIO.GT..001D0.AND.RATIO.LT..999D0) LTEST=.TRUE.
      IF(LTEST) LSHADS=.TRUE.
!     IF(LGPSHD.AND.LTEST) THEN
      IF(LGPSHD) THEN
        BX=.5D0
        TESTB=2.D0
        DO 1005 I=1,200
        IDTEST=YAWBID(I)+.01D0
        IF(IDTEST.NE.ISATID(1)) GO TO 1005
        TESTB=YAWBTP(I)
        ISATPX=I
        GO TO 1010
 1005   CONTINUE
 1010   CONTINUE
!
! SAT SUN VECTOR INTO WORK
!
        RX=SQRT(ELEM(1)*ELEM(1)+ELEM(2)*ELEM(2)+ELEM(3)*ELEM(3))
        OXYZ(1,1)=ELEM(1)/RX
        OXYZ(2,1)=ELEM(2)/RX
        OXYZ(3,1)=ELEM(3)/RX
        OXYZ(1,3)=ELEM(2)*ELEM(6)-ELEM(3)*ELEM(5)
        OXYZ(2,3)=ELEM(3)*ELEM(4)-ELEM(1)*ELEM(6)
        OXYZ(3,3)=ELEM(1)*ELEM(5)-ELEM(2)*ELEM(4)
        RZ=SQRT(OXYZ(1,3)*OXYZ(1,3)+OXYZ(2,3)*OXYZ(2,3)                &
     &          +OXYZ(3,3)*OXYZ(3,3))
        OXYZ(1,3)=OXYZ(1,3)/RZ
        OXYZ(2,3)=OXYZ(2,3)/RZ
        OXYZ(3,3)=OXYZ(3,3)/RZ
        OXYZ(1,2)=OXYZ(2,3)*OXYZ(3,1)-OXYZ(3,3)*OXYZ(2,1)
        OXYZ(2,2)=OXYZ(3,3)*OXYZ(1,1)-OXYZ(1,3)*OXYZ(3,1)
        OXYZ(3,2)=OXYZ(1,3)*OXYZ(2,1)-OXYZ(2,3)*OXYZ(1,1)
        SUNXYZ(1)=BDTRUE(1,8)
        SUNXYZ(2)=BDTRUE(2,8)
        SUNXYZ(3)=BDTRUE(3,8)
!
        RSX2=SUNXYZ(1)*SUNXYZ(1)+SUNXYZ(2)*SUNXYZ(2)+SUNXYZ(3)*SUNXYZ(3)
        RSX=SQRT(RSX2)
        COSBC=(OXYZ(1,3)*SUNXYZ(1)+OXYZ(2,3)*SUNXYZ(2)                  &
     &        +OXYZ(3,3)*SUNXYZ(3))/RSX
        BETA=.5D0*ACOS(-1.D0)-ACOS(COSBC)
!
! FOR YAW BIAS NOT 0,-1,1,-2,2 THEN BIAS IS BIAS VALUE
      BX=TESTB
!
! BIAS OF 1 IMPLIES NORMAL BIAS, -1 IMPLIES ANTI-NORMAL BIAS
      IF(TESTB.LT.1.01D0.AND.TESTB.GT.0.99D0) BX=SIGN(0.5D0,BETA)
      RNBETA=-BETA
      IF(TESTB.GT.-1.01D0.AND.TESTB.LT.-0.99D0) BX=SIGN(0.5D0,RNBETA)
!
! BIAS OF +-1 IMPLIES BIAS OF SAME SIGN REGARDLESS OF BETA
      IF(TESTB.LT.-1.99D0.AND.TESTB.GT.-2.01D0) BX=-0.5D0
      IF(TESTB.GT.1.99D0.AND.TESTB.LT.2.01D0) BX=0.5D0
      IF(JGPSBK.EQ.3) BX=0.D0
!
!
!
! PROJECT SUN INTO XY PLANE OF OXYZ SYSTEM
! SAT POS VECTOR IS (1,0,0) IN THIS SYSTEM
        SUN1=OXYZ(1,1)*SUNXYZ(1)+OXYZ(2,1)*SUNXYZ(2)                    &
     &      +OXYZ(3,1)*SUNXYZ(3)
        SUN2=OXYZ(1,2)*SUNXYZ(1)+OXYZ(2,2)*SUNXYZ(2)                    &
     &      +OXYZ(3,2)*SUNXYZ(3)
! GET UNIT MIDNIGHT VECTOR
        RSX=SQRT(SUN1*SUN1+SUN2*SUN2)
        SUN1=-SUN1/RSX
        SUN2=-SUN2/RSX
! GET ORBIT ANGLE
        IF(SUN2.GT.0.D0) THEN
           OA=2.D0*ACOS(-1.D0)-ACOS(SUN1)
        ELSE
           OA=ACOS(SUN1)
        ENDIF
        IF(OA.LT..6981D0.AND.RATIO.GT..01D0.AND.LSHADS) LTEST=.TRUE.
        IF(OA.GT.2.9452D0.AND.OA.LT.3.665D0.AND.LSHADS) LTEST=.TRUE.
        BETAD=ABS(BETA*57.29577951D0)
        BETGPS(ISATPX)=BETAD
        OAD=ABS(OA*57.29577951D0-180.D0)
        IF(BETAD.LT..5D0.AND.OAD.LT..5D0) LTEST=.FALSE.
        IF(LTEST) THEN
!
          RD1=RS
          RD2=RX
!
          COSE=-XHELIO(1)*ELEM(1)-XHELIO(2)*ELEM(2)-XHELIO(3)*ELEM(3)
          COSE=COSE/(RD1*RD2)
          SINE=SIN(ACOS(COSE))
          BBIAS=ASIN(.0175D0*BX/SINE)
!
          SINOA=SIN(OA)
          COSOA=COS(OA)
          TANBET=TAN(BETA)
          YAB1=ATAN2(-TANBET,SINOA)
          YAB2=YAB1+BBIAS
          IF(YAB2.LT.0.D0) YAB2=YAB2+2.D0*ACOS(-1.D0)
          OADOT=.00014486D0
          BDOT=-.0175D0*BX*COSE*COS(BETA)*SINOA*OADOT
          BDOT=BDOT/(COS(BBIAS)*SINE*SINE*SINE)
          YABDOT=OADOT*TANBET*COSOA/(SINOA*SINOA+TANBET*TANBET)
          YABDOT=YABDOT+BDOT
          CNX=180.D0/ACOS(-1.D0)
          BETA=BETA*CNX
          IF(LGPSHO) THEN
             OA=OA*CNX
             YAB2=YAB2*CNX
             YABDOT=YABDOT*CNX
             ITIME=(ESSTRT+FSSTRT)
             FSECE(1)=(ESSTRT+FSSTRT)-DBLE(ITIME)
             CALL UTCET(.FALSE.,1,ITIME,FSECE,FSECU,AA(KA1UT))
             CALL YMDHMS(ITIME,FSECU,IYMD,IHM,FSECE,1)
!
             IYFLG = IYMD(1)/1000000
             IF(IYFLG.GT.0) IYMD(1) = IYMD(1) - IYFLG*1000000
!
             WRITE(43,7000) ISATID(1),ITIME,OA,RATIO,BETA,YAB2,YABDOT,  &
     &                      IYMD(1),IHM(1),FSECE(1)
 7000        FORMAT(I7,2X,I10,2X,F9.5,2X,F9.5,2X,F9.5,2X,F9.5,2X,       &
     &              F10.7,2X,I6,2X,I4,2X,F5.1)
         ENDIF
        ENDIF
      ENDIF
!
!
!
!
      IF(RATIO.LE.ZERO)THEN
! SATELLITE NOT IN SUN OR NOT AFFECTED BY SOLAR RADIATION
        LCRSUN=.FALSE.
        NDARKS=NDARKS+1
        DO 2000 I=1,3
 2000   DXDD(I)=ZERO
        IF(LORBIT) GO TO 2500
        KPART=JSASRD(1)
        DO 2200 J=1,IORSRD
        IF(.NOT.LSASRD(J)) GO TO 2200
        GRPAR(KPART,1)=ZERO
        GRPAR(KPART,2)=ZERO
        GRPAR(KPART,3)=ZERO
        KPART=KPART+1
 2200   CONTINUE
        IF(.NOT.LSASRD(4)) GO TO 2500
        DO 2400 I=1,IPDSRD
        IF(.NOT.LAJSP(I)) GO TO 2400
        GRPAR(KPART,1)=ZERO
        GRPAR(KPART,2)=ZERO
        GRPAR(KPART,3)=ZERO
        KPART=KPART+1
 2400   CONTINUE
 2500   CONTINUE
      ENDIF
!
! INCREMENT THE SUN-SHADOW TRANSITION COUNT
! CALL ROUTINE TO PRINT SHADOW CROSSING INFO
!   IF REQUESTED ON PRNTVU CARD (IIS)
!
      IF(LCRSUN.NEQV.LSTSUN)THEN
         NTRANS=NTRANS+1
         LSTSUN=LCRSUN
         IF(LNPRNT(15))GO TO 2550
         IF(.NOT.LSTART)THEN
           IF(LSTITR)THEN
             CALL PRTSHD(MJDSEC,FSEC,ISATID,LCRSUN,LSTSUN,AA)
           ENDIF
         ENDIF
      ENDIF
 2550 CONTINUE
!
      IF(RATIO.LE.ZERO)  RETURN
!CCCCCCC
! *** COMPUTE ANGLE BETWEEN THE POSITION VECTOR AND SUN VECTOR
      DOT1 = DOTPDT(XHELIO,ZPOS)
      DMHELI = SQRT(DOTPDT(XHELIO,XHELIO))
      DMELEM = SQRT(DOTPDT(ZPOS,ZPOS))
      DIVID  = DOT1/(DMHELI*DMELEM)
!     PRINT 99799, DIVID
!9799 FORMAT(' SOLRK4: DIVID ',D24.16)
      IF(DIVID.GT.ONE) DIVID = ONE
!     ....acos is generic for dacos and acos
      ANGLEA = ACOS(DIVID)
!
!     IF(JGPSBK.EQ.3) ANGLEA=-ANGLEA
!
!     THE ABOVE LINE IS COMMENTED OUT ALTHOUGH ANGLEA
!     DOES HAVE THE OPPOSITE SIGN FOR BLOCK IIR SATELLITES.
!     (SEE IGSMAIL 1653 FROM 1997)
!
!     ANGLEA CAN BE USED WITHOUT A SIGN FLIP FOR BLOCK IIR
!     THE RESULT WILL NOT AFFECT THE Z AXIS ACCELERATION.
!     THE RESULT WILL FLIP THE SIGN IF THA X AXIS ACCELERATION.
!     THE X AXIS ACCELERATION SHOULD GET FLIPPED IF THE SAME
!     ALGORITHM FOR ROTATING FROM BODY FIXED TO INERTIAL
!     IS USED FOR BLOCK IIR SATELLITES AS FOR OTHER BLOCK II
!     II SATELLITES
!
!
!     PRINT 98001, ANGLEA
!8001 FORMAT('SOLRK4: ANGLEA ',D24.12)
! *** CALCULATE THE SOLAR PRESSURE FORCE ON GPS SPACECRAFT USING THE
! *** FORMULAS WHICH INCLUDE THERMAL RADIATION
      TWOA = TWO*ANGLEA
      THREA= THREE*ANGLEA
      FOURA = FOUR*ANGLEA
      FIVEA = FIVE*ANGLEA
      SEVNA = SEVN*ANGLEA
      SATMAS = AA(KXMASS+IRKSTN-1)
! *** IF BLOCK I SATELLITE:
      IF(JGPSBK.EQ.1) THEN
! ***** IF NEWER THERMAL ROCK4 MODEL IS REQUESTED FOR BLOCK I
        IF(IRKRAD.EQ.0)THEN
           XLT=-4.55D0*ONEM5/SATMAS
           ZLT=-4.54D0*ONEM5/SATMAS
           XFORCE = -4.55D0*SIN(ANGLEA)+0.08D0*SIN(TWOA+0.9D0)-         &
     &               0.06D0*COS(FOURA+0.08D0)+0.08D0
           ZFORCE = -4.54D0*COS(ANGLEA)+0.2D0*SIN(TWOA-0.3D0)-          &
     &               0.03D0*SIN(FOURA)
! ELSE OLD THERMAL VERSION OF ROCK4 MODEL:
        ELSE
           XLT=-4.5D0*ONEM5/SATMAS
           ZLT=-4.5D0*ONEM5/SATMAS
           XFORCE = -4.5D0*SIN(ANGLEA)+0.08D0*SIN(TWOA+0.9D0)-          &
     &              0.06D0*COS(FOURA+0.08D0)+0.08D0
           ZFORCE = -4.5D0*COS(ANGLEA)+0.2D0*SIN(TWOA-0.3D0)-           &
     &              0.03D0*SIN(FOURA)
        ENDIF
      ENDIF
! *** IF BLOCK II SATELLITE:
      IF(JGPSBK.EQ.2) THEN
! ***** IF NEWER THERMAL ROCK4 MODEL IS REQUESTED FOR BLOCK II
        IF (IRKRAD.EQ.0)THEN
           XLT=-8.96D0*ONEM5/SATMAS
           ZLT=-8.43D0*ONEM5/SATMAS
           XFORCE = -8.96D0*SIN(ANGLEA)+0.16D0*SIN(THREA)+              &
     &               0.10D0*SIN(FIVEA) -0.07D0*SIN(SEVNA)
           ZFORCE = -8.43D0*COS(ANGLEA)
!C ELSE OLD THERMAL VERSION OF ROCK4 MODEL
        ELSE
           SIN4A = SIN(FOURA+1.3D0)
           XLT=-8.5D0*ONEM5/SATMAS
           ZLT=-8.2D0*ONEM5/SATMAS
           XFORCE = -8.5D0*SIN(ANGLEA)+0.078D0*SIN(TWOA+2.0D0)-         &
     &               0.073D0*SIN4A+0.07D0
           ZFORCE = -8.2D0*COS(ANGLEA)+0.154D0*SIN(TWOA-0.3D0)-         &
     &               0.052D0*SIN4A+0.01D0
        ENDIF
      ENDIF
! *** IF BLOCK IIR SATELLITE:
      IF(JGPSBK.EQ.3)    THEN
           XLT=-11.0D0*ONEM5/SATMAS
           ZLT=-11.3D0*ONEM5/SATMAS
           XFORCE = -11.0D0*SIN(ANGLEA)-0.2D0*SIN(THREA)+               &
     &               0.2D0*SIN(FIVEA)
           ZFORCE = -11.3D0*COS(ANGLEA)+0.1D0*COS(THREA)+               &
     &               0.2D0*COS(FIVEA)
      ENDIF
!     PRINT 98002, XFORCE,ZFORCE
!8002 FORMAT('SOLRK4: XFORCE ZFORCE ',2D24.12)
!**************************************************************
!     NEW VERSION OF THE ROCK4 SOLAR RADIATION MODEL CAME FROM
!     A PAPER GIVEN BY H.F.FLIEGEL AND T.E.GALLINI AT THE AGU
!     30MAY91 WHERE FOR BLOCK I SATELLITES X AND Z ARE AS FOLLOWS
!
!     X=-4.55SIN(B)+.08SIN(2B+.9)-.06COS(4B+.08)+.08
!     Z=-4.54COS(B)+.20SIN(2B-.3)-.03SIN(4B)
!
!     FOR BLOCK II SATELLITES X AND Z ARE
!
!     X=-8.96SIN(B)+.16SIN(3B)+.10SIN(5B)-.07SIN(7B)
!     Z=-8.43COS(B)
!
!     THE DEFAULT IS THE NEWER VERSION
!****************************************************************
!
! *** COMPUTE ACCELERATION
      XACCEL = (XFORCE/SATMAS)*ONEM5
      YACCEL = ZERO
      ZACCEL = (ZFORCE/SATMAS)*ONEM5
!     PRINT 98012, SATMAS,XACCEL,ZACCEL
!8012 FORMAT(' SOLRK4: SATMAS XACCEL ZACCEL ',3D24.16)
! *** COMPUTE BODY CENTERED UNIT VECTOR ALONG THE Z-AXIS AND THE UNIT
! *** VECTOR FROM THE SATELLITE TO THE SUN, E
      DO 2600 I = 1,3
         UVECT(I,3) = ZPOS(I)/DMELEM
         USUN(I)    = XHELIO(I)/DMHELI
 2600 END DO
! *** CROSS UNIT VECTOR Z WITH UNIT VECTOR E GIVING BODY CENTERED UNIT
! *** VECTOR Y
      CALL CROSPR(UVECT(1,3),USUN(1),UVECT(1,2))
! *** CROSS UNIT VECTOR Y WITH UNIT VECTOR Z GIVING BODY CENTERED UNIT
! *** VECTOR X
      UMAG=SQRT(UVECT(1,2)*UVECT(1,2)+UVECT(2,2)*UVECT(2,2)            &
     &          +UVECT(3,2)*UVECT(3,2))
      UVECT(1,2)=UVECT(1,2)/UMAG
      UVECT(2,2)=UVECT(2,2)/UMAG
      UVECT(3,2)=UVECT(3,2)/UMAG
      CALL CROSPR(UVECT(1,2),UVECT(1,3),UVECT(1,1))
! *** MAP ACCELERATION INTO TRUE OF DATE COORDINATES
      DO 3000 I = 1,3
      DXDD(I) = (XACCEL*UVECT(I,1)+YACCEL*UVECT(I,2)+ZACCEL*UVECT(I,3)) &
     &          *RATIO
 3000 END DO
      TTEST=MJDSEC+FSEC
      TM(1)=ONE
      IF(IORSRD.GT.1) TM(2)=FSSTRT
      IF(IORSRD.GT.2) TM(3)=TM(2)*TM(2)
! *** DEFINE APGMR BASED ON THE ORDER OF THE SOLAR RADIATION POLYNOMIAL
! *** (I.E. ORDER 1 - CR, ORDER 2 - CR DOT, ORDER 3 - CR DOUBLE DOT)
! *** APGMR = (CR+CRDOT*TM(2)+CRDDOT*TM(3))*((A/M)*(RAD PRESS)*(AU**2))
      APGMR=ZERO
      IPD=0
      IUP=IORSRD-1
      IF(IUP.LE.0) GO TO 4110
      DO 4100 I=1,IUP
 4100 APGMR=APGMR+TM(I)*APGM(I)
 4110 IF(IPDSRD.GT.0) GO TO 4112
      APGMR=APGMR+TM(IORSRD)*APGM(IORSRD)
      GO TO 4150
 4112 CONTINUE
!   FIGURE WHICH PERIOD APPLIES
      DO 4115 I=1,IPDSRD
      IF(TTEST.LT.STACC(I)) GO TO 4116
 4115 END DO
      IPSN=IPDSRD
      IPD=0
      GO TO 4120
 4116 CONTINUE
      IPSN=I-1
      IPD=I
 4120 CONTINUE
      T0=ESSTRT
      IF(IPSN.EQ.0) GO TO 4140
      DO 4130 I=1,IPSN
      TD=STACC(I)-T0
      CALL POWER(IORSRD,TD,TDN,.FALSE.)
      APGMR=APGMR+APGM(I+IORSRD)*TDN
      T0=STACC(I)
 4130 END DO
 4140 CONTINUE
      TD=TTEST-T0
      CALL POWER(IORSRD,TD,TDN,.TRUE.)
      APGMR=APGMR+APGM(IORSRD+IPD)*TDN
 4150 CONTINUE
! SAVE CORRECT CR*APLM FOR ALBEDO
      ALAPGM=APGMR
! *** COMPUTE SOLAR RADIATION PARTIALS AND ROTATE FROM TRUE OF DATE TO
! *** TRUE OF REFERENCE
      PART(1)=RMI(1)*DXDD(1)+RMI(2)*DXDD(2)+RMI(3)*DXDD(3)
      PART(2)=RMI(4)*DXDD(1)+RMI(5)*DXDD(2)+RMI(6)*DXDD(3)
      PART(3)=RMI(7)*DXDD(1)+RMI(8)*DXDD(2)+RMI(9)*DXDD(3)
! *** SCALE ACCELERATIONS BY CR
      CR = APGMR/APLM
!     PRINT *,' SOLRK4: CR ',CR
      DO 4200 I = 1,3
        DXDD(I) = DXDD(I)*CR
 4200 END DO
      IF(.NOT.LORBIT) THEN
      CALL TESTS(ELEM,BDTRUE(1,8),XAXIS,YAXIS,ZAXIS,DXAXIS,DYAXIS,      &
     &           DZAXIS,DBDX,.FALSE.)
      DO 4202 I=1,3
      DO 4201 J=1,3
      VMAT(I,J)=VMAT(I,J)+RATIO*CR                                      &
     &         *(XACCEL*DXAXIS(I,J)+YACCEL*DYAXIS(I,J)                  &
     &          +ZACCEL*DZAXIS(I,J)                                     &
     &          +XLT*COS(ANGLEA)*DBDX(J)*UVECT(I,1)                     &
     &          -ZLT*SIN(ANGLEA)*DBDX(J)*UVECT(I,3))
 4201 END DO
 4202 END DO
      ENDIF
!     PRINT 98003, MJDSEC,DXDD(1),DXDD(2),DXDD(3)
!8003 FORMAT(' SOLRK4: MJD DXDD(1) DXDD(2) DXDD(3) ',I12,3D24.12)
      IF(LORBIT) GO TO 4700
      KPART=JSASRD(1)
      DO 4635 J=1,IORSRD
      IF(.NOT.LSASRD(J)) GO TO 4635
      GRPAR(KPART,1)=TM(J)*PART(1)
      GRPAR(KPART,2)=TM(J)*PART(2)
      GRPAR(KPART,3)=TM(J)*PART(3)
      KPART=KPART+1
 4635 END DO
      IF(.NOT.LSASRD(4)) GO TO 4700
      IF(.NOT.LSASRD(IORSRD)) GO TO 4637
      GRPAR(KPART-1,1)=ZERO
      GRPAR(KPART-1,2)=ZERO
      GRPAR(KPART-1,3)=ZERO
 4637 CONTINUE
      KPARTS=KPART
      DO 4640 I=1,IPDSRD
      IF(.NOT.LAJSP(I)) GO TO 4640
      GRPAR(KPART,1)=ZERO
      GRPAR(KPART,2)=ZERO
      GRPAR(KPART,3)=ZERO
      KPART=KPART+1
 4640 END DO
      KPART=KPARTS
      T0=ESSTRT
      IF(IPSN.EQ.0) GO TO 4647
      DO 4645 I=1,IPSN
      IF(.NOT.LAJSP(I)) GO TO 4643
      TD=STACC(I)-T0
      CALL POWER(IORSRD,TD,TDN,.FALSE.)
      GRPAR(KPART,1)=PART(1)*TDN
      GRPAR(KPART,2)=PART(2)*TDN
      GRPAR(KPART,3)=PART(3)*TDN
      KPART=KPART+1
 4643 CONTINUE
      T0=STACC(I)
 4645 END DO
 4647 CONTINUE
      IF(IPD.GT.0) GO TO 4648
      IF(.NOT.LSASRD(IORSRD)) GO TO 4700
      KPART=KPARTS-1
      GO TO 4649
 4648 CONTINUE
      IF(.NOT.LAJSP(IPD)) GO TO 4700
 4649 CONTINUE
      TD=TTEST-T0
      CALL POWER(IORSRD,TD,TDN,.TRUE.)
      GRPAR(KPART,1)=PART(1)*TDN
      GRPAR(KPART,2)=PART(2)*TDN
      GRPAR(KPART,3)=PART(3)*TDN
 4700 CONTINUE
      RETURN
      END
