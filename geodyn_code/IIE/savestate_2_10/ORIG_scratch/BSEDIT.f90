!$BSEDIT
      SUBROUTINE BSEDIT(PMPA  ,RESID ,EDTSIG,RATIO ,OBSSIG,             &
     &    EDFLAG,LEDIT ,NM    ,INDEXX,NINDEX)
!********1*********2*********3*********4*********5*********6*********7**
! BSEDIT           00/00/00            0000.0    PGMR - ?
!
!
! FUNCTION: EDIT OBSERVATION RESIDUALS AND FORM NORMALS FOR LOCAL
!           DIFFERENTIAL CORRECTIONS TO BIAS PARAMETERS. REMOVE LOCAL
!           CORRECTIONS FROM RESIDUALS AND ITERATE BACK TO EDIT STEP.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   PMPA          A    PARTIALS OF MEASUREMENTS WRT PARAMETERS
!   RESID         A    MEASUREMENT RESIDUALS
!   EDTSIG        S    EDITING SIGMA
!   RATIO         A    RATIO OF RESIDUAL TO MEASUREMENT SIGMA
!   OBSSIG        A    MEASUREMENT SIGMA
!   EDFLAG        A    ARRAY FOR EDITING FLAGS FOR RESIDUAL PRINTOUT
!   LEDIT         A    ARRAY OF EDITING SWITCHES FOR EACH POINT
!   NM            S    NUMBER OF MEASUREMENTS IN CURRENT BLOCK
!   INDEXX        A    INDICES OF BIASES THAT APPLY TO OBSERVATIONS
!   NINDEX        S    NUMBER OF BIASES THAT APPLY TO OBSERVATIONS
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/BIAOR/NAMBB,NCYCB,NAMBBH
      COMMON/CEDIT /EDITX ,EDTRMS,EDLEVL,CONVRG,GLBCNV,EBLEVL,EDITSW,   &
     &              ENPX  ,ENPRMS,ENPCNV,EDBOUN,FREEZI,FREEZG,FREEZA,   &
     &              XCEDIT
      COMMON/CESTML/LNPNM ,NXCSTL
      COMMON/CPMPA /KTMPAR(3)    ,KMBPAR(2)    ,KRFPAR(2)    ,          &
     &              KAEPAR,KFEPAR,KFPPAR,KVLPAR,KETPAR(2,2)  ,          &
     &              KPMPAR,KUTPAR,KSTPAR(3,4)  ,NADJST       ,          &
     &              NDIM1 ,NDIM2 ,NXDIM1,NXDIM2
      CHARACTER(8)     :: EDFLAG
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      DIMENSION PMPA  ( 1),RESID (NM),OBSSIG(NM),RATIO (NM),            &
     &          EDFLAG(NM),LEDIT (NM),EDTSIG(NM)
! INDEXX,ATWB,DIFCOR,BPARTL & BDELTA ARE DIMENS. BY THE MAXIMUM NO. OF
!        BIAS PARAMETERS APPLICABLE TO A SINGLE MEASUREMENT (MINDEX).
!        ATWA IS DIMENSIONED BY MSIZE=(MINDEX*(MINDEX-1))/2+MINDEX
      DIMENSION INDEXX(26),ATWA(351),ATWB(26),DIFCOR(26),BPARTL(26),    &
     &   BDELTA(26)
      DATA MINDEX/26/,MSIZE/351/,MITER/3/
      CHARACTER(1)     :: EDTFLG
      DATA EDTFLG/'E'/
      DATA ZERO/0.0D0/,ONE/1.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! COUNT THE NUMBER OF WEIGHTED OBSERVATIONS
      ITEST=NAMBB+IPVAL0(IXBISA)
      ISUB=IPVAL0(IXBISA)-1
      NWTD=0
      DO 2800 N=1,NM
      IF(.NOT.LEDIT(N)) NWTD=NWTD+1
 2800 END DO
! IF LESS THAN OR EQUAL TO THE NUMBER OF BIAS PARAMETERS, BYPASS EDITING
      IF(NWTD.LE.NINDEX) GO TO 5000
!
!***********************************************************************
!
! EDIT OBSERVATION RESIDUALS AND FORM NORMALS FOR LOCAL
!      DIFFERENTIAL CORRECTIONS TO BIAS PARAMETERS.
! REMOVE LOCAL CORRECTIONS FROM RESIDUALS AND ITERATE BACK TO EDIT STEP.
!
!***********************************************************************
!
! CLEAR DIFFERENTIAL CORRECTION ARRAY
      CALL CLEARA(DIFCOR,MINDEX)
      NITER=MITER
      ITER=0
      RMSPRV=EDTRMS
      EDTLVL=EDLEVL
 3000 CONTINUE
      ITER=ITER+1
      LAST=ITER.GE.NITER
! CLEAR SUMMATION ARRAYS
      CALL CLEARA(ATWA,MSIZE)
      CALL CLEARA(ATWB,MINDEX)
      RMS=ZERO
      NWTD=0
      DO 4000 N=1,NM
      IF(LEDIT(N)) GO TO 3900
! STORE PARTIALS INTO LOCAL ARRAY AND SUM PARTIALS*DIFFERENTIAL CORR.
      RESCOR=ZERO
      DO 3200 I=1,NINDEX
      INDEXB=INDEXX(I)
      IF(INDEXB.LT.ITEST) INDEXB=INDEXB-ISUB
      PARTDV   =PMPB(PMPA,NDIM1,NDIM2,N,INDEXB,LNPNM)
      BPARTL(I)=PARTDV
      RESCOR=RESCOR+DIFCOR(I)*PARTDV
 3200 END DO
! CORRECT LOCAL RESIDUAL, COMPUTE RATIO TO EDITING SIGMA AND EDIT
      RESDIF=RESID (N)-RESCOR
      RATDIF=RESDIF/EDTSIG(N)
      LNGOOD=ABS(RATDIF).GT.EDTLVL
      IF(LNGOOD) GO TO 3900
      NWTD=NWTD+1
      IF(LAST) GO TO 3800
! SUM THIS OBSERVATION EQUATION INTO LOCAL NORMALS
      WT=ONE/OBSSIG(N)**2
      CALL SUMBNP(BPARTL,RESDIF,WT,NINDEX,NINDEX,ATWA,ATWB,BDELTA)
      RMS=RMS+RATDIF**2
      GO TO 4000
 3800 CONTINUE
! SAVE EDITING RATIO ON LAST ITERATION
      RATIO(N)=RATDIF
      GO TO 4000
 3900 CONTINUE
      IF(.NOT.LAST) GO TO 4000
! ON LAST ITERATION FLAG EDITED RESIDUALS
      LEDIT (N)=.TRUE.
 4000 END DO
      IF(LAST) GO TO 5000
      IF(NWTD.LE.NINDEX) GO TO 4200
! INVERT NORMAL MATRIX
      CALL DSINV(ATWA  ,NINDEX,NINDEX,BDELTA)
! SOLVE FOR LOCAL BIAS DIFFERENTIAL CORRECTIONS
      CALL SOLVE(ATWA  ,ATWB  ,NINDEX,NINDEX,BDELTA)
! UPDATE LOCAL DIFFERENTIAL CORRECTION SUMS
      DO 4100 I=1,NINDEX
      DIFCOR(I)=DIFCOR(I)+BDELTA(I)
 4100 END DO
! COMPUTE NEW RMS
      DENOM=NWTD-NINDEX
      RMS=SQRT(RMS/DENOM)
 4200 CONTINUE
! CHECK FOR CONVERGENCE
      RMSCHK=CONVRG*RMS
      RMSDIF=ABS(RMSPRV-RMS)
      IF(RMSDIF.LE.RMSCHK) NITER=ITER+1
      EDTLVL=EDITX *RMS
      GO TO 3000
! FLAG EDITED DATA POINTS
 5000 CONTINUE
      DO 5100 N=1,NM
      IF(LEDIT(N)) EDFLAG(N)=EDTFLG
 5100 END DO
      RETURN
      END
