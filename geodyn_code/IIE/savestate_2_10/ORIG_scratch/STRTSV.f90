!$STRTSV
      SUBROUTINE STRTSV(LORBIT,XDDOT,PXDDOT,IOL1,N3,NEQN3,              &
     &                  NSAVST,ISTPST,XDDOTH,PDDOTH,N2,TOPXAT,          &
     &                  LTPXAT,TOPXTH,LTPXTH,XDDTNC)
!********1*********2*********3*********4*********5*********6*********7**
! STRTSV
!
! FUNCTION:  SAVE FROM THE LAST CALL TO THE STARTER (AT THE SMALL
!            STEPSIZE) THE VALUES OF ACCELERATION THAT OCCUR AT
!            THE LARGE STEPS.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   LORBIT   I    S    LOGICAL SWITCH INDICATING ORBIT-ONLY
!   XDDOT    I    A    ACCELERATIONS FROM STARTER
!   PXDDOT   I    A    PARTIAL DERIV ACCELERATIONS FROM STARTER
!   IOL1     I    S    INTEGRATION ORDER -1
!   N3       I    S    NSAT*3
!   NEQN3    I    S    NEQN*N3 (NEQN=#FORCE MODEL PARAMTERS)
!   NSAVST   I    S    NUMBER OF STEPS WHICH CAN BE SAVED
!   ISTPST   I    A    STEP NUMBERS WHICH CAN BE SAVED
!   XDDOTH   O    A    ARRAY WHICH HOLDS THE SAVED ACCELERATIONS
!                      FROM THE LARGE STEP FORCES
!   PDDOTH   O    A    ARRAY WHICH HOLDS THE SAVED PARTIAL ACCELERATIONS
!   N2       I    S    NSAT*2
!   TOPXAT   I    A    TOPEX ATTITUDE INFORMATION FROM STARTER
!   LTPXAT   I    A    TOPEX ATTITUDE INFORMATION FROM STARTER
!   TOPXTH   O    A    ARRAY WHICH HOLDS SAVED TOPEX ATTITUDE INFO
!   LTPXTF   O    A    ARRAY WHICH HOLDS SAVED TOPEX ATTITUDE INFO
!   XDDTNC   I    A    ACCELERATIONS FROM STARTER FROM THE SMALL STEP
!                      FORCES
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION ISTPST(30)
      DIMENSION XDDOT(N3,IOL1),PXDDOT(NEQN3,IOL1,2)
      DIMENSION XDDOTH(N3,IOL1),PDDOTH(NEQN3,IOL1)
      DIMENSION TOPXAT(N2,IOL1),LTPXAT(N3,IOL1)
      DIMENSION TOPXTH(N2,IOL1),LTPXTH(N3,IOL1)
      DIMENSION XDDTNC(N3,IOL1)
!
      IF(NSAVST.LE.0) RETURN
      DO 100 I=1,NSAVST
      DO 10 J=1,N3
      XDDOTH(J,I)=XDDOT(J,ISTPST(I))-XDDTNC(J,ISTPST(I))
      LTPXTH(J,I)=LTPXAT(J,ISTPST(I))
   10 END DO
      DO 15 J=1,N2
      TOPXTH(J,I)=TOPXAT(J,ISTPST(I))
   15 END DO
      IF(LORBIT) GO TO 100
      DO 20 J=1,NEQN3
      PDDOTH(J,I)=PXDDOT(J,ISTPST(I),1)
   20 END DO
  100 END DO
      RETURN
      END
