!$D71BF1
      SUBROUTINE D71BF1(NGOOD,IFGO,IFGN)
!********1*********2*********3*********4*********5*********6*********7**
! D71BF1           83/08/04            8308    PGMR - D. ROWLANDS
!
! FUNCTION:  TRANSFER PREVIOUSLY CALCULATED EPHEMERIS DATA NEEDED
!            BY THE D71 DENSITY ROUTINE FROM OLD BUFFER POSITION
!            TO NEW.RECALCULATE TC&KP QUANTITIES.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   NGOOD    I    S    TOTAL NUMBER OF GOOD RECORDS TO BE SAVED
!   IFGO     I    S    FIRST LOCATION IN OLD BUFFER OF GOOD INFORMATION
!   IFGN     I    S    FIRST LOCATION IN NEW BUFFER OF GOOD INFORMATION
!
! COMMENTS:  OUTPUT      THROUGH COMMON BLOCK D71BUF
!
! RESTRICTIONS: SHOULD ONLY BE CALLED BY BUFPOS
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      DIMENSION HOLD1(4),HOLD2(4)
!
      COMMON/D71BUF/DECSUN(4),RASUN(4),TCD71(3),XKPC(3),FSCTC,FSCKP
      COMMON/EPHMPT/NTOT,NTOTS,NNPDPR,NUTORD,IBODDG(8),IGRPDG(4),       &
     &              NENPD,INRCD(2),KPLAN(11),NINEP,NEPHMR,NEPHEM,       &
     &              IBHINT,NINTBH,NXEPHP
      COMMON/EPHSET/EMFACT,DTENPD,DTINPD,FSC1EN,FSCENP(2),FSCINP(4),    &
     &   FSDINP(4),XEPHST
      COMMON/EPHM/FSECXY(1),XP(261),YP(261),A1UT(261),EP(261),EC(261),  &
     &            FSCFLX(1),FLUXS(36),AVGFLX(36),FLUXM(288),FSECEP(1),  &
     &            EPHP(816),EPHN(96),ELIB(120),BUFT(2700),FSECNP(4)
!
      DATA P85/.85D0/,P15/.15D0/,SECDAY/86400.D0/,EPS/.01D0/
      DATA X379P0/379.D0/,X3P24/3.24D0/,X1P30/1.30D0/
      DATA XKPLG/67319.99999D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
      IF(NGOOD.LE.0) GO TO 30
      NSPOT2=IFGO-1
      NSPOT1=IFGN-1
      DO 10 I=1,NGOOD
      HOLD1(I)=DECSUN(I+NSPOT2)
   10 HOLD2(I)=RASUN(I+NSPOT2)
      DO 20 I=1,NGOOD
      DECSUN(I+NSPOT1)=HOLD1(I)
   20 RASUN(I+NSPOT1)=HOLD2(I)
   30 CONTINUE
      TTEST=FSCINP(1)
!   FSCTC IS THE TIME AT START OF DAY AT FIRST INTERPOLATION TIME
      FSCTC=MOD(TTEST,SECDAY)
      FSCTC=TTEST-FSCTC
      IND=(FSCTC-FSCFLX(1))/86400+1+EPS
      IF(IND.LE.35) GO TO 45
      IND=(FSCTC-FSCFLX(1+NTOT))/86400+EPS+1+NTOT
   45 CONTINUE
      DO 50 J=1,3
!   CALCULATE AVERAGE SOLAR FLUX AT A TIME 1.85 DAYS EARLIER
!   THAN CURRENT TIME
      AF=P85*AVGFLX(IND-2)+P15*AVGFLX(IND-1)
!   CALCULATE DAILY SOLAR FLUX AT A TIME 2.85 DAYS EARLIER
      DF=P85*FLUXS(IND-3)+P15*FLUXS(IND-2)
!   EVALUATE GLOBAL MIN NIGHT TEMPERATURE
      TCD71(J)=X379P0+X3P24*AF+X1P30*(DF-AF)
      IND=IND+1
   50 END DO
!   CALCULATE FIRST TIME AT WHICH KP VALUES COULD BE REQUESTED
      T1KP=TTEST-XKPLG
!   CALCULATE TIME AT START OF DAY ASSOCIATED WITH T1KP
      FSCKP=MOD(T1KP,SECDAY)
      FSCKP=T1KP-FSCKP
      IND=(FSCKP-FSCFLX(1))/86400+1+EPS
      IF(IND.LE.34) GO TO 55
      IND=(FSCKP-FSCFLX(1+NTOT))/86400+EPS+1+NTOT
   55 CONTINUE
! For the Jacchia density model load 3 magnetic flux values correspondin
! to 3 daily values. FLUXM is dimensioned (8,36), but only the
! (1,I) values contain values when the Jacchia model is used.
      XKPC(1)=FLUXM(1+(IND-1)*8)
      XKPC(2)=FLUXM(1+IND*8)
      XKPC(3)=FLUXM(1+(IND+1)*8)
      RETURN
      END
