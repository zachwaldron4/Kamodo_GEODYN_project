!$UTCET
      SUBROUTINE UTCET(LUTCET,NM,MJDSEC,FSECIN,FSECOT,ETUTC)
!********1*********2*********3*********4*********5********6*********7**
! UTCET            00/00/00            0000.0    PGMR - BILL EDDY
!                                                PGMR - D. ROWLANDS
!
! FUNCTION:  CHANGE UTC TIME TO ET TIME AND VICE VERSA
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   LUTCET   I         TRUE IF GOING FROM UTC TO ET
!   NM       I         NUMBER OF TIMES AT WHICH CONVERSION DESIRED
!   MJDSEC  I/O        INTEGER SECONDS SINCE GEODYN REF.TIME IN SYSTEM
!                      OF INPUT
!                      INTEGER SECONDS SINCE GEODYN REF.TIME IN SYSTEM
!                      OF OUTPUT
!   FSECIN   I         FRACTIONAL REMAINING SECONDS IN SYSTEM
!                      OF INPUT
!   FSECOT   O         FRACTIONAL REMAINING SECONDS IN SYSTEM
!                      OF OUTPUT
!   ETUTC    I         DYNAMIC ARRAY CONTAINING ET-UTC TIME
!                      DIFFERENCES,A1-UTC RATES,AND TIME AT WHICH
!                      TIME DIFFERENCE APPLIES
! RESTRICTIONS:
!                   TIMES REQUESTED FOR CONVERSION MUST SAMPLE EVERY
!                   UTC INTERVAL
!
!********1*********2*********3*********4*********5********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      COMMON/DTMGDN/TGTYMD,TMGDN1,TMGDN2,REPDIF,XTMGN
      COMMON/POLINF/NPOLE,NPOLEA,NUTA,MXUTC,NMODEL,NXPOLI
!
      DIMENSION ETUTC(MXUTC,3),FSECIN(NM),FSECOT(NM)
!
      DATA ONE/1.D0/,ETA1/32.1496183D0/
!
!**********************************************************************
!* START OF EXECUTABLE CODE *******************************************
!**********************************************************************
!
      DTM66=2439126.5D0-TMGDN1
!
      TTEST0=DBLE(MJDSEC)
!***** TEMPORARY FIX *****
      TTEST0=TTEST0/8.64D4-DTM66
!***** END FIX *****
! COMPUTE INDEX TO ETUTC ARRAY FOR TIMES
      TTEST1=DBLE(MJDSEC)+FSECIN(1)
!***** TEMPORARY FIX *****
      TTEST1=TTEST1/8.64D4-DTM66
!***** END FIX *****
      IF(TTEST1.LT.ETUTC(1,3)) GO TO 61500
!***** BEGIN DEBUG *****
!     WRITE(IOUT6,73737) MJDSEC,FSECIN(1),TTEST1,MXUTC,ETUTC
!73737 FORMAT(' ** UTCET **  MJDSEC,FSECIN(1),TTEST1,MXUTC ='/
!     1   1X,I12,2G23.16,I12/14X,'ETUTC ='/(1X,3G23.16))
!***** END DEBUG *****
      INDX1=1
      IF(MXUTC.EQ.1) GO TO 600
!
! FIND  FIRST INDEX (FIRST APPROX IF INPUT IS ET)
      DO 100 I=2,MXUTC
      IF(TTEST1.LT.ETUTC(I,3)) GO TO 150
  100 END DO
      INDX1=MXUTC
      UTC01=ETUTC(MXUTC,3)
      GO TO 160
  150 INDX1=I-1
      UTC01=ETUTC(INDX1,3)
  160 CONTINUE
!
! ADJUST (IF NECCESSARY) INDX1  FOR ET INPUT
      IF(LUTCET) GO TO 190
      TIMET1=UTC01+ETUTC(INDX1,1)/8.64D4
!***** TEMPORARY FIX *****
      TIMET1=TIMET1+ETA1/8.64D4
!***** END FIX *****
      IF(TTEST1.LT.TIMET1) INDX1=INDX1-1
  190 CONTINUE
!
!  FIND LAST INDEX (FIRST APPROX IF INPUT IS ET)
      IF(NM.EQ.1) GO TO 600
      TTEST2=DBLE(MJDSEC)+FSECIN(NM)
!***** TEMPORARY FIX *****
      TTEST2=TTEST2/8.64D4-DTM66
!***** END FIX *****
      DO 200 I=INDX1,MXUTC
      IF(TTEST2.LT.ETUTC(I,3)) GO TO 250
  200 END DO
      INDX2=MXUTC
      UTC02=ETUTC(MXUTC,3)
      GO TO 260
  250 INDX2=I-1
      UTC02=ETUTC(INDX2,3)
  260 CONTINUE
!
! ADJUST (IF NECCESSARY) INDX2  FOR ET INPUT
      IF(LUTCET) GO TO 300
      TIMET2=UTC02+ETUTC(INDX2,1)/8.64D4
!***** TEMPORARY FIX *****
      TIMET2=TIMET2+ETA1/8.64D4
!***** END FIX *****
      IF(TTEST2.LT.TIMET2) INDX2=INDX2-1
  300 CONTINUE
      IF(INDX1.EQ.INDX2) GO TO 600
!
! LOOP THRU THE NUMBER OF PERIODS (NUMBER OF PERIODS GREATER THAN 1)
      NML=0
      DO 500 INDEX=INDX1,INDX2
! FIND THE LAST TIME ASSOCIATED WITH EACH PERIOD
      NM1=NML+1
      NML=NM
      IF(INDEX.EQ.INDX2) GO TO 400
      NM11=NM1+1
      INDX11=INDEX+1
      TTTEST=ETUTC(INDX11,3)
      IF(.NOT.LUTCET) TTTEST=TTTEST+ETUTC(INDX11,1)/8.64D4
!***** TEMPORARY FIX *****
      IF(.NOT.LUTCET) TTTEST=TTTEST+ETA1/8.64D4
!***** END FIX *****
      DO 310 I=NM11,NM
      TTEST=DBLE(MJDSEC)+FSECIN(I)
!***** TEMPORARY FIX *****
      TTEST=TTEST/8.64D4-DTM66
!***** END FIX *****
      IF(TTEST.GE.TTTEST) GO TO 350
  310 END DO
      GO TO 61600
  350 CONTINUE
      NML=I-1
  400 CONTINUE
      A=ETUTC(INDEX,1)
!***** TEMPORARY FIX *****
      A=A+ETA1
!***** END FIX *****
      B=ETUTC(INDEX,2)
!***** TEMPORARY FIX *****
!     BP1=B+ONE
      BP1=B/8.64D4+ONE
!***** END FIX *****
      SCALER=A+B*(TTEST0-ETUTC(INDEX,3))
!ERRORIF(LUTCET) GO TO 450
      IF(.NOT.LUTCET) GO TO 450
! CONVERT UTC TO ET
      DO 410 I=NM1,NML
      FSECOT(I)=FSECIN(I)*BP1+SCALER
  410 END DO
      GO TO 500
  450 CONTINUE
! CONVERT ET TO UTC
      BP1=ONE/BP1
      DO 460 I=NM1,NML
      FSECOT(I)=(FSECIN(I)-SCALER)*BP1
  460 END DO
  500 END DO
      RETURN
!  ONE TIME PERIOD ONLY
  600 CONTINUE
      A=ETUTC(INDX1,1)
!***** TEMPORARY FIX *****
      A=A+ETA1
!***** END FIX *****
      B=ETUTC(INDX1,2)
!***** TEMPORARY FIX *****
!     BP1=B+ONE
      BP1=B/8.64D4+ONE
!***** END FIX *****
      SCALER=A+B*(TTEST0-ETUTC(INDX1,3))
!ERRORIF(LUTCET) GO TO 650
      IF(.NOT.LUTCET) GO TO 650
! CONVERT UTC TO ET
      DO 610 I=1,NM
      FSECOT(I)=FSECIN(I)*BP1+SCALER
  610 END DO
      RETURN
  650 CONTINUE
! CONVERT ET TO UTC
      BP1=ONE/BP1
      DO 660 I=1,NM
      FSECOT(I)=(FSECIN(I)-SCALER)*BP1
  660 END DO
      RETURN
!
! ERROR MESSAGES
!
! REQUESTED TIME BEFORE AVAILABLE TIMES
61500 CONTINUE
      WRITE(IOUT6,80100) TTEST1
      WRITE(IOUT6,80101) ETUTC(1,3)
      WRITE(IOUT6,*) 'UTCET: MJDSEC, FSECIN(1) ',MJDSEC,FSECIN(1)
      STOP
!
! TIME OUT OF ORDER
61600 CONTINUE
      WRITE(IOUT6,80200)
      STOP
80100 FORMAT(//1X,'EXECUTION TERMINATING IN SUBROUTINE UTCET:'/         &
     & ' FIRST TIME REQUESTED IS:  ',D25.16)
80101 FORMAT(1X,'FIRST TIME AVAILABLE IS ',D25.16//)
80200 FORMAT(//1X,'EXECUTION TERMINATING IN SUBROUTINE UTCET:'/         &
     &   ' TIMES APPARENTLY OUT OF ORDER '//)
      END
