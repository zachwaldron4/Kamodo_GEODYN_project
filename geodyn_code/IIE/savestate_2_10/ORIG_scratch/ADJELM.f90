!$ADJELM
      SUBROUTINE ADJELM(IELMTP,PARMV,PARMV0,PARMVP,PARMVC,SUM1  ,       &
     &                  MJDSB ,FSECB,ETUTC ,XYZCUR,CKEPLR,CKEPNS,       &
     &                  NSATST,RMSPOS)
!********1*********2*********3*********4*********5*********6*********7**
! ADJELM           85/01/12            0000.0    PGMR - BILL EDDY
!
!
! FUNCTION: CONTROL PRINT OUT OF ADJUSTED SATELLITE ELEMENTS IN
!           CARTESIAN, KEPLERIAN AND NON-SINGULAR KEPLERIAN
!           COORDINATES.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   IELMTP   I         ARRAY TELLING WHICH COORDINATE SYSTEM THE
!                      SATELLITE ELEMENTS ARE BEING ADJUSTED IN
!                      1 - CARTESIAN ; 2 - KEPLERIAN ; 3 - N.S KEP
!   PARMV    I         CURRENT VALUES FOR ALL PARAMETERS
!   PARMV0   I         A PRIORI VALUES FOR ADJUSTED PARAMTERS
!   PARMVP   I         PREVIOUS VALUES FOR ADJUSTED PARAMTERS
!   PARMVC   I         CURRENT  VALUES FOR ADJUSTED PARAMTERS
!   SUM1     I         ATWA MATRIX
!   MJDSB    I         SATELLITE EPOCH TIME IN MJD SECONDS. THIS
!                      IS THE RESET (START) TIME IF THE EPOCH AND
!                      START TIME ARE NOT THE SAME.
!   FSECB    I         FRACTION OF SECONDS OF SATELLITE EPOCH
!   ETUTC    I         ARRAY OF ET-UTC TIME DIFFERENCES
!   XYZCUR   I         CURRENT CARTESIAN SATELLITE ELEMENTS
!   CKEPLR   I         CURRENT KEPLERIAN SATELLITE ELEMENTS
!   CKEPNS   I         CURRENT NON SINGULAR SAT. ELEMENTS
!   NSATST   I         NUMBER OF SATELLITES IN EACH SET
!   RMSPOS
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/BSPHRE/DXMDN(3,3,2),DXBDN(3,3,2)
      COMMON/C12PRM/IDSATS(10,2),IP1,IP2,NXC12P
      COMMON/CBDSTA/BDSTAT(7,999),XBDSTA
      COMMON/CNIARC/NSATA,NSATG,NSETA,NEQNG,NMORDR,NMORDV,NSORDR,       &
     &              NSORDV,NSUMX,NXDDOT,NSUMPX,NPXDDT,NXBACK,NXI,       &
     &              NXILIM,NPXPF,NINTIM,NSATOB,NXBCKP,NXTIMB,           &
     &              NOBSBK,NXTPMS,NXCNIA
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/IBODPT/IBDCF(999),IBDSF(999),IBDGM(999),IBDAE(999),    &
     &              IBDPF(999),                                     &
     &              ICBDCF,ICBDSF,ICBDGM,ICBDAE,ICBDPF,             &
     &              ITBDCF,ITBDSF,ITBDGM,ITBDAE,ITBDPF,NXBDPT
      COMMON/LDUAL/LASTSN,LSTSNX,LSTSNY
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
!
      DIMENSION AKEPLR(6),AKEPNS(6)
      DIMENSION CKEPLR(6),CKEPNS(6)
      DIMENSION COVXYZ(6,6),COVELM(6,6)
      DIMENSION ETUTC(1)
      DIMENSION FSECB(1),FSEC(1)
      DIMENSION IELMTP(1)
      DIMENSION MJDSB(1),NSATST(1)
      DIMENSION PKEPLR(6),PKEPNS(6)
      DIMENSION PARMV0(1),PARMVP(1),PARMVC(1),PARMV(1)
      DIMENSION PDELTA(6),PK(1)
      DIMENSION RMSPOS(2,1)
      DIMENSION SUM1(1)
      DIMENSION XYZAPR(6),XYZPRV(6),XYZCUR(6)
      DIMENSION PAPR1(6),PAPR2(6),PPRV1(6),PPRV2(6),PCUR1(6),PCUR2(6)
      DIMENSION P1(6),P2(6),Q1(6),Q2(6),QC1(6),QC2(6)
      DIMENSION PP1(6),PP2(6),QP1(6),QP2(6),PPP1(6),PPP2(6)
!
!********1*********2*********3*********4*********5*********6*********7**
! START OF EXECUTABLE CODE
!********1*********2*********3*********4*********5*********6*********7**
      L12=.FALSE.
      GMX =PARMV(IPVAL(IXGM)+IBDGM(ICBDGM)-1)
      GMAX=GMX
! IF GM IS ADJUSTED SET GMA TO A PRIORI VALUE
      IF(NPVAL0(IXGM).GT.0) GMAX=PARMV0(IPVAL0(IXGM))
!
!     12 PARAMETER INTERSATELLITE CODE
      DO 450 ISET=1,NSETA
      NSATS=NSATST(ISET)
      GM=GMX
      GMA=GMAX
      IF(LASTSN.AND.ISET.EQ.1) THEN
        GM=BDSTAT(7,8)
        GMA=BDSTAT(7,8)
      ENDIF
      DO 400 ISAT=1,NSATS
      ISATNO=ISATNO+1
      ISAT6 =(ISATNO-1)*6
      INDX =IPVAL0(IXSATP)+ISAT6-1
      INDX1 =INDX+1
      IF(INDX1.EQ.IP1) THEN
      DO J=1,6
      PAPR1(J)=PARMV0(INDX+J)
      PPRV1(J)=PARMVP(INDX+J)
      PCUR1(J)=PARMVC(INDX+J)
      ENDDO
      ENDIF
      IF(INDX1.EQ.IP2) THEN
      DO J=1,6
      PAPR2(J)=PARMV0(INDX+J)
      PPRV2(J)=PARMVP(INDX+J)
      PCUR2(J)=PARMVC(INDX+J)
      ENDDO
      L12=.TRUE.
      ENDIF
  400 END DO
  450 END DO

      IF(L12) THEN
      CALL GETBCR(.FALSE.,P1,PAPR1,P2,PAPR2,DXMDN,DXBDN)
      CALL TWELVE(Q1,Q2,P1,P2,.FALSE.)
      CALL GETBCR(.FALSE.,PP1,PPRV1,PP2,PPRV2,DXMDN,DXBDN)
      CALL TWELVE(QP1,QP2,PP1,PP2,.FALSE.)
      CALL GETBCR(.FALSE.,PPP1,PCUR1,PPP2,PCUR2,DXMDN,DXBDN)
      CALL TWELVE(QC1,QC2,PPP1,PPP2,.FALSE.)
      L12=.FALSE.
      ENDIF

! LOOP ON EACH SATELLITE DETERMINING WHICH SYSTEM THE ELEMENTS ARE IN
! AND COMPUTING THE ELEMENTS IN THE REMAINING COORDINATE SYSTEMS
!
      ISATNO=0
      DO 2100 ISET=1,NSETA
      NSATS=NSATST(ISET)
      GM=GMX
      GMA=GMAX
      IF(LASTSN.AND.ISET.EQ.1) THEN
        GM=BDSTAT(7,8)
        GMA=BDSTAT(7,8)
      ENDIF
! CONVERT EPOCH TIME FROM MJDS IN E.T. TO YMD IN UTC
      CALL UTCET(.FALSE.,1,MJDSB(ISET),FSECB(ISET),FSEC(1),ETUTC)
      CALL YMDHMS(MJDSB(ISET),FSEC(1),IEPYMD,IEPHM,EPSEC,1)
      DO 2000 ISAT=1,NSATS
      ISATNO=ISATNO+1
      ISAT6 =(ISATNO-1)*6
      ISAT61=ISAT6+1
      INDX =IPVAL0(IXSATP)+ISAT6-1
      ISPACE = IELMTP(ISATNO)
      IF(ISPACE-2) 500,1000,1500
!********1*********2*********3*********4*********5*********6*********7**
! CARTESIAN COORDINATES - COMPUTE KEPLERIAN AND NON-SINGULAR KEPLERIAN
!                         A PRIORI AND PREVIOUS ELEMENTS
!********1*********2*********3*********4*********5*********6*********7**
  500 JSPACE=1
      INDX1=INDX+1
      IF(IP1.EQ.INDX1) GOTO 601
      IF(IP2.EQ.INDX1) GOTO 602
      DO 600 J=1,6
      XYZAPR(J)=PARMV0(INDX+J)
      XYZPRV(J)=PARMVP(INDX+J)
      XYZCUR(J+ISAT6 )=PARMVC(INDX+J)
      PDELTA(J)=XYZCUR(J+ISAT6 )-XYZPRV(J)
  600 END DO
      GOTO 603
  601 CONTINUE
      DO J=1,6
      XYZAPR(J)=Q1(J)
      XYZPRV(J)=QP1(J)
      XYZCUR(J+ISAT6 )=QC1(J)
      ENDDO
      GOTO 603
  602 CONTINUE
      DO J=1,6
      XYZAPR(J)=Q2(J)
      XYZPRV(J)=QP2(J)
      XYZCUR(J+ISAT6 )=QC2(J)
      ENDDO
  603 CONTINUE
      IDRAD=0
! CARTESIAN TO KEPLERIAN
      CALL ELEM(XYZAPR(1),XYZAPR(2),XYZAPR(3),XYZAPR(4),XYZAPR(5),      &
     &          XYZAPR(6),AKEPLR   ,IDRAD    ,PK       ,GMA)
      CALL ELEM(XYZPRV(1),XYZPRV(2),XYZPRV(3),XYZPRV(4),XYZPRV(5),      &
     &          XYZPRV(6),PKEPLR   ,IDRAD    ,PK       ,GM )
      IDRAD=8
! CARTESIAN TO NON-SINGULAR KEPLERIAN
      CALL ELEM(XYZAPR(1),XYZAPR(2),XYZAPR(3),XYZAPR(4),XYZAPR(5),      &
     &          XYZAPR(6),AKEPNS   ,IDRAD    ,PK       ,GMA)
      CALL ELEM(XYZPRV(1),XYZPRV(2),XYZPRV(3),XYZPRV(4),XYZPRV(5),      &
     &          XYZPRV(6),PKEPNS   ,IDRAD    ,PK       ,GM )
! PRINT DIFFERENT COORDINATE SYSTEMS AND CORRELATIONS
      CALL ADJXYZ(ISATNO,JSPACE,PDELTA,XYZPRV,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPLR(ISAT61),XYZAPR,          &
     &            CKEPNS(ISAT61),GM,IEPYMD,IEPHM,EPSEC,RMSPOS(1,ISATNO))
      CALL ADJKEP(ISATNO,JSPACE,PDELTA,PKEPLR,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPLR(ISAT61),AKEPLR,GM,       &
     &            IEPYMD,IEPHM,EPSEC)
      CALL ADJLEI(ISATNO,JSPACE,PDELTA,PKEPNS,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPNS(ISAT61),AKEPNS,GM,       &
     &            IEPYMD,IEPHM,EPSEC)
      GO TO 2000
!********1*********2*********3*********4*********5*********6*********7**
! KEPLERIAN COORDINATES - COMPUTE CARTESIAN AND NON-SINGUALR KEPLERIAN
!                         A PRIORI AND PREVIOUS ELEMENTS
!********1*********2*********3*********4*********5*********6*********7**
 1000 JSPACE=2
      DO 1100 J=1,6
      AKEPLR(J)=PARMV0(INDX+J)
      PKEPLR(J)=PARMVP(INDX+J)
      CKEPLR(J+ISAT6 )=PARMVC(INDX+J)
      PDELTA(J)=CKEPLR(J+ISAT6 )-PKEPLR(J)
 1100 END DO
! KEPLERIAN TO CARTESIAN
      IDRAD=1
      CALL POSVEL(XYZAPR,AKEPLR,IDRAD,GMA)
      CALL POSVEL(XYZPRV,PKEPLR,IDRAD,GM )
! CARTESIAN TO NON SINGULAR KEPLERIAN
      IDRAD=8
      CALL ELEM(XYZAPR(1),XYZAPR(2),XYZAPR(3),XYZAPR(4),XYZAPR(5),      &
     &          XYZAPR(6),AKEPNS   ,IDRAD    ,PK       ,GMA)
      CALL ELEM(XYZPRV(1),XYZPRV(2),XYZPRV(3),XYZPRV(4),XYZPRV(5),      &
     &          XYZPRV(6),PKEPNS   ,IDRAD    ,PK       ,GM )
! PRINT DIFFERENT COORDINATE SYSTEMS AND CORRELATIONS
      CALL ADJKEP(ISATNO,JSPACE,PDELTA,PKEPLR,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPLR(ISAT61),AKEPLR,GM,       &
     &            IEPYMD,IEPHM,EPSEC)
      CALL ADJXYZ(ISATNO,JSPACE,PDELTA,XYZPRV,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPLR(ISAT61),XYZAPR,          &
     &            CKEPNS(ISAT61),GM,IEPYMD,IEPHM,EPSEC,RMSPOS(1,ISATNO))
      CALL ADJLEI(ISATNO,JSPACE,PDELTA,PKEPNS,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPNS(ISAT61),AKEPNS,GM,       &
     &            IEPYMD,IEPHM,EPSEC)
      GO TO 2000
!********1*********2*********3*********4*********5*********6*********7**
! NON-SINGULAR KEPLERIAN - COMPUTE CARTESIAN AND KEPLERIAN
!                          A PRIORI AND PREVIOUS ELEMENTS
!********1*********2*********3*********4*********5*********6*********7**
 1500 JSPACE=3
      DO 1600 J=1,6
      AKEPNS(J)=PARMV0(INDX+J)
      PKEPNS(J)=PARMVP(INDX+J)
      CKEPNS(J+ISAT6 )=PARMVC(INDX+J)
      PDELTA(J)=CKEPNS(J+ISAT6 )-PKEPNS(J)
 1600 END DO
      IDRAD=1
! NON-SINGULAR KEPLERIAN TO CARTESIAN
      CALL PVNSK(XYZAPR,AKEPNS,IDRAD,GMA)
      CALL PVNSK(XYZPRV,PKEPNS,IDRAD,GM )
      IDRAD=0
! CARTESIAN TO KEPLERIAN
      CALL ELEM(XYZAPR(1),XYZAPR(2),XYZAPR(3),XYZAPR(4),XYZAPR(5),      &
     &          XYZAPR(6),AKEPLR   ,IDRAD    ,PK       ,GMA)
      CALL ELEM(XYZPRV(1),XYZPRV(2),XYZPRV(3),XYZPRV(4),XYZPRV(5),      &
     &          XYZPRV(6),PKEPLR   ,IDRAD    ,PK       ,GM )
! PRINT DIFFERENT COORDINATE SYSTEMS AND CORRELATIONS
      CALL ADJLEI(ISATNO,JSPACE,PDELTA,PKEPNS,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPNS(ISAT61),AKEPNS,GM,       &
     &            IEPYMD,IEPHM,EPSEC)
      CALL ADJXYZ(ISATNO,JSPACE,PDELTA,XYZPRV,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPLR(ISAT61),XYZAPR,          &
     &            CKEPNS(ISAT61),GM,IEPYMD,IEPHM,EPSEC,RMSPOS(1,ISATNO))
      CALL ADJKEP(ISATNO,JSPACE,PDELTA,PKEPLR,SUM1,COVXYZ,              &
     &            COVELM,XYZCUR(ISAT61),CKEPLR(ISAT61),AKEPLR,GM,       &
     &            IEPYMD,IEPHM,EPSEC)
 2000 END DO
 2100 END DO
      RETURN
      END
