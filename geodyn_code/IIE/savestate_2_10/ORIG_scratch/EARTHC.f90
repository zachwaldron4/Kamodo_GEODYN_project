!$EARTHC
      SUBROUTINE EARTHC
!********1*********2*********3*********4*********5*********6*********7**
! EARTHC           00/00/00            0000.0    PGMR - ?
!
!  FUNCTION:  GENERALLY SPEAKING EARTHC CALCULATES QUANITITES FOR
!             THE TRACKING BODY (THIS CAN ONLY BE THE EARTH AT PRESENT).
!             IT ALSO COMPUTES QUANITIES NECESSARY FOR GEOID
!             CALCULATIONS. AT PRESENT, THIS IS SET UP TO BE ONLY FOR
!             EARTH. ONE PROBLEM IS THAT WREF IS NOT USER INPUT.
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CEARTH/AEG,AEGSQ,FGINV,FG,EGSQ,EGSQP1,C1,C2,FOURC1,TWOC2,  &
     &              XH2,XL2,WREF,RMEAN,REFC20,REFC40,REFC60,BEG,CBLTC,  &
     &              WAE2,GAMMAA,FSTAR,F4
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CLIGHT/VLIGHT,ERRLIM,XCLITE
      DATA HALF/0.5D0/,ONE/1.0D0/,TWO/2.0D0/,P8/.8D0/
      DATA THREE/3.0D0/,FOUR/4.0D0/,FIVE/5.D0/,SEVEN/7.D0/
      DATA ELEVEN/11.D0/,C13/13.D0/,C21/21.D0/,C35/35.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      AEGSQ=AEG**2
      FG=ONE/FGINV
      C1LESQ=(ONE-FG)**2
      EGSQ=ONE-C1LESQ
      BEG=AEG*SQRT(ONE-EGSQ)
      EGSQP1=EGSQ+ONE
      C1=THREE*HALF*AEG*FG**2
      C2=AEG*FG+C1
      FOURC1=FOUR*C1
      TWOC2=TWO*C2
      THIRD=ONE/THREE
      RMEAN=AEG*(ONE-FG)**THIRD
!
      REFM=WREF*WREF*AEG**3*(ONE-FG)/GM
! REF ZONALS UNNORMALIZED
      REFC20=-TWO*(FG*(ONE-HALF*FG)-HALF*REFM*(ONE-TWO/SEVEN*FG+        &
     & ELEVEN/49.D0*FG*FG))/3.D0
      REFC40=4.D0*FG*(ONE-HALF*FG)*(SEVEN*FG*(ONE-HALF*FG)-FIVE*        &
     & REFM*(ONE-TWO/SEVEN*FG))/C35
      REFC60=-4.D0*FG*FG*(6.D0*FG-FIVE*REFM)/C21
! REF ZONALS NORMALIZED
      REFC20=REFC20/SQRT(FIVE)
      REFC40=REFC40*THIRD
      REFC60=REFC60/SQRT(C13)
!
! COMPUTE QUANTITIES FOR GRAVITY FORMULA
      BEGSQ=BEG*BEG
      EPSQ=(AEGSQ-BEGSQ)/BEGSQ
      XM=WREF*WREF*AEGSQ*BEG/GM
      GAMMAA=ONE-1.5D0*XM-(3.D0/14.D0)*EPSQ*XM
      GAMMAA=GAMMAA*GM/(AEG*BEG)
      F2=-FG+2.5D0*XM+.5D0*FG*FG-(26.D0/7.D0)*FG*XM+(15.D0/4.D0)*XM*XM
      F4=-.5D0*FG*FG+2.5D0*FG*XM
      FSTAR=F2+F4
!
! COMPUTE SATELLITE INDEPENDENT (CENTRAL BODY DEPENDENT)
! PART OF LENSE-THIRRING
!
!  BELOW NEEDS TO BE UPDATED (MADE MORE GENERAL)
      WCB=WREF
!  ABOVE NEEDS TO BE UPDATED (MADE MORE GENERAL)
      CBLTC=P8*GM*WCB*(RMEAN/VLIGHT)**2
      WAE2=AESQ*WREF
      RETURN
      END
