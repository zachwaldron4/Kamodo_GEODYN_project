!$AZELEV
      SUBROUTINE AZELEV(MJDSBL,FSECN,FSECM,OBS,OBSCOR,ENV,              &
     &   XTN,XSM,XMN,VSM,                                               &
     &   RNM,URNM,RDOT,UHAT,DIRCOS,EN,RESID,PMPXI,PMPXE,ELEVSC,OBRATE,  &
     &   NMP,INDP,RPOLE,PXPOLE,NINTPL,MINTPL,                           &
     &   S,S1,XTP,PXSPXP,COSTHG,SINTHG,RELV,DOTEN,ELEVPR,WORK,          &
     &   INDSAT,SIGNLT,NM,nm1,                                          &
     &   LPOLAD,LIGHT,LNRATE,LAZEL,INDSTA,                              &
     &   DELCTN,DELCTM,DELCTK,LPTS,LPSBL,LACC,MTYPE,INDSET,XDPOLE,      &
     &   LASER,LNUTAD,AA,II,LL)
!********1*********2*********3*********4*********5*********6*********7**
! AZELEV           83/05/03            8304.0    PGMR - TOM MARTIN
!
! FUNCTION:  COMPUTE AZIMUTH AND ELEVATION MEASUREMENTS.
!            COMPUTATIONS START AT THE FINAL RECEIVED TIME
!            MJDSn+FSECn AND PROCEED BACKWARDS UNTIL THE MODEL
!            IS COMPLETE. IF THE "LIGHT" SWITCH IS .FALSE.
!            ITERATIVE SOLUTIONS FOR THE LIGHT TIME ARE
!            PERFORMED. OTHERWISE, THE MEASUREMENT EVENT TIMES
!            ARE CONSIDERED TO BE KNOWN.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSBL   I    S    MODIFIED JULIAN DAY SECONDS OF THE FINAL
!                      RECEIVED TIME OF THE FIRST OBSERVATION IN
!                      THE BLOCK
!   FSECN   I\O   A    ELAPSED SECONDS FROM MJDSBL OF THE EVENT
!                      TIMES ASSOCIATED WITH TRACKING STATION Tn
!   FSECM    I    A    ELAPSED SECONDS FROM MJDSBL OF THE EVENT
!                      TIMES ASSOCIATED WITH SATELLITE Sm
!   OBS      I    A    THE OBSERVED AZIMUTH X OR ELEVATION ANGLES
!   OBSCOR  I\O   A    SUM OF OBSERVATION CORRECTIONS
!   ENV      I         STATION EAST,NORTH,VERTICAL VECTORS IN E.C.F.
!   XTN     I\O   A    TRUE OF DATE INERT. COORD. OF TRACKING STA. Tn
!   XSM     I\O   A    TRUE OF DATE INERT. COORD. OF SATELLITE Sm
!   XMN      I    A    MEAN POLE COORDINATES OF TRACKING STATION Tn
!   VSM      I    A    TRUE OF DATE INERT. VELOCITY OF SATELLITE Sm
!   RNM     I\O   A    SLANT RANGE FROM Tn TO Sm
!   URNM    I\O        UNIT VECTOR FROM Tn TO Sm
!   RDOT     O         RANGE RATE FROM Tn TO Sm
!   UHAT    I\O        E.C.F. UNIT VECTOR FROM STATION TO S/C
!   DIRCOS  I\O        S/C EAST,NORTH,VERTICAL DIRECTION COSINES
!   EN      I\O        COSINE SQUARED OF ELEVATION
!   RESID    O    A    OBSERVED MINUS COMPUTED OBSERVATION
!   PMPXI    O    A    PARTIAL OF OBS. W.R.T. INERTIAL S/C POS.
!   PMPXE    O    A    PARTIAL OF OBS. W.R.T. E.C.F. STATION POS.
!   ELEVSC   O    A    S/C ELEVATION IN DEGREES
!   OBRATE   O         TIME RATE OF CHANGE OF ELEVATION (RAD/SEC)
!   NMP      O         INDEX OF THE LAST MEASUREMENT ASSOCIATED WITH
!                      EACH INTERPOLATION INTERVAL
!   INDP     O         POINTER TO ADJUSTED POLAR MOTION VALUES
!                      ASSOCIATED WITH EACH INTERPOLATION INTERVAL
!   RPOLE         A    POLAR MOTION ROTATION MATRICES AT EACH END
!                      OF EACH INTERPOLATION INTERVAL
!   PXPOLE        A    PARTIALS OF THE TRUE POLE STATION LOCATION
!                      W.R.T. THE POLAR MOTION X & Y PARAMETERS AT
!                      EACH END OF EACH INTERPOLATION INTERVAL
!   XDPOLE        A    PARTIALS OF THE TRUE POLE STATION RATE
!                      W.R.T. THE POLAR MOTION X & Y PARAMETERS AT
!                      EACH END OF EACH INTERPOLATION INTERVAL
!   NINTPL        S
!   MINTPL   I    S    MAXIMUM NUMBER OF INTERPOLATION INTERVALS
!   S             A    INTERPOLATION FRACTIONS
!   S1            A    1.0-S
!   XTP           A    TRUE POLE COORDINATES OF A TRACKING STATION
!   PXSPXP   O         PARTIALS OF TRUE POLE STATION COORDINATES
!                      W.R.T. POLAR MOTION FOR EACH OF "NM"
!                      MEASUREMENT TIMES
!   COSTHG   O    A    COSINES OF RIGHT ASCENSION OF GREENWICH
!   SINTHG   O    A    SINES  OF RIGHT ASCENSION OF GREENWICH
!   RELV          A    VELOCITY OF SATELLITE W.R.T. TRACKING STATION
!   DOTEN         A    WORKING ARRAY OF DIMENSION (NM,2)
!   ELEVPR
!   WORK          A    WORKING ARRAY OF DIMENSION (NM)
!   INDSAT        A
!   SIGNLT   I    S    SIGN OF THE LIGHT TIME (+1. OR -1.)
!   NM       I    S    NUMBER OF MEASUREMENTS
!   LPOLAD   I    S    SWITCH INDICATING IF POLAR MOTION ADJUST.
!                      PARTIALS ARE REQUIRED FOR EACH INTERP.
!                      INTERVAL
!   LIGHT    I    S    .TRUE. IF LIGHT TIMES HAVE BEEN DETERMINED
!   LNRATE        S
!   LAZEL    I    S    MEASUREMENT SWITCH; F=AZIMUTH, T=ELEVATION
!   INDSTA   I    A    POINTER TO INTERNAL STATION NUMBERS
!   DELCTN   I         DELTA ET CET AT EVENT TIMES ASSOCIATED
!                      WITH TRACKING Tn
!   DELCTM   I         DELTA ET CET AT EVENT TIMES ASSOCIATED
!                      WITH SATELLITE Sm
!   DELCTK   I         DELTA ET CET AT EVENT TIMES ASSOCIATED
!                      WITH TRACKING TK
!   LPTS          A
!   LPSBL         S
!   LACC          A
!   MTYPE    I         MEASUREMENT TYPE
!   INDSET   I         FOR A GIVEN INTERNAL SAT. NUMBER THIS TELLS
!                      WHICH SET THE SAT. BELONGS TO.
!   AA      I/O   A    REAL DYNAMIC ARRAY
!   II      I/O   A    INTEGER DYNAMIC ARRAY
!   LL      I/O   A    LOGICAL DYNAMIC ARRAY
!   LTDRIV   I         .TRUE. IF TIME DERIVATIVES REQUIRED
!
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CBLOKA/FSECBL,SIGNL ,VLITEC,SECEND,BLKDAT(6,4),DOPSCL(5,4)
!      COMMON/CBLOKL/
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/COBLOC/KXMN  (3,4)  ,KENVN (3,4)  ,KSTINF(3,4)  ,          &
     &       KOBS  ,KDTIME,KSMCOR,KSMCR2,KTMCOR,KOBTIM,KOBSIG,KOBSG2,   &
     &       KIAUNO,KOBCOR(9,7)  ,KFSECS(6,8)  ,KOBSUM(8)    ,          &
     &       KEDSIG,KEDSG2
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/COPARM/LOP1

!  jjm added COMMON CORA02 and CBLOKL
!  call to mtstst refers to
!  AA(KXTNPC),AA(KXSMPC),AA(KXTKPC),AA(KXSJPC), AA(KXTIPC), etc.
!  but the kxtnpc, etc.  pointers were not in the routine since CORA02
!  was not present

      COMMON/CORA02/KFSCTB,KFSCTC,KFSCTE,KDSECT,                        &
     &              KFSECT,KS    ,KCIP  ,KCIV  ,                        &
     &       KXTN  ,KXSM  ,KXTK  ,KXSJ  ,KXTI  ,KXTKP ,                 &
     &       KVTN  ,KVSM  ,KVTK  ,KVSJ  ,KVTI  ,KVTKP ,                 &
     &       KSATLT,KSATLN,KSATH ,KCOSTH,KSINTH,                        &
     &       KPXPFM,KPXPFK,KPXPFJ,KTRBF ,KFSTRC,                        &
     &       KFSTRE,KDSCTR,KFSCVS,KXSMBF,KXSKBF,KXSJBF,                 &
     &       KRSSV1,KRSSV2,KRSSV3,KTPMES,KACOEF,KACTIM,                 &
     &       KXTNPC,KXSMPC,KXTKPC,KXSJPC,KXTIPC,KXTKPP,                 &
     &       KRLRNG,KASTO,KASTP,NXCA02
      COMMON/CORA03/KRFMT ,KROTMT,KSRTCH,KCFSC ,KTHG  ,KCOSTG,KSINTG,   &
     &              KDPSI ,KEPST ,KEPSM ,KETA  ,KZTA  ,KTHTA ,KEQN  ,   &
     &              KDPSR ,KSL   ,KH2   ,KL2   ,KXPUT ,KYPUT ,KUT   ,   &
     &              KSPCRD,KSPSIG,KSTAIN,KSXYZ ,KSPWT ,KDXSDP,KDPSDP,   &
     &              KXLOCV,KSTNAM,KA1UT ,KPLTIN,KPLTVL,KPLTDV,KPUTBH,   &
     &              KABCOF,KSPEED,KANGFC,KSCROL,KSTVEL,KSTVDV,KTIMVL,   &
     &              KSTEL2,KSTEH2,KSL2DV,KSH2DV,                        &
     &              KAPRES, KASCAL, KASOFF,KXDOTP,KSAVST,               &
     &              KANGT , KSPDT , KCONAM,KGRDAN,KUANG ,KFAMP ,        &
     &              KOFACT, KOTSGN,KWRKRS,KANFSD,KSPDSD,KPRMFS,KSCRFR,  &
     &              KCOSAS,KSINAS,KDXTID,KALCOF,KSTANF,KNUTIN,KNUTMD,   &
     &              KPDPSI,KPEPST,KDXDNU,KDPSIE,KEPSTE,NXCA03
      COMMON/CMETI /MODEL(999),MWET(999),MDRY(999),MODESC(999),        &
     &              MAPWET(999),MAPDRY(999),METP(999),IRFRC,           &
     &              IRFPRT(10),NXCMTI



      COMMON/CORA04/KABIAS,KABSTR,KABSTP,KCBIAS,KCBSTR,KCBSTP,          &
     &       KRNM  ,KRKM  ,KRKJ  ,KRIJ  ,KRKPM ,                        &
     &       KRRNM ,KRRKM ,KRRKJ ,KRRIJ ,KRRKPM,                        &
     &       KURNM ,KURKM ,KURKJ ,KURIJ ,KURKPM,                        &
     &       KPRRNM,KPRRKM,KPRRKJ,KPRRIJ,KPRRKP,                        &
     &       KPMPXI,KPMPXE,KRA   ,KRELV ,KUHAT ,KWORK ,KTMPCR,KU    ,   &
     &       KS0   ,KS1   ,KXTP  ,KPXSXP,KPXDXP,KXUTDT,KRPOLE,KPXPOL,   &
     &       KXDPOL,KOBBUF,KOBSC ,KRESID,KSIGMA,KRATIO,KELEVS,          &
     &       KPMPA ,KPXSLV,KTPRTL,                                      &
     &       KFRQOF,KXYZOF,KFRQST,KFRQSA,KSTDLY,KSADLY,KXYZCG,KSCMRA,   &
     &       KEBVAL,KEBSIG,KEBNAM,KBTWB ,KTBTWA,KBTWD ,KPMPE ,          &
     &       KFSEB1,KFSEB2,KSTAMT,KPXSPA,KDIAGN,KOBOUT,KGRLT1,KGRLT2,   &
     &       KGRLT3,KGRLT4,KGRLT5,KPSTAT,KRSUNS,KCHEMR,KCHMOR,KEPOSR,   &
     &       KCHEMT,KCHMOT,KEPOST,KCHCBS,KCPOSS,KSCRTM,KXSTT ,KXSTR ,   &
     &       KXSS  ,KRANGT,KRANGR,KCHB1 ,KCHB2 ,KCHBV1,KCHBV2,KCHEB ,   &
     &       KSSTFQ,KSSTCC,KSSTSS,KSSTWT,KRLCOR,KWVLBI,KQUINF,KBRTS ,   &
     &       KBRTSV,KLRARC,KXEPBF,KXEPBR,KPRCBD,KPRTBD,KXPMPA,KXL,      &
     &       KXSIG ,KXEDTS,KXALO1,KXALO2,KXYOF2,KDLATF,KDLATS,KDLONF,   &
     &       KDLONS,KPF   ,KPS   ,                                      &
     &       KXOBSV,KXOBSW,KXEDSW,                                      &
     &       KXSIGW,KPSF  ,KDNUM ,KCNUM ,KFCOR ,KCOSAR,KSINAR,KSABIA,   &
     &       KSBTM1,KSBTM2,KYAWBS,KVLOUV,KACMAG,KOBSTR,                 &
     &       KPRL1 ,KPRL2, KRL1  ,KT1SE ,KTCP  ,                        &
     &       KRATDR,KFQT1S,KFQT1E,KT1STR,KFTTSE,KFRSTR,KFRRAT,KSV1  ,   &
     &       KSV2  ,KTSLOV,                                             &
     &       KGLGR1,KGLGR2,KGLFR1,KGLFR2,                               &
     &       KARGR1,KARGR2,KARFR1,KARFR2,                               &
     &       KRDS1L,KFT1AV,KDFQP ,KFREQ1,KFREQ3,KSAVD1,KSAVD2,          &
     &       KANTOU,KFM3CF,KF2CF,KTMG,KLTMG,KX2TIM,KX2OBS,KXRNDX,KX2SCR,&
     &       KALTWV,KXXBM ,KX2PAR,KATIME,KPMPAT,KPMATT,KX2PAT,          &
     &       KPXEXI,KPXEPA,KPV,KPXEP2,KX2COF,KACOF2,KACOF3,KBCOF,KBCOF2,&
     &       KDDDA ,KX2AUX,KX2VPR,KX2VPA,KEXTRA,KVARAY,KATROT,KATPER,   &
     &       KLTAR ,KXHOLD,KANTBL,KPHC  ,KOFDRV,KGNAME,KGRSIZ,KGRCNT,   &
     &       KGRDAT,KACCDT,KCTBTI,KCTBWE,KCTCTM,KANTUV,KANTOR,KW1PU ,   &
     &       KPYSQH,KSIGSP,KXYOF3,KXVTM1,KXDIST,KXDST0,KTMSE ,KDSDP ,   &
     &       KEXCST,KEXCDT,KEXCGX,KEXCGY,KEXCGZ,KIMNDX,KIMOBS,KIMTIM,   &
     &       KIMSAT,KM2VPA,KDCDD ,KDWNWT,KDPOR ,KC2PAR,KBOUNC,KBPART,   &
     &       NXCA04
      COMMON/CPRESW/LPRE9 (24),LPRE  (24,3),LSWTCH(10,8),LOBSIN,LPREPW, &
     &              LFS   (40)
      COMMON/SIMLM/OBSINT,VINT,ELCUTO,XSMLM
      DIMENSION FSECN(nm1),FSECM(nm1),                                  &
     &   XTN(MINTIM,3),XSM(MINTIM,3),VSM(MINTIM,3),                     &
     &   OBS(nm1),OBSCOR(nm1),ENV(3,3),                                 &
     &   XMN(3),RNM(nm1),EN(nm1),RDOT(nm1),                             &
     &   PMPXE(nm1,3),UHAT(nm1,3),DIRCOS(nm1,3),                        &
     &   URNM(nm1,3,1),NMP(MINTPL,3),                                   &
     &   RESID(nm1),PMPXI(nm1,3),ELEVSC(nm1),OBRATE(nm1),NINTPL(3),     &
     &   INDP(MINTPL,3),RPOLE(3,3,MINTPL),PXPOLE(3,2,MINTPL),           &
     &   S(nm1),S1(nm1),XTP(nm1,3),PXSPXP(nm1,3,2,3),                   &
     &   COSTHG(nm1,3),SINTHG(nm1,3),RELV(nm1),DOTEN(nm1,2),INDSAT(3),  &
     &   ELEVPR(nm1),WORK(nm1),INDSTA(3),                               &
     &   DELCTN(nm1),DELCTM(nm1),DELCTK(nm1),                           &
     &   INDSET(3),AA(1),II(1),LL(1),XDPOLE(3,2,MINTPL)
      DIMENSION LPTS(nm1)
      DATA J3/3/
      DATA ONE/1.0D0/
!
!***********************************************************************
! SPART OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!      write(6,*) 'azelev: nm, nm1 ', nm, nm1
!
! IF ELEVATIONS BEING PROCESSED, BYPASS CERTAIN OPERATIONS,
!    EITHER NOT REQUIRED OR PREVIOUSLY DONE FOR AZIMUTHS
      LOP1=.NOT.LAZEL
      IF(LAZEL) GO TO 10000
! COMPUTE VECTORS AND RANGES BETWEEN STATION AND SPACECRAFT
      lyara = .false.
      isnumb = 0
      LADD=.TRUE.
      CALL MTSTST(MJDSBL,FSECN,FSECM,ELEVSC,ELEVSC,ELEVSC,XTN,XSM,      &
     &   ELEVSC,ELEVSC,ELEVSC,ELEVSC,VSM,ELEVSC,ELEVSC,ELEVSC,XMN,      &
     &   ELEVSC,ELEVSC,AA(KXTNPC),AA(KXSMPC),AA(KXTKPC),AA(KXSJPC),     &
     &   AA(KXTIPC),RNM,ELEVSC,ELEVSC,ELEVSC,ELEVSC,ELEVSC,ELEVSC,      &
     &   ELEVSC,URNM,ELEVSC,ELEVSC,ELEVSC,ELEVSC,ELEVSC,ELEVSC,ELEVSC,  &
     &   NMP,INDSTA,INDP,RPOLE,PXPOLE,NINTPL,MINTPL,S,S1,XTP,ELEVSC,    &
     &   PXSPXP,AA(KPXSLV),COSTHG,SINTHG,INDSAT,SIGNLT,NM,LPOLAD,LIGHT, &
     &  .TRUE.,.FALSE.,1,.FALSE.,AA(KGRLT1),AA(KGRLT2),AA(KGRLT3),      &
     &   AA(KGRLT4),DELCTN,DELCTK,DELCTK,AA(KPSTAT),                    &
     &   AA(KRSUNS),AA(KCHEMR),                                         &
     &   AA(KCHMOR),AA(KEPOSR),AA(KCHEMT),AA(KCHMOT),AA(KEPOST),        &
     &   AA(KCHCBS),AA(KCPOSS),AA(KSCRTM),AA(KXSTT) ,AA(KXSTR) ,        &
     &   AA(KXSS)  ,MTYPE  ,INDSET,AA(KBRTS),AA(KBRTSV),AA,II,LL,       &
     &   lyara, isnumb, LADD ,XDPOLE,AA(KPXDXP),AA(KXUTDT),.FALSE.,     &
     &   FSECN,LNUTAD)
! ROTATE UNIT POS VECTOR FROM INERTIAL TO E.C.F.
      CALL ECFIXP(URNM,COSTHG,SINTHG,UHAT,NM,NM,NM)
! COMPUTE DIRECTION COSINES
      CALL MATPRD(UHAT,ENV,DIRCOS,NM,3,3)
!***** BEGIN DEBUG *****
!     PRINT 88001,ENV
!8001 FORMAT(' ** AZELEV **  ENV='/(1X,3G24.16))
!     PRINT 88002,((UHAT(N,I),N=1,NM),I=1,3)
!8002 FORMAT(' ** AZELEV **  UHAT='/(1X,3G24.16))
!     PRINT 88003,((DIRCOS(N,I),N=1,NM),I=1,3)
!8003 FORMAT(' ** AZELEV **  DIRCOS='/(1X,3G24.16))
!***** END DEBUG *****
! COMPUTE AZIMUTHS AND RESIDUALS...
!
!...TANGENT OF AZIMUTH
!     DO 1800 N=1,NM
!     ELEVSC(N)=DIRCOS(N,1)/DIRCOS(N,2)
!1800 CONTINUE
!***** BEGIN DEBUG *****
!     PRINT 88004,ELEVSC
!8004 FORMAT(' ** AZELEV **  TAN(AZMUTH)='/(1X,3G24.16))
!***** END DEBUG *****
!...AZIMUTH
!     DO 2400 N=1,NM
!     ELEVSC(N)=DATAN(ELEVSC(N))
!2400 CONTINUE
! AZIMUTH IS THE ARCTANGENT OF EAST / NORTH DIRECTION COSINES
      CALL DARCTN(DIRCOS(1,1),DIRCOS(1,2),ELEVSC,NM)
!***** BEGIN DEBUG *****
!     PRINT 88005,ELEVSC
!8005 FORMAT(' ** AZELEV **  AZMUTH='/(1X,3G24.16))
!***** END DEBUG *****
!...RESIDUAL IS OBSERVATION MINUS CALCULATED
      DO 2600 N=1,NM
      RESID(N)=OBS(N)-ELEVSC(N)
 2600 END DO
!***** BEGIN DEBUG *****
!     PRINT 88006,RESID
!8006 FORMAT(' ** AZELEV **  RESID='/(1X,3G24.16))
!***** END DEBUG *****
!...BOTH OBSERVED AND CALCULATED ARE REPEATING FUNCTIONS,
!        HANDLE RESIDUAL ACCORDINGLY
      DO 3800 N=1,NM
      IF(ABS(RESID(N)).GT.PI) RESID(N)=RESID(N)-SIGN(TWOPI,RESID(N))
 3800 END DO
!***** BEGIN DEBUG *****
!     PRINT 88008,RESID
!8008 FORMAT(' ** AZELEV **  RESID='/(1X,3G24.16))
!***** END DEBUG *****
! COMPUTE COSINES SQUARED OF ELEVATION
      DO 4800 N=1,NM
      EN(N)=ONE-DIRCOS(N,3)*DIRCOS(N,3)
 4800 END DO
!***** BEGIN DEBUG *****
!     PRINT 88009,EN
!8009 FORMAT(' ** AZELEV **  COSSQ(ELEV)='/(1X,3G24.16))
!***** END DEBUG *****
! COMPUTE RANGES TIMES COSINES SQUARED OF ELEVATIONS
      DO 5800 N=1,NM
      ELEVSC(N)=EN(N)*RNM(N)
 5800 END DO
! COMPUTE E.C.F. SATELLITE PARTIALS FOR AZIMUTHS
      DO 7000 I=1,3
      DO 6200 N=1,NM
      PMPXE(N,I)=           ENV(I,1)*DIRCOS(N,2)
 6200 END DO
      DO 6400 N=1,NM
      PMPXE(N,I)=PMPXE(N,I)-ENV(I,2)*DIRCOS(N,1)
 6400 END DO
      DO 6800 N=1,NM
      PMPXE(N,I)=PMPXE(N,I)/ELEVSC(N)
 6800 END DO
 7000 END DO
! ROTATE PARTIALS TO INERTIAL
      CALL INERTP(PMPXE,COSTHG,SINTHG,PMPXI,NM,NM,NM)
! STATION PARTIALS ARE NEGATIVE OF SATELLITE PARTIALS
      DO 8000 I=1,3
      DO 7800 N=1,NM
      PMPXE(N,I)=-PMPXE(N,I)
 7800 END DO
 8000 END DO
!***** BEGIN DEBUG *****
!     PRINT 88102,((PMPXI(N,I),N=1,NM),I=1,3)
!8102 FORMAT(' ** AZELEV **  PMPXI='/(1X,3G24.16))
!     PRINT 88103,((PMPXE(N,I),N=1,NM),I=1,3)
!8103 FORMAT(' ** AZELEV **  PMPXE='/(1X,3G24.16))
!***** END DEBUG *****
      IF(LNRATE) GO TO 8500
      CALL RELVEL(VSM,COSTHG,SINTHG,UHAT,RNM,OBRATE,RELV,RDOT,NM,       &
     &   .FALSE.,.FALSE.)
! COMPUTE THE TIME DERIVATIVE OF AZIMUTH...
!
!...COMPUTE THE EAST AND NORTH COMPONENTS OF THE RELATIVE VELOCITY
      CALL MATPRD(RELV,ENV,DOTEN,NM,3,2)
!...COMPUTE THE PRODUCTS OF THE NORTH DIRECTION COSINES AND
!           THE EAST RELATIVE VELOCITIES
      DO 8100 N=1,NM
      OBRATE(N)=          DIRCOS(N,2)*DOTEN(N,1)
 8100 END DO
!...SUM IN THE PRODUCTS OF THE EAST DIRECTION COSINES AND
!          THE NORTH RELATIVE VELOCITIES
      DO 8200 N=1,NM
      OBRATE(N)=OBRATE(N)-DIRCOS(N,1)*DOTEN(N,2)
 8200 END DO
!...DIVIDE BY RANGE TIMES COSINE SQUARED OF ELEVATION
      DO 8300 N=1,NM
      OBRATE(N)=OBRATE(N)/ELEVSC(N)
 8300 END DO
 8500 CONTINUE
! COMPUTE COSINES OF ELEVATION
      DO 9800 N=1,NM
      EN(N)=SQRT(EN(N))
 9800 END DO
!***** BEGIN DEBUG *****
!     PRINT 88010,EN
!8010 FORMAT(' ** AZELEV **  COS(ELEV)='/(1X,3G24.16))
!***** END DEBUG *****
! COMPUTE ELEVATIONS
      CALL ELEV(UHAT,RNM,RDOT,VSM,COSTHG,SINTHG,ENV,DIRCOS(1,J3),EN,    &
     &   RESID,RELV,ELEVSC,OBRATE,NM,.TRUE.,.TRUE.,.FALSE.,.TRUE.,      &
     &   .TRUE.,.TRUE.,.TRUE.)
! LOAD ELEVATION PRINTOUT ARRAY
      DO 9900 N=1,NM
      ELEVPR(N)=ELEVSC(N)/DEGRAD
 9900 END DO
      RETURN
! PROCESS ELEVATION MEASUREMENTS
10000 CONTINUE
      IF(LNRATE) GO TO 10100
      LNELEV=.TRUE.
      IF(.NOT.LACC.AND.LPSBL)THEN
      LNELEV=.FALSE.
      ENDIF
      CALL ELEV(UHAT,RNM,RDOT,VSM,COSTHG,SINTHG,ENV,DIRCOS(1,J3),       &
     &   EN,RESID,RELV,ELEVSC,OBRATE,NM,.TRUE.,.TRUE.,LNELEV,.TRUE.,    &
     &   LNRATE,.TRUE.,.TRUE.)
      IF(.NOT.LACC)CALL SDELEV(LPTS,ELEVSC,NM,ELCUTO,.TRUE.)
10100 CONTINUE
!...RESIDUAL IS OBSERVED MINUS COMPUTED ELEVATION
      DO 10400 N=1,NM
      RESID(N)=OBS(N)-ELEVSC(N)
10400 END DO
!***** BEGIN DEBUG *****
!     PRINT 88011,RESID
!8011 FORMAT(' ** AZELEV **  ELEV RESID='/(1X,3G24.16))
!***** END DEBUG *****
      IF(.NOT.LITER1) GO TO 10900
      IF(LPRE(3,1).AND.LPRE(7,1)) GO TO 10900
! COMPUTE ELEVATION REFRACTION
      BLKMET=BLKDAT(1,1)
      JMET=KOBCOR(1,1)

!jtw replace call to refrac with call directly to refrac model
!      CALL REFRAC(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2),WORK,NM,3, &
!     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
!     &   .FALSE.,WORK,WORK,WORK,WORK,MTYPE,MJDSBL)


      IWPR=IRFPRT(MODEL(MTYPE))

      IF(MODEL(MTYPE).EQ.0) THEN

      CALL HOPFLD(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2),WORK,NM,3, &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,WORK,WORK,WORK,WORK,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.1) THEN

           IF(LASER) THEN

                CALL MARMUR(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2), &
               &    WORK,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,     &
               &    AA(JMET),LPRE(1,1),.FALSE.,WORK,WORK,WORK,WORK,     &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           ELSE IF(.NOT.LASER) THEN

                CALL HOPFLD(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2), &
               &    WORK,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,     &
               &    AA(JMET),LPRE(1,1),.FALSE.,WORK,WORK,WORK,WORK,     &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           END IF

      ELSE IF(MODEL(MTYPE).EQ.2) THEN

      CALL VLBGPS(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2),WORK,NM,3, &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,WORK,WORK,WORK,WORK,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.3) THEN

      CALL GPSNIL(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2),WORK,NM,3, &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,WORK,WORK,WORK,WORK,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.4) THEN

           IF(LASER) THEN

                CALL MARMUR(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2), &
               &    WORK,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,     &
               &    AA(JMET),LPRE(1,1),.FALSE.,WORK,WORK,WORK,WORK,     &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           ELSE IF(.NOT.LASER) THEN

                CALL GPSNIL(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2), &
               &    WORK,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,     &
               &    AA(JMET),LPRE(1,1),.FALSE.,WORK,WORK,WORK,WORK,     &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           END IF

      ELSE IF(MODEL(MTYPE).EQ.5) THEN

           IF(LASER) THEN

                CALL MENDEZ(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2), &
               &    WORK,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,     &
               &    AA(JMET),LPRE(1,1),.FALSE.,WORK,WORK,WORK,WORK,     &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           ELSE IF(.NOT.LASER) THEN

                CALL HOPFLD(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2), &
               &    WORK,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,     &
               &    AA(JMET),LPRE(1,1),.FALSE.,WORK,WORK,WORK,WORK,     &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           END IF

      ELSE IF(MODEL(MTYPE).EQ.6) THEN

      CALL GMFHOP(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2),WORK,NM,3, &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,WORK,WORK,WORK,WORK,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.7) THEN

      CALL GMFSAS(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2),WORK,NM,3, &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,WORK,WORK,WORK,WORK,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.8) THEN

      CALL VMF1(EN,DIRCOS(1,3),OBRATE,RNM,DOTEN,DOTEN(1,2),WORK,NM,3,   &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,WORK,WORK,WORK,WORK,MTYPE,MJDSBL,FSECN,IWPR,NSTA0,     &
     &   AA(KA1UT))

      END IF

      M=1
      DO 10600 IWORD =3,5,2
      IF(LPRE  (IWORD ,1)) GO TO 10550
! ADD TROPOSPHERIC REFRACTION TO SUM OF OBSERVATION CORRECTIONS
      DO 10450 N=1,NM
      OBSCOR(N)=OBSCOR(N)+DOTEN(N,M)
10450 END DO
      IWORD1=IWORD+1
      IF(.NOT.LSWTCH(IWORD1,2)) GO TO 10550
! STORE TROPOSPHERIC REFRACTION CORRECTIONS IN OBSERVATION BUFFER
      JOBCOR=KOBCOR(IWORD1,1)
      DO 10500 N=1,NM
      AA(JOBCOR)=DOTEN(N,M)
      JOBCOR=JOBCOR+1
10500 END DO
10550 CONTINUE
      M=M+1
10600 END DO
      IF(LPRE  (7,1)) GO TO 10900
! ADD IONOSPHERIC REFRACTION TO SUM OF OBSERVATION CORRECTIONS
      DO 10700 N=1,NM
      OBSCOR(N)=OBSCOR(N)+WORK(N)
10700 END DO
      IWORD1=IWORD+1
      IF(.NOT.LSWTCH(8,2)) GO TO 10900
! STORE IONOSPHERIC REFRACTION CORRECTIONS IN OBSERVATION BUFFER
      JOBCOR=KOBCOR(8,1)
      DO 10800 N=1,NM
      AA(JOBCOR)=WORK(N)
10800 END DO
10900 CONTINUE
! CONVERT ELEVATION TO DEGREES
      DO 11800 N=1,NM
      ELEVSC(N)=ELEVPR(N)
11800 END DO
! COMPUTE RANGES TIMES COSINES OF ELEVATION
      DO 12800 N=1,NM
      EN(N)=EN(N)*RNM(N)
12800 END DO
! COMPUTE PARTIALS OF ELEVATIONS W.R.T. E.C.F. SATELLITE POSITIONS
      DO 14000 I=1,3
      DO 13400 N=1,NM
      PMPXE(N,I)=ENV(I,3)-DIRCOS(N,3)*UHAT(N,I)
13400 END DO
      DO 13800 N=1,NM
      PMPXE(N,I)=PMPXE(N,I)/EN(N)
13800 END DO
14000 END DO
! ROTATE PARTIALS TO INERTIAL
      CALL INERTP(PMPXE,COSTHG,SINTHG,PMPXI,NM,NM,NM)
! STATION PARTIALS ARE NEGATIVE OF SATELLITE PARTIALS
      DO 15000 I=1,3
      DO 14800 N=1,NM
      PMPXE(N,I)=-PMPXE(N,I)
14800 END DO
15000 END DO
      RETURN
      END
