!$DTM
      SUBROUTINE DTM(DAY,FLUX,AVGFLX,FLUXKP,ALTI,COSHL,SINHL,           &
     &               COSLAT,SINLAT,DTOTAL,DRHODZ,MJDSEC)
!********1*********2*********3*********4*********5*********6*********7**
! DTM              00/00/00            8803.0    PGMR - WEI XIA
!
! FUNCTION: CALCULATE THE TEMPERATURE AND TOTAL DENSITY
!           AS GIVEN BY THE FRENCH DTM MODEL
!
!   I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   DAY                DAY OF THE YEAR
!   FLUX               SOLAR FLUX VALUES FROM THE PRECEDING DAY
!   AVGFLX             AVERAGE SOLAR FLUX VALUES WHERE AVERAGING IS
!                      PERFORMED OVER 3 SOLAR CYCLES
!   FLUXKP             3 HOURLY MAGNETIC FLUX KP VALUES
!   ALTI               ALTITUDE IN METER(M) ABOVE 12O,000 METERS
!   COSHL              THE COSINE OF HL(LOCAL HOUR)
!   SINHL              THE SINE OF HL(LOCAL HOUR)
!   COSLAT             THE COSINE OF LATITUDE IN RADIAN
!   SINLAT             THE SINE OF LATITUDE IN RADIAN
!   DTOTAL             TOTAL DENSITY WITH  GRAM/(CM)**3
!   DRHODZ             DERIVATIVE OF TOTAL DENSITY WRT ALTI
!   MJDSEC   I    S    TIME IN INTEGRAL SECONDS FROM GEODYN REF. TIME
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      COMMON/DTMPOL/P10,P20,P30,P40,P50,P11,P21,P31,P51,P22,P32,P33
!
      DIMENSION OGO(36),A(36),B(36),C(36),H(36)
      DIMENSION ALEFA(5),AMA(5),DBASE(5)
      DIMENSION P(12)
!
!
! THERMOPAUSE TEMPERATURE MODEL
      DATA OGO/999.8D0,                                                 &
     &-0.36357D-02, 0.24593D-01, 0.13259D-02,-0.56234D-05, 0.25361D-02, &
     & 0.17656D-01, 0.33677D-01,-0.37643D-02, 0.17452D-01,-3.63831D+00, &
     &-0.27270D-02, 0.27465D-01,-1.63795D+00,-0.13373D+00,-0.27321D-01, &
     &-0.96732D-02,-2.50880D-01,-0.27469D-01,-2.99288D+00,-0.66567D-01, &
     &-0.59604D-02, 0.67446D-02,-0.26620D-01, 0.14691D-01,-0.10971D+00, &
     & 0.88700D-02, 0.36918D-02, 0.12219D-01,-0.76358D-02,-0.44894D-02, &
     & 0.23646D-02, 0.50569D-02, 0.10792D-02,-0.71610D-03, 0.96385D-03/
!      HELIUM
      DATA A/3.0D+13,                                                   &
     & 0.12000D+00,-0.15000D+00, 0.23799D-02,-0.31008D-04, 0.56980D-02, &
     & 0.17103D-01,-0.17997D+00,-0.13251D+00,-0.64239D-01, 3.80793D+00, &
     & 0.24859D+00,-0.17732D+00, 1.81331D+00,-0.11071D+01,-0.36255D-01, &
     &-0.10180D+00,-3.36273D+00, 0.11711D+00,-3.70403D+00,-0.31594D+00, &
     & 0.52452D-01,-0.31686D-01,-0.13975D+00, 0.83399D-01, 0.21382D+00, &
     &-0.61816D-01,-0.15026D-01, 0.10574D+00,-0.97446D-01, 0.22606D-01, &
     & 0.12125D-01,-0.22391D-01,-0.24648D-02, 0.32432D-02,-0.57766D-02/
!      ATOMIC OXYGENE
      DATA B/0.93D+17,                                                  &
     &-0.16598D-02,-0.99095D-01, 0.78453D-03,-0.23733D-04, 0.80001D-02, &
     & 0.70000D-01,-0.18000D+00, 0.14597D+00, 0.10517D+00, 0.64263D-01, &
     & 0.24620D+00,-0.50845D-01, 1.85356D+00, 0.39103D+00, 0.96719D-01, &
     & 0.12624D+00,-0.28570D+00,-0.14463D+00, 1.88607D+00,-0.20686D+00, &
     & 0.82922D-02,-0.30261D-01, 0.14237D+00,-0.28977D-01, 0.22409D+00, &
     &-0.79313D-01,-0.16385D-01,-0.10113D+00, 0.65531D-01, 0.53655D-01, &
     &-0.23722D-02, 0.18910D-01,-0.26522D-02, 0.83050D-02,-0.38860D-02/
! MOLECULAR NITROGEN
      DATA C/3.842D+17,                                                 &
     & 0.28076D-01, 0.48462D-01,-0.81017D-03, 0.20983D-04, 0.29998D-02, &
     &-0.10000D-01,+0.80000D-01, 0.53709D-01,-0.13732D+00, 1.48687D+00, &
     & 0.19930D-01,-0.84711D-01, 1.53685D+00,-0.49083D-01, 0.91420D-02, &
     &-0.16362D-01, 8.46944D-01,-0.46712D-01, 9.07841D-01, 0.0D+00    , &
     & 0.0D+00    , 0.0D+00    , 0.0D+00    , 0.0D+00    , 0.0D+00    , &
     & 0.0D+00    , 0.0D+00    , 0.0D+00    , 0.0D+00    , 0.0D+00    , &
     & 0.0D+00    , 0.0D+00    , 0.0D+00    , 0.0D+00    , 0.0D+00    /
! HYDROGENE
      DATA H/1.761D+11,                                                 &
     &-1.33700D-01, 0.0D+00    ,-1.24600D-02, 0.0D+00    ,-1.93000D-02, &
     &-6.00000D-02,-0.20000D-02, 5.87800D-02, 0.0D+00    , 1.58727D+00, &
     & 0.0D+00    , 0.0D+00    , 0.0D+00    , 3.30100D-01, 1.04500D-01, &
     & 0.0D+00    ,-0.25408D+00,-9.06500D-02,-1.23857D+00, 2.09400D-01, &
     & 2.83000D-02, 0.0D+00    , 8.57100D-02,-2.47500D-02, 3.83000D-01, &
     & 2.94100D-02, 0.0D+00    ,-3.97400D-03, 4.35600D-02, 0.0D+00    , &
     & 0.0D+00    , 0.0D+00    , 0.0D+00    , 0.0D+00    , 0.0D+00    /
! TEMPERATURE OF MOLECULAR OXYGEN AT 120 KM (CONSTANT)
      DATA TO2/4.775D+16/
! THERMAL DICFFUSION FACTORS OF H2 AND HE (ZEROS FOR O,N2 AND O2)
      DATA ALEFA/-0.4D0,-0.38D0,0.0D0,0.0D0,0.0D0/
! MOLECULAR WEIGHT OF CONSTITUENTS H2, HE, O, N2, AND O2(KG/MOLE)
      DATA AMA/1.0D-03,4.0D-03,16.0D-03,28.0D-03,32.0D-03/
! REFERENCE LOWER BOUND ALTITUDE AND MEAN EARTH RADIUS
      DATA RE/6356.766D+03/,ZLB/120.0D+3/,REPZLB/6476.766D+03/
! GRAVITY & TEMPERATURE AT 120 KM, AND TEMP. GRADIENT (1/M)
      DATA GLB/9.446626D0/,T120/380.D0/,SIGMA/0.201544D-04/
! AVOGRADRO'S NUMBER, BOLTZMANN'S GAS CONSTANT(KG*M**2/MOLE/SEC**2/DEG/
      DATA USAVO/1.660421D-24/,RGAS/8.31432D0/
!
      DATA ONE/1.D0/,HALF/0.5D0/,THREE/3.D0/,FIVE/5.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! ALTITUDE TEST: FRENCH DRAG MODEL WORKS FOR ALTI>120,000 METERS
      ALTMIN=ZLB
      IF(ALTI.LE.ALTMIN) GOTO 2000
      R=SINLAT
      R2=R*R
      R4=R2*R2
      RR2=ABS(1.D0-R2)
      RR=SQRT(RR2)
      P(1)=R
      P(2)=HALF*(THREE*R2-ONE)
      P(3)=HALF*R*(FIVE*R2-THREE)
      P(4)=0.125*(35.D0*R4-30.D0*R2+THREE)
      P(5)=0.125*R*(63.D0*R4-70.D0*R2+15.D0)
      P(6)=RR
      P(7)=RR*THREE*R
      P(8)=RR*1.5*(FIVE*R2-ONE)
      P(9)=1.875*RR*(21.D0*R4-14.D0*R2+ONE)
      P(10)=THREE*RR2
      P(11)=15.D0*R*RR2
      P(12)=15.D0*RR*RR2
! COMPUTE ALTITUDE DEPENDENT FUNCTION
      ZETA=REPZLB/(RE+ALTI)
      DZETA=ZETA*ZETA
      ZETA=(ALTI-ZLB)*ZETA
      SIGZET=-SIGMA*ZETA
      EXPSZ=EXP(SIGZET)
! COMPUTE THERMOPAUSE TEMPERATURE
      COSHL2=COSHL*COSHL
      SINHL2=SINHL*SINHL
      COS2HL=COSHL2-SINHL2
      SIN2HL=2.D0*SINHL*COSHL
      COS3HL=COS2HL*COSHL-SIN2HL*SINHL
      SIN3HL=SIN2HL*COSHL+COS2HL*SINHL
      CALL GDELRB(AVGFLX,FLUX,FLUXKP,DAY,COSHL,SINHL,COS2HL,SIN2HL,     &
     &            COS3HL,SIN3HL,OGO,GDELT,1.0D0,P,MJDSEC)
      TINF=OGO(1)*GDELT
      FACT4=1.0D0-T120/TINF
      FACT3=FACT4*EXPSZ
      FACT1=1.0D0/(1.0D0-FACT3)
      FACT2=(1.0D0-FACT4)*FACT1
! COMPUTE THERMOSPHERIC DENSITY DUE TO HYDROGEN
      CALL GDELRB(AVGFLX,FLUX,FLUXKP,DAY,COSHL,SINHL,COS2HL,SIN2HL,     &
     &            COS3HL,SIN3HL,H,GDELHH,0.0D0,P,MJDSEC)
      DBASE(1)=H(1)*EXP(GDELHH-1.0D0)
! COMPUTE THERMOSPHERIC DENSITY DUE TO HELIUM
      CALL GDELRB(AVGFLX,FLUX,FLUXKP,DAY,COSHL,SINHL,COS2HL,SIN2HL,     &
     &            COS3HL,SIN3HL,A,GDELH,0.0D0,P,MJDSEC)
      DBASE(2)=A(1)*EXP(GDELH-1.0D0)
! COMPUTE THERMOSPHERIC DENSITY DUE TO OXYGEN
      CALL GDELRB(AVGFLX,FLUX,FLUXKP,DAY,COSHL,SINHL,COS2HL,SIN2HL,     &
     &            COS3HL,SIN3HL,B,GDELO,1.0D0,P,MJDSEC)
      DBASE(3)=B(1)*EXP(GDELO-1.0D0)
! COMPUTE THERMOSPHERIC DENSITY DUE TO NITROGEN
      CALL GDELRB(AVGFLX,FLUX,FLUXKP,DAY,COSHL,SINHL,COS2HL,SIN2HL,     &
     &            COS3HL,SIN3HL,C,GDELN,1.0D0,P,MJDSEC)
      DBASE(4)=C(1)*EXP(GDELN-1.0D0)
! SET SONSTANT THERMOSPHERIC DENSITY DUE TO MOLECULAR OXYGEN
      DBASE(5)=TO2
! COMPUTE THE TOTAL THERMOSPHERIC DENSITY
      RHOI=0.0D0
      RHOG=0.0D0
      RHOA=0.0D0
      DTOTAL=0.0D0
      DO 1000 I=1,5
      GAMMA=AMA(I)*GLB/(SIGMA*RGAS*TINF)
      DN=DBASE(I)*EXP(SIGZET*GAMMA)*FACT2**(1.0D0+ALEFA(I)+GAMMA)
      RHOI=AMA(I)*DN
      DTOTAL=DTOTAL+RHOI
      RHOG=RHOG+RHOI*GAMMA
      RHOA=RHOA+RHOI*(ALEFA(I)+1.0D0)
 1000 END DO
      RHOA=RHOA/DTOTAL
      RHOG=RHOG/DTOTAL
      DTOTAL=DTOTAL*USAVO
      ECH=SIGMA*DZETA*FACT1*(RHOG+FACT3*RHOA)
! COMPUTE THE PARTIAL DERIVATIVE OF DENSITY WRT THE ALTITUDE
      DRHODZ=-ECH*DTOTAL
 2000 RETURN
      END
