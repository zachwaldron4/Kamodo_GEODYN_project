      SUBROUTINE FTAST(XAXIS2,XMNRT2,CSA,GMB,CMAT,DNORM,DS123,DC123,    &
     &                 DVECT,ACCA,ACCB,TQA,TQB,LONLYF)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! NOTE: THERE ARE FOUR OUTPUTS : ACCA,ACCB,TQA,TQB. INTERNALLY, THE
!       ROUTINE COMPUTES THESE IN THE B BODYFIXED FRANE. UPON OUTPUT
!       THREE OF THE QUANTITES HAVE BEEN ROTATED TO ANOTHER FRAME.
!
!       ACCA  - ACCELERATION OF THE A ASTEROID DUE TO THE NON POINT MASS
!               INTERACTIONS BETWEEN A AND B. THE COODINATE SYSTEM IS
!               J2000 WITH AN ORIGIN SOMEWHERE OTHER THAN AT THE A OR B
!               ASTEROID.
!       ACCB  - ACCELERATION OF THE B ASTEROID DUE TO THE NON POINT MASS
!               INTERACTIONS BETWEEN A AND B. THE COODINATE SYSTEM IS
!               J2000 WITH AN ORIGIN SOMEWHERE OTHER THAN AT THE A OR B
!               ASTEROID.
!       TRQA  - TORQUE EXERTED ON THE A ASTEROID FROM THE NON POINT MASS
!               INTERACTIONS BETWEEN THE A AND B ASTEROIDS. THE
!               COORDINATE SYSTEM IS THE A BODY FIXED FRAME
!       TRQB  - TORQUE EXERTED ON THE B ASTEROID FROM THE NON POINT MASS
!               INTERACTIONS BETWEEN THE A AND B ASTEROIDS. THE
!               COORDINATE SYSTEM IS THE B BODY FIXED FRAME
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      DIMENSION XAXIS2(3,3)
      DIMENSION XMNRT2(3)
      DIMENSION CSA(200)
      DIMENSION CMAT(3,3)
      DIMENSION DS123(3),DC123(3),DVECT(3)
      DIMENSION FBL0(3)
      DIMENSION FBL0P(3)
      DIMENSION FBL2(3)
      DIMENSION FBT(3)
      DIMENSION FBTP(3)
      DIMENSION ACCA(3),ACCB(3),TQA(3),TQB(3)
      DIMENSION TQA_B(3)
      DIMENSION ACCBT(3)
      DATA GCON/6.67300D-11/
      DATA FUC20/2.23606797749979D0/
      DATA FUC22/0.645497224367906D0/
!
      C20U=CSA(5)*FUC20
      C22U=CSA(7)*FUC22
!
      XMASSB=GMB/GCON
      XMASSA=GM/GCON
!
      C31SQ=CMAT(3,1)*CMAT(3,1)
      C32SQ=CMAT(3,2)*CMAT(3,2)
      C11SQ=CMAT(1,1)*CMAT(1,1)
      C21SQ=CMAT(2,1)*CMAT(2,1)
      C12SQ=CMAT(1,2)*CMAT(1,2)
      C22SQ=CMAT(2,2)*CMAT(2,2)
!
      DN2=DNORM*DNORM
      DN5=DN2*DN2*DNORM
      DN7=DN5*DN2
!
      YK1=(XMNRT2(2)+XMNRT2(3)-XMNRT2(1))/2.D0
      YK2=(XMNRT2(1)+XMNRT2(3)-XMNRT2(2))/2.D0
      YK3=(XMNRT2(1)+XMNRT2(2)-XMNRT2(3))/2.D0
!
      XKSUM=YK1+YK2+YK3
      XKD2DT=YK1*DS123(1)*DS123(1)+YK2*DS123(2)*DS123(2)                &
     &      +YK3*DS123(3)*DS123(3)
!
      P63=XMASSB*DN2-3.5D0*XKSUM+31.5D0*XKD2DT
      P35=XMASSB*DN2-2.5D0*XKSUM+17.5D0*XKD2DT
!
!
      T1=CMAT(3,3)*CMAT(3,3)-0.5D0*C31SQ-0.5D0*C32SQ
      T2=2.D0*CMAT(1,3)*CMAT(1,3)-2.D0*CMAT(2,3)*CMAT(2,3)              &
     &   -C11SQ+C21SQ-C12SQ+C22SQ
      F201=T1*C20U+T2*C22U
!
      T1=CMAT(3,1)*CMAT(3,3)
      T2=2.D0*(CMAT(1,1)*CMAT(1,3)-CMAT(2,1)*CMAT(2,3))
      F211=T1*C20U+T2*C22U
!
      T1=CMAT(3,2)*CMAT(3,3)
      T2=2.D0*(CMAT(1,2)*CMAT(1,3)-CMAT(2,2)*CMAT(2,3))
      F212=T1*C20U+T2*C22U
!
      T1=(C31SQ-C32SQ)/4.D0
      T2=(C11SQ-C21SQ-C12SQ+C22SQ)/2.D0
      F221=T1*C20U+T2*C22U
!
      T1=CMAT(3,1)*CMAT(3,2)/2.D0
      T2=CMAT(1,1)*CMAT(1,2)-CMAT(2,1)*CMAT(2,2)
      F222=T1*C20U+T2*C22U
!
!
      T1=XMASSB*DN2-1.5D0*XKSUM+7.5D0*XKD2DT
      T1P=-1.5D0*XKSUM+7.5D0*XKD2DT
      FBL0(1)=T1*DC123(1)-3.D0*YK1*DC123(1)
      FBL0(2)=T1*DC123(2)-3.D0*YK2*DC123(2)
      FBL0(3)=T1*DC123(3)-3.D0*YK3*DC123(3)
      FBL0P(1)=T1P*DC123(1)-3.D0*YK1*DC123(1)
      FBL0P(2)=T1P*DC123(2)-3.D0*YK2*DC123(2)
      FBL0P(3)=T1P*DC123(3)-3.D0*YK3*DC123(3)
      FACT=-GM/DN5
      DO I=1,3
        FBL0(I)=FACT*FBL0(I)
        FBL0P(I)=FACT*FBL0P(I)
      ENDDO
!
!
      TDZ2=3.D0*DS123(3)*DS123(3)-1.D0
      T1=-2.5D0*(TDZ2*P63+14.D0*XKD2DT+2.D0*YK3-YK1-YK2)
      T2=DC123(1)*(17.5D0*TDZ2*YK1-P35+10.D0*YK1)
      T3=DC123(2)*(17.5D0*TDZ2*YK2-P35+10.D0*YK2)
      T4=DC123(3)*(17.5D0*TDZ2*YK3+2.D0*P35-20.D0*YK3)
      FBL2(1)=T1*DC123(1)+T2
      FBL2(2)=T1*DC123(2)+T3
      FBL2(3)=T1*DC123(3)+T4
      FACT=F201*GM*AE*AE/DN7
      DO I=1,3
        FBL2(I)=FACT*FBL2(I)
      ENDDO
!
!
      T1=-15.D0*DS123(1)*DS123(3)*(P63-7.D0*(YK1+YK3))
      T2=3.D0*DC123(3)*(35.D0*DS123(1)*DS123(1)*YK1+P35-5.D0*(YK1+YK3))
      T3=105.D0*DC123(2)*DS123(1)*DS123(3)*YK2
      T4=3.D0*DC123(1)*(35.D0*DS123(3)*DS123(3)*YK3+P35-5.D0*(YK1+YK3))
      FACT=F211*GM*AE*AE/DN7
      T1=T1*FACT
      T2=T2*FACT
      T3=T3*FACT
      T4=T4*FACT
      FBL2(1)=FBL2(1)+T1*DC123(1)+T2
      FBL2(2)=FBL2(2)+T1*DC123(2)+T3
      FBL2(3)=FBL2(3)+T1*DC123(3)+T4
!
      T1=-15.D0*DS123(2)*DS123(3)*(P63-7.D0*(YK2+YK3))
      T2=105.D0*DC123(1)*DS123(2)*DS123(3)*YK1
      T3=3.D0*DC123(3)*(35.D0*DS123(2)*DS123(2)*YK2+P35-5.D0*(YK2+YK3))
      T4=3.D0*DC123(2)*(35.D0*DS123(3)*DS123(3)*YK3+P35-5.D0*(YK2+YK3))
      FACT=F212*GM*AE*AE/DN7
      T1=T1*FACT
      T2=T2*FACT
      T3=T3*FACT
      T4=T4*FACT
      FBL2(1)=FBL2(1)+T1*DC123(1)+T2
      FBL2(2)=FBL2(2)+T1*DC123(2)+T3
      FBL2(3)=FBL2(3)+T1*DC123(3)+T4
!
!
      TDXY2=3.D0*(DS123(1)*DS123(1)-DS123(2)*DS123(2))
      T1=-5.D0*(TDXY2*P63+3.D0*(YK1-YK2)                                &
     &          -42.D0*(YK1*DS123(1)*DS123(1)-YK2*DS123(2)*DS123(2)))
      T2=DC123(1)*(35.D0*TDXY2*YK1+6.D0*P35-60.D0*YK1)
!!!   T3=DC123(2)*(35.D0*TDXY2*YK2-6.D0*P35-60.D0*YK2)
! CORRECTED IN RUBINCAM Two Triaxial Ellipsoids Corrected 21 December 2015
      T3=DC123(2)*(35.D0*TDXY2*YK2-6.D0*P35+60.D0*YK2)
      T4=35.D0*DC123(3)* TDXY2*YK3
      FACT=F221*GM*AE*AE/DN7
      T1=T1*FACT
      T2=T2*FACT
      T3=T3*FACT
      T4=T4*FACT
      FBL2(1)=FBL2(1)+T1*DC123(1)+T2
      FBL2(2)=FBL2(2)+T1*DC123(2)+T3
      FBL2(3)=FBL2(3)+T1*DC123(3)+T4
!
!
      T1=-30.D0*DS123(1)*DS123(2)*(P63-7.D0*(YK1+YK2))
      T2=6.D0*DC123(2)*(35.D0*DS123(1)*DS123(1)*YK1+P35-5.D0*(YK1+YK2))
      T3=6.D0*DC123(1)*(35.D0*DS123(2)*DS123(2)*YK2+P35-5.D0*(YK1+YK2))
      T4=210.D0*DC123(3)*DS123(1)*DS123(2)*YK3
      FACT=F222*GM*AE*AE/DN7
      T1=T1*FACT
      T2=T2*FACT
      T3=T3*FACT
      T4=T4*FACT
      FBL2(1)=FBL2(1)+T1*DC123(1)+T2
      FBL2(2)=FBL2(2)+T1*DC123(2)+T3
      FBL2(3)=FBL2(3)+T1*DC123(3)+T4
!
!
      DO I=1,3
        FBT(I)=FBL0(I)+FBL2(I)
        FBTP(I)=FBL0P(I)+FBL2(I)
      ENDDO
!
!
!    END FORCE
!
      IF(LONLYF) GO TO 1000
!
!
!
!
      FACT=-3.D0*GM/DN5
      T01=FACT*(YK3-YK2)*DC123(2)*DC123(3)
      T02=FACT*(YK1-YK3)*DC123(1)*DC123(3)
      T03=FACT*(YK2-YK1)*DC123(1)*DC123(2)
!
!
      FACT=F201*GM*AE*AE/DN7
      T11=FACT*DC123(2)*DC123(3)*(17.5D0*TDZ2-5.D0)*(YK2-YK3)
      T12=FACT*DC123(1)*DC123(3)*(17.5D0*TDZ2-5.D0)*(YK3-YK1)
      T13=FACT*DC123(1)*DC123(2)*(17.5D0*TDZ2+10.D0)*(YK1-YK2)
!
!
      FACT=F211*GM*AE*AE/DN7
      T21=DC123(1)*DC123(2)*(105.D0*DS123(3)*DS123(3)-15.D0)*(YK2-YK3)
      T22=105.D0*DS123(1)*DS123(3)*DC123(1)*DC123(3)
      T22=T22-15.D0*(DC123(1)*DC123(1)+DC123(3)*DC123(3))+3D0*DN2
      T22=T22*(YK3-YK1)
      T23=DC123(2)*DC123(3)*(105.D0*DS123(1)*DS123(1)-15.D0)*(YK1-YK2)
      T21=T21*FACT
      T22=T22*FACT
      T23=T23*FACT
!
!
      FACT=F212*GM*AE*AE/DN7
      T31=105.D0*DS123(2)*DS123(3)*DC123(2)*DC123(3)
      T31=T31-15.D0*(DC123(2)*DC123(2)+DC123(3)*DC123(3))+3D0*DN2
      T31=T31*(YK2-YK3)
      T32=DC123(1)*DC123(2)*(105.D0*DS123(3)*DS123(3)-15.D0)*(YK3-YK1)
      T33=DC123(1)*DC123(3)*(105.D0*DS123(2)*DS123(2)-15.D0)*(YK1-YK2)
      T31=T31*FACT
      T32=T32*FACT
      T33=T33*FACT
!
!
      FACT=F221*GM*AE*AE/DN7
      T41=DC123(2)*DC123(3)
      T41=T41*(105.D0*(DS123(1)*DS123(1)-DS123(2)*DS123(2))+30.D0)
      T41=T41*(YK2-YK3)
      T42=DC123(1)*DC123(3)
      T42=T42*(105.D0*(DS123(1)*DS123(1)-DS123(2)*DS123(2))+30.D0)
!!!   T42=T42*(YK3-YK1)
! CORRECTED IN RUBINCAM Two Triaxial Ellipsoids Corrected 21 December 2015
      T42=T42*(YK1-YK3)
      T43=DC123(1)*DC123(2)
      T43=T43*(105.D0*(DS123(1)*DS123(1)-DS123(2)*DS123(2)))
      T43=T43*(YK1-YK2)
      T41=T41*FACT
      T42=T42*FACT
      T43=T43*FACT
!
!
      FACT=F222*GM*AE*AE/DN7
      T51=DC123(1)*DC123(3)*(210.D0*DS123(2)*DS123(2)-30.D0)
      T51=T51*(YK2-YK3)
      T52=DC123(2)*DC123(3)*(210.D0*DS123(1)*DS123(1)-30.D0)
      T52=T52*(YK3-YK1)
      T53=210.D0*DS123(1)*DS123(2)*DC123(1)*DC123(2)
      T53=T53-30.D0*(DC123(1)*DC123(1)+DC123(2)*DC123(2))
      T53=T53+6.D0*DN2
      T53=T53*(YK1-YK2)
      T51=T51*FACT
      T52=T52*FACT
      T53=T53*FACT
!
!
      T1T=T01+T11+T21+T31+T41+T51
      T2T=T02+T12+T22+T32+T42+T52
      T3T=T03+T13+T23+T33+T43+T53
!!!      T1T=T01
!!!      T2T=T02
!!!      T3T=T03
!!!      T1T=0.D0
!!!      T2T=0.D0
!!!      T3T=0.D0
!!!      T1T=T11+T21+T31+T41+T51
!!!      T2T=T12+T22+T32+T42+T52
!!!      T3T=T13+T23+T33+T43+T53
!      TQB(1)=T1T*XAXIS2(1,1)+T2T*XAXIS2(1,2)+T3T*XAXIS2(1,3)
!      TQB(2)=T1T*XAXIS2(2,1)+T2T*XAXIS2(2,2)+T3T*XAXIS2(2,3)
!      TQB(3)=T1T*XAXIS2(3,1)+T2T*XAXIS2(3,2)+T3T*XAXIS2(3,3)
!
! TQB IS IN B BODY FIXED FRAME
!
      TQB(1)=T1T
      TQB(2)=T2T
      TQB(3)=T3T
! AT THIS POINT TQA IS IN B BODY FRAME
!
      TQA_B(1)=-TQB(1)-(DC123(2)*FBT(3)-DC123(3)*FBT(2))
      TQA_B(2)=-TQB(2)-(DC123(3)*FBT(1)-DC123(1)*FBT(3))
      TQA_B(3)=-TQB(3)-(DC123(1)*FBT(2)-DC123(2)*FBT(1))
!
! GET TQA IN A BODY FRAME
      TQA(1)=CMAT(1,1)*TQA_B(1)+CMAT(1,2)*TQA_B(2)+CMAT(1,3)*TQA_B(3)
      TQA(2)=CMAT(2,1)*TQA_B(1)+CMAT(2,2)*TQA_B(2)+CMAT(2,3)*TQA_B(3)
      TQA(3)=CMAT(3,1)*TQA_B(1)+CMAT(3,2)*TQA_B(2)+CMAT(3,3)*TQA_B(3)

!
!
 1000 CONTINUE
!
! ACCB DOES NOT HAVE SPHERICAL CB TERM FROM A IN IT
! ACCA DOES NOT HAVE SPHERICAL CB TERM FROM A IN IT
!
! ROTATE FRON B BODY FIXED TO J2000
!
      FQ1=FBTP(1)
      FQ2=FBTP(2)
      FQ3=FBTP(3)
      FBTP(1)=XAXIS2(1,1)*FQ1+XAXIS2(1,2)*FQ2+XAXIS2(1,3)*FQ3
      FBTP(2)=XAXIS2(2,1)*FQ1+XAXIS2(2,2)*FQ2+XAXIS2(2,3)*FQ3
      FBTP(3)=XAXIS2(3,1)*FQ1+XAXIS2(3,2)*FQ2+XAXIS2(3,3)*FQ3
!
      ACCB(1)=FBTP(1)/XMASSB
      ACCB(2)=FBTP(2)/XMASSB
      ACCB(3)=FBTP(3)/XMASSB
      ACCA(1)=-FBTP(1)/XMASSA
      ACCA(2)=-FBTP(2)/XMASSA
      ACCA(3)=-FBTP(3)/XMASSA
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!    ACCB(1)=FBT(1)/XMASSB
!!    ACCB(2)=FBT(2)/XMASSB
!!    ACCB(3)=FBT(3)/XMASSB
!!!      ACCBT(1)=FBT(1)/XMASSB-ACCB(1)
!!!      ACCBT(2)=FBT(2)/XMASSB-ACCB(2)
!!!      ACCBT(3)=FBT(3)/XMASSB-ACCB(3)
!!!      WRITE(6,78912) ACCBT
!!!78912 FORMAT(' ACCEL OF B IN FTAST ',3D20.11)
!

!
      RETURN
      END
