!$METDAT
      SUBROUTINE METDAT(DMET)
!********1*********2*********3*********4*********5*********6*********7**
! METDAT           00/00/00            0000.0    PGMR - ?
!
!
! FUNCTION:  DECOMPOSES THE METEOROLOGICAL DATA WORD
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   DMET     I    S    METEOROLOGICAL DATA WORD
!
! COMMENTS:  OUTPUT IN COMMON/CMET/
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CMET/P0,T0,RELHUM,E0,WAVLEN
      COMMON/CSTA/RLAT,COSLAT,SINLAT,RLON,COSLON,SINLON,HEIGHT,         &
     &   TMOUNT,DISP,WAVREC,WAVXMT,ELCUT,PLATNO,SITNOL,SDTLRA,ANTTNO
      DATA PBIAS/16384.0D0/,TBIAS/4294967296.0D0/
      DATA C1DM2/1.0D-2/,C1DM4/1.0D-4/,TEN/1.0D1/
      DATA FREEZE/273.16D0/,C6P11/6.11D0/,C7P5/7.5D0/,C35P86/35.86D0/
      DATA PRESS0/1013.5D0/,TEMP0/293.16D0/,RELHU0/0.4D0/,ZERO/0.0D0/
      DATA DPDH/-1.1138D-4/,ONE/1.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! DECOMPOSE METEOROLOGICAL DATA WORD
!
! TEMPERATURE IN .01 DEG. KELVIN (BITS 32-47)
      TEMP=DMET/TBIAS
      ITEMP=TEMP
      TEMP=ITEMP
! TEMPERATURE IN DEG. KELVIN
      T0=TEMP*C1DM2
      WORK=DMET-TEMP*TBIAS
! PRESSURE IN .01 MILLIBARS (BITS 14-31)
      PRESS=WORK/PBIAS
      IPRESS=PRESS
      PRESS=IPRESS
! PRESSURE IN MILLIBARS
      P0=PRESS*C1DM2
! RELATIVE HUMIDITY STORED AS .01 PERCENT (BITS 0-13)
!          CONVERTED HERE TO FRACTIONAL RELATIVE HUMIDITY
      RELHUM=(WORK-PRESS*PBIAS)*C1DM4
      IF(P0.GT.ZERO) GO TO 1000
      P0=PRESS0*(ONE+DPDH*HEIGHT)
      T0=TEMP0
      RELHUM=RELHU0
 1000 CONTINUE
      EXPON=C7P5*(T0-FREEZE)/(T0-C35P86)
! PARTIAL PRESSURE OF WATER VAPOR
      E0=C6P11*RELHUM*TEN**EXPON
      RETURN
      END
