!$LMDCOS
      SUBROUTINE LMDCOS(MJDSBL,FSECN,FSECM,OBS,OBSCOR,ENV,              &
     &   XMN,XTN,XSM,VSM,                                               &
     &   RNM,URNM,RDOT,UHAT,RESID,OBRATE,PMPXI,PMPXE,                   &
     &   NMP,INDP,RPOLE,PXPOLE,NINTPL,MINTPL,                           &
     &   S,S1,XTP,PXSPXP,COSTHG,SINTHG,DCOSI,TNLCOR,RELV,TROP,          &
     &   ELEVSC,INDSAT,NM, nm1,                                         &
     &   LNRATE,LPOLAD,LIGHT,ILM,INDSTA,DELCTN,DELCTM,                  &
     &   DELCTK,LPTS,LPSBL,LACC,MTYPE,INDSET,XDPOLE,LASER,LNUTAD,       &
     &   AA,II,LL)
!********1*********2*********3*********4*********5*********6*********7**
! LMDCOS           83/05/03            8304.0    PGMR - TOM MARTIN
!                  86/06/24            8609.0    PGMR - TOM MARTIN
!
! FUNCTION:  COMPUTE l AND m DIRECTION COSINE MEASUREMENTS.
!            COMPUTATIONS START AT THE FINAL RECEIVED TIME
!            MJDSn+FSECn AND PROCEED BACKWARDS UNTIL THE MODEL
!            IS COMPLETE. IF THE "LIGHT" SWITCH IS .FALSE.
!            ITERATIVE SOLUTIONS FOR THE LIGHT TIME ARE
!            PERFORMED. OTHERWISE, THE MEASUREMENT EVENT TIMES
!            ARE CONSIDERED TO BE KNOWN.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSBL   I    S    MODIFIED JULIAN DAY SECONDS OF THE FINAL
!                      RECEIVED TIME OF THE FIRST OBSERVATION IN
!                      THE BLOCK
!   FSECN    I    A    ELAPSED SECONDS FROM MJDSBL OF THE EVENT
!                      TIMES ASSOCIATED WITH TRACKING STATION Tn
!   FSECM   I/O   A    ELAPSED SECONDS FROM MJDSBL OF THE EVENT
!                      TIMES ASSOCIATED WITH SATELLITE Sm AS OUTPUT
!                      ELAPSED SECONDS FROM MJDSBL OF THE EVENT AS INPUT
!                      TIMES ASSOCIATED WITH SATELLITE Sm
!   OBS      I    A    THE OBSERVED l OR m DIRECTION COSINES
!   OBSCOR  I/O   A    SUM OF OBSERVATION CORRECTIONS
!   ENV      I    A    STATION EAST,NORTH,VERTICAL VECTORS IN E.C.F.
!   XMN      I    A    MEAN POLE COORDINATES OF TRACKING STATION Tn
!   XTN     I/O   A    TRUE OF DATE INERT. COORD. OF TRACKING STA. Tn
!   XSM     I/O   A    TRUE OF DATE INERT. COORD. OF SATELLITE Sm
!   VSM      I    A    TRUE OF DATE INERT. VELOCITY OF SATELLITE Sm
!   RNM     I/O   A    SLANT RANGE FROM Tn TO Sm
!   URNM    I/O   A    UNIT VECTOR FROM Tn TO Sm
!   RDOT     O    A    RANGE RATE FROM STATION TO S/C
!   UHAT    I/O   A    E.C.F. UNIT VECTOR FROM STATION TO S/C
!   RESID    O    A    OBSERVED MINUS COMPUTED OBSERVATION
!   OBRATE   O    A    TIME RATE OF CHANGE OF THE OBSERVATION
!   PMPXI    O    A    PARTIAL OF OBS. W.R.T. INERTIAL S/C POS.
!   PMPXE    O    A    PARTIAL OF OBS. W.R.T. E.C.F. STATION POS.
!   NMP      O    A    INDEX OF THE LAST MEASUREMENT ASSOCIATED WITH
!                      EACH INTERPOLATION INTERVAL
!   INDP     O    A    POINTER TO ADJUSTED POLAR MOTION VALUES
!                      ASSOCIATED WITH EACH INTERPOLATION INTERVAL
!   RPOLE    O    A    POLAR MOTION ROTATION MATRICES AT EACH END
!                      OF EACH INTERPOLATION INTERVAL
!   PXPOLE   O    A    PARTIALS OF THE TRUE POLE STATION LOCATION
!                      W.R.T. THE POLAR MOTION X & Y PARAMETERS AT
!                      EACH END OF EACH INTERPOLATION INTERVAL
!   XDPOLE   O    A    PARTIALS OF THE TRUE POLE STATION RATE
!                      W.R.T. THE POLAR MOTION X & Y PARAMETERS AT
!                      EACH END OF EACH INTERPOLATION INTERVAL
!   NINTPL   I    S
!   MINTPL   I    S    MAXIMUM NUMBER OF INTERPOLATION INTERVALS
!   S        I    A    INTERPOLATION FRACTIONS
!   S1       I    A    1.0-S
!   XTP      O    A    TRUE POLE COORDINATES OF A TRACKING STATION
!   PXSPXP   O    A    PARTIALS OF TRUE POLE STATION COORDINATES
!                      W.R.T. POLAR MOTION FOR EACH OF "NM"
!                      MEASUREMENT TIMES
!   COSTHG   O    A    COSINES OF RIGHT ASCENSION OF GREENWICH
!   SINTHG   O    A    SINES  OF RIGHT ASCENSION OF GREENWICH
!   DCOSI   I/O   A    WORKING ARRAY OF LENGTH "NM"
!   TNLCOR   O    A    TAN(ELEV.)*ELEV. REFRACTION CORRECTION
!   RELV     O    A    VELOCITY OF SATELLITE W.R.T. TRACKING STATION
!   TROP     O    A    TROPOSPHERIC REFRACTION (DRY AND WET)
!   INDSAT   I    A    INTERNAL SATELLITE NUMBERS
!   ELEVSC   O    A    S/C ELEVATION ANGLES IN DEGREES
!   NM       I    S    NUMBER OF MEASUREMENTS
!   LNRATE   I    S    .T.-TIME DERIVATIVE OF OBS. NOT REQUIRED
!                      .F.-COMPUTE TIME DERIVATIVE OF OBS.
!   LPOLAD   I    S    SWITCH INDICATING IF POLAR MOTION ADJUST.
!                      PARTIALS ARE REQUIRED FOR EACH INTERP.
!                      INTERVAL
!   LIGHT    I    S    .TRUE. IF LIGHT TIMES HAVE BEEN DETERMINED
!   ILM      I    S    MEASUREMENT NUMBER; 1=l COSINE, 2=m COSINE
!   INDSTA   I    A    INTERNAL STATION NUMBERS
!   DELCTN   I    A    DELTA ET CET AT EVENT TIMES ASSOCIATED
!                      WITH TRACKING Tn
!   DELCTM   I    A    DELTA ET CET AT EVENT TIMES ASSOCIATED
!                      WITH SATELLITE Sm
!   DELCTK   I    A    DELTA ET CET AT EVENT TIMES ASSOCIATED
!                      WITH TRACKING TK
!   LPTS    I/O   A    .TRUE. FOR VALID OBSERVATIONS IN A SIMULATION
!                      GENERATED MEASUREMENT BLOCK
!   LPSBL    I    S    .TRUE. FOR SIMULATED DATA GENERATION CAPABILITY
!   LACC    I/O   S    .TRUE. IF SIMULATION PSEUDO BLOCK HAS BEEN
!                      ACCEPTED
!   AA      I/O   A    REAL DYNAMIC ARRAY
!   II      I/O   A    INTEGER DYNAMIC ARRAY
!   II      I/O   A    LOGICAL DYNAMIC ARRAY
!   SIGNL    I    S    SIGN OF THE LIGHT TIME (+1. OR -1.)
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CBLOKA/FSECBL,SIGNL ,VLITEC,SECEND,BLKDAT(6,4),DOPSCL(5,4)
!      COMMON/CBLOKL/
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/CMETI /MODEL(999),MWET(999),MDRY(999),MODESC(999),        &
     &              MAPWET(999),MAPDRY(999),METP(999),IRFRC,           &
     &              IRFPRT(10),NXCMTI
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/COBLOC/KXMN  (3,4)  ,KENVN (3,4)  ,KSTINF(3,4)  ,          &
     &       KOBS  ,KDTIME,KSMCOR,KSMCR2,KTMCOR,KOBTIM,KOBSIG,KOBSG2,   &
     &       KIAUNO,KOBCOR(9,7)  ,KFSECS(6,8)  ,KOBSUM(8)    ,          &
     &       KEDSIG,KEDSG2
      COMMON/COPARM/LOP1
      COMMON/CORA02/KFSCTB,KFSCTC,KFSCTE,KDSECT,                        &
     &              KFSECT,KS    ,KCIP  ,KCIV  ,                        &
     &       KXTN  ,KXSM  ,KXTK  ,KXSJ  ,KXTI  ,KXTKP ,                 &
     &       KVTN  ,KVSM  ,KVTK  ,KVSJ  ,KVTI  ,KVTKP ,                 &
     &       KSATLT,KSATLN,KSATH ,KCOSTH,KSINTH,                        &
     &       KPXPFM,KPXPFK,KPXPFJ,KTRBF ,KFSTRC,                        &
     &       KFSTRE,KDSCTR,KFSCVS,KXSMBF,KXSKBF,KXSJBF,                 &
     &       KRSSV1,KRSSV2,KRSSV3,KTPMES,KACOEF,KACTIM,                 &
     &       KXTNPC,KXSMPC,KXTKPC,KXSJPC,KXTIPC,KXTKPP,                 &
     &       KRLRNG,KASTO,KASTP,NXCA02
      COMMON/CORA03/KRFMT ,KROTMT,KSRTCH,KCFSC ,KTHG  ,KCOSTG,KSINTG,   &
     &              KDPSI ,KEPST ,KEPSM ,KETA  ,KZTA  ,KTHTA ,KEQN  ,   &
     &              KDPSR ,KSL   ,KH2   ,KL2   ,KXPUT ,KYPUT ,KUT   ,   &
     &              KSPCRD,KSPSIG,KSTAIN,KSXYZ ,KSPWT ,KDXSDP,KDPSDP,   &
     &              KXLOCV,KSTNAM,KA1UT ,KPLTIN,KPLTVL,KPLTDV,KPUTBH,   &
     &              KABCOF,KSPEED,KANGFC,KSCROL,KSTVEL,KSTVDV,KTIMVL,   &
     &              KSTEL2,KSTEH2,KSL2DV,KSH2DV,                        &
     &              KAPRES, KASCAL, KASOFF,KXDOTP,KSAVST,               &
     &              KANGT , KSPDT , KCONAM,KGRDAN,KUANG ,KFAMP ,        &
     &              KOFACT, KOTSGN,KWRKRS,KANFSD,KSPDSD,KPRMFS,KSCRFR,  &
     &              KCOSAS,KSINAS,KDXTID,KALCOF,KSTANF,KNUTIN,KNUTMD,   &
     &              KPDPSI,KPEPST,KDXDNU,KDPSIE,KEPSTE,NXCA03
      COMMON/CORA04/KABIAS,KABSTR,KABSTP,KCBIAS,KCBSTR,KCBSTP,          &
     &       KRNM  ,KRKM  ,KRKJ  ,KRIJ  ,KRKPM ,                        &
     &       KRRNM ,KRRKM ,KRRKJ ,KRRIJ ,KRRKPM,                        &
     &       KURNM ,KURKM ,KURKJ ,KURIJ ,KURKPM,                        &
     &       KPRRNM,KPRRKM,KPRRKJ,KPRRIJ,KPRRKP,                        &
     &       KPMPXI,KPMPXE,KRA   ,KRELV ,KUHAT ,KWORK ,KTMPCR,KU    ,   &
     &       KS0   ,KS1   ,KXTP  ,KPXSXP,KPXDXP,KXUTDT,KRPOLE,KPXPOL,   &
     &       KXDPOL,KOBBUF,KOBSC ,KRESID,KSIGMA,KRATIO,KELEVS,          &
     &       KPMPA ,KPXSLV,KTPRTL,                                      &
     &       KFRQOF,KXYZOF,KFRQST,KFRQSA,KSTDLY,KSADLY,KXYZCG,KSCMRA,   &
     &       KEBVAL,KEBSIG,KEBNAM,KBTWB ,KTBTWA,KBTWD ,KPMPE ,          &
     &       KFSEB1,KFSEB2,KSTAMT,KPXSPA,KDIAGN,KOBOUT,KGRLT1,KGRLT2,   &
     &       KGRLT3,KGRLT4,KGRLT5,KPSTAT,KRSUNS,KCHEMR,KCHMOR,KEPOSR,   &
     &       KCHEMT,KCHMOT,KEPOST,KCHCBS,KCPOSS,KSCRTM,KXSTT ,KXSTR ,   &
     &       KXSS  ,KRANGT,KRANGR,KCHB1 ,KCHB2 ,KCHBV1,KCHBV2,KCHEB ,   &
     &       KSSTFQ,KSSTCC,KSSTSS,KSSTWT,KRLCOR,KWVLBI,KQUINF,KBRTS ,   &
     &       KBRTSV,KLRARC,KXEPBF,KXEPBR,KPRCBD,KPRTBD,KXPMPA,KXL,      &
     &       KXSIG ,KXEDTS,KXALO1,KXALO2,KXYOF2,KDLATF,KDLATS,KDLONF,   &
     &       KDLONS,KPF   ,KPS   ,                                      &
     &       KXOBSV,KXOBSW,KXEDSW,                                      &
     &       KXSIGW,KPSF  ,KDNUM ,KCNUM ,KFCOR ,KCOSAR,KSINAR,KSABIA,   &
     &       KSBTM1,KSBTM2,KYAWBS,KVLOUV,KACMAG,KOBSTR,                 &
     &       KPRL1 ,KPRL2, KRL1  ,KT1SE ,KTCP  ,                        &
     &       KRATDR,KFQT1S,KFQT1E,KT1STR,KFTTSE,KFRSTR,KFRRAT,KSV1  ,   &
     &       KSV2  ,KTSLOV,                                             &
     &       KGLGR1,KGLGR2,KGLFR1,KGLFR2,                               &
     &       KARGR1,KARGR2,KARFR1,KARFR2,                               &
     &       KRDS1L,KFT1AV,KDFQP ,KFREQ1,KFREQ3,KSAVD1,KSAVD2,          &
     &       KANTOU,KFM3CF,KF2CF,KTMG,KLTMG,KX2TIM,KX2OBS,KXRNDX,KX2SCR,&
     &       KALTWV,KXXBM ,KX2PAR,KATIME,KPMPAT,KPMATT,KX2PAT,          &
     &       KPXEXI,KPXEPA,KPV,KPXEP2,KX2COF,KACOF2,KACOF3,KBCOF,KBCOF2,&
     &       KDDDA ,KX2AUX,KX2VPR,KX2VPA,KEXTRA,KVARAY,KATROT,KATPER,   &
     &       KLTAR ,KXHOLD,KANTBL,KPHC  ,KOFDRV,KGNAME,KGRSIZ,KGRCNT,   &
     &       KGRDAT,KACCDT,KCTBTI,KCTBWE,KCTCTM,KANTUV,KANTOR,KW1PU ,   &
     &       KPYSQH,KSIGSP,KXYOF3,KXVTM1,KXDIST,KXDST0,KTMSE ,KDSDP ,   &
     &       KEXCST,KEXCDT,KEXCGX,KEXCGY,KEXCGZ,KIMNDX,KIMOBS,KIMTIM,   &
     &       KIMSAT,KM2VPA,KDCDD ,KDWNWT,KDPOR ,KC2PAR,KBOUNC,KBPART,   &
     &       NXCA04
      COMMON/CPRESW/LPRE9 (24),LPRE  (24,3),LSWTCH(10,8),LOBSIN,LPREPW, &
     &              LFS   (40)
      COMMON/SIMLM/OBSINT,VINT,ELCUTO,XSMLM
!jtw add CORA03 and CMETI and CLBOKL
      DIMENSION FSECN(NM1),FSECM(NM1),                                  &
     &   XTN(MINTIM,3),XSM(MINTIM,3),VSM(MINTIM,3),                     &
     &   OBS(NM1),OBSCOR(NM1),ENV(3,3),                                 &
     &   XMN(3),RNM(NM1),TNLCOR(NM1),                                   &
     &   PMPXE(NM1,3),UHAT(NM1,3),TROP(NM1,2),                          &
     &   URNM(NM1,3,1),NMP(MINTPL,3),                                   &
     &   RESID(NM1),PMPXI(NM1,3),OBRATE(NM1),NINTPL(3),                 &
     &   INDP(MINTPL,3),RPOLE(3,3,MINTPL),PXPOLE(3,2,MINTPL),           &
     &   S(NM1),S1(NM1),XTP(NM1,3),PXSPXP(NM1,3,2,3),                   &
     &   COSTHG(NM1,3),SINTHG(NM1,3),DCOSI(NM1),RELV(NM1,3),RDOT(NM1),  &
     &   INDSAT(3),INDSTA(3),DELCTN(NM1),DELCTM(NM1),DELCTK(NM1),       &
     &   INDSET(3),AA(1),II(1),LL(1),ELEVSC(NM1),XDPOLE(3,2,MINTPL)
      DIMENSION LPTS(NM1)
      DATA ONE/1.0D0/,ZERO/0.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! IF m COSINES BEING PROCESSED, BYPASS CERTAIN OPERATIONS,
!    EITHER NOT REQUIRED OR PREVIOUSLY DONE FOR l COSINES
      LOP1=ILM.LE.1
      IF(ILM.GT.1) GO TO 2000
! COMPUTE VECTORS AND RANGES BETWEEN STATION AND SPACECRAFT
      lyara = .false.
      isnumb = 0
      LADD=.TRUE.
      CALL MTSTST(MJDSBL,FSECN,FSECM,DCOSI,DCOSI,DCOSI,XTN,XSM,DCOSI,   &
     &   DCOSI,DCOSI,DCOSI,VSM,DCOSI,DCOSI,DCOSI,XMN,DCOSI,DCOSI,       &
     &   AA(KXTNPC),AA(KXSMPC),AA(KXTKPC),AA(KXSJPC),AA(KXTIPC),        &
     &   RNM,DCOSI,DCOSI,DCOSI,DCOSI,DCOSI,DCOSI,DCOSI,URNM,DCOSI,      &
     &   DCOSI,DCOSI,DCOSI,DCOSI,DCOSI,DCOSI,NMP,INDSTA,INDP,RPOLE,     &
     &   PXPOLE,NINTPL,MINTPL,S,S1,XTP,DCOSI,PXSPXP,AA(KPXSLV),COSTHG,  &
     &   SINTHG,INDSAT,SIGNL,NM,LPOLAD,LIGHT,.TRUE.,.FALSE.,1,.FALSE.,  &
     &   AA(KGRLT1),AA(KGRLT2),AA(KGRLT3),AA(KGRLT4),DELCTN,            &
     &   DELCTK,DELCTK,AA(KPSTAT),AA(KRSUNS),AA(KCHEMR),                &
     &   AA(KCHMOR),AA(KEPOSR),AA(KCHEMT),AA(KCHMOT),AA(KEPOST),        &
     &   AA(KCHCBS),AA(KCPOSS),AA(KSCRTM),AA(KXSTT) ,AA(KXSTR) ,        &
     &   AA(KXSS)  ,MTYPE  ,INDSET,AA(KBRTS),AA(KBRTSV),AA,II,LL,       &
     &   lyara, isnumb, LADD,XDPOLE,AA(KPXDXP),AA(KXUTDT),.FALSE.,      &
     &   FSECN,LNUTAD)
      CALL ECFIXP(URNM,COSTHG,SINTHG,UHAT,NM,NM,NM)
      IF(.NOT.LITER1) GO TO 1000
      IF(LPRE(3,1).AND.LPRE(7,1)) GO TO 1000
! REFRACTION CORRECTION REQUIRED
!
! DELTA L = -SIN(AZ) * SIN(EL) * DELTA EL = -L * TAN(EL) * DELTA EL
! DELTA M = -COS(AZ) * SIN(EL) * DELTA EL = -M * TAN(EL) * DELTA EL
!
!...COMPUTE SINE OF ELEVATION (ZENITH DIRECTION COSINE)
      CALL MATPRD(UHAT,ENV(1,3),DCOSI,NM,3,1)
!...COSINE SQUARED OF ELEVATION
      DO 100 N=1,NM
      TNLCOR(N)=ONE-DCOSI(N)*DCOSI(N)
  100 END DO
!...COSINE OF ELEVATION
      DO 200 N=1,NM
      TNLCOR(N)=SQRT(TNLCOR(N))
  200 END DO
!...ELEVATION REFRACTION
      BLKMET=BLKDAT(1,1)
      JMET  =KOBCOR(1,1)
!jtw replace refrac with calls to refrac models
!      CALL REFRAC(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),RESID,NM,3,     &
!     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
!     &   .FALSE.,RESID,RESID,RESID,RESID,MTYPE,MJDSBL)

      IWPR=IRFPRT(MODEL(MTYPE))

      IF(MODEL(MTYPE).EQ.0) THEN

      CALL HOPFLD(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),RESID,NM,3,     &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,RESID,RESID,RESID,RESID,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.1) THEN

           IF(LASER) THEN

                CALL MARMUR(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),      &
               &    RESID,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,    &
               &    AA(JMET),LPRE(1,1),.FALSE.,RESID,RESID,RESID,RESID, &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           ELSE IF(.NOT.LASER) THEN

                CALL HOPFLD(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),      &
               &    RESID,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,    &
               &    AA(JMET),LPRE(1,1),.FALSE.,RESID,RESID,RESID,RESID, &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           END IF

      ELSE IF(MODEL(MTYPE).EQ.2) THEN

      CALL VLBGPS(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),RESID,NM,3,     &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,RESID,RESID,RESID,RESID,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.3) THEN

      CALL GPSNIL(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),RESID,NM,3,     &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,RESID,RESID,RESID,RESID,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.4) THEN

           IF(LASER) THEN

                CALL MARMUR(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),      &
               &    RESID,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,    &
               &    AA(JMET),LPRE(1,1),.FALSE.,RESID,RESID,RESID,RESID, &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           ELSE IF(.NOT.LASER) THEN

                CALL GPSNIL(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),      &
               &    RESID,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,    &
               &    AA(JMET),LPRE(1,1),.FALSE.,RESID,RESID,RESID,RESID, &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           END IF

      ELSE IF(MODEL(MTYPE).EQ.5) THEN

           IF(LASER) THEN

                CALL MENDEZ(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),      &
               &    RESID,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,    &
               &    AA(JMET),LPRE(1,1),.FALSE.,RESID,RESID,RESID,RESID, &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           ELSE IF(.NOT.LASER) THEN

                CALL HOPFLD(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),      &
               &    RESID,NM,3,LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,    &
               &    AA(JMET),LPRE(1,1),.FALSE.,RESID,RESID,RESID,RESID, &
               &    MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

           END IF

      ELSE IF(MODEL(MTYPE).EQ.6) THEN

      CALL GMFHOP(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),RESID,NM,3,     &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,RESID,RESID,RESID,RESID,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.7) THEN

      CALL GMFSAS(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),RESID,NM,3,     &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,RESID,RESID,RESID,RESID,MTYPE,MJDSBL,FSECN,IWPR,NSTA0)

      ELSE IF(MODEL(MTYPE).EQ.8) THEN

      CALL VMF1(TNLCOR,DCOSI,RESID,RNM,TROP,TROP(1,2),RESID,NM,3,       &
     &   LPRE(3,1),LPRE(5,1),LPRE(7,1),BLKMET,AA(JMET),LPRE(1,1),       &
     &   .FALSE.,RESID,RESID,RESID,RESID,MTYPE,MJDSBL,FSECN,IWPR,       &
     &   NSTA0,AA(KA1UT))

      END IF

      IF(LPRE  (3,1)) GO TO 400
!...TANGENT OF ELEVATION
      DO 300 N=1,NM
      TNLCOR(N)=DCOSI(N)/TNLCOR(N)
  300 END DO
!...SAVE DRY TROPOSPHERIC COMPONENT TIMES TANGENT OF ELEVATION
      IF(.NOT.LSWTCH(3,2)) GO TO 350
      JOBCOR=KOBCOR(3,1)
      DO 325 N=1,NM
      AA(JOBCOR)=TROP(N,1)*TNLCOR(N)
      JOBCOR=JOBCOR+1
  325 END DO
  350 CONTINUE
      IF(.NOT.LSWTCH(4,2)) GO TO 400
      JOBCOR=KOBCOR(4,1)
      DO 375 N=1,NM
      AA(JOBCOR)=TROP(N,1)*TNLCOR(N)
      JOBCOR=JOBCOR+1
  375 END DO
  400 CONTINUE
      IF(LPRE  (5,1)) GO TO 500
!...SAVE WET TROPOSPHERIC COMPONENT TIMES TANGENT OF ELEVATION
      IF(.NOT.LSWTCH(5,2)) GO TO 450
      JOBCOR=KOBCOR(5,1)
      DO 425 N=1,NM
      AA(JOBCOR)=TROP(N,2)*TNLCOR(N)
      JOBCOR=JOBCOR+1
  425 END DO
  450 CONTINUE
      IF(.NOT.LSWTCH(6,2)) GO TO 500
      JOBCOR=KOBCOR(6,1)
      DO 475 N=1,NM
      AA(JOBCOR)=TROP(N,2)*TNLCOR(N)
      JOBCOR=JOBCOR+1
  475 END DO
  500 CONTINUE
      IF(LPRE  (7,1)) GO TO 700
!...SAVE IONOSPHERIC COMPONENT TIMES TANGENT OF ELEVATION
      IF(.NOT.LSWTCH(7,2)) GO TO 600
      JOBCOR=KOBCOR(7,1)
      DO 550 N=1,NM
      AA(JOBCOR)=RESID(N)*TNLCOR(N)
      JOBCOR=JOBCOR+1
  550 END DO
  600 CONTINUE
      IF(.NOT.LSWTCH(8,2)) GO TO 700
      JOBCOR=KOBCOR(8,1)
      DO 650 N=1,NM
      AA(JOBCOR)=RESID(N)*TNLCOR(N)
      JOBCOR=JOBCOR+1
  650 END DO
  700 CONTINUE
      IF(.NOT.LPRE(3,1)) GO TO 750
!...CLEAR DRY TROPOSPHERIC CORRECTION
      DO 725 N=1,NM
      TROP(N,1)=ZERO
  725 END DO
  750 CONTINUE
      IF(.NOT.LPRE(5,1)) GO TO 800
!...CLEAR WET TROPOSPHERIC CORRECTION
      DO 775 N=1,NM
      TROP(N,2)=ZERO
  775 END DO
  800 CONTINUE
      IF(.NOT.LPRE(7,1)) GO TO 900
!...CLEAR IONOSPHERIC CORRECTION
      DO 850 N=1,NM
      RESID(N)=ZERO
  850 END DO
  900 CONTINUE
!...MULTIPLY SUM OF ATMOSPHERIC CORRECTIONS BY TAN(ELEV.)
      DO 950 N=1,NM
      TNLCOR(N)=(TROP(N,1)+TROP(N,2)+RESID(N))*TNLCOR(N)
  950 END DO
 1000 CONTINUE
      IF(LNRATE) GO TO 2000
! IF TIME DERIVATIVE REQUIRED, COMPUTE THE S/C RELATIVE VELOCITY
      CALL RELVEL(VSM,COSTHG,SINTHG,UHAT,RNM,OBRATE,RELV,RDOT,NM,       &
     &   LNRATE,LNRATE)
 2000 CONTINUE
!
!     COMPUTE ELEVATION ANGLE
      CALL ECFIXP(URNM,COSTHG,SINTHG,UHAT,NM,NM,NM)
!...COMPUTE SINE OF ELEVATION (ZENITH DIRECTION COSINE)
      CALL MATPRD(UHAT,ENV(1,3),DCOSI,NM,3,1)
!...COSINE SQUARED OF ELEVATION
      DO 951 N=1,NM
      TNLCOR(N)=ONE-DCOSI(N)*DCOSI(N)
!...COSINE OF ELEVATION
      TNLCOR(N)=SQRT(TNLCOR(N))
  951 END DO
      CALL ELEV(UHAT,RNM,RDOT,VSM,COSTHG,SINTHG,ENV,DCOSI,TNLCOR,       &
     &   RESID,RELV,ELEVSC,OBRATE,NM,.TRUE.,.TRUE.,.FALSE.,.FALSE.,     &
     &   .TRUE.,.TRUE.,.TRUE.)
!
! COMPUTE DESIRED DIRECTION COSINES AS INDICATED BY ILM
      CALL MATPRD(UHAT,ENV(1,ILM),DCOSI,NM,3,1)
!
      IF(.NOT.LITER1) GO TO 3000
      IF(LPRE(3,1).AND.LPRE(7,1)) GO TO 3000
      IF(LPRE  (3,1)) GO TO 2100
      ILM2=ILM+2
      IF(.NOT.LSWTCH(ILM2,2)) GO TO 2100
! COMPUTE AND STORE DRY TROPOSPHERE CORRECTION IN OBSERVATION BUFFER
      JOBCOR=KOBCOR(ILM2,1)
      DO 2050 N=1,NM
      AA(JOBCOR)=AA(JOBCOR)*DCOSI(N)
      JOBCOR=JOBCOR+1
 2050 END DO
 2100 CONTINUE
      IF(LPRE  (5,1)) GO TO 2200
      ILM4=ILM+4
      IF(.NOT.LSWTCH(ILM4,2)) GO TO 2200
! COMPUTE AND STORE WET TROPOSPHERE CORRECTION IN OBSERVATION BUFFER
      JOBCOR=KOBCOR(ILM4,1)
      DO 2150 N=1,NM
      AA(JOBCOR)=AA(JOBCOR)*DCOSI(N)
      JOBCOR=JOBCOR+1
 2150 END DO
 2200 CONTINUE
      IF(LPRE  (7,1)) GO TO 2400
      ILM6=ILM+6
      IF(.NOT.LSWTCH(ILM6,2)) GO TO 2400
! COMPUTE AND STORE IONOSPHERE CORRECTION IN OBSERVATION BUFFER
      JOBCOR=KOBCOR(ILM6,1)
      DO 2300 N=1,NM
      AA(JOBCOR)=AA(JOBCOR)*DCOSI(N)
      JOBCOR=JOBCOR+1
 2300 END DO
 2400 CONTINUE
! ADD REFRACTION CORRECTIONS TO SUM OF OBSERVATION CORRECTIONS
      DO 2800 N=1,NM
      OBSCOR(N)=OBSCOR(N)+DCOSI(N)*TNLCOR(N)
 2800 END DO
 3000 CONTINUE
! FORM RESIDUALS
      DO 3400 N=1,NM
      RESID(N)=OBS(N)-DCOSI(N)
 3400 END DO
! COMPUTE PARTIALS OF DIR. COSINES W.R.T. E.C.F. SAT. POS.
      DO 6000 I=1,3
      DO 4800 N=1,NM
      PMPXE(N,I)=ENV(I,ILM)-DCOSI(N)*UHAT(N,I)
 4800 END DO
      DO 5800 N=1,NM
      PMPXE(N,I)=PMPXE(N,I)/RNM(N)
 5800 END DO
 6000 END DO
! ROTATE PARTIALS TO INERTIAL
      CALL INERTP(PMPXE,COSTHG,SINTHG,PMPXI,NM,NM,NM)
! STATION PARTIALS ARE NEGATIVE OF E.C.F. SAT. POS. PARTIALS
      DO 7000 I=1,3
      DO 6800 N=1,NM
      PMPXE(N,I)=-PMPXE(N,I)
 6800 END DO
 7000 END DO
      IF(LNRATE) GO TO 9000
! COMPUTE OBSERVATION TIME DERIVATIVE
      CALL MATPRD(RELV,ENV(1,ILM),OBRATE,NM,3,1)
      DO 7800 N=1,NM
      OBRATE(N)=OBRATE(N)-UHAT(N,ILM)*RDOT(N)
 7800 END DO
      DO 8800 N=1,NM
      OBRATE(N)=OBRATE(N)/RNM(N)
 8800 END DO
 9000 CONTINUE
      RETURN
      END
