!$ALBEDO
      SUBROUTINE ALBEDO(ECFSAT,ACCINT,R,RSQ,COSTHG,SINTHG,MJDSEC,FSEC,  &
     &RAM,PXDDT,NEQN,SVECT,SFLUX,DIST,NSEG,IALCAP,IEMCAP,GEOANG,ACS,    &
     &ECS,SOLNA,SOLNE,ALCON,EMMCON,AA,II,ISATID,IPNALB)
!********1*********2*********3*********4*********5*********6*********7**
!  ALBEDO          89/05/23            0000.0    PGMR: SBL,FGL,DEP
!
!  FUNCTION:  CONTROLS THE CALCULATION OF THE ACCELERATION DUE TO EARTH
!             RADIATION PRESSURE (ALBEDO)
!
!  I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   ECFSAT   I    A    INPUT  ARRAY CONTAINING X,Y,AND Z COMPONENTS
!                      OF THE SATELLITES POSITION IN EARTH CENTERED
!                      FIXED COORDINATES
!   ACCINT   O    A    THE ACCELERATION DUE TO THE EARTH'S RADIATION
!                      PRESSURE IN INERTIAL COORDINATES (X,Y,Z).
!   R        I    S    DISTANCE FROM CENTER OF EARTH TO SATELLITE
!   RSQ      I    S    SQUARE OF R
!   COSTHG   I    S    COSINE OF THE GREENWICH HOUR ANGLE
!   SINTHG   I    S    SINE OF THE GREENWICH HOUR ANGLE
!   MJDSEC   I    S    MODIFIED JULIAN DATE IN SECONDS SINCE GEODYN
!                      REFERENCE TIME
!   FSEC     I    S    FRACTION OF THE MODIFIED JULIAN DATE SECONDS
!   RAM      I    S    CR TERM WHICH INCLUDES AREA TO MASS RATIO*SOLAR
!                      CONSTANT*(1+SAT REFLECTIVITY)
!   PXDDT    I    A    PARTIALS OF ACCELERATION WRT PARAMETERS
!   NEQN     I    S    NUMBER OF FORCE MODEL EQUATIONS
!   SVECT   I/O   A    EARTH CENTERED FIXED xyz SPOT VECTORS
!   SFLUX   I/O   A    SUMMATION OF LONG WAVE AND SHORT WAVE RADIANCE
!                      FOR AREA SPOTS.
!   DIST    I/O   A    DISTANCE SPOT-SATELLITE
!   NSEG     I    A    ARRAY CONTAINING NUMBER OF SEGMENTS AS RINGS
!                      INCREASE
!   IALCAP   I    A    ARRAY CONTAINING THE NORTH AND SOUTH LATITUDE
!                      BOUNDARIES FOR POLAR CAPS FOR ALBEDO
!   IEMCAP   I    A    ARRAY CONTAINING THE NORTH AND SOUTH LATITUDE
!                      BOUNDARIES FOR POLAR CAPS FOR EMISSIVITY
!   GEOANG  I/O   A    GEOCENTRIC ANGLES TO RINGS
!   ACS      I    A    ALBEDO MODEL C AND S COEFFICIENTS
!   ECS      I    A    EMISSIVITY MODEL C AND S COEFFICIENTS
!   SOLNA    I    A    SOLAR LONGITUDE ARRAY FOR ALBEDO
!   SOLNE    I    A    SOLAR LONGITUDE ARRAY FOR EMMISIVITY
!   AA      I/O   A    REAL DYNAMIC ARRAY
!   II      I/O   A    INTG DYNAMIC ARRAY
!
! REFERENCES: KNOCKE, P.,AND RIES, J.,"EARTH RADIATION PRESSURE EFFECTS
!                   ON SATELLITES"; CENTER FOR SPACE RESEARCH,
!                   UNIVERSITY OF TEXAS AT AUSTIN, SEPTEMBER, 1987.
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/ALBINT/IRINGS,ISPOTS,IMAXA,IMAXE,MMAXA,MMAXE,KMAXA,KMAXE,  &
     &              KALB,KEMM,KLMOD,IMODEL,NPNALB,NXALBI
      COMMON/ALBMAX/MRINGS,MSPOTS,MIMAXA,MIMAXE,MMMAXA,MMMAXE,          &
     &              MKMAXA,MKMAXE,                                      &
     &              MIMA1A,MIMA1E,MMMA1A,MMMA1E,                        &
     &              MKMA1A,MKMA1E,                                      &
     &              NXALMX
      COMMON/BWREAL/SHADOW(2),SUNPCT,SCAREA,SCMASS,BWMEAN
      COMMON/BWINTG/JBFNRM,JTDNRM,JBWARE,JBWSPC,JBWDIF,JBWEMS,JBWTPA,   &
     &              JBWTPC,JBWTMD,JBWTMF,JBWTHX,JARADJ,JSPADJ,JDFADJ,   &
     &              JEMADJ,JTAADJ,JTCADJ,JTDADJ,JTFADJ,JTXADJ,JARUA ,   &
     &              JSPUA ,JDFUA ,JEMUA ,JTAUA ,JTCUA ,JTDUA ,JTFUA ,   &
     &              JTXUA ,NFACE ,NMOVE,NTFACE,JBFNR2,JBFNR3,JTDNR2,    &
     &              JTDNR3
      COMMON/BWLOGC/LVAREA,LTOPEX,LSPOT2,LGPSVA,LERS1,LMO,LMOC,LTDRSA,  &
     &              LMAGNA,LGFO,LTRMM,LEUVE,LVCL,LENVS,LCRYO,LGRAIL,    &
     &              LHY2A,LSARAL
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/CORA01/KFSEC0,KFSECB,KFSEC ,KFSECV,KH    ,KHV   ,KCTOL ,   &
     &              KRSQ  ,KVMATX,KCPP  ,KCPV  ,KCCP  ,KCCV  ,KCCPV ,   &
     &              KCCVV ,KXPPPP,KX    ,KPX   ,KSUMX ,KXDDOT,KSUMPX,   &
     &              KPXDDT,KAB   ,KPN   ,KAORN ,KSINLM,KCOSLM,KTANPS,   &
     &              KCRPAR,KVRARY,KXM   ,KXNP1 ,KXPRFL,KXM2  ,KXNNP1,   &
     &              KWRK  ,KFI   ,KGE   ,KB0DRG,KBDRAG,KAPGM ,KAPLM ,   &
     &              KCN   ,KSN   ,KSTID ,KTIDE ,KSTDRG,KSTSRD,KSTACC,   &
     &              KLGRAV,KGM   ,KAE   ,KFPL  ,KFEQ  ,KPLNPO,KPLNVL,   &
     &              KXEPOC,KCD   ,KCDDOT,KCR   ,KGENAC,KACN  ,KASN  ,   &
     &              KTHDRG,KCKEP ,KCKEPN,KXNRMZ,KXNRMC,KFSCEP,KFSCND,   &
     &              KAREA ,KXMASS,KRMSPO,KTCOEF,KTXQQ ,KTIEXP,KTXMM ,   &
     &              KTXLL1,KTXSN1,KTS2QQ,KT2M2H,KT2MHJ,KTXKK ,KTSCRH,   &
     &              KPXPK ,KAESHD,KCSAVE,KSSAVE,KCGRVT,KSGRVT,KXDTMC,   &
     &              KDNLT ,KTXSN2,KTNORM,KTWRK1,KTWRK2,KUNORM,KAERLG,   &
     &              KSINCO,KPARLG,KCONST,KBFNRM,KTDNRM,KCSTHT,KTPSTR,   &
     &              KTPSTP,KTPFYW,KPLMGM,KTPXAT,KEAQAT,KEAFSS,KEAINS,   &
     &              KACS  ,KECS  ,KSOLNA,KSOLNE,KSVECT,KSFLUX,KFACTX,   &
     &              KFACTY,KADIST,KGEOAN,KPALB ,KALBCO,KEMMCO,KCNAUX,   &
     &              KSNAUX,KPPER ,KACOSW,KBSINW,KACOFW,KBCOFW,KANGWT,   &
     &              KWT   ,KPLNDX,KPLANC,KTGACC,KTGDRG,KTGSLR,KWTACC,   &
     &              KWTDRG,KWTSLR,KTMACC,KTMDRG,KTMSLR,KATTUD,KDYACT,   &
     &              KACCBT,KACPER,KXDDNC,KXDDAO,KXNC  ,KXPPNC,KSMXNC,   &
     &              KXDDTH,KPDDTH,KXSSBS,KCPPNC,KEXACT,KXACIN,KXACOB,   &
     &              KPXHDT,KTPXTH,KPACCL,KTXSTA,KDELXS,KSMRNC,KPRX  ,   &
     &              KSMRNP,KDSROT,KXUGRD,KYUGRD,KZUGRD,KSUMRC,KXDDRC,   &
     &              KTMOS0,KTMOS, KTMOSP,KSMXOS,KSGTM1,KSGTM2,KSMPNS,   &
     &              KXGGRD,KYGGRD,KZGGRD,KXEGRD,KYEGRD,KZEGRD,KSSDST,   &
     &              KSDINS,KSDIND,KSSDSR,KSSDDG,KTATHM,KTAINS,KTAFSS,   &
     &              KSRAT ,KTRAT ,KHLDV ,KHLDA1,KHLDA4,KHLDA7,KQAST1,   &
     &              KQAST2,KQAST3,KQAST4,KQAST5,KQAST6,NXCA01
!
      DIMENSION ECFSAT(3),ACCINT(3),GEOANG(1),AUV(3),BUV(3)
      DIMENSION XUV(3),SVECT(1),SFLUX(1),DIST(1),ACCSUM(3)
      DIMENSION NSEG(1)
      DIMENSION VECTOR(3),PXDDT(NEQN,3)
      DIMENSION IALCAP(MKMAXA,2),IEMCAP(MKMAXE,2)
      DIMENSION ALCON(MKMAXA,2),EMMCON(MKMAXE,2)
      DIMENSION AA(1),II(1)
      DIMENSION ACS(1),ECS(1)
      DIMENSION SOLNA(1),SOLNE(1)
! ISPOTS IS THE MAX NSEG()
      DIMENSION SFLUXSW(ISPOTS),SFLUXLW(ISPOTS)
      DIMENSION IPNALB(NPNALB)
!
      DATA ZERO/0.0D0/,TWO/2.0D0/
!
!**********************************************************************
! START OF EXECUTABLE CODE ********************************************
!**********************************************************************
!
! the nseg array will look like {1,7,19,43,91,187,379} for 6 rings
!
      NPOINT=NSEG(IRINGS+1)
!
! CALCULATE MJD IN DAYS FROM GEODYN REFERENCE TIME
      AMJD=(MJDSEC+FSEC)/SECDAY
! CALCULATE MAXIMUM VIEWING ANGLE AND ANGLES TO RINGS ON EARTH SURFACE
      CALL ALBANG(ECFSAT,GEOANG,R,APAREA,NSEG)
! CALCULATE NORMALS TO SATELLITE POSTITION VECTOR
      CALL ALNRML(ECFSAT,AUV,BUV,XUV,R)
! CALCULATE ECF POSITION VECTORS TO AREA SPOTS
      CALL ALBLOC(XUV,AUV,BUV,GEOANG,SVECT,AA(KFACTX),AA(KFACTY),NSEG,  &
     &NPOINT)
      do 46464 JJ=1,57
46464 continue
! CALCULATE THE SUMMATION OF LONG WAVE AND SHORT WAVE RADIANCE
      CALL ALBFLX(SVECT,SFLUX,COSTHG,SINTHG,AMJD,RSUN,NSEG,ACS,         &
     &ECS,SOLNA,SOLNE,NPOINT,AA,SFLUXSW,SFLUXLW)
! CALCULATE THE CR*A/M*ES/C FOR THE RIGHT EARTH SUN DISTANCE
       RAMS=RAM/(RSUN**2)
! CALCULATE DISTANCE TO SPOTS, EACH SPOT ON THE SAME RING HAVE
! THE SAME DISTANCE TO THE SATELLITE
      AE2=AE**2
      DO 110 J=1,IRINGS
      DISTAN=SQRT(AE2+RSQ-TWO*AE*R*COS(GEOANG(J+1)))
         DO 100 I= NSEG(J),NSEG(J+1)-1
         DIST(I) = DISTAN
  100    CONTINUE
  110 END DO
!99326 FORMAT(' ** ALBEDO **  GEOCENTRIC ANGLES TO RINGS ='/
!    1   (1X,4D12.5))
      DIST(NSEG(IRINGS+1)) = R-AE
! CLEAR THE ACCELERATION SUM ARRAY
      DO 200 I=1,3
      ACCSUM(I)=ZERO
  200 END DO
! DETERMINE IF THE VARIABLE AREA MODEL(I.E. BOX-WING) IS BEING USED
      IF(LVAREA) THEN
!write(6,*)' dbg albedo ',jtdnrm,jtdnr2,jtdnr3
!write(6,*)' dbg albedo aa  ',aa(jtdnrm),aa(jtdnr2),aa(jtdnr3)
         CALL BWALBD(AA(JTDNRM),AA(JTDNR2),AA(JTDNR3),                  &
     &               ECFSAT,SVECT,SFLUX,DIST,APAREA,                    &
     &               RAMS,SCAREA,AA(JBWARE),AA(JBWSPC),AA(JBWDIF),      &
     &               ACCSUM,PXDDT,JARADJ,JSPADJ,JDFADJ,                 &
     &               II(JARUA),II(JSPUA),II(JDFUA),                     &
     &               NFACE,NEQN,SINTHG,COSTHG,NPOINT,ISATID, SFLUXSW,   &
     &               SFLUXLW,ISPOTS,IPNALB,NPNALB)
      ELSE
! SUM THE ACCELERATION IN ECF COORDINATES FOR ALL THE SPOTS
!NT  MODIFY THE CODE BELOW
! CALCULTE VECTOR FROM SPOT TO SAT
      DO 500 I=1,NPOINT
! CALCULTE VECTOR FROM SPOT TO SAT
      JJ=0
      DO 300 J=1,3
      VECTOR(J)=ECFSAT(J)-SVECT(I+JJ)
      JJ=JJ+NPOINT
  300 END DO
!     IINDEX=(I-1)/6+1
!     IF(IINDEX.GT.2) IINDEX=IINDEX-1

      IF (IPNALB(I).EQ.1) THEN ! just short-wave radiation applies
         ACCEL=RAMS*APAREA*SFLUXSW(I)
      ELSEIF (IPNALB(I).EQ.2) THEN ! just long-wave radiation applies
         ACCEL=RAMS*APAREA*SFLUXLW(I)
      ELSE ! default: both
         ACCEL=RAMS*APAREA*SFLUX(I)
      ENDIF

      DO 400 J=1,3
!     ACCSUM(J)=ACCSUM(J)+ACCEL*VECTOR(J)/DIST(IINDEX)
      ACCSUM(J)=ACCSUM(J)+ACCEL*VECTOR(J)/DIST(I)
  400 END DO
  500 END DO
      ENDIF
!99325 FORMAT(' ** ALBEDO **  SUM OF LONG AND SHORT WAVEL RADIANCE ='/
!    1   (1X,6D12.5))
! CONVERT ACCSUM FROM ECF TO TRUE OF DATE
      ACCINT(1)=ACCSUM(1)*COSTHG-ACCSUM(2)*SINTHG
      ACCINT(2)=ACCSUM(1)*SINTHG+ACCSUM(2)*COSTHG
      ACCINT(3)=ACCSUM(3)
      RETURN
      END
