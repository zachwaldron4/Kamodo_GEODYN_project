!$CHNAT2
      SUBROUTINE CHNAT2(PRPY,PMPA,NM,NDM1,NDM2,LNPNM,                   &
     &           TIME, PER,LAVOID,IPT)
!********1*********2*********3*********4*********5*********6*********7**
! CHNAT2              00/00/00            0000.0    PGMR -DAVE ROWLANDS
!
! FUNCTION:
!              LOAD THE EXPAND PARTIALS OF ATTITUDE (INCLUDING LINEAR,
!              QUADRATIC AND PERIODIC) FOR THE CASE (FOR EXAMPLE,
!              OPTICAL CONSYTAINTS) WHERE THE PARTIAL OF THE MEASUREMENT
!              WRT TO A CONSTANT ROLL, PITCH AND YAW HAS ALREADY BEEN
!              COMPUTED
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!
!     PRPY          PARTIALS OF MEASUREMENT WRT ROLL PITCH YAW
!     PMPA          FULL ARRAY OF MEASUREMENT PARTIALS
!     NM            NUMBER OF MEASUREMENTS TO BE PROCESSED
!     NDM1          FIRST DIMENSION OF PPMPA ARRAY (NP IF LNPNM IS TRUE
!                   NM OTHERWISE)
!     NDM2          SECOND DIMENSION OF PPMPA ARRAY
!     LNPNM         IF TRUE PARTIALS HAVE NP  AS FIRST DIMENSION
!     TIME          TIMES AT WHICH THE ATTITUDE PARTIALS ARE BEING APPLI
!                   REFERENCED FROM START OF ATTITUDE PERIOD
!     PER           PERIOD OF ATTITUDE COS & SIN TERMS
!     LAVOID        ARRAY TO ACCOUNT IF PARAMTER APPLIES TO THIS MEAS
!     IPT           STARTING LOCATION FOR THIS BLOCK OF 15 ATTITUDE
!                   PARAMTERS IN PMPA ARRAY
!
!********1*********2*********3*********4*********5*********6*********7**
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!

      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
!
      DIMENSION PRPY(3,NM)
      DIMENSION PMPA(NDM1,NDM2)
      DIMENSION TIME(NM)
      DIMENSION LAVOID(1)
!
!
!**********************************************************************
! START OF EXECUTABLE CODE ********************************************
!**********************************************************************
!
      DO IQP=1,15
        LAVOID(IPT-1+IQP)=.FALSE.
      ENDDO
      IF(LNPNM) GO TO 300
      DO 200 K=1,3
      IPT1=IPT+(K-1)*5
      IPT2=IPT+(K-1)*5+1
      IPT3=IPT+(K-1)*5+2
      IPT4=IPT+(K-1)*5+3
      IPT5=IPT+(K-1)*5+4
      DO 100 I=1,NM
      IF(PER.GT.0.D0) THEN
      WT=(TWOPI/PER)*TIME(I)
      SINWT=SIN(WT)
      COSWT=COS(WT)
      ELSE
      SINWT=0.D0
      COSWT=0.D0
      ENDIF
      PMPA(I,IPT1)=PMPA(I,IPT1)+PRPY(K,I)
      PMPA(I,IPT2)=PMPA(I,IPT2)+PRPY(K,I)*TIME(I)
      PMPA(I,IPT3)=PMPA(I,IPT3)+PRPY(K,I)*TIME(I)*TIME(I)
      PMPA(I,IPT4)=PMPA(I,IPT4)+PRPY(K,I)*SINWT
      PMPA(I,IPT5)=PMPA(I,IPT5)+PRPY(K,I)*COSWT
  100 END DO
  200 END DO
      RETURN
!
  300 CONTINUE
      DO 500 K=1,3
      IPT1=IPT+(K-1)*5
      IPT2=IPT+(K-1)*5+1
      IPT3=IPT+(K-1)*5+2
      IPT4=IPT+(K-1)*5+3
      IPT5=IPT+(K-1)*5+4
      DO 400 I=1,NM
      IF(PER.GT.0.D0) THEN
      WT=(TWOPI/PER)*TIME(I)
      SINWT=SIN(WT)
      COSWT=COS(WT)
      ELSE
      SINWT=0.D0
      COSWT=0.D0
      ENDIF
      PMPA(IPT1,I)=PMPA(IPT1,I)+PRPY(K,I)
      PMPA(IPT2,I)=PMPA(IPT2,I)+PRPY(K,I)*TIME(I)
      PMPA(IPT3,I)=PMPA(IPT3,I)+PRPY(K,I)*TIME(I)*TIME(I)
      PMPA(IPT4,I)=PMPA(IPT4,I)+PRPY(K,I)*SINWT
      PMPA(IPT5,I)=PMPA(IPT5,I)+PRPY(K,I)*COSWT
  400 END DO
  500 END DO
      RETURN
      END
