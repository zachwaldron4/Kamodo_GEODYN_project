!$DYNPMP
      SUBROUTINE DYNPMP(EXPRFL,PXDDOT,NEQN,C,CORPAR,VRARAY,             &
     &                  XM,XNP1,P,COSLAM,SINLAM,AORN)
!*********1*********2*********3*********4*********5*********6*********7
! DYNPMP            00/00/00            8811      PGMR - DAVE ROWLANDS
!
! FUNCTION:  CALCULATES PARTIALS AND/OR MODIFICATIONS TO PARTIALS OF
!            PARAMETERS ASSOCIATED WITH DYNAMIC POLAR MOTION
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   EXPRFL   I    A    PARTIALS OF RFL ACCELERATIONS WRT C & S
!                      COEFFICIENTS
!   PXDDOT   O    A    PARTIALS OF XYZ ACCELERATIONS WRT ALL FORCE MODEL
!                      PARAMETERS
!   NEQN     I    S    TOTAL NUMBER OF FORCE MODEL PARAMETERS
!   C        I    A    C GRAVITY COEFFCIENTS USED ON THIS INTEGRATION
!                      STEP (TIME VARYING)
!   CORPAR   I    A    MATRIX OF PARTIALS OF R,PSI,LAMDA WRT XYZ
!   VRARAY   I    A    (1-3)  ACCELERATIONS DUE TO GEOPOTENTIAL IN
!                      R,PPSI,LAMDA
!                      (4-8)  R,RSQ,XYSQ,RTXYSQ,GM/R
!                      (9-14)=BODY FIXED STATE VECTOR
! XM       I    A    XM ARRAY OF ORDERS
! XNP1     I    A    XNP1 ARRAY OF DEGREE PLUS ONE'S
! P        I    A    LEGENDRE POLYNOMIALS
! COSLAM   I    A    ARRAY OF COS(LAMDA*M)
! SINLAM   I    A    ARRAY OF SIN(LAMDA*M)
! AORN     I    A    ARRAY OF (GM/R)*(AE/R)**N
!
! COMMENTS:
!
!*********1*********2*********3*********4*********5*********6*********7
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CLGVTM/LGVTM,LDPM,LGVTPM,LOPT,LCSDPM,L_MP_IERS2003,        &
     &              L_CS_IERS2003,L_MP_IERS2010,L_CS_IERS2010
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/DYNPOL/DPMEP ,DPMXP ,DPMYP ,DPMXDT,DPMYDT,DPMOPT,DPMXPC,   &
     &              DPMYPC,DPMKF ,DPMC21,DPMS21,DPMPD ,DPMUDK,DPMVDK,   &
     &              DPMOCP,DPMNLD,DPMERS,XDYNPL
      COMMON/STARTT/ESSTRT,FSSTRT
      COMMON/GEODEG/NMAX,NMAXP1,NP,NTOLD,NTOLO,NADJC,NADJS,NADJCS,      &
     &              NPMAX,NXGDEG
      COMMON/CRMB/RMB(9), rmb0(9)
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/SETADJ/LSADRG(4),LSASRD(4),LSAGA(4),LSAGP,LSATID,LSAGM,    &
     &              LSADPM,LSAKF,LSADNX,LSADJ2,LSAPGM
      COMMON/SETAPT/                                                    &
     &       JSADRG(1),JSASRD(1),JSAGA(1),JFAADJ,JTHDRG,JACBIA,JATITD,  &
     &       JDSTAT,JDTUM ,                                             &
     &       JSACS, JSATID,JSALG,JSAGM,JSAKF,JSAXYP,JSADJ2,JSAPGM,      &
     &       JFGADJ,JLTPMU,JLTPAL,JL2CCO,JL2SCO,JL2GM,JLRELT,           &
     &       JLJ2SN,JLGMSN,JLPLFM,JL2AE,JSAXTO,JSABRN
!
      DIMENSION EXPRFL(NP,6),PXDDOT(NEQN,3),C(NP),CORPAR(9),VRARAY(14)
      DIMENSION XM(NP),XNP1(NP),P(NP),COSLAM(NMAXP1),                   &
     &          SINLAM(NMAXP1),AORN(NMAX)
      DIMENSION BFRTTR(9)
!
      DATA SQRT3/1.732050807568877D0/
      DATA EPS/.001D0/,ZERO/0.D0/
      DATA EPSP1/1.0001D0/,EPSM1/-.9999D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
!
!     DYNAMIC POLAR MOTION OPTION MODIFIES C& S 21
!     NEED TO KNOW PARTIAL OF TOD XYZ ACELERATION WRT C &S21
!
!   GET MATRX TO GO FROM R,PSI,LAMDA TO TRUE OF REFERENCE
      BFRTTR(1)=RMB(1)*CORPAR(1)+RMB(2)*CORPAR(4)+RMB(3)*CORPAR(7)
      BFRTTR(2)=RMB(4)*CORPAR(1)+RMB(5)*CORPAR(4)+RMB(6)*CORPAR(7)
      BFRTTR(3)=RMB(7)*CORPAR(1)+RMB(8)*CORPAR(4)+RMB(9)*CORPAR(7)
      BFRTTR(4)=RMB(1)*CORPAR(2)+RMB(2)*CORPAR(5)+RMB(3)*CORPAR(8)
      BFRTTR(5)=RMB(4)*CORPAR(2)+RMB(5)*CORPAR(5)+RMB(6)*CORPAR(8)
      BFRTTR(6)=RMB(7)*CORPAR(2)+RMB(8)*CORPAR(5)+RMB(9)*CORPAR(8)
      BFRTTR(7)=RMB(1)*CORPAR(3)+RMB(2)*CORPAR(6)+RMB(3)*CORPAR(9)
      BFRTTR(8)=RMB(4)*CORPAR(3)+RMB(5)*CORPAR(6)+RMB(6)*CORPAR(9)
      BFRTTR(9)=RMB(7)*CORPAR(3)+RMB(8)*CORPAR(6)+RMB(9)*CORPAR(9)
!   DEVIDE FIRST COLUMN BY R TO ACCOUNT FOR THE FACT THAT
!   R PARTIALS ARE TOO LARGE BY R
      T1=BFRTTR(1)/VRARAY(4)
      T2=BFRTTR(2)/VRARAY(4)
      T3=BFRTTR(3)/VRARAY(4)
!
!  GET AND/OR MODIFY APPROPRIATE LOCATION OF EXPRFL ARRAY
      XORDP1=XM(6)+EPSP1
      IORDP1=XORDP1
      XDEG=XNP1(6)+EPSM1
      IDEG=XDEG
! C21
      PRC21R=-AORN(IDEG)*XNP1(6)*COSLAM(IORDP1)*P(6)
      PRC21F=AORN(IDEG)*COSLAM(IORDP1)*EXPRFL(6,2)
      PRC21L=-AORN(IDEG)*XM(6)*SINLAM(IORDP1)*P(6)
! TRANSFORM INTO PARTIALS OF XYZ ACCELS
      PRC21X=T1*PRC21R+BFRTTR(4)*PRC21F+BFRTTR(7)*PRC21L
      PRC21Y=T2*PRC21R+BFRTTR(5)*PRC21F+BFRTTR(8)*PRC21L
      PRC21Z=T3*PRC21R+BFRTTR(6)*PRC21F+BFRTTR(9)*PRC21L
! S21
      PRS21R=-AORN(IDEG)*XNP1(6)*SINLAM(IORDP1)*P(6)
      PRS21F=AORN(IDEG)*SINLAM(IORDP1)*EXPRFL(6,2)
      PRS21L=AORN(IDEG)*XM(6)*COSLAM(IORDP1)*P(6)
! TRANSFORM INTO PARTIALS OF XYZ ACCELS
      PRS21X=T1*PRS21R+BFRTTR(4)*PRS21F+BFRTTR(7)*PRS21L
      PRS21Y=T2*PRS21R+BFRTTR(5)*PRS21F+BFRTTR(8)*PRS21L
      PRS21Z=T3*PRS21R+BFRTTR(6)*PRS21F+BFRTTR(9)*PRS21L
!
!
!
!*************** CODE FROM GRVTIM******************
!     DPMUDK=(DPMXPC-DPMXP-DPMXDT*(STIME-DPMEP))
!     DPMVDK=(DPMYPC-DPMYP-DPMYDT*(STIME-DPMEP))
!     C(6)=C(6)+DPMKF*DPMUDK*C(5)*SQRT3
!     S(6)=S(6)-DPMKF*DPMVDK*C(5)*SQRT3
!**************************************************
!
!
! GET POLAR MOTION SCALE FACTOR PARTIAL
      IF(.NOT.LSAKF) GO TO 2000
      DC21DK=DPMUDK*C(5)*SQRT3
      DS21DK=-DPMVDK*C(5)*SQRT3
      PXDDOT(JSAKF,1)=PRC21X*DC21DK+PRS21X*DS21DK
      PXDDOT(JSAKF,2)=PRC21Y*DC21DK+PRS21Y*DS21DK
      PXDDOT(JSAKF,3)=PRC21Z*DC21DK+PRS21Z*DS21DK
!
!
! GET POLAR MOTION PARTIALS (DYNAMIC PART)
 2000 CONTINUE
      IF(.NOT.LSADNX) GO TO 3000
! FIRST ZERO OUT ALL OF THE ADJUSTED PERIODS
! UT1 IS ZEROED OUT FOR ORDERING CONVENIENCE
      ISTART=JSAXYP
      ILEN=3*NPVAL0(IXPOLX)
      IFIN=ISTART+ILEN-1
      DO 2100 I=ISTART,IFIN
      PXDDOT(I,1)=ZERO
      PXDDOT(I,2)=ZERO
      PXDDOT(I,3)=ZERO
 2100 END DO
! DPMPD CONTAINS THE ADJUSTED PERIOD NUMBER (0 ==> UNADJUSTED)
      IPD=DPMPD+EPS
! IT MAY BE THAT THE CURRENT INTEGRATION STEP IS IN AN UNADJUSTED PERIOD
      IF(IPD.EQ.0) GO TO 3000
! CURRENT STEP IS IN AN ADJUSTED PERIOD; IPDP WILL POINT TO XP LOCATION
      IPDPX=JSAXYP+(IPD-1)*3
      IF (L_CS_IERS2003) THEN
          DC21DX = DPMKF*C(5)*SQRT3
          DS21DX = 0.0D0
      ELSE IF (L_CS_IERS2010) THEN
          DC21DX = -1.333D-9 / SECRAD
          DS21DX = 1.333D-9 * 0.0115D0 / SECRAD
      END IF
      PXDDOT(IPDPX,1) = PRC21X*DC21DX + PRS21X*DS21DX
      PXDDOT(IPDPX,2) = PRC21Y*DC21DX + PRS21Y*DS21DX
      PXDDOT(IPDPX,3) = PRC21Z*DC21DX + PRS21Y*DS21DX
      IPDPY=IPDPX+1
      IF (L_CS_IERS2003) THEN
          DS21DY = -DPMKF*C(5)*SQRT3
          DC21DY = 0.0D0
      ELSE IF (L_CS_IERS2010) THEN
          DS21DY = 1.333D-9 / SECRAD
          DC21DY = 1.333D-9 * 0.0115D0 / SECRAD
      END IF
      PXDDOT(IPDPY,1) = PRS21X*DS21DY + PRC21X*DC21DY
      PXDDOT(IPDPY,2) = PRS21Y*DS21DY + PRC21Y*DC21DY
      PXDDOT(IPDPY,3) = PRS21Z*DS21DY + PRC21Z*DC21DY
!
!
!  SUM IN DYNAMIC POLAR MOTION EFFECT ON C20 PARTIAL
 3000 CONTINUE
      IF(.NOT.LSADJ2) RETURN

      IF (L_CS_IERS2003) THEN
          DC21J2= DPMKF*DPMUDK*SQRT3
          DS21J2=-DPMKF*DPMVDK*SQRT3
      ELSE IF (L_CS_IERS2010) THEN
          TM=ESSTRT+FSSTRT
          MJDSEC=TM
          FSEC=TM-MJDSEC
          CALL IERSMP(MJDSEC, FSEC, Djxp, Djyp)
          DC21J2= Djxp*SQRT3
          DS21J2=-Djyp*SQRT3
      ELSE
          WRITE(*,'(2A)') 'ERROR: IERSCS21: EITHER L_CS_IERS2003 OR ',  &
                          'L_CS_IERS2010 MUST BE TRUE'
          STOP 16
      END IF

      PXDDOT(JSADJ2,1)=PXDDOT(JSADJ2,1)+PRC21X*DC21J2+PRS21X*DS21J2
      PXDDOT(JSADJ2,2)=PXDDOT(JSADJ2,2)+PRC21Y*DC21J2+PRS21Y*DS21J2
      PXDDOT(JSADJ2,3)=PXDDOT(JSADJ2,3)+PRC21Z*DC21J2+PRS21Z*DS21J2
!
!
      RETURN
      END
