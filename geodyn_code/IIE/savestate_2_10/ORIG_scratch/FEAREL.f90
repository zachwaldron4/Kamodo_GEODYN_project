!$FEAREL
      SUBROUTINE FEAREL(X,R,RSQ,DXDD,PARMV,PARMV0,PARMVC,PXDDT,NEQN,    &
     &                  ILTMSC,ISATID)
!********1*********2*********3*********4*********5*********6*********7**
! FEAREL           88/09/16            8810.0    PGMR - D. ROWLANDS
!                  91/02/04            9102.0    PGMR - B. LUTHCKE
!
! FUNCTION: (1) CALCULATE THE RELATIVISTIC POINT MASS ACCELERATION
!               FROM THE CENTRAL BODY IN PROPER (PLANET FRAME) UNITS
!               OF DISTANCE AND TIME.
!           (2) CALCULATE GEODETIC PRECESSION (RELATIVISTIC CORIOLIS
!               FORCE) FOR NON-HELIOCENTRIC ORBITERS
!           (3) FOR EARTH ORBITING SATELITES CALCULATE LENSE-THIRRING
!
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   X        I         STATE VECTOR OF SATELLITE
!   R        I         DISTANCE OF SATELLITE FROM CENTER OF INTEGRATION
!   RSQ      I         DISTANCE OF SATELLITE FROM CENTER OF INTEGRATION
!                      SQUARED (PROPER UNITS)
!   DXDD     O         OUTPUT ACCELERATION IN SAME COORDINATE SYSTEM
!                      AS INPUT X
!
!
! COMMENTS:
!
!   REFERENCE FOR RELATIVISTIC CORIOLIS FORCE:
!    HUANG, RIES, AND WATKINS: 1990, RELATIVISTIC EFFECTS FOR NEAR-
!     EARTH SATELLITE ORBIT DETERMINATION, CELESTIAL MECHANICS AND
!     DYNAMICAL ASTRONOMY 48:167-185,1990. PAGE 175.
!
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      PARAMETER ( FOUR = 4.D0,  ZERO = 0.D0,  CP8 = .8D0,               &
     &            TWO = 2.D0,   C2P4 = 2.4D0, CM1P5 = -1.50D0 )
      COMMON/CBDSTA/BDSTAT(7,999),XBDSTA
      COMMON/CBDTRU/BDTRUE(7,999)
      COMMON/CEGREL/LGRELE,LRLCLK,LGRELR,LXPCLK,LBPCLK,NXEGRL
      COMMON/CITER /NINNER,NARC,NGLOBL
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/CNIARC/NSATA,NSATG,NSETA,NEQNG,NMORDR,NMORDV,NSORDR,       &
     &              NSORDV,NSUMX,NXDDOT,NSUMPX,NPXDDT,NXBACK,NXI,       &
     &              NXILIM,NPXPF,NINTIM,NSATOB,NXBCKP,NXTIMB,           &
     &              NOBSBK,NXTPMS,NXCNIA
      COMMON/CRM5OE/RM5OE(9)
      COMMON/PLNETI/IPLNET(999),IPLNIN(999),MPLNGD(999),MPLNGO(999),   &
     &              IPLNZ(999),NXPLNI
      COMMON/CEARTH/AEG,AEGSQ,FGINV,FG,EGSQ,EGSQP1,C1,C2,FOURC1,TWOC2,  &
     &              XH2,XL2,WREF,RMEAN,REFC20,REFC40,REFC60,BEG,CBLTC,  &
     &              WAE2,GAMMAA,FSTAR,F4
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CLIGHT/VLIGHT,ERRLIM,XCLITE
      COMMON/IBODPT/IBDCF(999),IBDSF(999),IBDGM(999),IBDAE(999),    &
     &              IBDPF(999),                                     &
     &              ICBDCF,ICBDSF,ICBDGM,ICBDAE,ICBDPF,             &
     &              ITBDCF,ITBDSF,ITBDGM,ITBDAE,ITBDPF,NXBDPT
      COMMON/LICASE/LINT(2),LCASE(18),NXLCAS
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/SETAPT/                                                    &
     &       JSADRG(1),JSASRD(1),JSAGA(1),JFAADJ,JTHDRG,JACBIA,JATITD,  &
     &       JDSTAT,JDTUM ,                                             &
     &       JSACS, JSATID,JSALG,JSAGM,JSAKF,JSAXYP,JSADJ2,JSAPGM,      &
     &       JFGADJ,JLTPMU,JLTPAL,JL2CCO,JL2SCO,JL2GM,JLRELT,           &
     &       JLJ2SN,JLGMSN,JLPLFM,JL2AE,JSAXTO,JSABRN
      COMMON/VRBLOK/SINPSI,COSPSI,XLAMDA,GMR,AOR
      DIMENSION X(6),DXDD(3),VTSUN(3),OMG(3),TERM1(3),TERM2(3)
      DIMENSION PARMV(1),PARMV0(1),PARMVC(1)
      DIMENSION PXDDT(NEQN,3)
      DIMENSION ILTMSC(1)
!
!**********************************************************************
!* START OF EXECUTABLE CODE *******************************************
!**********************************************************************
!
      LTOP=.FALSE.
      IF(LINT(1).AND.LCASE(18))LTOP=.TRUE.
!
      VLIT2=VLIGHT*VLIGHT
      CFACT=GMR/(RSQ*VLIT2)
      VEL2=X(4)*X(4)+X(5)*X(5)+X(6)*X(6)
      XFACT=(FOUR*GMR-VEL2)*CFACT
      VFACT=(X(1)*X(4)+X(2)*X(5)+X(3)*X(6))*FOUR*CFACT
!
      DXDD(1)=XFACT*X(1)+VFACT*X(4)
      DXDD(2)=XFACT*X(2)+VFACT*X(5)
      DXDD(3)=XFACT*X(3)+VFACT*X(6)
!
! IN CASE OF GENERAL RELATIVITY APPLICATION IN THE INTERCOMPARISON
! DO NOT APPLY THE LENS THIRRING EFFECT
      IF( LTOP ) RETURN
 1000 CONTINUE
!ccc      IF(LGRELR) THEN
!
!**********************************************
!
! CALCULATE CONTRIBUTION FROM RELATIVISTIC CORIOLIS FORCE
!
!    .....don't calculate if central body is sun
!
!
      IF (LGRELR .and. ICBDGM .ne. 11 ) THEN
!
      RES=SQRT(BDTRUE(1,8)**2+BDTRUE(2,8)**2+BDTRUE(3,8)**2)
!
      VTSUN(1)=RM5OE(1)*BDSTAT(4,8)+RM5OE(4)*BDSTAT(5,8)                &
     &        +RM5OE(7)*BDSTAT(6,8)
      VTSUN(2)=RM5OE(2)*BDSTAT(4,8)+RM5OE(5)*BDSTAT(5,8)                &
     &        +RM5OE(8)*BDSTAT(6,8)
      VTSUN(3)=RM5OE(3)*BDSTAT(4,8)+RM5OE(6)*BDSTAT(5,8)                &
     &        +RM5OE(9)*BDSTAT(6,8)
!
! BDTRUE CONTAINS XSUN-XCB AND VSUN-VCB BUT XCB-XSUN AND
! VCB-VSUN IS NEEDED.  THIS IS WHY THE FOLLOWING CALCULATION
! HAS DIFFERENT SIGNS THEN THE REFERENCE STATED ABOVE.
!
      DO 2000 I=1,3
      TERM1(I)=CM1P5*VTSUN(I)
!c      TERM2(I)=(BDSTAT(7,8)*BDTRUE(I,8))/(VLIT2*(RES**3))
      TERM2(I)=(((BDSTAT(7,8)*BDTRUE(I,8))/VLIT2/RES)/RES)/RES
 2000 END DO
!
      OMG(1)=TERM1(2)*TERM2(3) - TERM1(3)*TERM2(2)
      OMG(2)=TERM1(3)*TERM2(1) - TERM1(1)*TERM2(3)
      OMG(3)=TERM1(1)*TERM2(2) - TERM1(2)*TERM2(1)
!
      DXDD(1)=DXDD(1)+TWO*(OMG(2)*X(6) - OMG(3)*X(5))
      DXDD(2)=DXDD(2)+TWO*(OMG(3)*X(4) - OMG(1)*X(6))
      DXDD(3)=DXDD(3)+TWO*(OMG(1)*X(5) - OMG(2)*X(4))
!
      ENDIF
!**********************************************
!
      IF(ICBDGM.NE.3) RETURN
!
! GET THE FANTOM VALUE FOR THE CONSTANT "CONST"
!
!C SEE IF THIS S/C REQUIRES APPLICATION OF THE NEW LT MODEL
      CALL FNDNUM(ISATID,ILTMSC,NSATA,IRET)
      IF(IRET.EQ.0)GOTO 100
          IF(NPVAL(IXLNTM).EQ.0) GOTO 100
      INDEX=IPVAL(IXLNTM)
      INDEX0=IPVAL0(IXLNTM)
        IF(NPVAL0(IXLNTM).EQ.0) THEN
      CNST=PARMV(INDEX)
        ELSE
      IF(NGLOBL.EQ.1) THEN
      CNST=PARMV0(INDEX0)
      ELSE
      CNST=PARMVC(INDEX0)
      ENDIF
        ENDIF
        GOTO 200
  100 CONTINUE
      CNST=0.D0
  200 CONTINUE
!
! LENSE-THIRRING FORMULA
!
!   NOTE THAT SOME CONSTANTS IN CODE ARE FOR EARTH ONLY
!
!
! G = GRAV. CONST.
! L = EARTH RADIUS
! R = SATELLITE DISTANCE FROM CENTER OF THE EARTH
! OMEGA = EARTH'S ROTATIONAL FREQUENCY
! X,Y,Z = POSITION OF THE SATELLITE
! XDOT, YDOT, ZDOT = VELOCITY COMPONENTS OF THE SATELLITE
! AX,AY,AZ = ACCELERATION DUE TO LENSE-THIRRING
!
!       ALPHA = G*M/(R*R)
!       BETA  = OMEGA*L*L/R
!
! LAMBDAX = (4/5)*((X*X+Y*Y-2*Z*Z)/(R*R))*YDOT +(12/5)(Y*Z/(R*R))*ZDOT
! LAMBDAY =-(4/5)*((X*X+Y*Y-2*Z*Z)/(R*R))*XDOT -(12/5)(X*Z/(R*R))*ZDOT
! LAMBDAZ = (12/5)*(Z/R)*((X*YDOT - Y*XDOT)/R)
!
!
!    AX = ALPHA*BETA*LAMBDAX/CSQUARED
!    AY = ALPHA*BETA*LAMBDAY/CSQUARED
!    AZ = ALPHA*BETA*LAMBDAZ/CSQUARED
!
!  CSQUARED= SPEED OF LIGHT SQUARED
!  M = MASS OF EARTH
!
      ABFACT=CFACT*WAE2
      XLAMBD = CP8*((X(1)*X(1)+X(2)*X(2)-TWO*X(3)*X(3))                 &
     &         /(RSQ))*X(5) +C2P4*(X(2)*X(3)/(RSQ))*X(6)
      YLAMBD =-CP8*((X(1)*X(1)+X(2)*X(2)-TWO*X(3)*X(3))                 &
     &         /(RSQ))*X(4) -C2P4*(X(1)*X(3)/(RSQ))*X(6)
      ZLAMBD = C2P4*(X(3)/R)*((X(1)*X(5) - X(2)*X(4))/R)
! OLD MODEL
!     DXDD(1)=DXDD(1)+ABFACT*XLAMBD
!     DXDD(2)=DXDD(2)+ABFACT*YLAMBD
!     DXDD(3)=DXDD(3)+ABFACT*ZLAMBD
! NEW FANTOM MODEL
      CT=(1.D0+CNST)
      DXDD(1)=DXDD(1)+CT*ABFACT*XLAMBD
      DXDD(2)=DXDD(2)+CT*ABFACT*YLAMBD
      DXDD(3)=DXDD(3)+CT*ABFACT*ZLAMBD
!
! FORM DIRECT PARTIALS
      IF(.NOT.LSTINR) GOTO 3000
      IF(NPVAL0(IXLNTM).GT.0) THEN
      INDX=JLTPMU
      PXDDT(INDX,1)=ABFACT*XLAMBD
      PXDDT(INDX,2)=ABFACT*YLAMBD
      PXDDT(INDX,3)=ABFACT*ZLAMBD
      ENDIF
 3000 CONTINUE
!
      RETURN
      END
