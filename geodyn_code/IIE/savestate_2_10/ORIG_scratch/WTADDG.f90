!$WTADDG
      SUBROUTINE WTADDG(NRMTOT,PARRSD,PARVAR,NPOLE,ISPOL,POLCOV,NTIDE,  &
     &           ITIDS,TIDCOV,NSTA,NSTAV,ISTAS,STACOV,ATPA,ATPL,ISTAV)
!********1*********2*********3*********4*********5*********6*********7**
! WTADDG           00/00/00            0000.0    PGMR - ?
!
! FUNCTION:  ADD ARC PARAMETER VARIANCE-COVARIANCE INFORMATION
!            TO THE NORMAL MATRIX AND RHS OF THE NORMAL EQUATIONS
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   NRMTOT   I         ACTUAL LENGTH OF FIRST ROW IN NORMAL MATRIX
!   PARRSD   I         DIFFERENCE BETWEEN LATEST ESTIMATE OF
!                      PARAMETERS AND APRIORI ESTIMATE
!   PARVAR   I         DIAGONAL POTION OF THE INVERTED APRIORI
!                      PARAMETER VARIANCE COVARIANCE MATRIX
!   NPOLE    I         NUMBER OF ADJUSTED POLE PARAMETER TRIPPLES
!   ISPOL    I         STARTING PARAMETER NUMBER OF POLE PARAMETER
!   POLCOV   I         POLE COVARIANCE INFORMATION (STRICTLY OFF DIAG)
!   NTIDE    I         NUMBER OF ADJUSTED EXTENDED TIDE PARAMETER
!                      PAIRS
!   ITIDS    I         STARTING PARAMETER NUMBER OF EX TIDE PARAMETER
!   TIDCOV   I         TIDE COVARIANCE INFORMATION (STRICTLY OFF DIAG)
!   NSTA     I         NUMBER OF ADJUSTED STATION PARAMETER TRIPPLES
!   ISTAS    I         STARTING PARAMETER LOCATION FOR STATIONS
!   STACOV   I         STATION  COVARIANCE INFORMATION (STRICTLY OFF
!                      DIAGONAL)
!   ATPA     I         NORMAL EQUATION MATRIX WITHOUT APRIORI
!                      PARAMETER INFORMATION AS INPUT
!                      NORMAL EQUATION MATRIX WITH APRIORI
!                      PARAMETER INFORMATION AS OUTPUT
!   ATPL     I         RHS OF NORMAL EQUATIONS WITHOUT APRIORI
!                      PARAMETER INFORMATION AS INPUT
!                      RHS OF NORMAL EQUATIONS WITH APRIORI
!                      PARAMETER INFORMATION AS OUTPUT
!   ISTAV    I         STARTING PARAMETER LOCATION FOR VELOCITIES
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION PARRSD(1),PARVAR(1),POLCOV(3,NPOLE),TIDCOV(1),          &
     &STACOV(3,NSTA),ATPA(1),ATPL(1)
      IDEXNO(I)=(I-1)*NRMTOT-(I-1)*(I-2)/2+1
      ILX(I)=NRMTOT-I+1
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! ADD IN LEFT HAND SIDE DIAGONAL FIRST
      IPT=1
      IL=NRMTOT
      DO 100 I=1,NRMTOT
      ATPA(IPT)=ATPA(IPT)+PARVAR(I)
      IPT=IPT+IL
      IL=IL-1
  100 END DO
!
! ADD IN INFORMATION TO RIGHT HAND SIDE
      DO 200 I=1,NRMTOT
      ATPL(I)=ATPL(I)+PARRSD(I)*PARVAR(I)
  200 END DO
!
! ADD IN COVARIANCE INFORMATION TO LEFT &RIGHT HAND SIDES OF POLE INFO
      IF(NPOLE.LE.0) GO TO 410
      ISP=IDEXNO(ISPOL)
      ILP=ILX(ISPOL)
! LEFT HAND SIDE
      DO 300 I=1,NPOLE
      ATPA(ISP+1)=ATPA(ISP+1)+POLCOV(1,I)
      ATPA(ISP+2)=ATPA(ISP+2)+POLCOV(2,I)
      ISP=ISP+ILP
      ILP=ILP-1
      ATPA(ISP+1)=ATPA(ISP+1)+POLCOV(3,I)
      ISP=ISP+2*ILP-1
      ILP=ILP-2
  300 END DO
! RIGHT HAND SIDE
      DO 400 I=1,NPOLE
      IPTP=(I-1)*3+ISPOL
! FIRST ROW OF COVAR MATRIX
      ATPL(IPTP)=ATPL(IPTP)+POLCOV(1,I)*PARRSD(IPTP+1)                  &
     &          +POLCOV(2,I)*PARRSD(IPTP+2)
! SECOND ROW
      ATPL(IPTP+1)=ATPL(IPTP+1)+POLCOV(1,I)*PARRSD(IPTP)                &
     &          +POLCOV(3,I)*PARRSD(IPTP+2)
! THIRD ROW
      ATPL(IPTP+2)=ATPL(IPTP+2)+POLCOV(2,I)*PARRSD(IPTP)                &
     &          +POLCOV(3,I)*PARRSD(IPTP+1)
  400 END DO
  410 CONTINUE
!  TIDE INFORMATION
      IF(NTIDE.LE.0) GO TO 610
! LHS
      IST=IDEXNO(ITIDS)
      ILT=ILX(ITIDS)
      DO 500 I=1,NTIDE
      ATPA(IST+1)=ATPA(IST+1)+TIDCOV(I)
      IST=IST+2*ILT-1
      ILT=ILT-2
  500 END DO
! RHS
      DO 600 I=1,NTIDE
      IPTT=(I-1)*2+ITIDS
! FIRST ROW OF COVAR MATRIX
      ATPL(IPTT)=ATPL(IPTT)+TIDCOV(I)*PARRSD(IPTT+1)
! SECOND ROW
      ATPL(IPTT+1)=ATPL(IPTT+1)+TIDCOV(I)*PARRSD(IPTT)
  600 END DO
  610 CONTINUE
! ADD IN LEFT &RIGHT HAND SIDES OF STATION INFO
      IF(NSTA.LE.0) RETURN
      ISP=IDEXNO(ISTAS)
      ILP=ILX(ISTAS)
! LEFT HAND SIDE
      DO 700 I=1,NSTA
      ATPA(ISP+1)=ATPA(ISP+1)+STACOV(1,I)
      ATPA(ISP+2)=ATPA(ISP+2)+STACOV(2,I)
      ISP=ISP+ILP
      ILP=ILP-1
      ATPA(ISP+1)=ATPA(ISP+1)+STACOV(3,I)
      ISP=ISP+2*ILP-1
      ILP=ILP-2
  700 END DO
! RIGHT HAND SIDE
      DO 800 I=1,NSTA
      IPTS=(I-1)*3+ISTAS
! FIRST ROW OF COVAR MATRIX
      ATPL(IPTS)=ATPL(IPTS)+STACOV(1,I)*PARRSD(IPTS+1)                  &
     &          +STACOV(2,I)*PARRSD(IPTS+2)
! SECOND ROW
      ATPL(IPTS+1)=ATPL(IPTS+1)+STACOV(1,I)*PARRSD(IPTS)                &
     &          +STACOV(3,I)*PARRSD(IPTS+2)
! THIRD ROW
      ATPL(IPTS+2)=ATPL(IPTS+2)+STACOV(2,I)*PARRSD(IPTS)                &
     &          +STACOV(3,I)*PARRSD(IPTS+1)
  800 END DO
      RETURN
      END
