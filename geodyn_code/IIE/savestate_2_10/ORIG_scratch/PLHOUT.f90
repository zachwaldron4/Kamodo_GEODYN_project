!$PLHOUT
      SUBROUTINE PLHOUT(STAP,PHI,COSPHI,SINPHI,XLAMB,COSLAM,SINLAM,     &
     &                  H,DXMDPM,LXYON,LPARON)
!********1*********2*********3*********4*********5*********6*********7**
! PLHOUT           83/10/25            8311.0    PGMR - D. ROWLANDS
!
! FUNCTION:  1) TO COMPUTE GEODETIC PHI, LAMBDA, H FROM
!               GEOCENTRIC X, Y, Z
!            2) TO COMPUTE PARTIAL DERIVATIVES OF GEOCENTRIC
!               COORDINATES WITH RESPECT TO GEODETIC
!               COORDINATES
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   STAP     I    A    XYZ STATION POSITION
!   PHI     I/O   S    GEODETIC LATITUDE (RADIANS)
!                      INPUT (IF LPARON=.TRUE.) OUTPUT OTHERWISE
!   COSPHI  I/O   S    COSINE OF GEODETIC LATITUDE
!                      INPUT (IF LPARON=.TRUE.) OUTPUT OTHERWISE
!   SINPHI  I/O   S    SINE OF GEODETIC LATITUDE
!                      INPUT (IF LPARON=.TRUE.) OUTPUT OTHERWISE
!   XLAMB   I/O   S    LONGITUDE (RADIANS)
!                      INPUT (IF LPARON=.TRUE.) OUTPUT OTHERWISE
!   COSLAM  I/O   S    COSINE OF LONGITUDE
!                      INPUT (IF LPARON=.TRUE.) OUTPUT OTHERWISE
!   SINLAM  I/O   S    SINE OF LONGITUDE
!                      INPUT (IF LPARON=.TRUE.) OUTPUT OTHERWISE
!   H       I/O   S    HEIGHT ABOVE ELLIPSOID (M)
!   DXMDPM   O    A    PARTIALS OF XYZ WRT GEODETIC
!                      OUTPUT (IF LXYON=.FALSE.) INPUT OTHERWISE
!   LXYON    I    S    .TRUE. IF THE PARTIALS OF GEOCENTRIC WRT GEODETIC
!                      ARE NOT NEEDED.
!   LPARON  I/O   S    .TRUE.IF ONLY THE PARTALS OF GEOCENTIC WRT
!                      GEODETIC ARE NEEDED
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION STAP(3),DXMDPM(3,3)
      COMMON/CEARTH/AEG,AEGSQ,FGINV,FG,EGSQ,EGSQP1,C1,C2,FOURC1,TWOC2,  &
     &              XH2,XL2,WREF,RMEAN,REFC20,REFC40,REFC60,BEG,CBLTC,  &
     &              WAE2,GAMMAA,FSTAR,F4
      DATA DELTA/0.001D0/
      DATA ZERO/0.0D0/,ONE/1.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      IF(LPARON) GO TO 100
! CALCULATE CONSTANTS
      T=EGSQ*STAP(3)
      XYSQ=STAP(1)**2+STAP(2)**2
! ITERATIVE PROCEDURE FOR HEIGHT
      DO 10 J=1,25
      ZT=STAP(3)+T
      H1=SQRT(XYSQ+ZT**2)
      SINPHI=ZT/H1
      ESQSP=EGSQ*SINPHI
      H2=AEG/SQRT(ONE-ESQSP*SINPHI)
      T1=H2*ESQSP
      IF(ABS(T1-T).LT.DELTA) GO TO 20
   10 T=T1
! HEIGHT
   20 H=H1-H2
! GEODETIC LATITUDE
      RTXYSQ=SQRT(XYSQ)
      PHI=ATAN2(ZT,RTXYSQ)
      COSPHI=SQRT(ONE-SINPHI*SINPHI)
! EAST LONGITUDE
      XLAMB=ATAN2(STAP(2),STAP(1))
      SINLAM=SIN(XLAMB)
      COSLAM=COS(XLAMB)
      IF(LXYON) RETURN
  100 CONTINUE
      E2SIN2=EGSQ*SINPHI*SINPHI
      OME2S2=ONE-E2SIN2
      XN=AEG/SQRT(OME2S2)
      XNPH=XN+H
      E2COS2=EGSQ*COSPHI*COSPHI
      DXMDPM(1,1)=-SINPHI*COSLAM*(XNPH-XN*E2COS2/OME2S2)
      DXMDPM(2,1)=-XNPH*COSPHI*SINLAM
      DXMDPM(3,1)=COSPHI*COSLAM
      DXMDPM(1,2)=-SINPHI*SINLAM*(XNPH-XN*E2COS2/OME2S2)
      DXMDPM(2,2)=XNPH*COSPHI*COSLAM
      DXMDPM(3,2)=COSPHI*SINLAM
      DXMDPM(1,3)=COSPHI*(H+XN*(ONE-EGSQ)*(ONE+E2SIN2/OME2S2))
      DXMDPM(2,3)=ZERO
      DXMDPM(3,3)=SINPHI
      RETURN
      END
