      SUBROUTINE MCRCON(XSTART)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! INITIALIZE PARAMETERS OF KONOPLIV/YODER MARS ORIENTATION MODEL
! ALSO FROM THE BASIC PARAMETER SET COMPUTE QUANTITIES THAT
! CAN BE COMPUTED ONCE PER RUN
!
! INPUT:   XSTART TIME TAG OF ARC START IN GEODYN ELAPSED SECONDS
!
! OUTPUT:  THROUGH COMMON BLOCK YODER
!
! NOTE:    THE PHASE QUANTITIES FOR THE PERIODIC TERMS OF NUTATION
!          DEPEND ON THE MARS ARG OF PERIGEE (IN THE MARS ORBIT
!          AND EQ SYSTEM). THIS CHANGES SLOWLY WRT TIME AND SO
!          THE NUTATION PHASES ARE COMPUTED ONCE PER RUN
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
!!!!  REAL*16 QSTART,QTWOPI,QFI0MR,QFIMRD
      COMMON/YODER /RFXM2E(9),XI0MR,XI0MRD,XI00MR,XIMR(9),              &
     &                        TSI0MR,TSIMRD,TSIMR(9),                   &
     &              ALFMAR(9),THTMAR(9),XNPMAR,XMAMR0,XS2000,XRATE0,    &
     &              FI0MR,FI0MRD,FICMR(4),FISMR(4)
!
!
!
      DIMENSION XIM0(9),TSIM0(9),FISREL(3)
      DIMENSION SMAT1(3,3),SMAT2(3,3),SMAT3(3,3)
!
!
!
!
!
!      INTIALIZE PARAMETERS OF MARS PRECESSION NUTATION
!
!
!
!
!
!  B MATRIX (FRAME BIAS BETEEN ICRF AND J2000) ANGLES IN MILLIAS
      DALF0=14.6D0
      EPS0=-16.6170D0
      XNU0=-6.8192D0
!
!  NODE OF MARS ORBIT ON EARTH EQUATOR FROM EQUINOX IN DEGREES
      XNDMRO=3.37919183D0
!
!  INCLINATION OF MARS ORBIT ON EARTH EQUATOR IN DEGRESS
      XJNMRO=24.67682669D0
!
!  INCLINATION OF MARS EQUATOR WRT MARS 1980 ORBIT AT J2000 DEGREES
!  AND RATE IN DEGREES PER DAY
      XI0MR=25.1893984585D0
      XI0MRD=0.5D-8
!
!  TSI ANGLE ON MARS MEAN ORBIT BETWEEN EARTH AND MARS EQUATORS AT J2000
!  IN DEGREES AND RATE IN DEGREES PER DAY
      TSI0MR=81.9683671267D0
      TSIMRD=-.5756D-5
!
!  MARS CORE FACTOR (UNITLESS)
      FMRCOR=0.07D0
!
!  MARS FREE CORE NUTATION RATE INPUT AS DEGREES PER DAY
      SIG0=-1.5D0
!
!  MARS "I" UNCORRECTED NUTATION AMPLITUDES INPUT AS MILLIARCSECONDS
      XI00MR=-1.4D0
      XIM0(1)=-0.4D0
      XIM0(2)= 0.D0
      XIM0(3)= 0.D0
      XIM0(4)=-49.1D0
      XIM0(5)= 515.7D0
      XIM0(6)= 112.8D0
      XIM0(7)= 19.2D0
      XIM0(8)= 3.D0
      XIM0(9)= 0.4D0
!
!  MARS "TSI" UNCORRECTED NUTATION AMPLITUDES INPUT AS MILLIARCSECONDS
      TSIM0(1)=-632.6D0
      TSIM0(2)=-44.2D0
      TSIM0(3)=-4.0D0
      TSIM0(4)=-104.5D0
      TSIM0(5)= 1097.D0
      TSIM0(6)= 240.1D0
      TSIM0(7)= 40.9D0
      TSIM0(8)= 6.5D0
      TSIM0(9)= 1.0D0

!
!  MARS MEAN ANMOLY AT J2000 INPUT AS DEGREES
      XMAMR0=19.41248D0
!
!  TWICE MARS PERIGEE IN MARS ORBIT PLANE AND EQ J2000 INPUT AS DEGREES
      QMARS0=142.D0
!
!  TWICE MARS PERIGEE IN MARS ORBIT PLANE AND EQ RATE INPUT AS DEG/CEN
      QMARSR=1.3D0
!
!
!
!
!
!
!
!      INTIALIZE PARAMETERS OF MARS ROTATION
!
!
!
!
!
!
!
!  NON PERIOIC PORTION OF PRIME MERDIAN ANGLE WRT (NODE) AT J2000
!  IN DEGREES AND RATE IN DEGREES PER DAY
      QFI0MR=133.38465Q0
      QFIMRD=350.891985286Q0
!
!  PERIOIC COSINE PORTIONS OF PRIME MERDIAN MOTION WRT (NODE)
!  IN MAS
      FICMR(1)= 398.D0
      FICMR(2)=-110.D0
      FICMR(3)= 7.D0
      FICMR(4)=-16.D0
!
!  PERIOIC SINE PORTIONS OF PRIME MERDIAN MOTION WRT (NODE)
!  (NO RELATIVITY) IN MAS
      FISMR(1)=-222.D0
      FISMR(2)=-128.D0
      FISMR(3)=-30.D0
      FISMR(4)= 6.D0
!
!  PERIOIC SINE PORTIONS OF PRIME MERDIAN MOTION WRT (NODE)
!  (PORTION DUE TO RELATIVITY) IN MAS
      DATA FISREL/-176.D0,-8.D0,-1.D0/
      FISREL(1)=-176.D0
      FISREL(2)=-8.D0
      FISREL(3)=-1.D0
!
!  MARS MEAN MOTION, NPRIME, INPUT AS ARCSECONDS PER CENT
      XNPMAR=68903543.D0
      XNP=XNPMAR
!
!
!
!
!
!
!
!
!
!   CONVERT ALL PARAMETERS TO PROPER UNITS
!
!
!
!
!
!
!
!
!
!  B MATRIX (FRAME BIAS BETEEN ICRF AND J2000) ANGLES IN RADIANS
      DALF0=DALF0*ACOS(-1.D0)/(180.D0*3600000.D0)
      EPS0=EPS0*ACOS(-1.D0)/(180.D0*3600000.D0)
      XNU0=XNU0*ACOS(-1.D0)/(180.D0*3600000.D0)
!
!  NODE OF MARS ORBIT ON EARTH EQUATOR FROM EQUINOX IN RADIANS
      XNDMRO=XNDMRO*ACOS(-1.D0)/180.D0
!
!  INCLINATION OF MARS ORBIT ON EARTH EQUATOR IN RADIANS
      XJNMRO=XJNMRO*ACOS(-1.D0)/180.D0
!
!  INCLINATION OF MARS EQUATOR WRT MARS 1980 ORBIT AT J2000 IN RADIANS
!  AND RATE IN RADIANS PER SEC
      XI0MR=XI0MR*ACOS(-1.D0)/180.D0
      XI0MRD=XI0MRD*ACOS(-1.D0)/(180.D0*86400.D0)
!
!  TSI ANGLE ON MARS MEAN ORBIT BETWEEN EARTH AND MARS EQUATORS AT J2000
!  IN RADIANS AND RATE IN RADIANS PER SEC
      TSI0MR=TSI0MR*ACOS(-1.D0)/180.D0
      TSIMRD=TSIMRD*ACOS(-1.D0)/(180.D0*86400.D0)
!
!  MARS FREE CORE NUTATION RATE IN RADIANS PER SECOND
      SIG0=SIG0*ACOS(-1.D0)/180.D0
      SIG0=SIG0/86400.D0
!
!  MARS "I" AND TSI UNCORRECTED NUTATION AMPLITUDES  AS RADIANS
      XI00MR=XI00MR*ACOS(-1.D0)/(3600000.D0*180.D0)
      DO I=1,9
        XIM0(I)=XIM0(I)*ACOS(-1.D0)/(3600000.D0*180.D0)
        TSIM0(I)=TSIM0(I)*ACOS(-1.D0)/(3600000.D0*180.D0)
      ENDDO
!
!  MARS MEAN MOTION, NPRIME, IN RADIANS/SECOND
      XNP=XNP/3600.D0
      XNP=XNP*ACOS(-1.D0)/180.D0
      XNP=XNP/36525.D0
      XNP=XNP/86400.D0
      XNPMAR=XNP
!
!  MARS MEAN ANMOLY AT J2000 IN RADIANS
      XMAMR0=XMAMR0*ACOS(-1.D0)/180.D0
!  TWICE MARS PERIGEE IN MARS ORBIT PLANE AND EQ J2000 AS RADIANS
      QMARS0=QMARS0*ACOS(-1.D0)/180.D0
!  TWICE MARS PERIGEE IN MARS ORBIT PLANE AND EQ RATE RAD/SEC
      QMARSR=QMARSR*ACOS(-1.D0)/180.D0
      QMARSR=QMARSR/36525.D0
      QMARSR=QMARSR/86400.D0
!
!  NON PERIOIC PORTION OF PRIME MERDIAN ANGLE WRT (NODE) AT J2000
!  IN RADIANS AND RATE IN RADIANS PER SECOND
!
!
!   FIGURE OUT THE INTERNAL TIME FOR J2000
      IYIMID=000101
      IHIMIS=120000
      CALL YMDTIS(IYIMID,IHIMIS,MS2000)
      XS2000=DBLE(MS2000)
      QS2000=REAL(MS2000,KIND=KIND(QS2000))
      QFI0MR=QFI0MR*ACOS(-1.Q0)/180.Q0
      QFIMRD=QFIMRD*ACOS(-1.Q0)/(180.Q0*86400.Q0)
      ISTART=XSTART
      ISTART=ISTART-86400
      QSTART=REAL(ISTART,KIND=KIND(QS2000))
      QFI0MR=QFI0MR+(QSTART-QS2000)*QFIMRD
      QTWOPI=2.Q0*ACOS(-1.Q0)
      QFI0MR=MOD(QFI0MR,QTWOPI)
      FI0MR=QFI0MR
      FI0MRD=QFIMRD
      XRATE0=DBLE(ISTART)
!
!  PERIOIC COSINE PORTIONS OF PRIME MERDIAN MOTION WRT (NODE)
!  IN RADIANS
!      AND
!  PERIOIC SINE PORTIONS OF PRIME MERDIAN MOTION WRT (NODE)
!  (NO RELATIVITY) IN RADIANS
!      AND
!  PERIOIC SINE PORTIONS OF PRIME MERDIAN MOTION WRT (NODE)
!  (RELATIVISTIC PART) IN RADIANS
      DO I=1,4
        FICMR(I)=FICMR(I)*ACOS(-1.D0)/(180.D0*3600000.D0)
        FISMR(I)=FISMR(I)*ACOS(-1.D0)/(180.D0*3600000.D0)
      ENDDO
      DO I=1,3
        FISREL(I)=FISREL(I)*ACOS(-1.D0)/(180.D0*3600000.D0)
      ENDDO
!
!
!
!
!
!
!
!
!      COMPUTE QUANTITIES THAT CAN BE COMPUTED ONCE PER RUN
!
!
!
!
!
!
!
!
      ALFMAR(1)=XNP
      ALFMAR(2)=2.D0*XNP
      ALFMAR(3)=3.D0*XNP
      ALFMAR(4)=XNP
      ALFMAR(5)=2.D0*XNP
      ALFMAR(6)=3.D0*XNP
      ALFMAR(7)=4.D0*XNP
      ALFMAR(8)=5.D0*XNP
      ALFMAR(9)=6.D0*XNP
!
!  Q AT START OF THIS RUN (Q VARIRES SLOWLY AND ISASSUMED CONSTANT
!                          DURING RUN )
!
      Q=QMARS0+(XSTART-XS2000)*QMARSR
!
      THTMAR(1)=XMAMR0
      THTMAR(2)=2.D0*XMAMR0
      THTMAR(3)=3.D0*XMAMR0
      THTMAR(4)=XMAMR0+Q
      THTMAR(5)=2.D0*XMAMR0+Q
      THTMAR(6)=3.D0*XMAMR0+Q
      THTMAR(7)=4.D0*XMAMR0+Q
      THTMAR(8)=5.D0*XMAMR0+Q
      THTMAR(9)=6.D0*XMAMR0+Q
!
!  RATIOS FOR CORRECTING NUTATION AMPLITUDES
      SIG02=SIG0*SIG0
      SINI0=SIN(XI0MR)
      SINI0R=1.D0/SINI0
      DO I=1,9
        DENOM=ALFMAR(I)*ALFMAR(I)-SIG02
        RAT1=ALFMAR(I)*ALFMAR(I)/DENOM
        RAT2=ALFMAR(I)*SIG0/DENOM
        XIMR(I)=XIM0(I)+FMRCOR*XIM0(I)*RAT1+FMRCOR*TSIM0(I)*SINI0*RAT2
        TSIMR(I)=TSIM0(I)+FMRCOR*XIM0(I)*SINI0R*RAT2                    &
     &                   +FMRCOR*TSIM0(I)*RAT1
      ENDDO
!
! COMPUTE THE FIXED (TIME INVARIENT) PORTION OF THE MARS TO J2000 EARTH
! MATRIX
!
      ANG=-XJNMRO
      CALL R1(ANG,SMAT1)
      ANG=-XNDMRO
      CALL R3(ANG,SMAT2)
      CALL MMULT3(SMAT2,SMAT1,SMAT3)
      ANG=DALF0
      CALL R3(ANG,SMAT1)
      CALL MMULT3(SMAT1,SMAT3,SMAT2)
      ANG=EPS0
      CALL R2(ANG,SMAT1)
      CALL MMULT3(SMAT1,SMAT2,SMAT3)
      ANG=-XNU0
      CALL R1(ANG,SMAT1)
      CALL MMULT3(SMAT1,SMAT3,RFXM2E)
!
!
!
!
!
!  ADD PERIOIC SINE PORTIONS OF PRIME MERDIAN MOTION WRT (NODE)
!  RELATIVISTIC AND RELATIVISTIC PORTIONS
      DO I=1,3
       FISMR(I)=FISMR(I)+FISREL(I)
      ENDDO
!
!
!
      RETURN
      END
