!$OPTPRE
      SUBROUTINE OPTPRE(MJDSEC,FSEC  ,OBS1  ,OBS2  ,THETG ,XM    ,      &
     &    UNITX ,UNITY ,UNITZ ,X     ,Y     ,Z     ,R     ,ELEV  ,      &
     &    OBSCOR,OBSCR2,DCSYS1,DCSYS2,DABER1,DABER2,PARLX1,PARLX2,      &
     &    AABER1,AABER2,LTRUED,LNDABR,LNPRLX,LNAABR,AA,II    )
!********1*********2*********3*********4*********5*********6*********7**
! OPTPRE           08/07/84            8408.0    PGMR - TOM MARTIN
!
! FUNCTION:  COMPUTE CORRECTIONS TO OPTICAL RIGHT ASCENSION
!            AND DECLINATION DATA.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I    S    (JULIAN DATE-GEODYN REF.TIME)*86400+ISEC
!                      WHERE ISEC IS THE INTEGRAL ELAPSED SECONDS
!                      TO EPOCH OF FIRST OBSRV. IN BLOCK
!   FSEC     I    A    ELAPSED SECONDS FROM "MJDSEC" UNTIL EPOCH OF
!                      THIS OBSERVATION
!   OBS1     I    S    OBSERVED RIGHT ASCENSION OF S/C FROM STATION
!   OBS2     I    S    OBSERVED DECLINATION OF S/C FROM STATION
!   THETG    I    S    RIGHT ASCENSION OF GREENWICH
!   XM       I    S    MEAN POLE COORDINATES OF TRACKING STATION
!   UNITX    I    S    TOPOCENTRIC X COORD. OF S/C
!   UNITY    I    S    TOPOCENTRIC Y COORD. OF S/C
!   UNITZ    I    S    TOPOCENTRIC Z COORD. OF S/C
!   X        I    S    E.C.F. S/C X POSITION
!   Y        I    S    E.C.F. S/C Y POSITION
!   Z        I    S    E.C.F. S/C Z POSITION
!   R        I    S    SLANT RANGE FROM STATION TO S/C
!   ELEV     I    S    S/C ELEVATION ABOVE HORIZON IN RADIANS
!   OBSCOR  I/O   S    SUM OF THE OBSERVATION CORRECTIONS R.A.
!   OBSCR2  I/O   S    SUM OF THE OBSERVATION CORRECTIONS DECLIN.
!   DCSYS1   O    S    R.A. CORR. FOR COORD. ROT. TO TRUE-OF-DATE
!   DCSYS2   O    S    DECL CORR. FOR COORD. ROT. TO TRUE-OF-DATE
!   DABER1   O    S    DIURNAL ABERRATION IN R.A.
!   DABER2   O    S    DIURNAL ABERRATION IN DECLIN.
!   PARLX1   O    S    PARALLACTIC REFRACTION IN R.A.
!   PARLX2   O    S    PARALLACTIC REFRACTION IN DECLIN.
!   AABER1   O    S    ANNUAL ABERRATION IN R.A.
!   AABER2   O    S    ANNUAL ABERRATION IN DECLIN.
!   LTRUED   I    S    .TRUE. INDICATES DATA REQUIRES NO ROTATION
!   LNDABR   O    S    .TRUE. INDICATES BYPASS DIURNAL ABERRATION
!   LNPRLX   O    S    .TRUE. INDICATES BYPASS PARALLACTIC REFRACT
!   LNAABR   O    S    .TRUE. INDICATES BYPASS ANNUAL ABERRATION
!   AA      I/O   A    REAL DYNAMIC ARRAY
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION PSAT(3),STASAT(3),XM(3),STPSAT(3),SUNXYZ(3),XYZLUN(3),  &
     &   AA(1),II(1),XYZIN(3),XYZOUT(3)
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
! /CSTA  / STATION GEODETIC INFORMATION USED IN MEASUREMENT CORRECTIONS
      COMMON/CSTA/RLAT,COSLAT,SINLAT,RLON,COSLON,SINLON,HEIGHT,         &
     &   TMOUNT,DISP,WAVREC,WAVXMT,ELCUT,PLATNO,SITNOL,SDTLRA,ANTTNO
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      DATA ZERO/0.0D0/,ONE/1.0D0/,HALF/0.5D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! DIURNAL ABERRATION COEFFICIENT  (ARCSEC) * (RADIAN/ARCSEC)
! CHANGED 5/17/85    DABERR   =     0.320  * 0.4848136811095365D-5
!                    DABERR   =    -0.320  * 0.4848136811095365D-5
      DATA DABERR/-0.155140377960D-5/
!
! PARALLACTIC REFRACTION COEFF.   (ARCSEC) * (RADIAN/ARCSEC)
!                    PARLAX   =   0.435E6  * 0.4848136811095365D-5
      DATA PARLAX/ 0.210893951282D01/
      DATA RCZLIM/1.1D6/,EXCOEF/-1.385D-4/
!
! ANNUAL ABERRATION COEFFICIENT   (ARCSEC) * (RADIAN/ARCSEC)
! CHANGED 5/17/85    AABERR   =    -20.5   * 0.4848136811095365D-5
!                    AABERR   =    +20.5   * 0.4848136811095365D-5
      DATA AABERR/+0.993868046274D-4/
!
! DEFINE SUNPO CALLING SWITCHES
!          LNAABR = .TRUE. INDICATES SUN POS. AND OBLIQUITY NOT REQUIRED
!          LTRUED = .TRUE. INDICATES DATA REQUIRES NO ROTATION
!          LNPREC = .TRUE. INDICATES PRECESSION NOT REQUIRED
!          L50EQT = .TRUE. INDICATES 1950 REFERENCE INSTEAD OF 1957
      DATA LNPREC/.FALSE./,L50EQT/.TRUE./
!
!
!
!***** BEGIN DEBUG *****
!     WRITE(IOUT6,10000) MJDSEC,FSEC  ,OBS1  ,OBS2,
!    1                   THETG ,ELEV  ,XM
!10000 FORMAT(' ** OPTPRE **  MJDSEC,FSEC  ,OBS1  ,OBS2'/
!    1       '               THETG ,ELEV  ,XM ='/
!    2       1X,I16,3G16.9/1X,5G16.9)
!***** END DEBUG *****
!
! COMPUTE SINE AND COSINE OF DECLINATION
      CD=COS(OBS2)
      SD=SIN(OBS2)
!
! SET SWITCH INDICATING THAT NO SUNPO CALL IS REQUIRED.
!     (DATA ALREADY IN TRUE OF DATE AND ANNUAL ABERRATION NOT REQUIRED)
      LNSUNP=LTRUED.AND.LNAABR
      IF(LNSUNP) GO TO 1000
!
! COMPUTE SINE AND COSINE OF RIGHT ASCENSION
      CA=COS(OBS1)
      SA=SIN(OBS1)
!
      IF(LTRUED) GO TO 1000
      XYZIN(1)=CA*CD
      XYZIN(2)=SA*CD
      XYZIN(3)=SD
 1000 CONTINUE
!
! COMPUTE SOLAR POSITION AND TRUE OBLIQUITY OF DATE
!         AND/OR ROTATE OBSERVATION COORD. TO TRUE OF DATE.
      CALL SUNPO (MJDSEC,FSEC  ,SUNXYZ,XYZLUN,TOBLIQ,XYZIN ,XYZOUT,     &
     &            XYZOUT,LNAABR,LTRUED,LNPREC,L50EQT,.FALSE.,AA ,II   )
!
      IF(LTRUED) GO TO 2000
!
! COMPUTE ROTATION CORRECTION TO OBSERVATIONS
      TOBS1=ATAN2(XYZOUT(2),XYZOUT(1))
      TOBS1=MOD(TOBS1+TWOPI,TWOPI)
!CC      TOBS2=DARSIN(XYZOUT(3))
      TOBS2=ASIN(XYZOUT(3))
      DCSYS1=OBS1-TOBS1
      DCSYS2=OBS2-TOBS2
      OBSCOR=OBSCOR+DCSYS1
      OBSCR2=OBSCR2+DCSYS2
!***** BEGIN DEBUG *****
!     WRITE(IOUT6,20000) DCSYS1,DCSYS2,OBSCOR,OBSCR2
!20000 FORMAT(' ** OPTPRE **  DCSYS1,DCSYS2,OBSCOR,OBSCR2 ='/
!    1   (1X,4G19.12))
!***** END DEBUG *****
 2000 CONTINUE
!
      IF(LNDABR) GO TO 3000
! DIURNAL ABERRATION
      H=RLON+THETG-OBS1
!     DABER2=-0.0213E0*SECRAD*15.0E0*COSLAT*DSIN(H)*DSIN(OBS2)
      DABER2=DABERR*COSLAT*SIN(H)*SIN(OBS2)
      DABER2=DABERR*COSLAT*SIN(H)*SIN(OBS2)
      OBSCR2=OBSCR2+DABER2
      DABER1=DABERR*COSLAT*COS(H)/COS(OBS2)
      OBSCOR=OBSCOR+DABER1
!***** BEGIN DEBUG *****
!     WRITE(IOUT6,30000) DABER1,DABER2,OBSCOR,OBSCR2
!30000 FORMAT(' ** OPTPRE **  DABER1,DABER2,OBSCOR,OBSCR2 ='/
!    1   (1X,4G19.12))
!***** END DEBUG *****
! PARALLACTIC REFRACTION
 3000 IF(LNPRLX) GO TO 8000
      PSAT(1)=-UNITY
      PSAT(2)= UNITX
      PSAT(3)=ZERO
      STASAT(1)= XM(2)*Z-XM(3)*Y
      STASAT(2)=-XM(1)*Z+XM(3)*X
      STASAT(3)= XM(1)*Y-XM(2)*X
      T2=PSAT(1)**2+PSAT(2)**2
      UT=STASAT(1)**2+STASAT(2)**2+STASAT(3)**2
      T2=SQRT(T2)
      UT=SQRT(UT)
      DO 4000 J=1,3
      PSAT(J)=PSAT(J)/T2
 4000 STASAT(J)=STASAT(J)/UT
      CQ=PSAT(1)*STASAT(1)+PSAT(2)*STASAT(2)+PSAT(3)*STASAT(3)
      STPSAT(1)= UNITZ*PSAT(2)-UNITY*PSAT(3)
      STPSAT(2)=-UNITZ*PSAT(1)+UNITX*PSAT(3)
      STPSAT(3)= UNITY*PSAT(1)-UNITX*PSAT(2)
      UT=SQRT(STPSAT(1)**2+STPSAT(2)**2+STPSAT(3)**2)
      SQ=STPSAT(1)*STASAT(1)+STPSAT(2)*STASAT(2)+STPSAT(3)*STASAT(3)
      SQ=SQ/UT
      ZENITH=HALF*PI-ELEV
      IF(ELEV.GT.ZERO) GO TO 5000
      ZENITH=ZERO
      WRITE(IOUT6,90000)
 5000 RCOSZ=R*COS(ZENITH)
      IF(RCOSZ.GT.RCZLIM) GO TO 6000
      H=ONE-EXP(EXCOEF*RCOSZ)
      GO TO 7000
 6000 H=ONE
!7000 DR=0.435E0*4.84813E0*DTAN(ZENITH)*H/RCOSZ
!7000 DR= PARLAX*DTAN(ZENITH)*H/RCOSZ   *** CHANGED BY TVM 5/16/85
 7000 DR=-PARLAX*TAN(ZENITH)*H/RCOSZ
      PARLX2=DR*CQ
      OBSCR2=OBSCR2+PARLX2
      PARLX1=DR*CQ/T2
      OBSCOR=OBSCOR+PARLX1
!***** BEGIN DEBUG *****
!     WRITE(IOUT6,70000) PARLX1,PARLX2,OBSCOR,OBSCR2
!70000 FORMAT(' ** OPTPRE **  PARLX1,PARLX2,OBSCOR,OBSCR2 ='/
!    1   (1X,4G19.12))
!***** END DEBUG *****
! ANNUAL ABERRATION
 8000 IF(LNAABR) RETURN
      CE=COS(TOBLIQ)
      XL=ATAN2(SUNXYZ(2),CE*SUNXYZ(1))
      CL=COS(XL)
      SL=CL*SUNXYZ(2)/(SUNXYZ(1)*CE)
      CA=COS(OBS1)
      SA=SIN(OBS1)
      TE=SIN(TOBLIQ)/CE
      AABER1=AABERR*(CA*CL*CE+SA*SL)/CD
      OBSCOR=OBSCOR+AABER1
      AABER2=AABERR*(CL*CE*(TE*CD-SA*SD)+CA*SD*SL)
      OBSCR2=OBSCR2+AABER2
!***** BEGIN DEBUG *****
!     WRITE(IOUT6,80000) AABER1,AABER2,OBSCOR,OBSCR2
!80000 FORMAT(' ** OPTPRE **  AABER1,AABER2,OBSCOR,OBSCR2 ='/
!    1   (1X,4G19.12))
!***** END DEBUG *****
      RETURN
90000 FORMAT('0**OPTPRE**  ELEVATION NEGATIVE. MAXIMUM CORRECTION ',    &
     &   'FOR PARALLACTIC REFRACTION USED.'/1X )
      END
