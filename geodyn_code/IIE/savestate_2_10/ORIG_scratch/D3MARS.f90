!$D3MARS
      SUBROUTINE D3MARS(XSC,MJDSEC,FSEC,FLXAVG,DENST)
!********1*********2*********3*********4*********5*********6*********7**
! D3MARS           00/00/00            0000.0    PGMR - ?
!
!
! FUNCTION: COMPUTE ATMOSPHERIC DENSITY OF MARS USING THE REVISED
!           STEWART 1987 MODEL.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   XSC(I)             POSITION VECTOR OF THE SPACECRAFT (KM) (INPUT)
!   MJDSEC             NUMBER OF SECONDS CORRESPONDING TO MJD (INPUT)
!   FSEC               FRACTIONAL SECOND CORRESPONDING TO THE ABOVE
!                      (INPUT)
!   FLXAVG             AVERAGED SOLAR FLUX FOR THIS DATE AND TIME.
!
!   DENST              MASS DENSITY (KG/M**3) (OUTPUT)
!   LBENCH             FLAG TO COMPUTE BENCHMARK DENSITIES
!   LONCE              FLAG TO COMPUTE AVERAGE DENSITIES AROUND THE
!                      ORBIT
!
!    RMB(I)            ROTATION MATRIX (COMMON)
!    XMNSNS            (1-3,2) POSITION VECTOR OF THE SUN (M) (COMMON)
!
!    A                 SEMIMAJOR AXIS OF MARS ORBIT (KM)
!    AL                = TF / 9.2
!    AUOA              INVERSE OF A IN ASTRONOMICAL UNITS
!    AVGDEN            AVERAGE DENSITY AROUND THE ORBIT
!    AVOG              AVOGADROS NUMBER
!    ALS               AEROCENTRIC LONGITUDE OF THE SUN
!    AVOGDR            RECIPROCAL OF AVOGADROS NUMBER (KG-MOLE)
!    CSCLAT            COSINE OF THE SPACECRAFT LATITUDE
!    DEGRAD            CONSTANT TO CONVERT DEGREES TO RADIANS
!    DELR61            CORRECTION TO R61MB DUE TO PRESSURE VARIATION
!    DENSTI            DENSITY PER MOLECULAR SPECIES
!    DL                LONGITUDE DIFFERENCE BETWEEN SPACECRAFT AND SUN
!    DT                DIFFERENCE OF DATE BETWEEN PRESENT AND REFERENCE
!    DZDS              INCREMENT OF ALTITUDE DUE TO DUST STORMS (KM)
!    EAU               DEFINITION OF ASTRONOMICAL UNIT
!    F(I)              VECTOR STORED FRACTION OF EACH GAS AT ZF
!                      WHERE     I = 1 REPRESENTS CO2
!                                    2     "      O
!                                    3     "      N2
!                                    4     "      AR
!                                    5     "      CO
!                                    6     "      O2
!                                    7     "      HE
!                                    8     "      H
!                                    9     "      H2
!    F107             SOLAR FLUX AT 10.7 CM WAVELENGTH
!    FP               FRACTION OF HELIOCENTRIC PERIOD
!    GF               GRAVITY AT ALTITUDE ZF (CM/SEC**2)
!    GFOKT            PARAMETER EQUALS 10.*GF/(K*TINF)
!    HINF             RECIPROCAL OF SCALE HEIGHT FOR HIGH ALTITUDE
!                     (1/KM)
!    NC               NUMBER OF CONSTITUENT GASES
!    PF               PRESSURE AT ZF
!    PP(I)            VECTOR STORED PARTIAL PRESSURE OF EACH GAS
!    PROPR            PRESSURE OVER AVERAGE PRESSURE, SEASONAL VARIATION
!    PERIOD           SIDERIAL PERIOD OF MARS (DAYS)
!    PF               PRESSURE AT THE F LAYER (1.24 NANOBAR)
!    R61MB            AEROCENTRIC DISTANCE OF 6.1 MILLIBAR SPHEROID (KM)
!    REFDAT           DATE OF MARS PERIHELION
!    RSC              AEROCENTRIC DISTANCE OF SPACECRAFT (KM)
!    RSN              HELIOCENTRIC DISTANCE OF MARS (KM)
!    SCLAT            LATITUDE OF SPACECRAFT (RAD.) (ROTATING
!                     COORDINATES)
!    SCLONG           LONGITUDE OF SPACECRAFT (RAD.)
!    SNLAT            LATITUDE OF SUN (RAD.) (ROTATING COORDINATES)
!    SNLONG           LONGITUDE OF SUN (RAD.)
!    SOLDAY           DAYS IN EARTH YEAR
!    SPCLT            THE QUANTITY 1.219D-2 / T
!    T                TEMPERATURE AT SPACECRAFT POSITION
!    TF               TEMPERATURE AT THE 1.24 NANOBAR LEVEL
!    TO               TEMPERATURE AT 6.1 MILLIBAR SURFACE
!    TOTF             = T / TF
!    TIME             LOCAL TIME AT SPACECRAFT POSITION (RAD.)
!    TINF             EXOSPHERIC TEMPERATURE (K)
!    XK               UNIVERSAL GAS CONSTANT (J/KG-MOLE * K)
!    XMW(I)           VECTOR STORED MOLECULAR WEIGHT OF EACH GAS
!    XNBDNS(I)        VECTOR STORED NUMBER DENSITY OF EACH GAS (1/CM**3)
!    U                UNIVERSAL GAS CONSTANT (CM*KM**2/SEC*2)
!    YMYF             AEROPOTENTIAL ALTITUDE (KM)
!    Z                ALTITUDE OF SPACECRAFT ABOVE REFERENCE
!                     SURFACE (KM)
!    ZF               ALTITUDE OF 1.24 NANOBAR LEVEL (KM)
!
!
!
! COMMENTS:
!
!   A MAJOR MODIFICATION TO THIS ROUTINE IS BEING MADE TO IMPLEMENT THE
!   UPDATED MARS ATMOSPHERE MODEL OF A.I.F. STEWART PUBLISHED IN 1987
!   AS JPL PO# NQ-802429.  THE EARLIER VERSION OF THE MODEL
!   PROGRAMMED IN GEODYNE HAD BEEN PUBLISHED BY STEWART IN 1983.  I AM
!   ALSO ADDING MORE DOCUMENTATION STARTING DIRECTLY BELOW WITH THE
!   VARIABLES.
!                                       A. MALLAMA
!                                       MARCH, 1989
!
!   MODIFIED TO RUN ON GEODYN II BY JOE CHAN, MARCH 1989
!
!  MODIFIED BY F. G. LEMOINE, 7/10/2000.
!
!  THE PARAMETERIZATION FOR THE VARIATION OF THE 6.1 MB ELLIPSOID
!   FOR THE GLOBAL PRESSURE VARIATION IS NOT REALLY PERIODIC
!   AS CURRENTLY FORMULATED. DISCONTINUTIES IN THE VARIABLE
!   DEL61 OCCUR AT LS=300, LS=360, AND LS= 30.
!   THIS WAS REMEDIED BY REDEFINING LS WITH PSEUDO VARIABLES
!   LS1 AND LS2 AND USING APPROPRIATE IF STATEMENTS.
! ALSO THE MODELLING OF THE INCREMENT IN ZF DUE TO THE AVERAGE
!   DUST STORMS IS ERRONEOUS. IN READING THE STEWART 87 REPORT,
!   ONE CAN SEE THAT WHAT IS INTENDED IS A DUST STORM ONSET AT
!   LS = 205, WITH 10 DEG OF LS FROM ONSET TO PEAK.  THE SAME
!   IS TRUE FOR THE SECOND DUST STORM ONSET AT LS=275.0
!   THE INCREMENT IN ZF IS SET TO ZERO PRIOR TO DUST
!   STORM ONSET.
!   ANOTHER PROBLEM IS THAT THE EXPONENTIAL FUNCTIONS DO NOT
!   DECAY TO ZERO BY LS=360, SO THERE IS A DISCONTINUITY FROM
!   LS=360 TO LS=0. THIS CAN BE FIXED BY EVALUATING DZDS
!   AT LS=360, AND SUBTRACTING THIS BIAS FOR LS >275, SUCH THAT
!   THE BIAS SUBTRACTED IS ZERO AT LS=275 AND MAXIMUM OF 0.911174 KM AT
!   LS=360, RESULTING IN DZDS = 0.0 AT LS=360.
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/CRMB/RMB(9), rmb0(9)
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      COMMON/DTMGDN/TGTYMD,TMGDN1,TMGDN2,REPDIF,XTMGN
      COMMON/MNSNSP/XMNSNS(7,2)
      COMMON/STARTT/ESSTRT,FSSTRT
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      DIMENSION XSNI(3)
      DIMENSION XSC(3),XSN(3),PP(9),              F(9)
      DIMENSION XNBDNS(9),XMW(9)
!     REAL MB , MBAR,MBOK ,NBDNS ,MW ,K ,LWFCTR
!          XMB,XMBAR,XMBOK,XNBDNS,XMW,XK,FCTLW
!     DATA BDAT/2430000.5D0/
      DATA A,AF,AVOGDR,ED,ES/2.279D8,60.D0,0.1660081676D-20,.1D0,.1D0/
      DATA AUOA/0.656300975D0/, PF/1.24D-9/, AVOG /6.022045D23/
      DATA F/.932D0,.01D0,.027D0,.016D0,.013D0,.002D0,5.0D-5,1.D-6,     &
     &        4.D-6/
      DATA XK,XMW/8314.39D0,44.01D0,16.D0,28.012D0,39.948D0,28.01D0,    &
     &           32.D0,4.D0,1.D0,2.D0/
      DATA XMBAR,NC,PERIOD/43.3D0,9,686.98D0/
      DATA P6,RA,RB,RC/610.D0,3394.67D0,3393.21D0,3376.78D0/
      DATA REFDAT,SIGMA,SOLDAY,U/2443951.D0,1.D0,365.25636D0,           &
     &                          .42828443D10/
      DATA SCLNGA/1.893682238D0/
      DATA DJ3935/2443935.D0/,D26P35/26.35D0/,D360/360.D0/,D250/250.D0/
      DATA DP0145/.0145D0/,DP001/.001D0/
      DATA EMO/.09338D0/
      DATA ZERO/0.D0/,HALF/.5D0/,ONE/1.D0/,THREE/3.D0/
      DATA ICOUNT/0/, SUMDAY/0.0/
! DEFINITION OF AU IN METERS
      DATA EAU /1.4959787866D+11/
! HARD CODE REFERENCE RADII AND MARS FLATTENING FOR OUTPUT
      DATA  DMARSAE/3397000.0D0/
      DATA  DMARSF/6.123050038D-03/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
!     IN THE MARCH, 1989 MODIFICATION THERE ARE NO CHANGES TO THE OLD
!     EXECUTABLE CODE FOR COMPUTING SPACECRAFT OR SUN COORDINATES,
!     LOCAL TIME, FRACTION OF HELIOCENTRIC PERIOD, OR RADIUS OF THE
!     REFERENCE SPHEROID.
!
! *** RECONSTITUTE THE JULIAN DATE
!
      DATE=TMGDN1+(DBLE(MJDSEC)+FSEC)/SECDAY
      DMJD=TMGDN1-2400000.50D0
!
! *** PULL QUANTITIES FOR SUN FRMO XMNSNS
!
      RSN=XMNSNS(4,2)
      XSNI(1)=RSN*XMNSNS(1,2)
      XSNI(2)=RSN*XMNSNS(2,2)
      XSNI(3)=RSN*XMNSNS(3,2)
      RSN=RSN*DP001
!
! *** CONVERT COORDINATE SYSTEM
!
      XSN(1)=RMB(1)*XSNI(1)+RMB(4)*XSNI(2)+RMB(7)*XSNI(3)
      XSN(2)=RMB(2)*XSNI(1)+RMB(5)*XSNI(2)+RMB(8)*XSNI(3)
      XSN(3)=RMB(3)*XSNI(1)+RMB(6)*XSNI(2)+RMB(9)*XSNI(3)
      XYSC=XSC(1)*XSC(1)+XSC(2)*XSC(2)
      XYSN=XSN(1)*XSN(1)+XSN(2)*XSN(2)
      RSC=SQRT(XYSC+XSC(3)*XSC(3))*DP001
      SCLONG=ATAN2(XSC(2),XSC(1))
      SCLAT=ATAN2(XSC(3),SQRT(XYSC))
      SNLONG=ATAN2(XSN(2),XSN(1))
      SNLAT=ATAN2(XSN(3),SQRT(XYSN))
!
! *** CALCULATE LOCAL TIME
!
      DL=SCLONG-SNLONG
      TIME=PI+DL
      IF(TIME.LT.ZERO) TIME=TIME+TWOPI
      IF(TIME.GE.TWOPI) TIME=TIME-TWOPI
      SCLONG=SCLONG+SCLNGA
!
! *** CALCULATE THE FRACTION OF HELIOCENTRIC PERIOD
!
      DT=DATE-REFDAT
      FP=MOD(DT,PERIOD)/PERIOD
      IF(FP.LT.ZERO) FP=FP+ONE
      DAYS=MOD(DATE-DJ3935,D26P35)
      IF(DAYS.LT.ZERO) DAYS=DAYS+D26P35
!
! *** CALCULATE RADIUS OF REF SPHEROID
!
      AOR=A/RSN
      AC=(AOR*(ONE-EMO*EMO)-ONE)/EMO
        IF(ABS(AC).GT.ONE) AC=INT(AC)
!CC      ALM=DARCOS(AC)/DEGRAD
      ALM=ACOS(AC)/DEGRAD
      IF(FP.GT.HALF) ALM=D360-ALM
      ALS = ALM + D250
      IF(ALS.GT.D360) ALS=ALS-D360
      CSCLAT=COS(SCLAT)
      CSCLNG=COS(SCLONG)
      SSCLAT=SIN(SCLAT)
      SSCLNG=SIN(SCLONG)
      RSQ=(CSCLAT*CSCLNG/RA)**2+(CSCLAT*SSCLNG/RB)**2+(SSCLAT/RC)**2
      R61MB=SQRT(ONE/RSQ)
!
! *** CORRECTION TO 6.1 MILLIBAR ELLIPSOID FOR SEASONAL GLOBAL PRESSURE
! *** VARITION
!
!  AM1 and AM2 REPARAMETERIZED WITH VARIABLES ALS1, ALS2 BY FGL ON 7/10/
!     AM1 = (DCOS(0.75D0*DEGRAD*(ALS-270.D0)))**2
!     AM2 = (DCOS((6.D0/7.D0)*DEGRAD*(ALS-45.D0)))**2
! THESE SIX LINES ADDED BY FGL ON 7/10/2000.
      ALS1=ALS
      IF(ALS.LE.30.0D0)ALS1=ALS+360.0D0
      ALS2=ALS
      IF(ALS.GE.300.0D0)ALS2=ALS-360.0D0
      AM1 = (COS(0.75D0*DEGRAD*(ALS1-270.D0)))**2
      AM2 = (COS((6.D0/7.D0)*DEGRAD*(ALS2-45.D0)))**2

      IF (ALS.GE.30.D0 .AND. ALS.LE.150.D0)  AM1 = ZERO
      IF (ALS.GE.150.D0 .AND. ALS.LE.300.D0) AM2 = ZERO
      PROPR = (1.D0 + 0.310D0*AM1 + 0.225D0*AM2) / 1.169D0
      T0 = 220.D0 *AOR
      DEL61 = (T0/19.51D0) * LOG(PROPR)
      R61MB = R61MB + DEL61
!
! *** CALCULATE THE HEIGHT OF THE F LAYER ABOVE THE 6.1 MILLIBAR LAYER
!
      ZF = 124.4 * AOR
!
! *** CALCULATE THE INCREMENT IN F DUE TO THE AVERAGE DUST STORMS
!
      DL1 = ALS - 205.D0
      DL2 = ALS - 275.D0
      IF (DL1.LT.ZERO) DL1 = -DL1
      IF (DL2.LT.ZERO) DL2 = -DL2
! MODIFICATIONS ADDED BY FGL 07/11/2000
! QA, QB SET TO ZERO PRIOR TO DUST STORM ONSET.
! CALCULATE BIAS TERM SO THERE IS NO DISCONTINUITY IN Z AT LS=360/LS=0.
      QA = EXP(-DL1/25.D0)
      QB = EXP(-DL2/25.D0)
      IF(ALS.LT.205.0D0)QA=0.0D0
      IF(ALS.LT.275.0D0)QB=0.0D0
      IF(ALS.LT.275.0D0)THEN
         DZBIAS=0.0D0
      ELSE
         DZBIAS=-(ALS-275.0D0)*0.911174D0/85.0D0
      ENDIF

      DZDS = 1.869D0 * (10.D0 * QA * (ONE-QA**4)                        &
     &                 +14.D0 * QB * (ONE-QB**4))
      ZF = ZF + DZDS + DZBIAS
!
! *** CALCULATE THE HEIGHT OF THE SPACECRAFT ABOVE THE 6.1 MILLIBAR
! *** SURFACE
!
      Z = RSC - R61MB
!
! *** CHECK THAT THE SPACECRAFT IS ABOVE THE HOMOSPHERE
!
      IF (Z.LT.ZF) THEN
        WRITE(IOUT6,8009) Z,ZF
 8009   FORMAT(' SPACECRAFT ALTITUDE TOO LOW FOR ATMOSPHERE MODEL',     &
     &         F10.5,1X,'<',1X,F10.5)
        STOP
      ENDIF
!
! *** COMPUTE 10.7 CM FLUX WITH SHORT PERIOD TERM IGNORED AND
! *** ADJUSTED TO THE DISTANCE OF MARS FROM THE SUN (TAKEN FROM
! *** OLD CODE)
!
!FLXAVG HAS BEEN SCALED BY EARTH ECCENTRICITY; THEREFORE
! IT MUST BE 'UNSCALED' BACK TO THE 1 AU VALUE.
      DMJD=DATE-2400000.50D0
      EARAU=EDIST(DMJD)
      FLXSCL=FLXAVG*EARAU*EARAU
      Y = (DATE-2442870.D0)*TWOPI / (11.04D0*SOLDAY)
      F107 = 75.D0 + 60.D0*(ONE-COS(Y+30.D0*DEGRAD*(ONE-COS(Y))))
      F107 = F107 * AOR**2 * AUOA**2
      DMAAU = EAU*DP001/RSN
      F107A = FLXSCL * (DMAAU*DMAAU)
! DEBUG PRINTOUT
!     IF(LSTINR)THEN
!       WRITE(98,6001)DMJD,FLXAVG,EARAU,FLXSCL,DMAAU,F107A,F107
!       WRITE(98,6001)DMJD,F107A
 6001  FORMAT(1X,F16.8,F10.4,F10.5,F9.3,F10.5,2F9.3)
!     ENDIF
!
! *** START HETEROSPHERE DENSITY CALCULATIONS.  COMPUTE AUXILLIARY
! *** QUANTITIES TF, L, TINF, AND YMYF; THEN TEMPERATURE AND PRESSURE.
!
      TF = 170.D0 * AOR
      AL = TF / 9.20D0
! REPLACE FOLLOWING LINE TO ALLOW STEWART ROUTINE TO USE
!  ACTUAL FLUX AVERAGED OVER THREE ROTATIONS OR 81 DAYS.
!      TINF = 4.11D0 * (11.D0 + F107)
       TINF = 4.11D0 * (11.D0 + F107A)

      RF = R61MB + ZF
      YMYF = ((Z-ZF)*RF) / (RF + (Z-ZF)) * 0.9935D0
      T = TINF - (TINF-TF)*EXP(-YMYF/AL)
!
! *** PRESSURE IS MOLECULE-DEPENDENT.  DENSITY WILL BE SUMMED OVER ALL
! *** MOLECULES.  FIRST COMPUTE AUXILLIARY QUANTITIES GF, GFOKT, AND
! *** TFOT.
!
       DENST = ZERO
       GF = U / (R61MB + ZF)**2
       GFOKT = 10.D0 * GF / (XK * TINF)
       TOTF = T / TF
       SPECLT = 1.219D-2 / T
!
! *** LOOP OVER DIFFERENT MOLECULAR SPECIES.    DIFFERENT ONES ARE
! *** HANDLED DIFFERENT WAYS
!
      DO 100 I = 1,9
        HINF = XMW(I) * GFOKT
        IF (I.LE.6 .AND. I.NE.2) THEN
          PP(I) = PF * F(I)
        ELSE IF (I.EQ.2) THEN
!CCC  TIME = AHR * DEGRAD ..AHR was not defined. It should be TIME
!CC....s.luo  6/10/98..
          PP(I) = PF * F(I) * (ONE-0.18D0*SIN(TIME)*CSCLAT)
        ELSE IF (I.EQ.7) THEN
          PP(I) = 3.3D-16*TINF
        ELSE IF (I.EQ.8) THEN
          IF (TINF.LE.330.D0) THEN
            PP(I) = 5.2D-16*TINF*EXP(-TINF/70.D0)
          ELSE
            PP(I) = 5.8D-18*SQRT(TINF)*EXP(1440.D0/TINF)               &
     &            / (ONE+ 1440.D0/TINF)
          ENDIF
        ELSE IF (I.EQ.9) THEN
          PP(I) = 2.4D-15
        ENDIF
        PP(I) = PP(I)*EXP(-YMYF*HINF - AL*HINF*LOG(TOTF))
        DENSTI = SPECLT * PP(I) * XMW(I)
        XNBDNS(I) = DENSTI * AVOG / XMW(I)
!
! *** CONVERT FROM G / CM**3 TO KG / M**3
!
        DENSTI = DENSTI * 1.D3
        DENST = DENST + DENSTI
  100 END DO
!
! debug printout for DTM  DRAG CALCULATIONS
      IF(LSTINR)THEN
        CALL REFELL2(XSC, DMARSAE, DMARSF,PHI, DLAM,DMH)
        if((DMH/1.0D+03).LT.425.0D0)THEN
        write(99,9876)DMJD,DENST,PHI/DEGRAD,TIME/DEGRAD,                &
     &                DMH/1.0D+3,ALS,F107A
        endif
 9876   format(f14.7,E16.8,2F12.6,1X,F8.2,F8.2,F9.3)
       ENDIF
      ICOUNT=ICOUNT+1
      SUMDAY=SUMDAY+DENST
      ICHECK=ICOUNT/1440.*1440.
      IF (ICHECK.NE.ICOUNT) GOTO 1001
      SUMAVG=SUMDAY/1440.
!
! THIS WRITE WAS COMMENTED OUT 1/4/93 - IT COMES THROUGH HERE WHEN
! THE STEWART 1987 ATMOSPHERIC DENSITY MODEL IS REQUESTED ON THE
! ATMDEN CARD
!
!      WRITE(6,1000) (XSC(I),I=1,3),MJDSEC,SUMAVG
      SUMDAY=0.0
      GOTO 1001
 1000 FORMAT(1X,3(E15.5,1X),I15,1X,E15.5)
!
 1001 CONTINUE
      RETURN
      END
