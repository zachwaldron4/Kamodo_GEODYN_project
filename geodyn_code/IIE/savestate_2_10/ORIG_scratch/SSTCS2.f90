!$SSTCS2
      SUBROUTINE SSTCS2(MJDSEC,FSEC,ISSTNM,PARMV,C,S,SSTFRQ,            &
     &                  TDIFF,CCOSWT,CSINWT,SCOSWT,SSINWT)
!*********1*********2*********3*********4*********5*********6*********7
! SSTCS2            08/09/89            0000.0    PGMR - A. MARSHALL
!
! FUNCTION: INITIALIZES POINTERS AND ARRAYS FOR TIME DEPENDENT
!           SEA SURFACE TOPOGRAPHY C&S COEFFICIENTS.
!           COMPUTES C&S TERMS BASED ON:
!            C=CBAR+CDOT*T+SUMMATION(CA*SIN(W*T)+CB*COS(W*T))
!            S=SBAR+SDOT*T+SUMMATION(SA*SIN(W*T)+SB*COS(W*T))
!           CALLED ONCE FOR EACH ALTIMETRIC MEASUREMENT.
!
! I/O PARAMETERS:
!
! NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
! ------  ---  ---   ------------------------------------------------
! MJDSEC   I         MODIFIED JULIAN DAY SECONDS AT MEAS. BLOCK START
! FSEC     I         OBS. TIME IN FRACTIONAL SECONDS SINCE BEGINNING
!                    OF BLOCK
! ISSTNM   I         INDEX ARRAY FOR NOFFSETS OF SEA SURFACE
!                    TOPOGRAPHY
! PARMV    I         PARAMETERS ARRAY PASSED FROM IIS
! C        O         C SEA SURFACE TOPOGRAPHY COEFFICIENTS
! S        O         S SEA SURFACE TOPOGRAPHY COEFFICIENTS
! SSTFRQ   I         FREQUENCY FOR PERIODIC SST TERMS
! TDIFF    O         OBS TIME MINUS SST EPOCH TIME
! CCOSWT   O         COS(SSTFRQ*TDIFF) FOR C COEFFICIENTS
! CSINWT   O         SIN(SSTFRQ*TDIFF) FOR C COEFFICIENTS
! SCOSWT   O         COS(SSTFRQ*TDIFF) FOR S COEFFICIENTS
! SSINWT   O         SIN(SSTFRQ*TDIFF) FOR S COEFFICIENTS
!
! COMMENTS: ISSTNM IS STORED IN THE FOLLOWING ORDER
!            CBAR,CDOT,CAPRD,CBPRD,SBAR,SDOT,SAPRD,SBPRD
!
!*********1*********2*********3*********4*********5*********6*********7
      IMPLICIT DOUBLE PRECISION(A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/GEOMCS/MDEGEO,MORGEO,NPGEOM,NXGEOM
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/SSTCOM/NCBAR, NSBAR ,NCDOT ,NSDOT ,NCAPRD,NCBPRD,NSAPRD,   &
     &              NSBPRD,NCBARA,NSBARA,NCDOTA,NSDOTA,NCAPDA,NCBPDA,   &
     &              NSAPDA,NSBPDA,ISSTEP,NXSST
      COMMON/SSTPTR/KCBAR, KSBAR ,KCDOT ,KSDOT ,KCAPRD,KCBPRD,KSAPRD,   &
     &              KSBPRD,KCBARA,KSBARA,KCDOTA,KSDOTA,KCAPDA,KCBPDA,   &
     &              KSAPDA,KSBPDA,NSSTOT,NSSTDP
!
      DIMENSION PARMV(1),C(1),S(1),SSTFRQ(1)
      DIMENSION ISSTNM(1)
      DIMENSION CCOSWT(1),CSINWT(1),SCOSWT(1),SSINWT(1)
!
      DATA ZERO/0.D0/
!
!**********************************************************************
! START OF EXECUTABLE CODE ********************************************
!**********************************************************************
!
! DETERMINE POINTER FOR START OF SST C COEFFICIENTS IN PARMV ARRAY
      ISTRTC=IPVAL(IXSSTC)
      ISTRTS=IPVAL(IXSSTS)
!
! COMPUTE DIFFERENCE BETWEEN OBSERVATION TIME AND SST EPOCH TIMES(SECS)
      TDIFF = DBLE(MJDSEC-ISSTEP)+FSEC
!
! INITIALIZE C & S ARRAYS
      DO 100 I=1,NPGEOM
      C(I+3)=ZERO
      S(I)=ZERO
  100 END DO
! INITALIZE DEGREE ZERO TERMS FOR C ARRAY
      DO 200 I=1,3
      C(I)=ZERO
  200 END DO
!
! STORE TIME INDEPENDENT (MEAN) C COEFFICIENTS
      DO 300 I=1,NCBAR
      C(ISSTNM(I)) = PARMV(ISTRTC+I-1)
  300 END DO
! STORE TIME INDEPENDENT (MEAN) S COEFFICIENTS
      DO 400 I=KSBAR,KSBAR+NSBAR-1
      S(ISSTNM(I)) = PARMV(ISTRTC+I-1)
  400 END DO
!
! STORE CDOT TERMS
      DO 500 I=KCDOT,KCDOT+NCDOT-1
      C(ISSTNM(I)) = C(ISSTNM(I))+TDIFF*PARMV(ISTRTC+I-1)
  500 END DO
!
! STORE CAPRD PERIODIC TERMS
      DO 600 I=KCAPRD,KCAPRD+NCAPRD-1
      CCOSWT(I-KCAPRD+1)=COS(MOD((SSTFRQ(I)*TDIFF),TWOPI))
      C(ISSTNM(I)) =C(ISSTNM(I))+PARMV(ISTRTC+I-1)*CCOSWT(I-KCAPRD+1)
  600 END DO
! STORE CBPRD PERIODIC TERMS
      DO 700 I=KCBPRD,KCBPRD+NCBPRD-1
      CSINWT(I-KCBPRD+1)=SIN(MOD((SSTFRQ(I)*TDIFF),TWOPI))
      C(ISSTNM(I)) =C(ISSTNM(I))+PARMV(ISTRTC+I-1)*CSINWT(I-KCBPRD+1)
  700 END DO
! STORE SDOT TERMS
      DO 800 I=KSDOT,KSDOT+NSDOT-1
      S(ISSTNM(I)) = S(ISSTNM(I))+TDIFF*PARMV(ISTRTC+I-1)
  800 END DO
! STORE SAPRD PERIODIC TERMS
      DO 900 I=KSAPRD,KSAPRD+NSAPRD-1
      SCOSWT(I-KSAPRD+1)=COS(MOD((SSTFRQ(I)*TDIFF),TWOPI))
      S(ISSTNM(I)) =S(ISSTNM(I))+PARMV(ISTRTC+I-1)*SCOSWT(I-KSAPRD+1)
  900 END DO
! STORE CBPRD PERIODIC TERMS
      DO 950 I=KSBPRD,KSBPRD+NSBPRD-1
      SSINWT(I-KSBPRD+1)=SIN(MOD((SSTFRQ(I)*TDIFF),TWOPI))
      S(ISSTNM(I)) =S(ISSTNM(I))+PARMV(ISTRTC+I-1)*SSINWT(I-KSBPRD+1)
  950 END DO
      RETURN
      END
