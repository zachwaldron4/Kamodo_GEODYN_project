!$RNGRAT
      SUBROUTINE RNGRAT(V2,V1,UR,R,RRATE,RELV,PRRPX,NM,LPARTL)
!********1*********2*********3*********4*********5*********6*********7**
! RNGRAT           83/04/01            8304.0    PGMR - TOM MARTIN
!
! FUNCTION:  COMPUTE THE RANGE RATE BETWEEN ONE POINT AND
!            ANOTHER
!            ALSO COMPUTE ANY NECESSARY PARTIALS OF RANGE RATE
!            W.R.T. POSITION AND VELOCITY
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   V2       I    A    VELOCITY VECTORS OF POINT #2
!   V1       I    A    VELOCITY VECTORS OF POINT #1
!   UR       I    A    UNIT VECTORS FROM POINT #1 TO POINT #2
!   R        I    A    SLANT RANGE FROM POINT #1 TO POINT #2
!   RRATE    O    A    RANGE RATE BETWEEN POINTS #1 AND #2
!   RELV     O    A    RELATIVE VELOCITY BETWEEN POINTS #1 AND #2
!   PRRPX    O    A    PARTIAL OF RANGE RATE W.R.T. S/C POSITION
!   NM       O    S    NUMBER OF MEASUREMENTS
!   LPARTL   I    S    .TRUE. IF PARTIALS TO BE COMPUTED
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      DIMENSION V2(MINTIM,3),V1(MINTIM,3),UR(NM,3),R(NM),RRATE(NM),     &
     &       RELV(NM,3),PRRPX(NM,3)
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! COMPUTE RELATIVE VELOCITY BETWEEN POINTS #1 AND #2
      DO 2000 I=1,3
      DO 1000 N=1,NM
      RELV(N,I)=V2(N,I)-V1(N,I)
 1000 END DO
 2000 END DO
! RANGE RATE IS SIMPLY THE DOT PRODUCT OF RELATIVE VELOCITY
!       WITH UNIT RELATIVE POSITION
      CALL DOTPRD(RELV,UR,RRATE,NM,NM,NM,3)
! RETURN IF PARTIALS NOT REQUIRED
      IF(.NOT.LPARTL) RETURN
! PRRPX IS THE PARTIAL OF RANGE RATE W.R.T. POSITION
      DO 7000 I=1,3
      DO 6000 N=1,NM
      PRRPX(N,I)=(RELV(N,I)-RRATE(N)*UR(N,I))/R(N)
 6000 END DO
 7000 END DO
! THE PARTIAL OF RANGE RATE W.R.T. VELOCITY IS THE SAME AS UR
!     AND WILL THEREFORE NOT BE RECOMPUTED
      RETURN
      END
