!$CHAINR
      SUBROUTINE CHAINR(PMPX,NM,I3,IPV,PXPF,NDIMX1,NDIMX2,NDIMX3,       &
     &    NEQN,N3,IND,IL,NPVECT,PMPF,NDIM1,NDIM2,LNPNM,LNEG,LSCALE,     &
     &    PSCALE,SCALE,LAVOID)
!********1*********2*********3*********4*********5*********6*********7**
! CHAIN            86/06/23            8609.0    PGMR - TOM MARTIN
!
! FUNCTION:  CHAIN THE PARTIALS OF THE MEASUREMENTS W.R.T. THE
!            S/C POS-VEL WITH THE PARTIALS OF THE S/C POS-VEL
!            W.R.T. THE ADJUSTED FORCE MODEL PARAMETERS
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   PMPX     I    A    PARTIALS OF MEASUREMENTS W.R.T. POS-VEL
!   NM       I    S    NUMBER OF MEASUREMENTS
!   I3       I    S    NUMBER OF SATELLITES CHAINED TIMES 3
!   IPV      I    S    1, IMPLIES POSITION ONLY
!                 A    2, IMPLIES VELOCITY ONLY
!   PXPF     I    S    PARTIALS OF POS-VEL W.R.T. ADJUSTED FORCE
!                      MODEL PARAMETERS
!   NDIMX1   I    S    FIRST  DIMENSION OF PXPF
!   NDIMX2   I    S    SECOND DIMENSION OF PXPF
!   NDIMX3   I    S    THIRD  DIMENSION OF PXPF
!   NEQN     I    S    NUMBER OF FORCE MODEL PARAMETERS
!   N3       I    S    NUMBER OF SATELLITES TIMES 3
!   IND      I    A    STARTING INDICES OF DESTINATIONS
!                      SUBSETS OF CHAINED PARTIALS
!   IL       I    A    LENGTH OF SUBSET
!   NPVECT   I         NUMBER OF PARTIAL DERIVATIVE SUBSETS
!   PMPF     O    A    PARTIALS OF MEASUREMENTS W.R.T. ADJUSTED
!                      FORCE MODEL PARAMETERS
!   NDIM1    I    S    FIRST  DIMENSION OF PMPF
!   NDIM2    I    S    SECOND DIMENSION OF PMPF
!   LNPNM    I    S    .FALSE. IF PMPF DIMENSIONED (NM,NADJST)
!                      .TRUE.  IF PMPF DIMENSIONED (NADJST,NM)
!   LNEG     I    S    .FALSE. IF CHAINED PARTIALS TO BE ADDED
!                      .TRUE.  IF CHAINED PARTIALS TO BE SUBTRACTED
!   LSCALE   I    S    .FALSE. IF SIMPLE SUMS TO BE USED
!                      .TRUE.  IF PARTIALS TO BE SCALED
!   PSCALE   I    S    SCALE FACTOR FOR PARTIALS
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/BIAOR/NAMBB,NCYCB,NAMBBH
      COMMON/CESTIM/MPARM,MAPARM,MAXDIM,MAXFMG,ICLINK,IPROCS,NXCEST
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
!
!.....FOR LNPNM .TRUE.
!     DIMENSION PMPX(NM,I3),PXPF(NEQN,N3,NM,2),
!    1   PMPF(NADJST,NM),IND(NPVECT),IL(NPVECT)
!
!.....FOR LNPNM .FALSE.
!     DIMENSION PMPX(NM,I3),PXPF(NM,NEQN,N3,2),
!    1   PMPF(NM,NADJST),IND(NPVECT),IL(NPVECT)
!
      DIMENSION PMPX(NM,I3),IND(NPVECT),IL(NPVECT)
      DIMENSION PXPF(NDIMX1,NDIMX2,NDIMX3,2),PMPF(NDIM1,NDIM2)
      DIMENSION PSCALE(NM),SCALE(NM)
      DIMENSION LAVOID(1)
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      ISGLB=IPVAL0(IXBISA)
      ISGLB1=IPVAL0(IXBISA)-1
      DO 10 IPVECT=1,NPVECT
      INDS=IND(IPVECT)
      IF(INDS.LT.ISGLB) INDS=IND(IPVECT)+NAMBB
      INDF=INDS+IL(IPVECT)-1
      DO 5 INDX=INDS,INDF
      LAVOID(INDX)=.FALSE.
    5 END DO
   10 END DO
      IF(LSCALE) GO TO 4000
      IF(LNPNM) GO TO 2000
      IF(LNEG) GO TO 1000
! THRU  800 LOOPS OVER THREE CARTESIAN COMPONENTS
      DO  800 I=1,I3
! THRU  600 LOOPS OVER FORCE MODEL PARAMETER SUBSETS
      N=0
      DO  600 IPVECT=1,NPVECT
      INDS=IND(IPVECT)
      IF(INDS.LT.ISGLB) INDS=IND(IPVECT)+NAMBB
      INDF=INDS+IL(IPVECT)-1
! THRU  400 LOOPS OVER FORCE MODEL PARAMETERS WITHIN SUBSETS
      DO  400 INDX=INDS,INDF
      N=N+1
! THRU  200 LOOPS OVER MEASUREMENTS
      DO  200 M=1,NM
      PMPF(M,INDX)=PMPF(M,INDX)+PMPX(M,I)*PXPF(M,N,I,IPV)
  200 END DO
  400 END DO
  600 END DO
  800 END DO
      RETURN
 1000 CONTINUE
! THRU 1800 LOOPS OVER THREE CARTESIAN COMPONENTS
      DO 1800 I=1,I3
! THRU 1600 LOOPS OVER FORCE MODEL PARAMETER SUBSETS
      N=0
      DO 1600 IPVECT=1,NPVECT
      INDS=IND(IPVECT)
      IF(INDS.LT.ISGLB) INDS=IND(IPVECT)+NAMBB
      INDF=INDS+IL(IPVECT)-1
! THRU 1400 LOOPS OVER FORCE MODEL PARAMETERS WITHIN SUBSETS
      DO 1400 INDX=INDS,INDF
      N=N+1
! THRU 1200 LOOPS OVER MEASUREMENTS
      DO 1200 M=1,NM
      PMPF(M,INDX)=PMPF(M,INDX)-PMPX(M,I)*PXPF(M,N,I,IPV)
 1200 END DO
 1400 END DO
 1600 END DO
 1800 END DO
      RETURN
 2000 CONTINUE
      IF(LNEG) GO TO 3000
! THRU 2800 LOOPS OVER THREE CARTESIAN COMPONENTS. PRESENTLY ONLY ONE
! SAT PER CALL(I3=3)
      DO 2800 I=1,I3
      DO 2600 M=1,NM
      NN=0
! THRU 2400 LOOPS OVER FORCE MODEL PARAMETERS WITHIN SUBSETS
      DO 2400 IPVECT=1,NPVECT
      INDS=IND(IPVECT)-1
      IF(INDS.LT.ISGLB1) INDS=IND(IPVECT)-1+NAMBB
      ILS=IL(IPVECT)
! THRU 2200 LOOPS OVER MEASUREMENTS
      DO 2200 N=1,ILS
      PMPF(INDS+N,M)=PMPF(INDS+N,M)+PMPX(M,I)*PXPF(NN+N,I,M,IPV)
 2200 END DO
      NN=NN+ILS
 2400 END DO
 2600 END DO
 2800 END DO
      RETURN
 3000 CONTINUE
! THRU 3800 LOOPS OVER THREE CARTESIAN COMPONENTS. PRESENTLY ONLY ONE
! SAT PER CALL(I3=3)
      DO 3800 I=1,I3
      DO 3600 M=1,NM
      NN=0
! THRU 3400 LOOPS OVER FORCE MODEL PARAMETERS WITHIN SUBSETS
      DO 3400 IPVECT=1,NPVECT
      INDS=IND(IPVECT)-1
      IF(INDS.LT.ISGLB1) INDS=IND(IPVECT)-1+NAMBB
      ILS=IL(IPVECT)
! THRU 3200 LOOPS OVER MEASUREMENTS
      DO 3200 N=1,ILS
      PMPF(INDS+N,M)=PMPF(INDS+N,M)-PMPX(M,I)*PXPF(NN+N,I,M,IPV)
 3200 END DO
      NN=NN+ILS
 3400 END DO
 3600 END DO
 3800 END DO
      RETURN
 4000 CONTINUE
      FACTX=1.D0
      IF(LNEG) FACTX=-1.D0
        DO 4010 I=1,NM
        SCALE(I)=PSCALE(I)*FACTX
 4010   CONTINUE
      IF(LNPNM) GO TO 6000
! THRU 4800 LOOPS OVER THREE CARTESIAN COMPONENTS
      DO 4800 I=1,I3
! THRU 4600 LOOPS OVER FORCE MODEL PARAMETER SUBSETS
      N=0
      DO 4600 IPVECT=1,NPVECT
      INDS=IND(IPVECT)
      IF(INDS.LT.ISGLB) INDS=IND(IPVECT)+NAMBB
      INDF=INDS+IL(IPVECT)-1
! THRU 4400 LOOPS OVER FORCE MODEL PARAMETERS WITHIN SUBSETS
      DO 4400 INDX=INDS,INDF
      N=N+1
! THRU 4200 LOOPS OVER MEASUREMENTS
      DO 4200 M=1,NM
      PMPF(M,INDX)=PMPF(M,INDX)+PMPX(M,I)*PXPF(M,N,I,IPV)*SCALE(M)
 4200 END DO
 4400 END DO
 4600 END DO
 4800 END DO
      RETURN
 6000 CONTINUE
! THRU 6800 LOOPS OVER THREE CARTESIAN COMPONENTS. PRESENTLY ONLY ONE
! SAT PER CALL(I3=3)
      DO 6800 I=1,I3
      DO 6600 M=1,NM
      NN=0
! THRU 6400 LOOPS OVER FORCE MODEL PARAMETERS WITHIN SUBSETS
      DO 6400 IPVECT=1,NPVECT
      INDS=IND(IPVECT)-1
      IF(INDS.LT.ISGLB1) INDS=IND(IPVECT)-1+NAMBB
      ILS=IL(IPVECT)
! THRU 6200 LOOPS OVER MEASUREMENTS
      DO 6200 N=1,ILS
      PMPF(INDS+N,M)=PMPF(INDS+N,M)+PMPX(M,I)*PXPF(NN+N,I,M,IPV)        &
     &                             *SCALE(M)
 6200 END DO
      NN=NN+ILS
 6400 END DO
 6600 END DO
 6800 END DO
      RETURN
      END
