      SUBROUTINE ST2LON(NM,NTOLFR,COSLAT,SINLAT,COSLON,SINLON,          &
     &                  COSANG,SINANG,PARM,XCRSTA)
!********1*********2*********3*********4*********5*********6*********7**
! FUNCTION: THIS ONE OF THE SUBROUTINES FOR THE STEP2 OF THE SET TIDAL
!           DISPLACEMENTS. IT CALLED TO ACCOUNT FOR THE FREQUENCY
!           DEPENDENCE OF THE LOVE NUMBERS. THE RANGE OF FREQUENCIES
!           IS LONG PERIOD
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   NM       I    S    NUMBER OF OBSERVATIONS IN THE BLOCK
!   NTOLFR   I    S    NUMBER OF FREQUENCIES
!   COSLAT   I    A    COSINES OF LATITUDES WHERE SET DESIRED
!   SINLAT   I    A    SINES OF LATITUDES WHERE SET DESIRED
!   COSLON   I    A    COSINES OF LONGITUDES WHERE SET DESIRED
!   SINLON   I    A    SINES OF LONGITUDES WHERE SET DESIRED
!   COSANG   I    A    COSINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   SINANG   I    A    SINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   PARM     I    A    DELTA R(IP), DELTA R(OP), DELTA T(IP),DELTA T(OP)
!                      FOR DIURNAL FREQUENCIES
!   XCRSTA   O    A    TIDAL DISPLACEMENTS (AT THE NM TIMES) DUE TO
!                      LONG PERIOD DEPENDENCE OF THE LOVE NUMBERS.
!
! COMMENTS:  THIS SUBROUTINE WAS MODIFIED FROM SUBROUTINE STEP2LON
!            FROM DEHANT.
!
!
!********1*********2*********3*********4*********5*********6*********7**
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      DIMENSION COSLAT(NM),SINLAT(NM),COSLON(NM),SINLON(NM)
      DIMENSION COSANG(NM,NTOLFR),SINANG(NM,NTOLFR)
      DIMENSION PARM(4,NTOLFR)
      DIMENSION XCRSTA(3,NM)
!
!
!
      DO 500 K=1,NM
!
      SC2=2.D0*SINLAT(K)*COSLAT(K)
      P2=1.5D0*SINLAT(K)*SINLAT(K)-0.5D0
      DR=0.D0
      DN=0.D0
!
! NOTE PARMS SCALED TO M (ORIGINAL CODE MM)
!
      DO 100 J=1,NTOLFR
      DR=DR +P2*(PARM(1,J)*COSANG(K,J)+PARM(2,J)*SINANG(K,J))
      DN=DN+SC2*(PARM(3,J)*COSANG(K,J)+PARM(4,J)*SINANG(K,J))
  100 END DO
      XCRSTA(1,K)=XCRSTA(1,K)+DR*COSLON(K)*COSLAT(K)                    &
     &                       -DN*SINLAT(K)*COSLON(K)
      XCRSTA(2,K)=XCRSTA(2,K)+DR*SINLON(K)*COSLAT(K)                    &
     &                       -DN*SINLAT(K)*SINLON(K)
      XCRSTA(3,K)=XCRSTA(3,K)+DR*SINLAT(K)+DN*COSLAT(K)
!
  500 END DO
      RETURN
      END
