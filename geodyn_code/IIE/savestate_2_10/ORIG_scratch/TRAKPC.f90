!$TRAKPC
      SUBROUTINE TRAKPC(XSM,VSM,UI,OFFSET,FSECRA,U,RA,DR,LASER,         &
     &                  LOFFAD,L1ST,DWRKDO)
!********1*********2*********3*********4*********5*********6*********7**
! TRAKPC           83/07/12            8307.0    PGMR - TOM MARTIN
!
!
! FUNCTION:  COMPUTE CORRECTIONS TO RANGE DUE TO LOCATION OF
!            TRACKING POINT NOT AT S/C C.G.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   XSM      I         EARTH CENTERED INERTIAL POSITION VECTORS
!   VSM      I         EARTH CENTERED INERTIAL VELOCITY VECTORS
!   UI       I         UNIT INERTIAL TOPOCENTRIC S/C POSITION
!   OFFSET   I         BODY CENTERED FIXED TRACKING POINT LOCATION
!   FSECRA
!   U             A    UNIT INERTIAL BODY CENTERED AXES DIR. COSINES
!   RA            A    OFFSET LOCATION MAPPED INTO INERTIAL COORD.
!   DR       O         CORRECTIONS TO RANGES
!   LASER    I         .T.-TRACKING OF LASER REFLECTOR RING
!                      .F.-TRACKING OF ANTENNA
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CBLOKI/MJDSBL,MTYPE ,NM    ,JSTATS,NPSEG ,JSATNO(3),ITSYS ,&
     &       NHEADB,NELEVS,ISTAEL(12),INDELV(3,4),JSTANO(3),ITARNO,     &
     &       KTARNO
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/COFFST/JXYZOF(2),JSADLY(2),JSTDLY(2),JXYZCG(2,2),          &
     &              MJDFCG(2,2),JXYOF2(2),JEXTOF(2),JXYOF3(2)
      DIMENSION XSM(MINTIM,3),VSM(MINTIM,3),UI(NM,3),OFFSET(3,2),       &
     &   U(NM,3,3),RA(NM,3),DR(NM),OFFST0(3),OFFDOT(3),FSECRA(NM)
      DIMENSION DWRKDO(NM,3)
      DATA ZERO/0.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      IF(MJDFCG(2,1).GT.MJDFCG(1,1)) GO TO 200
      DO 100 I=1,3
      OFFST0(I)=OFFSET(I,1)
      OFFDOT(I)=ZERO
  100 END DO
      GO TO 500
  200 CONTINUE
      DENOM =MJDFCG(2,1)-MJDFCG(1,1)
      DIFSEC=MJDSBL   -MJDFCG(1,1)
      DO 300 I=1,3
      OFFDOT(I)=(OFFSET(I,2)-OFFSET(I,1))/DENOM
  300 END DO
      DO 400 I=1,3
      OFFST0(I)=OFFSET(I,1)+OFFDOT(I)*DIFSEC
  400 END DO
  500 CONTINUE
! TRACKING POINT CORRECTION
!...MAGNITUDE OF VELOCITY VECTOR
      CALL DOTPRD(VSM,VSM,RA,MINTIM,MINTIM,NM,3)
      DO 1000 N=1,NM
      DR(N)=SQRT(RA(N,1))
 1000 END DO
!...UNIT VELOCITY(BODY CENTERED X) VECTOR
      DO 2000 I=1,3
      DO 1800 N=1,NM
      U(N,I,1)=VSM(N,I)/DR(N)
 1800 END DO
 2000 END DO
!...-POSITION CROSS UNIT VELOCITY GIVES BODY CENTERED Y
      CALL CROSSP(U,XSM,U(1,1,2),NM,MINTIM,NM)
!...MAGNITUDE OF BODY CENTERED Y
      CALL DOTPRD(U(1,1,2),U(1,1,2),RA,NM,NM,NM,3)
      DO 2800 N=1,NM
      DR(N)=SQRT(RA(N,1))
 2800 END DO
!...UNIT BODY CENTERED Y
      DO 4000 I=1,3
      DO 3800 N=1,NM
      U(N,I,2)=U(N,I,2)/DR(N)
 3800 END DO
 4000 END DO
!...UNIT B.C. X CROSS UNIT B.C. Y GIVES UNIT B.C. Z
      CALL CROSSP(U,U(1,1,2),U(1,1,3),NM,NM,NM)
      IF(.NOT.LASER) GO TO 5000
!...LASERS
!...UNIT B.C. Y NORMAL TO PLANE DEFINED BY UNIT B.C. Z AND
!                                          UNIT TOPOCENTRIC VECTORS
      CALL CROSSP(UI,U(1,1,3),U(1,1,2),NM,NM,NM)
!...MAGNITUDE B.C. Y
      CALL DOTPRD(U(1,1,2),U(1,1,2),RA,NM,NM,NM,3)
      DO 4400 N=1,NM
      DR(N)=SQRT(RA(N,1))
 4400 END DO
!...UNIT BODY CENTERED Y
      DO 4800 I=1,3
      DO 4800 N=1,NM
      U(N,I,2)=U(N,I,2)/DR(N)
 4800 CONTINUE
!...UNIT B.C. Y CROSS UNIT B.C. Z GIVES UNIT B.C. X
      CALL CROSSP(U(1,1,2),U(1,1,3),U,NM,NM,NM)
 5000 CONTINUE
!...MAP OFFSET INTO INERTIAL COORDINATES
      DO 7000 I=1,3
      DO 6200 N=1,NM
      DR(N)=OFFST0(1)+OFFDOT(1)*FSECRA(N)
 6200 END DO
      DO 6400 N=1,NM
      RA(N,I)=        DR(N)*U(N,I,1)
 6400 END DO
      DO 6800 J=2,3
      DO 6600 N=1,NM
      DR(N)=OFFST0(J)+OFFDOT(J)*FSECRA(N)
 6600 END DO
      DO 6800 N=1,NM
      RA(N,I)=RA(N,I)+DR(N)*U(N,I,J)
 6800 CONTINUE
 7000 END DO
!...RANGE CORRECTION IS DOT PRODUCT OF INERTIAL OFFSET AND
!                                UNIT INERTIAL TOPOCENTRIC VECTORS
      CALL DOTPRD(RA,UI,DR,NM,NM,NM,3)
      IF(.NOT.LOFFAD) RETURN
      IF(L1ST) THEN
        DO 8000 I=1,NM
        DWRKDO(I,1)=U(I,1,1)*UI(I,1)+U(I,2,1)*UI(I,2)+U(I,3,1)*UI(I,3)
        DWRKDO(I,2)=U(I,1,2)*UI(I,1)+U(I,2,2)*UI(I,2)+U(I,3,2)*UI(I,3)
        DWRKDO(I,3)=U(I,1,3)*UI(I,1)+U(I,2,3)*UI(I,2)+U(I,3,3)*UI(I,3)
 8000   CONTINUE
      ELSE
        DO 8500 I=1,NM
        DWRKDO(I,1)=-U(I,1,1)*UI(I,1)+U(I,2,1)*UI(I,2)+U(I,3,1)*UI(I,3)
        DWRKDO(I,2)=-U(I,1,2)*UI(I,1)+U(I,2,2)*UI(I,2)+U(I,3,2)*UI(I,3)
        DWRKDO(I,3)=-U(I,1,3)*UI(I,1)+U(I,2,3)*UI(I,2)+U(I,3,3)*UI(I,3)
 8500   CONTINUE
      ENDIF
      RETURN
      END
