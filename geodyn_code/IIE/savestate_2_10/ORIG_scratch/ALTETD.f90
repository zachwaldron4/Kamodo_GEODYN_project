!$ALTETD
      SUBROUTINE ALTETD(TRUEX,TRUEY,TRUEZ,HT,MJDSEC,FSEC,NM,SCRTCH,     &
     &                  CORR,LPART,PMPA,LNPNM,NDIM1,NDIM2,XY2,R,COSLAT, &
     &                  SINLAT,AA,II)
!********1*********2*********3*********4*********5*********6*********7**
! ALTETD           00/00/00            0000.0    PGMR - ?
!
! FUNCTION: COMPUTE CORRECTION TO ALTIMETRY MEASUREMENTS DUE
!           TO SOLID EARTH TIDES
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   TRUEX    I    A    TRUE OF DATE SATELLITE X COORDINATE
!   TRUEY    I    A    TRUE OF DATE SATELLITE Y COORDINATE
!   TRUEZ    I    A    TRUE OF DATE SATELLITE Z COORDINATE
!   HT       I    A    SATELLITE HT ABOVE ELLIPSOID
!   MJDSEC   I    S    INTEGER SECOND TIME SINCE GEODYN REFERENCE TIME T
!                      WHICH EVER MEASUREMENT IN BLOCK IS REFERENCED
!   FSEC     I    A    TIME SINCE MJDSEC
!   NM       I    S    NUMBER OF MEASUREMENT TIMES
!   SCRTCH   I    A    SCRATCH SPACE AT LEAST 14*NM IN LENGTH
!                      NOTE:THIS ROUTINE COULD USE EVEN MORE!
!   CORR     O    A    CORRECTION TO MEASUREMENT RESIDUAL
!                      DUE TO SOLID EARTH TIDES
!   LPART    I    S    TRUE IF MEAS PARTIAL WRT H2 IS NEEDED
!   PMPA     O    A    PARTIALS OF MEASUREMENT WRT H2
!   LNPNM    I    S    .TRUE. IF NP IS THE FIRST DIMENSION
!   NDIM1    I    S    FIRST DIMENSION OF PMPA
!   NDIM2    I    S    SECOND DIMENSION OF PMPA
!   XY2      I    A    (TRUEX**2+TRUEY**2)**.5
!   R        I    A    (TRUEX**2+TRUEY**2+TRUEZ**2)**.5
!   COSLAT   I    A    COSINE OF LAT
!   SINLAT   I    A    SINE   OF LAT
!
! COMMENTS:  CORRECTION IS COMPUTED NOT AT TRUE SUBSATELLITE
!            POINT BUT AT A POINT ALONG THE STAELLITE RADIUS
!            VECTOR WHICH IS HT DISTANCE LOWER THAN THE SAT.
!            FOR A SATELLITE AT 1 EARTH RADIUS,THIS CAUSES A MAX
!            ERROR IN THE RADIUS OF 18M AND A MAX ERROR IN THE
!            ANGULAR ORIENTATION OF THE RADIUS VECTOR OF .1DEG
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION TRUEX(NM),TRUEY(NM),TRUEZ(NM),HT(NM),FSEC(NM)
      DIMENSION SCRTCH(NM,14),CORR(NM),PMPA(NDIM1,NDIM2)
      DIMENSION POS(4,2,2),FSP(2),SCRP(2),EF(2),ZF(2),TF(2),RPMAT(3,3)
      DIMENSION XY2(NM),R(NM),COSLAT(NM),SINLAT(NM)
      DIMENSION AA(1),II(1)
      COMMON/ADJGEO/LADATT,LADEBS,LADABS,LADGBS,LADLOV,LADPOL
      COMMON/CBDSTA/BDSTAT(7,999),XBDSTA
      COMMON/CEARTH/AEG,AEGSQ,FGINV,FG,EGSQ,EGSQP1,C1,C2,FOURC1,TWOC2,  &
     &              XH2,XL2,WREF,RMEAN,REFC20,REFC40,REFC60,BEG,CBLTC,  &
     &              WAE2,GAMMAA,FSTAR,F4
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      DATA ZERO/0.D0/,ONE/1.D0/,THREE/3.D0/
      DATA XP3/.3333333333333333D0/,X1P5/1.5D0/
      DATA HK1WHR/.5203D0/,CDHK1/.2849D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! GET ELEMENTS OF PRECESSION SINCE 1950 FOR FIRST & LAST TIMES
      FSP(1)=FSEC(1)
      FSP(2)=FSEC(NM)
      CALL PRECSS(MJDSEC,FSP,EF,ZF,TF,SCRP,2)
!
! GET MEAN UNIT VECTOR & RADIUS OF MOON&SUN AT FIRST AND LAST TIMES
      DO 10 IT=1,2
!
! FORM PRECESSION MATRIX
      SINE=SIN(EF(IT))
      COSE=SQRT(ONE-SINE*SINE)
      SINZ=SIN(ZF(IT))
      COSZ=SQRT(ONE-SINZ*SINZ)
      SINT=SIN(TF(IT))
      COST=SQRT(ONE-SINT*SINT)
      RPMAT(1,1)=COSZ*COST*COSE-SINZ*SINE
      RPMAT(2,1)=SINZ*COST*COSE+COSZ*SINE
      RPMAT(3,1)=SINT*COSE
      RPMAT(1,2)=-COSZ*COST*SINE-SINZ*COSE
      RPMAT(2,2)=-SINZ*COST*SINE+COSZ*COSE
      RPMAT(3,2)=-SINT*SINE
      RPMAT(1,3)=-COSZ*SINT
      RPMAT(2,3)=-SINZ*SINT
      RPMAT(3,3)=COST
!
! GET UNIT VECTOR AND RADIUS OF MOON AND SUN
      CALL PLANPO(MJDSEC,FSP(IT),.FALSE.,.FALSE.,AA,II)
!
! MOON MEAN
      POS(1,IT,1)=RPMAT(1,1)*BDSTAT(1,11)+RPMAT(1,2)*BDSTAT(2,11)       &
     &           +RPMAT(1,3)*BDSTAT(3,11)
      POS(2,IT,1)=RPMAT(2,1)*BDSTAT(1,11)+RPMAT(2,2)*BDSTAT(2,11)       &
     &           +RPMAT(2,3)*BDSTAT(3,11)
      POS(3,IT,1)=RPMAT(3,1)*BDSTAT(1,11)+RPMAT(3,2)*BDSTAT(2,11)       &
     &           +RPMAT(3,3)*BDSTAT(3,11)
!
! SUN MEAN
      POS(1,IT,2)=RPMAT(1,1)*BDSTAT(1,8)+RPMAT(1,2)*BDSTAT(2,8)         &
     &           +RPMAT(1,3)*BDSTAT(3,8)
      POS(2,IT,2)=RPMAT(2,1)*BDSTAT(1,8)+RPMAT(2,2)*BDSTAT(2,8)         &
     &           +RPMAT(2,3)*BDSTAT(3,8)
      POS(3,IT,2)=RPMAT(3,1)*BDSTAT(1,8)+RPMAT(3,2)*BDSTAT(2,8)         &
     &           +RPMAT(3,3)*BDSTAT(3,8)
!
! STORE UNIT VECTOR AND NORMALIZE
      POS(4,IT,1)=POS(1,IT,1)*POS(1,IT,1)+POS(2,IT,1)*POS(2,IT,1)       &
     &           +POS(3,IT,1)*POS(3,IT,1)
      POS(4,IT,2)=POS(1,IT,2)*POS(1,IT,2)+POS(2,IT,2)*POS(2,IT,2)       &
     &           +POS(3,IT,2)*POS(3,IT,2)
      POS(4,IT,1)=SQRT(POS(4,IT,1))
      POS(4,IT,2)=SQRT(POS(4,IT,2))
      POS(1,IT,1)=POS(1,IT,1)/POS(4,IT,1)
      POS(2,IT,1)=POS(2,IT,1)/POS(4,IT,1)
      POS(3,IT,1)=POS(3,IT,1)/POS(4,IT,1)
      POS(1,IT,2)=POS(1,IT,2)/POS(4,IT,2)
      POS(2,IT,2)=POS(2,IT,2)/POS(4,IT,2)
      POS(3,IT,2)=POS(3,IT,2)/POS(4,IT,2)
   10 END DO
! APPROX SUBSATELLITE RADIUS
      DO 30 I=1,NM
      SCRTCH(I,1)=R(I)-HT(I)
   30 END DO
! SCALE FACTOR
      DO 35 I=1,NM
      SCRTCH(I,5)=ONE/R(I)
   35 END DO
! APPROXIMATE SUBSATELLITE UNIT VECTOR
      DO 40 I=1,NM
      SCRTCH(I,2)=TRUEX(I)*SCRTCH(I,5)
      SCRTCH(I,3)=TRUEY(I)*SCRTCH(I,5)
      SCRTCH(I,4)=TRUEZ(I)*SCRTCH(I,5)
   40 END DO
!  SUBSATELLITE RADIUS 4TH
      DO 45 I=1,NM
      SCRTCH(I,5)=SCRTCH(I,1)*SCRTCH(I,1)
      SCRTCH(I,1)=SCRTCH(I,5)*SCRTCH(I,5)
   45 END DO
!
! GET INTERPOLATION COEFFCIENTS FOR PLANET POSITIONS
      TDIF=FSEC(NM)-FSEC(1)
      TTIME=ZERO
      IF(NM.GT.1) TTIME=ONE/TDIF
      DO 50 I=1,NM
      SCRTCH(I,5)=(FSEC(I)-FSEC(1))*TTIME
   50 END DO
!
! ZERO OUT SPACE THAT WILL BE SUMMED INTO OVER MOON AND SUN
      DO 60 I=1,NM
      SCRTCH(I,6)=ZERO
   60 END DO
!
      DO 1000 IP=1,2
!
! INTERPOLATE FOR THESE FCTOR & RADIUS OF SUN AND MOON AT OTHER TIMES
      POSDIF=POS(1,2,IP)-POS(1,1,IP)
      DO 110 I=1,NM
      SCRTCH(I,7)=POS(1,1,IP)+SCRTCH(I,5)*POSDIF
  110 END DO
      POSDIF=POS(2,2,IP)-POS(2,1,IP)
      DO 120 I=1,NM
      SCRTCH(I,8)=POS(2,1,IP)+SCRTCH(I,5)*POSDIF
  120 END DO
      POSDIF=POS(3,2,IP)-POS(3,1,IP)
      DO 130 I=1,NM
      SCRTCH(I,9)=POS(3,1,IP)+SCRTCH(I,5)*POSDIF
  130 END DO
      POSDIF=POS(4,2,IP)-POS(4,1,IP)
      DO 140 I=1,NM
      SCRTCH(I,10)=POS(4,1,IP)+SCRTCH(I,5)*POSDIF
  140 END DO
!
! GET RLANET RADIUS CUBED
      DO 160 I=1,NM
      SCRTCH(I,11)=SCRTCH(I,10)*SCRTCH(I,10)*SCRTCH(I,10)
  160 END DO
      DO 170 I=1,NM
      SCRTCH(I,12)=RATIOM(IP)*SCRTCH(I,1)/SCRTCH(I,11)
  170 END DO
!
! GET COS OF ANGLE BETWEEN PLANET AND SUBSATELLITE
      DO 180 I=1,NM
      SCRTCH(I,13)=SCRTCH(I,2)*SCRTCH(I,7)                              &
     &            +SCRTCH(I,3)*SCRTCH(I,8)                              &
     &            +SCRTCH(I,4)*SCRTCH(I,9)
  180 END DO
!
! FILL PARTH
      DO 200 I=1,NM
      SCRTCH(I,6)=SCRTCH(I,6)                                           &
     &           +(SCRTCH(I,13)*SCRTCH(I,13)-XP3)*SCRTCH(I,12)
  200 END DO
 1000 END DO
!
!
! K1 CORRECTION (MERIT STANDARDS) ON THE RADIAL DIRECTION
      DO 1060 I=1,NM
!  SCRTCH1 WILL CONTAIN THE  K1 CORRECTION THAT DEPENDS ON H2
      SCRTCH(I,1)=CDHK1*TRUEY(I)*SINLAT(I)*COSLAT(I)/XY2(I)
      SCRTCH(I,6)=X1P5*SCRTCH(I,6)-SCRTCH(I,1)
!  SCRTCH1 WILL CONTAIN K1 CORRECTION THAD DOES NOT DEPEND ON H2
      SCRTCH(I,1)=SCRTCH(I,1)*HK1WHR
 1060 END DO
!
!  ET CORRECTION
      DO 1070 I=1,NM
      CORR(I)=SCRTCH(I,1)+SCRTCH(I,6)*XH2
 1070 END DO
      IF(.NOT.LPART) RETURN
      IF(LNPNM) GO TO 3500
      INDEXH=IPVAL0(IXH2LV)
      DO 3000 I=1,NM
      PMPA(I,INDEXH)=-SCRTCH(I,6)
 3000 END DO
      RETURN
 3500  CONTINUE
      INDEXH=IPVAL0(IXH2LV)
      DO 4000 I=1,NM
      PMPA(INDEXH,I)=-SCRTCH(I,6)
 4000 END DO
      RETURN
      END
