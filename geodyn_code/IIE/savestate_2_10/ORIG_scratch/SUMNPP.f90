!$SUMNPP
      SUBROUTINE SUMNPP(PMPP,RESID,WT,NRMTOT,NM,ATPA,ATPL,SCRTCH)
!********1*********2*********3*********4*********5*********6*********7**
! SUMNPP           85/10/08            0000.0    PGMR - TOM MARTI
!                  84/02/27                      PGMR - D. ROWLAND
!
! FUNCTION:  ACCUMULATE PARTIALS & RESIDUALS INTO 1ST PARTITION
!            NORMAL MATRIX AND RIGHT HAND SIDE OF THE NORMAL EQ.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   PMPP     I         FULL ARRAY OF PARTIALS(INCLUDING ZEROS WHERE
!                      NECCESSARY) FOR EACH MEASUREMENT.SOMETIMES A
!                      INCLUDES ONLY ARC,SOMTIMES ARC&COMMON.
!   RESID    I         RESIDUAL ARRAY
!   WT       I         MEASUREMENT WEIGHT ARRAY
!   NRMTOT   I         MAXIMUM DIMENSION OF NORMAL MATRIX
!   NM       I         NUMBER OF MEASUREMENTS
!   ATPA     O         NORMAL MATRIX
!   ATPL     O         RIGHT HAND SIDE OF NORMAL EQUATIONS
!   SCRTCH        A    SCRATCH ARRAY
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CPARTI/MAXLP ,KORSIZ,MAXWRK,                               &
     &              IUNTP1,IUNTP2,IUNTPF,ISIZPF,IRECPF,NRECPF,          &
     &              NPDONE,NP1   ,NP2   ,NRECND,NPDIM ,NXPARI
      COMMON/CGLBAR/LGDRAG,NXGLAR
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
!
      DIMENSION PMPP(NRMTOT,NM),RESID(NM),WT(NM),ATPA(1),ATPL(NRMTOT),  &
     &   SCRTCH(NRMTOT)
!
      DATA ZERO/0.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! IF GLOBAL DRAG WAS REQUESTED PUT THE DRAG PARTIALS AT THE END OF
! THE ARC PARTIALS.  NOTE: IF THIS ROUTINE IS EXECUTED THEN
! THIS IS THE LAST INNER ITERATION OF AN EMAT RUN.
      IF(LGDRAG) THEN
      IF(NPVAL0(IXDRAG).EQ.0) GOTO 80
! SET INDEX EQUAL TO THE POSITION OF THE FIRST DRAG PARAMETER
      INDEX=IPVAL0(IXDRAG)
      DO 70 INM=1,NM
! RUN THROUGH ALL OF THE DRAG PARAMETERS FOR A PARTICULAR MEASUREMENT
      DO 60 I=1,NPVAL0(IXDRAG)
! SET DRAG PARTIAL TO TEMP SPACE AND SHIFT OVER THE REST OF THE ARC
! PARTIALS.  THIS IS DONE FOR EACH DRAG PARAMETER.
      TEMP=PMPP(INDEX,INM)
      DO 50 J=INDEX,NPVAL0(IXARC)-1
      PMPP(J,INM)=PMPP(J+1,INM)
   50 END DO
! PUT TEMP (ARC DRAG PARTIAL) AT END OF ARC PARTIALS
      PMPP(NPVAL0(IXARC),INM)=TEMP
   60 END DO
   70 END DO
      ENDIF
   80 CONTINUE
!
      DO 1000 IOBS=1,NM
      IF(WT(IOBS).LE.ZERO) GO TO 1000
!     SCRATCH IS ATP (IN ATPA)
      DO 100 I=1,NRMTOT
      SCRTCH(I)=WT(IOBS)*PMPP(I,IOBS)
  100 END DO
!$OMP PARALLEL DO DEFAULT (SHARED) PRIVATE(ISROW)
      DO 500 I=1,NP2
      ISROW=(I-1)*NRMTOT-((I-1)*(I-2))/2
!!!!!!!!!!!!!!!!DIR$ IVDEP
      DO 200 II=1,NRMTOT-I+1
      ATPA(ISROW+II)=ATPA(ISROW+II)+SCRTCH(I)*PMPP(I-1+II,IOBS)
  200 END DO
  500 END DO
!$OMP END PARALLEL DO
      DO 600 I=1,NRMTOT
      ATPL(I)=ATPL(I)+RESID(IOBS)*SCRTCH(I)
  600 END DO
      PMPP(NP2,IOBS)=WT(IOBS)
      CALL PARWRT(PMPP(NP2,IOBS))
 1000 END DO
      RETURN
      END
