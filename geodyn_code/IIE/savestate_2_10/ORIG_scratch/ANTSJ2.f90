!$ANTSJ2
      SUBROUTINE ANTSJ2(ROT,XV,YV,ZV,PHASEQ,ISEQ,PHC)
!********1*********2*********3*********4*********5*********6*********7**
! ANTSAC                                         PGMR - D. ROWLANDS
!
!
! FUNCTION:  COMPUTE THE ANTENNA MAP CORRECTION AT A USER SATELLITE
!            NOTE THIS SUBROUTINE USES THE HARD CODED PHASE CENTER
!            VARIATION MAP
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   ROT      I    A    MATRIX FROM SPACECRAFT BF TO TOR (ROUTINE USES
!                      TRANSPOSE)
!   XV       I    S    X COMP OF TOR UNIT VECTOR FROM U SAT TO GPS SAT
!   YV       I    S    Y COMP OF TOR UNIT VECTOR FROM U SAT TO GPS SAT
!   ZV       I    S    Z COMP OF TOR UNIT VECTOR FROM U SAT TO GPS SAT
!                      STATION TO SATELLITE
!   PHASEC   I    A    ANTENNA CORRECTION ARRAY. CORRECTIONS SHOULD
!                      BE ADDED TO COMPUTED OR
!                         SUBTRACTED FROM OBSERVED
!   ISEQ     I    S    SEQUENCE NUMBER (1-4) OF A GPS MEASUREMENT
!                      (WHICH PART OF THE DIFFERENCED MEASUREMENT IS
!                       THIS CALL BEING MADE FOR)
!   PHC      O    S    CORRECTION FROM PHASEC
!
!
! COMMENTS: SEE VOLUME 3 (ANTPHC CARD) FOR STRUCTURE OF PHASEC ARRAY
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      COMMON/J2AZEL/AZELJ2(36,91)
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/PYUA  /XGPSTB,WVL3,VHIGH(3,3,4),VLOW(3,3,4),PYUSQ(5),      &
     &              VISATH(4),VISATL(4),XANTSL(4),XCUTSL(4),XANTSH(4)
      DIMENSION PHASEQ(72,37)
      DIMENSION PHASEC(36,91)
      DIMENSION ROT(9)
!
      EQUIVALENCE(PHASEC(1,1),AZELJ2(1,1))
!
      XXX=ROT(1)*XV+ROT(2)*YV+ROT(3)*ZV
      YYY=ROT(4)*XV+ROT(5)*YV+ROT(6)*ZV
      ZZZ=ROT(7)*XV+ROT(8)*YV+ROT(9)*ZV
!
      XX=VLOW(1,1,ISEQ)*XXX+VLOW(2,1,ISEQ)*YYY+VLOW(3,1,ISEQ)*ZZZ
      YY=VLOW(1,2,ISEQ)*XXX+VLOW(2,2,ISEQ)*YYY+VLOW(3,2,ISEQ)*ZZZ
      ZZ=VLOW(1,3,ISEQ)*XXX+VLOW(2,3,ISEQ)*YYY+VLOW(3,3,ISEQ)*ZZZ
!
!         print*, 'ddd in antsj2'
      ZEN=ACOS(ZZ)/DEGRAD
      AZ=ATAN2(YY,XX)/DEGRAD
      AZ=AZ+3600.D0
      AZ=MOD(AZ,360.D0)
      AZ=360.D0-AZ
      AZ=MOD(AZ,360.D0)
      J=ZEN/1.D0
      J=J+1
      IF(J.GT.90) THEN
        PHC=0.D0
        RETURN
      ENDIF
      J1=J
      J2=J+1
      I=AZ/10.D0
      I=I+1
      I1=I
      I2=I+1
      IF(I.EQ.36) I2=1
      CORU1=PHASEC(I1,J1)
      CORU2=PHASEC(I2,J1)
      AZ1=10.D0*DBLE(I1-1)
      CORRU=CORU1+(CORU2-CORU1)*(AZ-AZ1)/10.D0
      CORL1=PHASEC(I1,J2)
      CORL2=PHASEC(I2,J2)
      CORRL=CORL1+(CORL2-CORL1)*(AZ-AZ1)/10.D0
      ZN1=1.D0*DBLE(J1-1)
      PHC=CORRU+(CORRL-CORRU)*(ZEN-ZN1)/1.D0
!         print*,'ddd in antsj2', phc
      RETURN
      END
