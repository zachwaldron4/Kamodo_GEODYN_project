      SUBROUTINE ORBANG(SATPOS, SATVEL, SUNPOS, BETAP_DEG, &
              YAW_DEG, SOMEGA_DEG)
!***********************************************************************
!  ROUTINE NAME:   ORBANG   DATE: 2017-05-10    PGMR: JOSEPH B. NICHOLAS
!
!   FUNCTION - TO COMPUTE THE ORBIT ANGLES OF A SATELLITE
!
!  I/O PARAMETERS:
!
!   NAME    A/S    I/O   DESCRIPTION OF I/O PARAMETERS IN ARGUMENT LIST
!   -----  ------ -----  -----------------------------------------------
!   SATPOS     A      I  SPACECRAFT POSITION IN J2000 (M)
!   SATVEL     A      I  SPACECRAFT VELOCITY IN J2000 (M/S)
!   SUNPOS     A      I  SUN POSITION IN J2000 (M)
!   BETAP_DEG  S      O  SPACECRAFT BETA ANGLE IN DEGREES
!   YAW_DEG    S      O
!   SOMEGA_DEG S      O
!***********************************************************************

      IMPLICIT NONE

      DOUBLE PRECISION, PARAMETER :: PI = 4.D0 * ATAN(1.D0)
      DOUBLE PRECISION, PARAMETER :: RAD2DEG = 180.D0 / PI

      DOUBLE PRECISION, INTENT(IN) :: SATPOS(3), SATVEL(3)
      DOUBLE PRECISION, INTENT(IN) :: SUNPOS(3)
      DOUBLE PRECISION, INTENT(OUT) :: BETAP_DEG, YAW_DEG, SOMEGA_DEG

      INTEGER :: I
      DOUBLE PRECISION :: HELIOS(3), SATN(3), HELION(3), ACROSS(3)
      DOUBLE PRECISION :: ACROSSN(3), ALONGN(3), SUNX(3), SUNBETAV(3)
      DOUBLE PRECISION :: SBPERP(3)
      DOUBLE PRECISION :: SATPOSMOD, HELIOSMOD, SACROSS, COSALPHA0, YAWN
      DOUBLE PRECISION :: BETAP, SUNXMOD, COSMU, SGNMU, SOMEGA, SINMU

      HELIOS(1) = SUNPOS(1)
      HELIOS(2) = SUNPOS(2)
      HELIOS(3) = SUNPOS(3)

      CALL VNORM(SATPOS(1:3), SATN, SATPOSMOD)
      CALL VNORM(HELIOS, HELION, HELIOSMOD)
      CALL VPROD(SATN, SATVEL(1:3), ACROSS)
      CALL VNORM(ACROSS, ACROSSN, SACROSS)
      CALL VPROD(ACROSSN, SATN, ALONGN)
      CALL SPROD(HELION, ACROSSN, COSALPHA0)

      BETAP = PI/2.0D0 - ACOS(COSALPHA0)
      BETAP_DEG = RAD2DEG * BETAP

      DO I = 1,3
          SUNX(I) = -(HELION(I) - COSALPHA0*ACROSSN(I))
      ENDDO
      CALL VNORM(SUNX, SUNBETAV, SUNXMOD)
      CALL SPROD(SATN, SUNBETAV, COSMU)
      CALL VPROD(SUNBETAV, ACROSSN, SBPERP)
      CALL SPROD(SATN, SBPERP, SGNMU)
      SOMEGA = ACOS(COSMU) * (-SIGN(1.D0,SGNMU))
      SOMEGA_DEG = RAD2DEG * SOMEGA
      SINMU = SIN(SOMEGA)

      YAWN = ATAN2(-TAN(BETAP), SINMU)
      YAW_DEG = RAD2DEG * YAWN

      END SUBROUTINE
