!$GENACC
      SUBROUTINE GENACC(MJDSEC,FSEC,XS,DXDD,PXDDT,VMATRX,LORBIT,        &
     &                  NEQN,ITACC,ACC,STACC,LAJGP)
!********1*********2*********3*********4*********5*********6*********7**
! GENACC           83/11/16            8311.0    PGMR - D. ROWLANDS
!
! FUNCTION:  (1) CALCULATE ACCELERATION DUE TO GENERAL ACCELERATION
!            (2) IF AN ADJUSTMENT IS BEING DONE,CALCULATE THE
!                CONTRIBUTION FROM GENERAL ACCELERATION THE VMATRX
!            (3) IF THESE ACCELERATIONS ARE BEING ADJUSTED,CALCULATE
!                THEIR EXPLICIT PARTIALS.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I    S    CURRENT INTEGRATION TIME (ET SECONDS)
!                      SINCE GEODYN REFERENCE TIME
!   FSEC     I    S    REMAINING FRACTIONAL SECONDS
!   XS       I    A    TRUE OF DATE STATEVECTOR OF SATELLITE
!   DXDD     O    A    TRUE OF DATE GENERAL ACCELERATION
!                      CONTRIBUTION TO ACC
!   PXDDT    O    A    EXPLICIT PARTIALS OF FORCE MODEL PARAMETRS
!                      IN TRUE OF REFERENCE
!   VMATRX  I/O   A    VMATRX IN TRUE OF INTEGRATION WITHOUT
!                      DRAG CONTRIBUTION AS INPUT
!                      VMATRX IN TRUE OF INTEGRATION WITH
!                      GENERAL ACCELERATION CONTRIBUTION AS OUTPUT
!   LORBIT   I    S    .TRUE. IF ORBIT GENERATOR RUN
!   NEQN     I    S    NUMBER OF FORCE MODEL PARAMETERS
!   ITACC    I    S    TYPE ACCELERATION (1 ALONG TRACK,2 RADIAL,
!                      3 CROSS TRACK
!   ACC      I    A    GENERAL ACCELERATION FROM ZERO-IORGA ORDERS
!                      INCLUDING CORRCTIONS TO HIGHEST ORDER
!                      IN VARIOUS  PERIODS.
!   STACC    I    A    STOP TIMES (EPHEMERIS SECONDS SINCE GEODYN REF.TI
!                      OF CORRECTION  PERIODS
!   LAJGP    I    A    LOGICAL TELLING WHETHER EACH PERIOD IS ADJUSTED
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      COMMON/CRMI/RMI(9)
      COMMON/MNSNSP/XMNSNS(7,2)
      COMMON/SETADJ/LSADRG(4),LSASRD(4),LSAGA(4),LSAGP,LSATID,LSAGM,    &
     &              LSADPM,LSAKF,LSADNX,LSADJ2,LSAPGM
      COMMON/SETAPT/                                                    &
     &       JSADRG(1),JSASRD(1),JSAGA(1),JFAADJ,JTHDRG,JACBIA,JATITD,  &
     &       JDSTAT,JDTUM ,                                             &
     &       JSACS, JSATID,JSALG,JSAGM,JSAKF,JSAXYP,JSADJ2,JSAPGM,      &
     &       JFGADJ,JLTPMU,JLTPAL,JL2CCO,JL2SCO,JL2GM,JLRELT,           &
     &       JLJ2SN,JLGMSN,JLPLFM,JL2AE,JSAXTO,JSABRN
      COMMON/SETIOR/IORDRG,IORSRD,IORGA,IPDDRG,IPDSRD,IPDGA
      COMMON/STARTT/ESSTRT,FSSTRT
!
      DIMENSION XS(6),DXDD(3),PXDDT(NEQN,3),VMATRX(3,6),                &
     &          STACC(1),ACC(1),TM(3),VECT(3),FACT(3),LAJGP(1)
      DIMENSION XAXIS(3),YAXIS(3),ZAXIS(3),VMAT(3,6)
!
      DATA ZERO/0.D0/,ONE/1.D0/,TWO/2.D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
      TM(1)=ONE
      TTEST=MJDSEC+FSEC
      NVMAT=3
      GO TO(200,300,100,350,400,425,450),ITACC
!  ALONG TRACK ACCELERATION  (V-axis)
  100 CONTINUE
      VECT(1)=XS(4)
      VECT(2)=XS(5)
      VECT(3)=XS(6)
      GO TO 500
!   RADIAL ACCELERATION (Z-axis)
  200 CONTINUE
      SIGNZ= ONE
      GO TO 475
!   CROSS TRACK (Y-axis)
  300 CONTINUE
      NVMAT=6
      SIGNZ= ONE
      CALL BDAXES(XS(1),XS    (  4),XAXIS,VECT ,ZAXIS,SIGNZ,            &
     &   VMAT,VMAT,2,.TRUE.,LORBIT)
      GO TO 550
!   IN-PLANE (X-axis)
  350 CONTINUE
      NVMAT=6
      SIGNZ= ONE
      CALL BDAXES(XS(1),XS    (  4),VECT ,YAXIS,ZAXIS,SIGNZ,            &
     &   VMAT,VMAT,2,LORBIT,.TRUE.)
      GO TO 550
!   GPS X-AXIS (X-BIAS)
  400 CONTINUE
      SIGNZ=-ONE
      CALL BDAXES(XS(1),XMNSNS(1,2),VECT ,YAXIS,ZAXIS,SIGNZ,            &
     &   VMAT,VMAT,1,LORBIT,.TRUE.)
      GO TO 550
!   GPS Y-AXIS (Y-BIAS)
  425 CONTINUE
      SIGNZ=-ONE
      CALL BDAXES(XS(1),XMNSNS(1,2),XAXIS,VECT ,ZAXIS,SIGNZ,            &
     &   VMAT,VMAT,1,.TRUE.,LORBIT)
      GO TO 550
!   GPS Z-AXIS (Earth pointing)
  450 CONTINUE
      SIGNZ=-ONE
  475 CONTINUE
      VECT(1)=SIGNZ*XS(1)
      VECT(2)=SIGNZ*XS(2)
      VECT(3)=SIGNZ*XS(3)
!   CALCULATE ACCELERATION
  500 CONTINUE
      XMAG2=VECT(1)*VECT(1)+VECT(2)*VECT(2)+VECT(3)*VECT(3)
      XMAG=SQRT(XMAG2)
      VECT(1)=VECT(1)/XMAG
      VECT(2)=VECT(2)/XMAG
      VECT(3)=VECT(3)/XMAG
  550 CONTINUE
      IF(IORGA.GT.1) TM(2)=FSSTRT
      IF(IORGA.GT.2) TM(3)=TM(2)*TM(2)
      YMAG=ZERO
      IPD=0
      IUP=IORGA-1
      IF(IUP.LE.0) GO TO 610
      DO 600 I=1,IUP
  600 YMAG=YMAG+TM(I)*ACC(I)
! ADD CORRECTION TO HIGHEST ORDER TERM IF NECESSARY
  610 IF(IPDGA.GT.0) GO TO 620
      YMAG=YMAG+TM(IORGA)*ACC(IORGA)
      GO TO 750
  620 CONTINUE
!
!  FIGURE WHICH PERIOD APPLIES
      DO 650 I=1,IPDGA
      IF(TTEST.LT.STACC(I)) GO TO 660
  650 END DO
      IPGN=IPDGA
      IPD=0
      GO TO 700
  660 CONTINUE
      IPGN=I-1
      IPD=I
  700 CONTINUE
      T0=ESSTRT
      IF(IPGN.EQ.0) GO TO 720
      DO 710 I=1,IPGN
      TD=STACC(I)-T0
      CALL POWER(IORGA,TD,TDN,.FALSE.)
      YMAG=YMAG+TDN*ACC(I+IORGA)
      T0=STACC(I)
  710 END DO
  720 CONTINUE
      TD=TTEST-T0
      CALL POWER(IORGA,TD,TDN,.TRUE.)
      YMAG=YMAG+TDN*ACC(IPD+IORGA)
  750 CONTINUE
      DXDD(1)=YMAG*VECT(1)
      DXDD(2)=YMAG*VECT(2)
      DXDD(3)=YMAG*VECT(3)
      IF(LORBIT) RETURN
!   ADD VMATRX CONTRIBUTION & (IF NECCESSARY) CALCULATE EXPLICIT
!   PARTIALS
      GO TO (1500,2000,1000,2000,2000,2000,1500),ITACC
! ALONG TRACK V-MATRIX. IGNORE OFF-DIAGONAL TERMS
 1000 CONTINUE
      ZMAG=YMAG/XMAG
      VMATRX(1,4)=VMATRX(1,4)+(ONE-VECT(1)*VECT(1))*ZMAG
      VMATRX(2,5)=VMATRX(2,5)+(ONE-VECT(2)*VECT(2))*ZMAG
      VMATRX(3,6)=VMATRX(3,6)+(ONE-VECT(3)*VECT(3))*ZMAG
      GO TO 4000
! Z-AXIS V-MATRIX. IGNORE OFF-DIAGONAL TERMS
 1500 CONTINUE
      ZMAG=SIGNZ*YMAG/XMAG
      VMATRX(1,1)=VMATRX(1,1)+(ONE-VECT(1)*VECT(1))*ZMAG
      VMATRX(2,2)=VMATRX(2,2)+(ONE-VECT(2)*VECT(2))*ZMAG
      VMATRX(3,3)=VMATRX(3,3)+(ONE-VECT(3)*VECT(3))*ZMAG
      GO TO 4000
! X-AXIS AND Y-AXIS V-MATRIX.
 2000 CONTINUE
      DO 2500 J=1,NVMAT
      DO 2500 I=1,3
      VMATRX(I,J)=VMATRX(I,J)+VMAT(I,J)*YMAG
 2500 CONTINUE
!   CALCULATE EXPLICIT PARTIALS OF GENERAL ACCELERATION
 4000 CONTINUE
! PUT PARTIALS INTO TRUE OF REFERENCE
      FACT(1)=VECT(1)*RMI(1)+VECT(2)*RMI(2)+VECT(3)*RMI(3)
      FACT(2)=VECT(1)*RMI(4)+VECT(2)*RMI(5)+VECT(3)*RMI(6)
      FACT(3)=VECT(1)*RMI(7)+VECT(2)*RMI(8)+VECT(3)*RMI(9)
      KPART=JSAGA(1)
      DO 5000 J=1,IORGA
      IF(.NOT.LSAGA(J)) GO TO 5000
      PXDDT(KPART,1)=FACT(1)*TM(J)
      PXDDT(KPART,2)=FACT(2)*TM(J)
      PXDDT(KPART,3)=FACT(3)*TM(J)
      KPART=KPART+1
 5000 END DO
      IF(.NOT.LSAGA(4)) RETURN
      IF(.NOT.LSAGA(IORGA)) GO TO 5010
      PXDDT(KPART-1,1)=ZERO
      PXDDT(KPART-1,2)=ZERO
      PXDDT(KPART-1,3)=ZERO
 5010 CONTINUE
      KPARTS=KPART
      DO 5500 I=1,IPDGA
      IF(.NOT.LAJGP(I)) GO TO 5500
      PXDDT(KPART,1)=ZERO
      PXDDT(KPART,2)=ZERO
      PXDDT(KPART,3)=ZERO
      KPART=KPART+1
 5500 END DO
      KPART=KPARTS
      T0=ESSTRT
      IF(IPGN.LE.0) GO TO 5700
      DO 5600 I=1,IPGN
      IF(.NOT.LAJGP(I)) GO TO 5550
      TD=STACC(I)-T0
      CALL POWER(IORGA,TD,TDN,.FALSE.)
      PXDDT(KPART,1)=FACT(1)*TDN
      PXDDT(KPART,2)=FACT(2)*TDN
      PXDDT(KPART,3)=FACT(3)*TDN
      KPART=KPART+1
 5550 CONTINUE
      T0=STACC(I)
 5600 END DO
 5700 CONTINUE
      IF(IPD.GT.0) GO TO 5800
      IF(.NOT.LSAGA(IORGA)) RETURN
      KPART=KPARTS-1
      GO TO 5900
 5800 CONTINUE
      IF(.NOT.LAJGP(IPD)) RETURN
 5900 CONTINUE
      TD=TTEST-T0
      CALL POWER(IORGA,TD,TDN,.TRUE.)
      PXDDT(KPART,1)=FACT(1)*TDN
      PXDDT(KPART,2)=FACT(2)*TDN
      PXDDT(KPART,3)=FACT(3)*TDN
      RETURN
      END
