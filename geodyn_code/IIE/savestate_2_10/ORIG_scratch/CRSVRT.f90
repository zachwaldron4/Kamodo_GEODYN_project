!$CRSVRT
      SUBROUTINE CRSVRT(SUM1A,SUM1G,SCRTCH,NDIM,NAPARM,NGPARM,NSTART)
!********1*********2*********3*********4*********5*********6*********7**
! CRSVRT           00/00/00            0000.0    PGMR - ?
!
! FUNCTION:   MULTIPLY THE QUANTITY -(AINV*AK) TIMES THE PART OF
!             THE INVERTED NORMAL MATRIX CORRESPONDING TO THE GLOBAL
!             PARAMETERS.AINV IS THE ARC PORTION OF THE NORMAL MATRIX
!             THAT HAS BEEN INVERTED INDEPENDENTLY OF THE REST OF THE
!             MATRIX.AK IS THE CROSS ARC/GLOBAL PORTION OF THE NORMAL
!             MATRIX
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   SUM1A   I/O   A    NORMAL MATRIX STARTING AT ARC PORTION.AT THE
!                      POINT THAT THE ROUTINE IS CALLED THE PORTION OF
!                      THE NORMAL MATRIX CORRESSPONDING TO AK (DESCRIBED
!                      ABOVE) SHOULD CONTAIN (AINV*AK)
!                      FOR OUTPUT SUM1A FROM INPUT IS MODIFIED IN THE
!                      AK PORTION SO THAT THE ENTIRE NORMAL MATRIX HAS
!                      AT LAST BEEN INVERTED
!   SUM1G    I    A    PORTION OF INVERTED NORMAL MATRIX CORRESPONDING
!                      TO GLOBAL PARAMTERS.
!   SCRTCH   I    A    SCRATCH SPACE NGPARM IN LENGTH
!   NDIM     I    S    LENGTH OF FIRST ROW OF NORMAL MATRIX
!   NAPARM   I    S    NUMBER OF ARC PARAMETERS
!   NGPARM   I    S    NUMBER OF GLOBAL PARAMETERS
!   NSTART   I    S    STARTING LOCATION OF AK IN FIRST ROW OF NORMAL
!                      MATRIX
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION SUM1A(1),SUM1G(1),SCRTCH(1)
      DATA ZERO/0.D0/
      INDXNO(I)=NDIM*(I-1)-(I*(I-1))/2+NSTART
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      DO 1000 IROWA=1,NAPARM
      ISA=INDXNO(IROWA)
!  USE THE PART OF THE GLOBAL MATRIX THAT IS CONTIG
      DO 500 ICOL=1,NGPARM
      NL=NGPARM-ICOL+1
      ISG=(ICOL-1)*NGPARM- ICOL*(ICOL-1)/2+ICOL-1
      ISA1=ISA+ICOL-2
      SCRTCH(ICOL)=ZERO
      DO 400 J=1,NL
      SCRTCH(ICOL)=SCRTCH(ICOL)+SUM1A(ISA1+J)*SUM1G(ISG+J)
  400 END DO
  500 END DO
!  USE THE PART OF THE GLOBAL MATRIX THAT IS NOT CONTIG
      IGPM1=NGPARM-1
      DO 700 IROWG=1,IGPM1
      NL=NGPARM-IROWG
      ISA1=ISA+IROWG-1
      ICOLS=IROWG
      ISG=(IROWG-1)*NGPARM-IROWG*(IROWG-1)/2+IROWG
      DO 600 J=1,NL
      SCRTCH(ICOLS+J)=SCRTCH(ICOLS+J)+SUM1A(ISA1)*SUM1G(ISG+J)
  600 END DO
  700 END DO
! SCRTCH NOW CONTAIINS THE IROWA ROW OF THE DESIRED MATRIX;THIS
!     IS THE LAST TIME NEED THAT ROW IN SUM1A.STORE BACK IN SUM1A.
      ISA1=ISA-1
      DO 800 J=1,NGPARM
      SUM1A(ISA1+J)=-SCRTCH(J)
  800 END DO
 1000 END DO
      RETURN
      END
