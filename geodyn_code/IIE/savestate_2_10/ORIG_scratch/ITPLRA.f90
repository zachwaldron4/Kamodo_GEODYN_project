!$ITPLRA
      SUBROUTINE ITPLRA(DNM,ENM,FNM,GNM,IPT,RACOR,NLRAIN,NMAX,MMAX,     &
     &                  NLRAST,X,Y,ANGINC,INDSTA,STAINF)
!********1*********2*********3*********4*********5*********6*********7**
! ITPLRA           92/08/16            9110.3    PGMR - ANDREW MARSHALL
!
!
! FUNCTION:  LINEARLY INTERPOLATE TOPEX RANGE CORRECTION BETWEEN
!            INCIDENCE ANGLES.  RANGE CORRECTION COMPUTE FROM FOURIER
!            TRANSFORM COEFFICIENTS INPUT ON UNIT 8 IN 2S.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   DNM      I    A    FOURIER COEFF. OF RANGE CORRECTIONS FOR TOPEX LRA
!   ENM      I    A    FOURIER COEFF. OF RANGE CORRECTIONS FOR TOPEX LRA
!   FNM      I    A    FOURIER COEFF. OF RANGE CORRECTIONS FOR TOPEX LRA
!   GNM      I    A    FOURIER COEFF. OF RANGE CORRECTIONS FOR TOPEX LRA
!   RACOR    O    S    INTERPOLATED RANGE CORRECTION
!   IPT      I    S    INCIDENCE ANGLE POINTER
!   NLRAIN   I    S    NUMBER OF RNGCOR INCIDENCE ANGLES
!   NMAX     I    S    NUMBER OF RNGCOR X FOURIER COEF.
!   MMAX     I    S    NUMBER OF RNGCOR Y FOURIER COEF.
!   NLRAST   I    S    NUMBER OF RNGCOR STATION VALUES
!   X        I    S    X LOCATION IF RNGCOR PATTERN
!   Y        I    S    Y LOCATION IF RNGCOR PATTERN
!   ANGINC   I    S    INCIDENCE ANGLE(DEG)
!   INDSTA   I    A    STATION ARRAY POINTER
!   STAINF   I    A    STATION INFORMATION ARRAY
!                       LASER STATION DETECTOR TYPE IN ELEMENT 15
!
!
! COMMENTS:
!
! REFERENCE:
!   MINOTT, P.O., "READER'S GUIDE TO THE "RETRO" PROGRAM OUTPUT,"
!    NASA GSFC X-722-76-267, SEPTEMBER, 1976.
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      DIMENSION STAINF(NSTAIN,1),INDSTA(3)
      DIMENSION DNM(NLRAIN,NMAX,MMAX,NLRAST)
      DIMENSION ENM(NLRAIN,NMAX,MMAX,NLRAST)
      DIMENSION FNM(NLRAIN,NMAX,MMAX,NLRAST)
      DIMENSION GNM(NLRAIN,NMAX,MMAX,NLRAST)
      DATA ZERO/0.0D0/,ONE/1.0D0/,TWO/2.0D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! DETERMINE STATION DETECTION SYSTEM AND MATCH WITH RNGCOR
!
      ISTAT = INT(STAINF(15,INDSTA(1)))
      IF(IPT.EQ.NLRAIN) GOTO 810
!
! COMPUTE FOURIER FUNCTION FOR ELEVATION ABOVE THIS POINT
!     COMPUTE BIAS AND TILT FIRST
      PLANE = DNM(IPT+1,1,1,ISTAT)+ENM(IPT+1,1,1,ISTAT)*X               &
     &       +FNM(IPT+1,1,1,ISTAT)*Y
!
!    COMPUTE PERIODIC SINUSOIDS (x-xmin)/(xmax-xmin)
      SUM=ZERO
! the 50 is hard coded temp. for a 1 to 51 grid
      XVAL=(X-1.0D0)/DBLE(50)*TWOPI
      YVAL=(Y-1.0D0)/DBLE(50)*TWOPI
      N=1
      DO 785 M=2,MMAX
         COSY=COS(YVAL*REAL(M-1))
         SINY=SIN(YVAL*REAL(M-1))
         SUM=SUM+DNM(IPT+1,N,M,ISTAT)*COSY +ENM(IPT+1,N,M,ISTAT)*SINY
  785 END DO
      M=1
      DO 786 N=2,NMAX
         COSX=COS(XVAL*REAL(N-1))
         SINX=SIN(XVAL*REAL(N-1))
         SUM=SUM+DNM(IPT+1,N,M,ISTAT)*COSX +FNM(IPT+1,N,M,ISTAT)*SINX
  786 continue
      DO 800 N=2,NMAX
      DO 800 M=2,MMAX
         COSX=COS(XVAL*REAL(N-1))
         SINX=SIN(XVAL*REAL(N-1))
         COSY=COS(YVAL*REAL(M-1))
         SINY=SIN(YVAL*REAL(M-1))
         SUM=SUM+DNM(IPT+1,N,M,ISTAT)*COSX*COSY +                       &
     &           ENM(IPT+1,N,M,ISTAT)*COSX*SINY+                        &
     &           FNM(IPT+1,N,M,ISTAT)*SINX*COSY +                       &
     &           GNM(IPT+1,N,M,ISTAT)*SINX*SINY
!
  800 CONTINUE
      FUNCT1=PLANE+SUM
! COMPUTE fOURIER FUNCTION FOR NEXT ELEVATION BELOW THIS POINT
!     COMPUTE BIAS AND TILT FIRST
  810 CONTINUE
      PLANE = DNM(IPT,1,1,ISTAT)+ENM(IPT,1,1,ISTAT)*X+                  &
     &        FNM(IPT,1,1,ISTAT)*Y
!
!    COMPUTE PERIODIC SINUSOIDS
      SUM=ZERO
      N=1
      DO 885 M=2,MMAX
         COSY=COS(YVAL*REAL(M-1))
         SINY=SIN(YVAL*REAL(M-1))
         SUM=SUM+DNM(IPT,N,M,ISTAT)*COSY +ENM(IPT,N,M,ISTAT)*SINY
  885 END DO
      M=1
      DO 886 N=2,NMAX
         COSX=COS(XVAL*REAL(N-1))
         SINX=SIN(XVAL*REAL(N-1))
         SUM=SUM+DNM(IPT,N,M,ISTAT)*COSX +FNM(IPT,N,M,ISTAT)*SINX
  886 END DO
      DO 890 N=2,NMAX
      DO 890 M=2,MMAX
         COSX=COS(XVAL*REAL(N-1))
         SINX=SIN(XVAL*REAL(N-1))
         COSY=COS(YVAL*REAL(M-1))
         SINY=SIN(YVAL*REAL(M-1))
         SUM=SUM+DNM(IPT,N,M,ISTAT)*COSX*COSY                           &
     &          +ENM(IPT,N,M,ISTAT)*COSX*SINY                           &
     &          +FNM(IPT,N,M,ISTAT)*SINX*COSY                           &
     &          +GNM(IPT,N,M,ISTAT)*SINX*SINY
  890 continue
!
!
      FUNCT2=PLANE+SUM
!
      IF(IPT.EQ.NLRAIN) THEN
         RACOR=FUNCT2
      ELSE
! LINEARLY INTERPOLATE
! THIS ASSUMES 2.5DEG RNGCOR INTERVALS FROM INCIDENCE ANG 0-20 AND
!              5.0DEG RNGCOR INTERVALS FROM INCIDENCE ANG 20-60
      IF(ANGINC.LT.20.0) THEN
        RACOR=(FUNCT1-FUNCT2)/2.5D0*(ANGINC-DBLE(IPT-1)*2.5D0)+FUNCT2
      ELSE
        RACOR=(FUNCT1-FUNCT2)/5.0D0*(ANGINC+DBLE(5-IPT)*5.0D0)+FUNCT2
      ENDIF
      ENDIF
!
! CONVERT FROM MM to METERS
      RACOR = -RACOR/1000.0
      RETURN
      END
