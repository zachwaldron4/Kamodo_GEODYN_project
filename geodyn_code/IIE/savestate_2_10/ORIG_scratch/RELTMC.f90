!$RELTMC
      SUBROUTINE RELTMC(XTPOS,XRPOS,XTVEL,MSECT,FSECT,NM,               &
     & RLRCOR,IGPS,LRRATE,LADD)
!********1*********2*********3*********4*********5*********6*********7**
! RELTMC           89/12/15            0000.0    PGMR - SBL
!
! FUNCTION:         COMPUTE RANGE CORRECTION DUE TO THE UNMODELED
!                   RELATIVISTIC PERIODIC AND LINEAR SPACE CRAFT
!                   CLOCK DRIFT
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   XTPOS   I/O        CARTESIAN POSITION VECTOR OF TRANSMITTING BODY
!   XRPOS   I/O        CARTESIAN POSITION VECTOR OF RECEIVING BODY
!   XTVEL   I/O        CARTESIAN VELOCITY VECTOR OF TRANSMITTING BODY
!   MSECT   I/O        INTEGER MJD AT TRANSMITTING BODY
!   FSECT   I/O        FRACTIONAL SECONDS AT TRANSMITTING BODY
!   NM      I/O        NUMBER OF MEASUREMENTS
!   RLRCOR   O         RANGE CORRECTION DUE TO UNMODELED RELATIVISTIC
!                      PERIODIC AND LINEAR SPACE CRAFT CLOCK DRIFT
!   IGPS    I/O        = 1 THEN HAVE A GPS SAT AND ONLY APPLY PERIODIC
!                      CORRECTION OTHERWISE APPLY BOTH PERIODIC AND
!                      SECULAR
!   LADD     I    S    CONTROLS WHETHER THE GIVEN SIDE OF A DIFFERENCE
!                      MEASUREMENT IS HANDLED AS POSITIVE(=.TRUE.) OR
!                      NEGATIVE(=.FALSE.). =.TRUE. IS FIRST EXECUTION OF
!                      RANGE DIFFERENCE.
!
! *** NOTE: FOR NOW THIS ROUTINE ASSUMES THAT THE TRANSMITTING BODY
!           IS A SPACECRAFT AND THE RECEIVING BODY IS AN EARTH BOUND
!           STATION.  FOR LINKS THAT EMINATE AT THE STATION AND ARE
!           RECEIVED AT THE S/C (MEASUREMENT TYPES 39 AND 40) THE SIGN
!           OF THIS CORRECTION IS CHANGED IN SUBROUTINE ROBSUM.
!
! RESTRICTIONS: THIS ROUTINE IS NOT MEANT FOR INTERPLANETARY RUNS.
!
! REFERENCES: "RELATIVITY IN ASTROMETRY, CELESTIAL MECHANICS AND
!             GEODESY", M.H.SOFFEL, PAGE 150-151.
!
! RELATIVISTIC CORRECTION: RLRCOR = PERIODIC TERM - SECULAR CONS *
!                                   (SYCHRONIZATION TIME)
!
! THE SYNCHRONIZATION TIME IS THE AMOUNT OF TIME SINCE THE S/C CLOCK
! AND THE STATION CLOCK WERE SYNCHRONIZED (IN STATION CLOCK FRAME).
! FOR NOW THE SYNCHRONIZATION TIME IS TAKEN FROM THE BLOCK START TIME.
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION(A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/CLIGHT/VLIGHT,ERRLIM,XCLITE
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CTHDOT/THDOT
      COMMON/CNIARC/NSATA,NSATG,NSETA,NEQNG,NMORDR,NMORDV,NSORDR,       &
     &              NSORDV,NSUMX,NXDDOT,NSUMPX,NPXDDT,NXBACK,NXI,       &
     &              NXILIM,NPXPF,NINTIM,NSATOB,NXBCKP,NXTIMB,           &
     &              NOBSBK,NXTPMS,NXCNIA
      COMMON/CRELAT/AER(11),GRLFC2(11),GRLFC3(11),SECCOR
!
      DIMENSION XTPOS(MINTIM,3),XTVEL(MINTIM,3),XRPOS(MINTIM,3)
      DIMENSION FSECT(NM)
      DIMENSION RLRCOR(NM)
!
      DATA TWO/2.0D0/,ONE/1.0D0/,C1P5/1.5D0/
!
!**********************************************************************
! START OF EXECUTABLE CODE ********************************************
!**********************************************************************
!
!
      TWODVL=TWO/VLIGHT
! COMPUTE X DOT V
! COMPUTE PERIODIC TIME CORRECTION
!
!     PER=RRDOT*(TWO/(VLIGHT**2))
! SINCE WE WILL MULTIPLY BY VLIGHT TO GET RANGE CORRECTION NEED ONLY
!     PER=RRDOT*(TWO/VLIGHT)
!
      DO 100 I=1,NM
      RLRCOR(I)=(XTPOS(I,1)*XTVEL(I,1)+XTPOS(I,2)*XTVEL(I,2)            &
     & +XTPOS(I,3)*XTVEL(I,3))*TWODVL
  100 END DO
!
! COMPUTATION OF THE SECULAR CORRECTION
!
      IF(IGPS.EQ.0) THEN
      IF(.NOT.LADD) GOTO 200
! COMPUTE SEMI-MAJOR AXIS, SAME FOR ALL MEASUREMENTS IN BLOCK
      R=SQRT(XTPOS(1,1)**2+XTPOS(1,2)**2+XTPOS(1,3)**2)
      VSQ=XTVEL(1,1)**2+XTVEL(1,2)**2+XTVEL(1,3)**2
      AINV=(TWO/R)-(VSQ/GM)
      A=ONE/AINV
! COMPUTE CONSTANT SCALE FACTOR
      VST=((THDOT**2)/2)*(XRPOS(1,1)**2+XRPOS(1,2)**2)
      RST=SQRT(XRPOS(1,1)**2+XRPOS(1,2)**2+XRPOS(1,3)**2)
      SECCOR=((GM/RST)+VST-(C1P5*GM/A))/(VLIGHT)
  200 CONTINUE
!
! CALCULATE SYNCHRONIZATION TIME AS BLOCK START TIME MINUS 1 SECOND
! WHICH IS FRACTIONAL SECONDS PLUS ONE SECOND
!
! APPLY SECULAR CORRECTION TO CORRECTION ARRAY
!
      DO 300 I=1,NM
      RLRCOR(I)=RLRCOR(I)-((FSECT(I)+ONE)*SECCOR)
  300 END DO
!
      ENDIF
      RETURN
      END
