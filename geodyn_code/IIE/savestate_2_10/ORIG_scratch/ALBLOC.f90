!$ALBLOC
      SUBROUTINE ALBLOC(XUV,AUV,BUV,GEOANG,SVECT,FACTX,FACTY,NSEG,      &
     &NPOINT)
!********1*********2*********3*********4*********5*********6*********7**
! ALBLOC           89/05/23            0000.0    PGMR: JJM, SBL
!                                                      FGL, DEP
!
!
! FUNCTION: CALCULATE CARTESIAN COORDINATES OF THE SPOTS ON THE
!           EARTH'S SURFACE FOR ALBEDO CALCULATION.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   XUV      I    A    SATELLITE ECF XYZ UNIT VECTOR
!   AUV      I    A    SPOT X-VECTOR IN SAT-CENTERED COORDINATES
!   BUV      I    A    SPOT Y-VECTOR IN SAT-CENTERED COORDINATES
!   GEOANG   I    A    GEOCENTRIC ANGLES TO RINGS THAT DEFINE SPOT
!                      LOCATION
!   SVECT    O    A    EARTH CENTERED FIXED XYZ SPOT VECTORS
!   FACTX    I    A    COS OF ANGLE TO SPOT IN AUV-BUV REFERENCE FRAME ;
!                      DEFINES FRACTION OF Y VECTOR IN AUV DIRECTION
!   FACTY    I    A    SIN OF ANGLE TO SPOT IN AUV-BUV REFERENCE FRAME ;
!                      DEFINES FRACTION OF Y VECTOR IN BUV DIRECTION
!   NSEG     I    A    NUMBER OF RING SEGMENTS FOR ALL RINGS
!
!
! COMMENTS: A GEODYN I ROUTINE MODIFIED FOR GEODYN II, AND TO INCLUDE
!           19 ALBEDO SPOT LOCATIONS RATHER THAN 12
!
!
!********1*********2*********3*********4*********5*********6*********7**
!
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/ALBINT/IRINGS,ISPOTS,IMAXA,IMAXE,MMAXA,MMAXE,KMAXA,KMAXE,  &
     &              KALB,KEMM,KLMOD,IMODEL,NPNALB,NXALBI
      COMMON/CGRAV/GM,AE,AESQ,FE,FFSQ32,FSQ32,XK2,XK3,XLAM,SIGXK2,      &
     &      SIGXK3,SIGLAM,RATIOM(2),AU,RPRESS
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
!
      DIMENSION VECT(3),AUV(3),BUV(3),XUV(3),GEOANG(1),SVECT(NPOINT,3)
      DIMENSION FACTX(1),FACTY(1),NSEG(1)
      DIMENSION FX(18),FY(18)
!
      DATA ONE/1.0D0/
      DATA FX /  1.D0,     0.5D0,    -0.5D0,   -1.D0,    -0.5D0,        &
     & 0.5D0, 1.D0 ,   0.8660254038D0, 0.5D0 ,   0.D0, -0.5D0,          &
     & -0.8660254038D0,-1.D0,-0.8660254038D0, -0.5D0,    0.D0,          &
     &  0.5D0, 0.8660254038D0  /
      DATA FY /  0.D0,     0.8660254038D0,     0.8660254038D0,          &
     & 0.D0,     -0.8660254038D0,    -0.8660254038D0, 0.D0,             &
     & 0.5D0, 0.8660254038D0,   1.D0, 0.8660254038D0, 0.5D0, 0.D0,      &
     & -0.5D0,-0.8660254038D0,-1.D0,-0.8660254038D0,-0.5D0 /
!
!   FACTX  - COS OF ANGLE TO SPOT IN AUV-BUV REFERENCE FRAME ; DEFINES
!            FRACTION OF Y VECTOR IN AUV DIRECTION
!   FACTY  - SIN OF ANGLE TO SPOT IN AUV-BUV REFERENCE FRAME ; DEFINES
!            FRACTION OF Y VECTOR IN BUV DIRECTION
!
!********1*********2*********3*********4*********5*********6*********7**
! START OF EXECUTABLE CODE
!********1*********2*********3*********4*********5*********6*********7**
!     do 81818 i=1,18
!     factx(i)=fx(i)
!     facty(i)=fy(i)
!81818 continue
!
!
!     ....W IS THE LENGTH OF THE SPOT VECTOR (AE) PROJECTED ON XUV
!     ....Y IS THE THIRD LEG OF THE TRIANGLE (SPOT,EARTH CENTER,
!     ....SUB-SATELLITE POINT)
!     ....AE AND W ARE THE OTHER LEGS
!
      DO 400 J=2,IRINGS+1
      NSRING=NSEG(J)-NSEG(J-1)
      W = COS(GEOANG(J))
      Y = SQRT(ONE - W**2)
      Y = AE*Y
      W = AE*W
      DO 100 K=1,3
      VECT(K) = W * XUV(K)
  100 END DO
      ILEVEL = NSEG(J-1) - 1
      DO 300 I2=1,NSRING
      IVAL = I2 + ILEVEL
      DO 200 N=1,3
      SVECT(IVAL,N) =VECT(N)+Y*(FACTX(IVAL)*AUV(N)+FACTY(IVAL)*BUV(N))
  200 END DO
  300 END DO
  400 END DO
!
!     .... FOR THE SUB-SATELLITE SPOT
!
      IVAL = IVAL+1
      DO 500 N=1,3
      SVECT(IVAL,N) = AE*XUV(N)
  500 END DO
      RETURN
      END
