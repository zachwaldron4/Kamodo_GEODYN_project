!$GFOYAW
       SUBROUTINE GFOYAW(BETA,SOMEGA,YAW)
!*******************************************************************
!  ROUTINE NAME:   GFOYAW   DATE: 01/24/97      PGMR: A. MARSHALL
!
!   FUNCTION -  Adjust the yaw attitude of GFO for beta angles less
!               than 15 degrees (in absolute value) by interpolating
!               between fixed functional forms.  The functional forms
!               are given at discrete values of beta. They were
!               generated by Doug Wiemer of Ball Aerospace.
!
!              MODIFIED CODE ORIGINALLY WRITTEN FOR GIPSY BY
!              YOAZ BAR-SEVER (JPL:  yeb@cobra.jpl.nasa.gov)
!
!  I/O PARAMETERS:
!
!   NAME    A/S    I/O   DESCRIPTION OF I/O PARAMETERS IN ARGUMENT LIST
!   -----  ------ -----  -----------------------------------------------
!   BETA     S      I    BETA' Angle
!   SOMEGA   S      I    Orbit Angle
!   YAW      S      O    Yaw Angle
!
!*******************************************************************
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
       DIMENSION ADJUST(4),CINV(4,4),COEFF(4)
!
!
!
! CINV is the matrix from which the coefficients of the 3rd order
! interpolating polynominal is computed. It is the inverse of:
!           3^3   3^2   3  1
!           6^3   6^2   6  1
!          10^3  10^2  10  1
!          15^3  15^2  15  1
! and is based on interpolating through 4 adjustment curves,
! corresponding to 4 beta value: 3,6,10 and 15.
      data CINV/-3.968253968253965D-03,1.230158730158730D-01,           &
     &          -1.190476190476190D+00,3.571428571428571D+00,           &
     &           9.259259259259255D-03,-2.592592592592592D-01,          &
     &           2.083333333333333D+00,-4.166666666666666D+00,          &
     &          -7.142857142857142D-03, 1.714285714285714D-01,          &
     &          -1.092857142857143D+00,1.928571428571428D+00,           &
     &           1.851851851851851D-03,-3.518518518518517D-02,          &
     &           2.000000000000000D-01,-3.333333333333333D-01/
!
!
       DATA ONE/1.0D0/,C90P0/90.0D0/
!
!********1*********2*********3*********4*********5*********6*********7**
! START OF EXECUTABLE CODE
!********1*********2*********3*********4*********5*********6*********7**
!
!** If somega is greater than 260 use the negative of the adjustement fo
!   somega - 180.  It is symmetric about 180.
       if (somega .ge. 260.0D0) then
          x=somega-180.0D0
       else
          x=somega
       endif
       y=ABS(beta)
!
!** Evaluate functional forms supplied by Ball at given SOMEGA, for
!   each of four B' angles
       call ADJFIT(3,x,adjust(1))
       call ADJFIT(6,x,adjust(2))
       call ADJFIT(10,x,adjust(3))
       call ADJFIT(15,x,adjust(4))
!
! Solve for the coefficients of the polynomial:
!     yaw = coeff(1)*y**3 + coeff(2)*y**2 +
!           coeff(3)*y + coeff(4)
! based on the series of equation obtained from ADJFIT
! This is the same interpolating polynomial as described in
! section 3.5 of Numerical Recipies (p. 92).
       do 10 i=1,4
          coeff(i)=0.0D0
          do 20 j=1,4
             coeff(i)=coeff(i)+cinv(i,j)*adjust(j)
   20     continue
   10  continue
!
       YAW=coeff(1)*y**3 + coeff(2)*y**2 +                              &
     &     coeff(3)*y + coeff(4)
!
!** correct the sign based on somega and beta
       if (somega .ge. 260.0D0) then
          YAW=-YAW
       endif
       if (beta .lt. 0.0D0) then
          YAW=-YAW
       endif
!
       return
!
      END
