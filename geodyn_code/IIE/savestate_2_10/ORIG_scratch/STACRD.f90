!$STACRD
      SUBROUTINE STACRD(ICRD,ICON,ISTANO,STNAME,SACRD,SPSIG,SXYZ,DXSDPM,&
     &                  DPSDPM,STAINF,XLOCV,STAPRI,PLTINF,PLTVEL,PLTDRV)
!********1*********2*********3*********4*********5*********6*********7**
! STACRD           83/10/15            8311.0    PGMR - D. ROWLANDS
!
! FUNCTION:    (1) GET XYZ COORDINATES OF STATIONS
!              (2) GET PARTIALS OF STATION XYZ WRT ADJUSTED PARAMTERS
!                  (MASTER STATION COORDINATES IN COORDINATE SYSTEM
!                  OF CHOICE)
!              (3) GET PARTIALS OF STATION COORDINATES WRT MASTER
!                  STATION COORDINATES IN COORDINATE SYSTEM OF CHOICE
!              (4) GET SINES&COSINES OF STATION GEODETIC LAT AND LON
!              (5) GET LOCAL DIRECTION VECTORS(VERTICAL,NORTH,EAST)
!                  FOR EACH STATION
!              (6) CALCULATE STATION COVARIANCES IN ALL SYSTEMS
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   ICRD     I    A    POINTER TO COORDINATE SYSTEM OF INPUT AND
!                      ADJUSTMENT (1=> XYZ;2=>GEODETIC;3=>CYLINDRICAL)
!   ICON     I    A    POINTER TO (INTERNAL) MASTER STATION NUMBER
!                      FOR EACH STATION
!   ISTANO   I    A    EXTERNAL STATION NUMBER
!   STNAME   I    A    STATION NAME
!   SACRD    I    A    STATION COORDIATES IN SYSTEM OF ADJUSTMENT
!   SPSIG    I    A    MASTER STATION VARIANCE COVARIANCES IN SYSTEM
!                      OF ADJUSTMENT AS INPUT
!                      MASTER STATION VARIANCE/COVARIANCES ALL THREE
!                      SYSTEMS FOR PRINT OUT AS OUTPUT
!   SXYZ     O    A    EARTH CENTERD XYZ COORDINATES
!   DXSDPM   O    A    MATRIX OF PARTIALS OF SLAVE STAION XYZ COORD-
!                      INATES WRT MASTER STATION COORDINATES (SYSTEM OF
!                      ADJUSTMENT)
!   DPSDPM   O    A    MATRIX OF PARTIALS OF SLAVE STATION COORDINATES
!                      (COORDINATE SYSTEM OF ADJUSTMENT) WRT MASTER
!                      STATION COORDINATES (COORDINATE SYSTEM OF
!                      ADJUSTMENT)
!   STAINF(1)O    A    LATITUDE OF STATION (RADIANS)
!   STAINF(2)O    A    COSINE OF STATION LATITUDE
!   STAINF(3)O    A    SINE OF STATION LATITUDE
!   STAINF(4)O    A    LONGITUDE OF STATION (RADIANS)
!   STAINF(5)O    A    COSINE OF STATION LONGITUDE
!   STAINF(6)O    A    SINE OF STATION LONGITUDE
!   STAINF(7)O    A    SPHEROID HT
!   STAINF(20)O   A    INDEX TO ANTENNA THERMAL DEFORMATION APPLICATION
!                      =0 NO =1 YES
!   XLOCV    O    A    XYZ OF EAST,NORTH,VERT AT STATION
!   STAPRI   O    A    APRIORI VALUES OF ADJUSTED STATIONS
!                      (FILLED ON FIRST CALL ONLY)
!   PLTINF   I    A    LAT,LON OF EACH PLATE POLE AND SPIN RATE
!   PLTVEL   O    A    STATION VELOCITY VECTOR DUE TO PLATE MOTION
!   PLTDRV   O    A    DERVIVATIVES OF PLTVEL WRT PLATE PARAMRTERS
!
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION ICRD(1),ICON(1),SACRD(3,1),SPSIG(6,3,1),                &
     &          SXYZ(3,1),DXSDPM(3,3,1),DPSDPM(3,3,1),                  &
     &          STAINF(NSTAIN,1),XLOCV(3,3,1),TEMP(3),DPSDXS(3,3,2),    &
     &          ITEMP(3),DXSDPS(3,3),ISTANO(1),STNAME(1),SPPSIG(7),     &
     &          STAPRI(25,1),PLTINF(3,1),PLTVEL(3,1),PLTDRV(3,3,1)
! /CEARTH/ GEOMETRICAL EARTH CONSTANTS USED FOR MEAS. PROCESSING
      COMMON/CEARTH/AEG,AEGSQ,FGINV,FG,EGSQ,EGSQP1,C1,C2,FOURC1,TWOC2,  &
     &              XH2,XL2,WREF,RMEAN,REFC20,REFC40,REFC60,BEG,CBLTC,  &
     &              WAE2,GAMMAA,FSTAR,F4
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      COMMON/PLATE /NPLATE,MJDPLR,NPLTMS,NXPLAT
      COMMON/STATN /NSTA,NSTAE,NMSTA,NCSTA,NSTAV,NSTLV,NSTEL2,          &
     &              NSTEH2,                                             &
     &              NPH2L2,                                             &
     &              NSTNUM, NSTIME, NALLST,KCONAD, NXSTAT
      DATA ZERO /0.D0/,ONE/1.D0/,EPS/.1D0/,SECPML/31556952000000.D0/
      DATA LNFRST/.FALSE./
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      ILINE6=MLINE6
      ISTACT=0
   10 CONTINUE
      ISTACT=ISTACT+1
      LNMSTA=ISTACT.GT.NMSTA
      LNSTAE=ISTACT.GT.NSTAE
!   SECOND TIME THRU;RECALCULATE OUTPUT QUANTITIES FOR ADJUSTED
!   STATIONS ONLY
      IF(LNFRST.AND.LNSTAE) RETURN
!   FIRST TIME THRU NEED TO CALCULATE OUTPUT QUANTITIES FOR ALL STATIONS
      IF(ISTACT.LE.NSTA) GO TO 20
      LNFRST=.TRUE.
      RETURN
   20 CONTINUE
      IF(LNSTAE) GO TO 30
      DPSDPM(1,1,ISTACT)=ONE
      DPSDPM(2,1,ISTACT)=ZERO
      DPSDPM(3,1,ISTACT)=ZERO
      DPSDPM(1,2,ISTACT)=ZERO
      DPSDPM(2,2,ISTACT)=ONE
      DPSDPM(3,2,ISTACT)=ZERO
      DPSDPM(1,3,ISTACT)=ZERO
      DPSDPM(2,3,ISTACT)=ZERO
      DPSDPM(3,3,ISTACT)=ONE
   30 CONTINUE
      JADJ=ICRD(ISTACT)/10
      GO TO (200,100,300),JADJ
!
!
!   ADJUSTMENT IS IN XYZ
  100 CONTINUE
!   SACRD IS ALREADY IN XYZ
      SXYZ(1,ISTACT)=SACRD(1,ISTACT)
      SXYZ(2,ISTACT)=SACRD(2,ISTACT)
      SXYZ(3,ISTACT)=SACRD(3,ISTACT)
!   GET GEODETIC QUANTITIES INCLUDING PARTIALS IF MASTER STATION
!     write(6,*)' dbg STACRD STAINF', STAINF(14,ISTACT),
!    .        STAINF(17,ISTACT),ISTACT
      CALL PLHOUT(SXYZ(1,ISTACT),STAINF(1,ISTACT),STAINF(2,ISTACT),     &
     &            STAINF(3,ISTACT),STAINF(4,ISTACT),                    &
     &            STAINF(5,ISTACT),STAINF(6,ISTACT),STAINF(7,ISTACT),   &
     &            DPSDXS(1,1,1),LNSTAE,.FALSE.)
      IF(LNSTAE) GO TO 500
      DXSDPM(1,1,ISTACT)=ONE
      DXSDPM(2,1,ISTACT)=ZERO
      DXSDPM(3,1,ISTACT)=ZERO
      DXSDPM(1,2,ISTACT)=ZERO
      DXSDPM(2,2,ISTACT)=ONE
      DXSDPM(3,2,ISTACT)=ZERO
      DXSDPM(1,3,ISTACT)=ZERO
      DXSDPM(2,3,ISTACT)=ZERO
      DXSDPM(3,3,ISTACT)=ONE
!   GET GEODETIC COVARIANCES
!   DPSDXS(1,1,1) CONTAINS PARTIALS OF XYZ WRT PHI,LAMDA,H
!   GET PARTIALS OF PHI,LAMDA,H WRT XYZ
      CALL DNVERT(3,DPSDXS(1,1,1),3,ITEMP)
!   GET PARTIALS OF XYZ WRT CYLIND COORDINATES
      CALL CYLIND(SXYZ(1,ISTACT),TEMP,DPSDXS(1,1,2),.FALSE.,.FALSE.)
!   DPSDXS CONTAINS PARTIALS OF XYZ WRT CYLIND
!   GET PARTIALS OF CYLIND WRT XYZ
      CALL DNVERT(3,DPSDXS(1,1,2),3,ITEMP)
      IF(.NOT.LNMSTA) GO TO 110
! GET XYZ COVARIANCES OF SLAVE STAION FROM MASTER (DPSDPM IS IDENTITY)
      IICON=ICON(ISTACT)
      SPSIG(1,2,ISTACT)=SPSIG(1,2,IICON)
      SPSIG(2,2,ISTACT)=SPSIG(2,2,IICON)
      SPSIG(3,2,ISTACT)=SPSIG(3,2,IICON)
      SPSIG(4,2,ISTACT)=SPSIG(4,2,IICON)
      SPSIG(5,2,ISTACT)=SPSIG(5,2,IICON)
      SPSIG(6,2,ISTACT)=SPSIG(6,2,IICON)
  110 CONTINUE
!   GET COVARIANCES FOR STATIONS:GEODETIC&CYLIND
      CALL ATWA3(DPSDXS(1,1,1),SPSIG(1,2,ISTACT),SPSIG(1,1,ISTACT))
      CALL ATWA3(DPSDXS(1,1,2),SPSIG(1,2,ISTACT),SPSIG(1,3,ISTACT))
      GO TO 500
!
!
!   ADJUSTMENT IS GEODETIC
  200 CONTINUE
!   GET BASIC GEODETIC QUANTITIES FOR STAINF ARRAY
      STAINF(3,ISTACT)=SIN(SACRD(1,ISTACT))
      STAINF(2,ISTACT)=SQRT(ONE-STAINF(3,ISTACT)*STAINF(3,ISTACT))
      STAINF(5,ISTACT)=COS(SACRD(2,ISTACT))
      STAINF(6,ISTACT)=SIN(SACRD(2,ISTACT))
      STAINF(1,ISTACT)=SACRD(1,ISTACT)
      STAINF(4,ISTACT)=SACRD(2,ISTACT)
      STAINF(7,ISTACT)=SACRD(3,ISTACT)
!   GET XYZ
      XN=AEG/SQRT(ONE-EGSQ*STAINF(3,ISTACT)*STAINF(3,ISTACT))
      SXYZ(1,ISTACT)=(XN+STAINF(7,ISTACT))*STAINF(2,ISTACT)             &
     &              *STAINF(5,ISTACT)
      SXYZ(2,ISTACT)=(XN+STAINF(7,ISTACT))*STAINF(2,ISTACT)             &
     &              *STAINF(6,ISTACT)
      SXYZ(3,ISTACT)=(XN*(ONE-EGSQ)+STAINF(7,ISTACT))*STAINF(3,ISTACT)
      IF(LNSTAE) GO TO 500
!   GET XYZ/GEODETIC PARTIALS IF ADJUSTED
      CALL PLHOUT(SXYZ(1,ISTACT),STAINF(1,ISTACT),STAINF(2,ISTACT),     &
     &            STAINF(3,ISTACT),STAINF(4,ISTACT),                    &
     &            STAINF(5,ISTACT),STAINF(6,ISTACT),STAINF(7,ISTACT),   &
     &            DXSDPM(1,1,ISTACT),.FALSE.,.TRUE.)
!   GET PARTIALS OF XYZ WRT CYLIND COORDINATES
      CALL CYLIND(SXYZ(1,ISTACT),TEMP,DPSDXS(1,1,2),.FALSE.,.FALSE.)
!   DPSDXS(1,1,2) CONTAINS PARTIALS OF XYZ WRT CYLIND
!   GET PARTIALS OF CYLIND WRT XYZ
      CALL DNVERT(3,DPSDXS(1,1,2),3,ITEMP)
      IF(.NOT.LNMSTA) GO TO 210
!   FOR SLAVE STATIONS AT THIS POINT,DXSDPM ACTUALLY CONTAINS DXSDPS.
!   NEED TO GET THE REAL DXSDPM
!   SET CONSTANTS NEED FOR DPSDPM MATRX;NEED GEOCENTRIC LATITUDE
!   MULTPLY DPSDPM*DXSDPS TO GET DXSDPM
      DO 205 J=1,3
      DO 205 I=1,2
      DXSDPS(I,J)=DXSDPM(I,J,ISTACT)
  205 CONTINUE
      SINLM=STAINF(6,ICON(ISTACT))
      COSLM=STAINF(5,ICON(ISTACT))
      TANLC=STAINF(6,ISTACT)/STAINF(5,ISTACT)
      TANLC2=TANLC*TANLC
      TANFC=SXYZ(3,ISTACT)/SQRT(SXYZ(1,ISTACT)*SXYZ(1,ISTACT)          &
     &   +SXYZ(2,ISTACT)*SXYZ(2,ISTACT))
      COSLC=STAINF(5,ISTACT)
      COSLMC=STAINF(4,ICON(ISTACT))-STAINF(4,ISTACT)
      COSLMC=COS(COSLMC)
!   CORRECT THE FIRST TWO ELEMENTS IN FIRST ROW OF DPSDPM
      DPSDPM(1,2,ISTACT)=(SINLM-COSLM*TANLC)*TANFC/((ONE+TANLC2)*       &
     &                    COSLC)
      DPSDPM(1,1,ISTACT)=COSLMC
!   CORRECT THE AFFECTED POSITIONS INE DXSDPM
      DXSDPM(1,1,ISTACT)=DPSDPM(1,1,ISTACT)*DXSDPS(1,1)                 &
     &                  +DPSDPM(1,2,ISTACT)*DXSDPS(2,1)
      DXSDPM(1,2,ISTACT)=DPSDPM(1,1,ISTACT)*DXSDPS(1,2)                 &
     &                  +DPSDPM(1,2,ISTACT)*DXSDPS(2,2)
      DXSDPM(1,3,ISTACT)=DPSDPM(1,1,ISTACT)*DXSDPS(1,3)                 &
     &                  +DPSDPM(1,2,ISTACT)*DXSDPS(2,3)
      IF(.NOT.LNMSTA) GO TO 210
!   GET COVARIANCES IN GEODETIC FOR SLAVE FROM MASTER USE DPSDPM
      IICON=ICON(ISTACT)
      CALL ATWA3(DPSDPM(1,1,ISTACT),SPSIG(1,1,IICON),SPSIG(1,1,ISTACT))
  210 CONTINUE
! GET XYZ &CYLIND  COVARIANCES
      CALL ATWA3(DXSDPM(1,1,ISTACT),SPSIG(1,1,ISTACT),SPSIG(1,2,ISTACT))
      CALL ATWA3(DPSDXS(1,1,2),SPSIG(1,2,ISTACT),SPSIG(1,3,ISTACT))
      GO TO 500
!
! ADJUSTMENT IS CYLINDRICAL
  300 CONTINUE
!   GET XYZ
      SXYZ(1,ISTACT)=SACRD(1,ISTACT)*COS(SACRD(2,ISTACT))
      SXYZ(2,ISTACT)=SACRD(1,ISTACT)*SIN(SACRD(2,ISTACT))
      SXYZ(3,ISTACT)=SACRD(3,ISTACT)
!   GET GEODETIC QUANTITIES INCLUDING PARTIALS IF MASTER STATION
      CALL PLHOUT(SXYZ(1,ISTACT),STAINF(1,ISTACT),STAINF(2,ISTACT),     &
     &            STAINF(3,ISTACT),STAINF(4,ISTACT),                    &
     &            STAINF(5,ISTACT),STAINF(6,ISTACT),STAINF(7,ISTACT),   &
     &            DPSDXS(1,1,1),LNSTAE,.FALSE.)
      IF(LNSTAE) GO TO 500
!   DPSDXS(1,1,1) CONTAINS PARTIALS OF XYZ WRT PHI,LAMDA,H
!   GET PARTIALS OF PHI,LAMDA,H WRT XYZ
      CALL DNVERT(3,DPSDXS(1,1,1),3,ITEMP)
!   GET PARTIALS OF XYZ WRT CYLINDRICAL
      CALL CYLIND(SXYZ(1,ISTACT),SACRD(1,ISTACT),DXSDPM(1,1,ISTACT),    &
     &            .FALSE.,.TRUE.)
      IF(.NOT.LNMSTA) GO TO 310
! GET CYLINDRICAL SLAVE COVARIANCES FROM MASTER (DPSDPM IS IDENTITY)
      IICON=ICON(ISTACT)
      SPSIG(1,3,IICON)=SPSIG(1,3,ISTACT)
      SPSIG(2,3,IICON)=SPSIG(2,3,ISTACT)
      SPSIG(3,3,IICON)=SPSIG(3,3,ISTACT)
      SPSIG(4,3,IICON)=SPSIG(4,3,ISTACT)
      SPSIG(5,3,IICON)=SPSIG(5,3,ISTACT)
      SPSIG(6,3,IICON)=SPSIG(6,3,ISTACT)
  310 CONTINUE
!    GET XYZ AND GEODETIC  COVARIANCES
      CALL ATWA3(DXSDPM(1,1,ISTACT),SPSIG(1,3,ISTACT),SPSIG(1,2,ISTACT))
!   GET GEODETIC COVARIANCES
      CALL ATWA3(DPSDXS(1,1,1),SPSIG(1,2,ISTACT),SPSIG(1,1,ISTACT))
!
  500 CONTINUE
!
!   CALCULATE LOCAL NORTH&EAST
!
!   CALCULATE LOCAL VERTICLE FIRST
      XLOCV(1,3,ISTACT)=STAINF(2,ISTACT)*STAINF(5,ISTACT)
      XLOCV(2,3,ISTACT)=STAINF(2,ISTACT)*STAINF(6,ISTACT)
      XLOCV(3,3,ISTACT)=STAINF(3,ISTACT)
!   CALCULATE LOCAL NORTH
      XLOCV(1,2,ISTACT)=-STAINF(3,ISTACT)*STAINF(5,ISTACT)
      XLOCV(2,2,ISTACT)=-STAINF(3,ISTACT)*STAINF(6,ISTACT)
      XLOCV(3,2,ISTACT)=STAINF(2,ISTACT)
!   CALCULATE LOCAL EAST
      XLOCV(1,1,ISTACT)=-STAINF(6,ISTACT)
      XLOCV(2,1,ISTACT)=STAINF(5,ISTACT)
      XLOCV(3,1,ISTACT)=ZERO

!
!   CALCULATE PLATE QUANITIES IF PLATE OPTION IS USED
!     IF(NPLATE.LE.0) GO TO 600
      IPLATE=STAINF(13,ISTACT)+EPS
      IF(IPLATE.GT.0) GO TO 505
      PLTVEL(1,ISTACT)=ZERO
      PLTVEL(2,ISTACT)=ZERO
      PLTVEL(3,ISTACT)=ZERO
      GO TO 600
  505 CONTINUE
      OMEGDT=PLTINF(3,IPLATE)/SECPML
      DLATPT=PLTINF(1,IPLATE)
      DLONPT=PLTINF(2,IPLATE)
      SINLTP=SIN(DLATPT)
      COSLTP=COS(DLATPT)
      SINLNP=SIN(DLONPT)
      COSLNP=COS(DLONPT)
      XDOT=COSLNP*SXYZ(1,ISTACT)+SINLNP*SXYZ(2,ISTACT)
      XDOT=(XDOT*SINLTP-COSLTP*SXYZ(3,ISTACT))*OMEGDT
      YDOT=(-SINLNP*SXYZ(1,ISTACT)+COSLNP*SXYZ(2,ISTACT))*OMEGDT
      PLTVEL(1,ISTACT)=-SINLNP*XDOT-SINLTP*COSLNP*YDOT
      PLTVEL(2,ISTACT)=COSLNP*XDOT-SINLTP*SINLNP*YDOT
      PLTVEL(3,ISTACT)=COSLTP*YDOT
  600 CONTINUE
!  ON FISRT CALL PRINT OUT TRACKING COMPLIMENT AND STORE SOME QUANTITIES
      IF(LNFRST) GO TO 10
!
!   PRINT OUT STATION COORDINATES AND SIGMAS
      JLINE6=ILINE6+2
      IF(JLINE6.LE.MLINE6) GO TO 605
      IPAGE6=IPAGE6+1
      JLINE6=5
      WRITE(IOUT6,10000) IPAGE6
  605 CONTINUE
      ILINE6=JLINE6
      CALL OUTRAD(STAINF(1,ISTACT),ILATD,ILATM,SLAT,.FALSE.)
      CALL OUTRAD(STAINF(4,ISTACT),ILOND,ILONM,SLON,.TRUE.)
      SAD=SXYZ(1,ISTACT)*SXYZ(1,ISTACT)+SXYZ(2,ISTACT)*SXYZ(2,ISTACT)
      SAD=SQRT(SAD)
      IF(LNSTAE) WRITE(IOUT6,10200) STNAME(ISTACT),ISTANO(ISTACT),      &
     &                   ILATD,ILATM,SLAT,                              &
     &                   ILOND,ILONM,SLON,                              &
     &                   STAINF(7,ISTACT),                              &
     &                   (SXYZ(J,ISTACT),J=1,3),SAD
      IF(LNSTAE) GO TO 10
      SPPSIG(1)=SQRT(SPSIG(1,1,ISTACT))/SECRAD
      SPPSIG(2)=SQRT(SPSIG(4,1,ISTACT))/SECRAD
      SPPSIG(3)=SQRT(SPSIG(6,1,ISTACT))
      SPPSIG(4)=SQRT(SPSIG(1,2,ISTACT))
      SPPSIG(5)=SQRT(SPSIG(4,2,ISTACT))
      SPPSIG(6)=SQRT(SPSIG(6,2,ISTACT))
      SPPSIG(7)=SQRT(SPSIG(1,3,ISTACT))
      WRITE(IOUT6,10300) STNAME(ISTACT),ISTANO(ISTACT),                 &
     &                   ILATD,ILATM,SLAT,SPPSIG(1),                    &
     &                   ILOND,ILONM,SLON,SPPSIG(2),                    &
     &                   STAINF(7,ISTACT),SPPSIG(3),                    &
     &                   SXYZ(1,ISTACT)  ,SPPSIG(4),                    &
     &                   SXYZ(2,ISTACT)  ,SPPSIG(5),                    &
     &                   SXYZ(3,ISTACT)  ,SPPSIG(6),                    &
     &                   SAD             ,SPPSIG(7)
! FILL UP STATION APRIORI VALUE ARRAY
      STAPRI(1,ISTACT)=STAINF(1,ISTACT)
      STAPRI(2,ISTACT)=STAINF(4,ISTACT)
      STAPRI(3,ISTACT)=STAINF(7,ISTACT)
      STAPRI(4,ISTACT)=SXYZ(1,ISTACT)
      STAPRI(5,ISTACT)=SXYZ(2,ISTACT)
      STAPRI(6,ISTACT)=SXYZ(3,ISTACT)
      STAPRI(7,ISTACT)=SAD
      DO 650 I=1,3
      IIP=8+(I-1)*6
      STAPRI(IIP  ,ISTACT)=SQRT(SPSIG(1,I,ISTACT))
      STAPRI(IIP+3,ISTACT)=SQRT(SPSIG(4,I,ISTACT))
      STAPRI(IIP+5,ISTACT)=SQRT(SPSIG(6,I,ISTACT))
      STAPRI(IIP+1,ISTACT)=SPSIG(2,I,ISTACT)/(STAPRI(IIP  ,ISTACT)*     &
     &                     STAPRI(IIP+3,ISTACT))
      STAPRI(IIP+2,ISTACT)=SPSIG(3,I,ISTACT)/(STAPRI(IIP  ,ISTACT)*     &
     &                     STAPRI(IIP+5,ISTACT))
      STAPRI(IIP+4,ISTACT)=SPSIG(5,I,ISTACT)/(STAPRI(IIP+3,ISTACT)*     &
     &                     STAPRI(IIP+5,ISTACT))
  650 END DO
      STAPRI(8,ISTACT)=STAPRI(8,ISTACT)/SECRAD
      STAPRI(11,ISTACT)=STAPRI(11,ISTACT)/SECRAD
      STAPRI(23,ISTACT)=STAPRI(23,ISTACT)/SECRAD
      GO TO 10
10000 FORMAT('1',56X,'TRACKING COMPLEMENT',34X,'UNIT  6 PAGE NO.',I6/   &
     & '0',5X,'STATION',6X,'GEODETIC LATITUDE',3X,                      &
     & 'EAST LONGITUDE',5X,'SPHEROID HEIGHT',                           &
     & 5X,'ECF  X',9X,'ECF  Y',9X,'ECF  Z',9X,'S.A.D.'/                 &
     & 3X,'NAME',4X,'NUMBER',1X,2(1X,'DEG MN SECONDS SIG.'),            &
     & 2X,'METERS  SIG.',4(2X,'METERS   SIG.'))
10200 FORMAT(1X,A8,1X,I8,2(1X,2I3,F8.4,1X, 4X ),F9.3,1X, 4X ,           &
     &   3(F10.1,1X, 4X ),1X,F9.1,1X, 4X )
10300 FORMAT(1X,A8,1X,I8,2(1X,2I3,F8.4,1X,F4.1),F9.3,1X,F4.0,           &
     &   3(F10.1,1X,F4.0),1X,F9.1,1X,F4.0)
      END
