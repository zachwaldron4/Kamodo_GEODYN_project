!$SOLRAT
      SUBROUTINE SOLRAT(AEBOD,ISHDPT,FLATCX,RSTSN,RSTSN2,RSTCI,RSTCI2,  &
     &                  XSAT,RATIO)
!**********************************************************************
! VERSION 8701             DATE 2/3/87       D. ROWLANDS
!
! PURPOSE       CALCULATE THE PERCENT SUNLIGHT THAT A SATELLITE
!               EXPERIENCES DUE TO SHADOWING
!
! INPUT         AEBOD -ARRAY CONTAINING RADIUS OF THE SUN FOLLOWED
!                      BY RADII OF BODIES THAT MIGHT OBSCURE THE
!                      SUN FROM THE SATELLITE
!               ISHDPT-ARRAY CONTAINING POINTERS TO THE BDTRUE
!                      ARRAY IN COMMON CBDSAT FOR BODIES THAT MIGHT
!               FLATCI-FLATTENING OF CENTER OF INTEGRATION
!               RSTSN -DISTANCE FROM SATELLITE TO SUN
!               RSTSN2-RSTSN*RSTSN
!               RSTCI -DISTANCE FROM SATELLITE TO CENTER OF INTEGRATION
!               RSTCI2-RSTCI*RSTCI
!               XSAT-ARRAY CONTAINING CENTER OF INTEGRATION
!                      POSITION OF SATELLITE IN TRUE OF DATE
!
! OUTPUT        RATIO -PERCENT OF THE SUN THAT THE SATELLITE SEES
!**********************************************************************
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/BWLOGC/LVAREA,LTOPEX,LSPOT2,LGPSVA,LERS1,LMO,LMOC,LTDRSA,  &
     &              LMAGNA,LGFO,LTRMM,LEUVE,LVCL,LENVS,LCRYO,LGRAIL,    &
     &              LHY2A,LSARAL
      COMMON/BWREAL/SHADOW(2),SUNPCT,SCAREA,SCMASS,BWMEAN
      COMMON/CSHAD/NSBDY,NSBDY1,NXSHAD
      COMMON/CBDTRU/BDTRUE(7,999)
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/LICASE/LINT(2),LCASE(18),NXLCAS
      COMMON/TOPEXA/BETAP,PRVBET,SOMEGA,PRVOMG,YAWANG,SGAMMA,           &
     &              BETAMX,TDLOUV(8,3)
      COMMON/STARTT/ESSTRT,FSSTRT
      COMMON/VRBLOK/SINPSI,COSPSI,XLAMDA,GMR,AOR
      COMMON/XPRNT/ISATIX
!!      DIMENSION AEBOD(NSBDY1),ISHDPT(NSBDY),XSAT(3) ! original
                                                      ! jjm 8/98
      DIMENSION AEBOD(NSBDY1),ISHDPT(NSBDY1),XSAT(3)
! FIXED ARRAYS THAT DEPEND ON THE MAXIMUM NUMBER OF BODIES (MSBDY)
      DIMENSION RSTCX2(5),RSTCX(5),AX(5),LSSHD(6),LBSHD(5,5)
      DATA MSBDY/5/
!
      DATA ZERO/0.D0/,ONE/1.D0/,TWO/2.D0/,THREE/3.D0/
      DATA HALF/0.5D0/
!
!***********************************************************************
! START OF EXECQTABLE CODE *********************************************
!***********************************************************************
!
!C    WRITE(6,*) 'SOLRAT: AEBOD,ISHDPT,FLATCX ',
!C   1                    AEBOD,ISHDPT,FLATCX
!C    WRITE(6,*) 'SOLRAT: RSTSN,RSTSN2,RSTCI,RSTCI2 ',
!C   1                    RSTSN,RSTSN2,RSTCI,RSTCI2
!C    WRITE(6,*) 'SOLRAT: XSAT,RATIO ', XSAT, RATIO
      RATIOC=RATIO
      LTOP=.FALSE.
      LSOL=.FALSE.
      IF(LCASE(12).OR.LCASE(17).OR.LCASE(16))LSOL=.TRUE.
      IF(LINT(1).AND.LSOL)LTOP=.TRUE.
!****************TEMP IN 8704 MAKE SPHERICAL
      FLATCI=ZERO
!****************
!
! IN CASE OF SOLAR RADIATION APPLICATION WHERE THE SUN IS CONSIDERED
! AS POINT MASS IN THE INTERCOMPARISON WE MUST REDEFINE THE RADIUS
! OF THE SUN HERE
      IF(.NOT.LTOP) GOTO 5
      AEBOD(1)=696000000.D0
    5 CONTINUE
!C    WRITE(6,*) 'SOLRAT: MSBDY ', MSBDY
      DO 10 J=1,MSBDY
      DO 10 I=1,MSBDY
      LBSHD(I,J)=.FALSE.
   10 CONTINUE
      LXSECT=.FALSE.
      RATIO=ONE
!
! SHADOWING DUE TO CENTER OF INTEGRATION ALWAYS DONE
! DO IT SEPARATELY FROM OTHER BODIES.
!   UNLIKE OTHER BODIES GET AN EFFECTIVE RADIUS DUE TO
!   ATMOSPHERE AND ELLIPSOIDAL SHAPE.
!
!
! CALCULATE RANGE**2  FROM CENTER OF INTEGRATION TO SUN
!
      RCISN2=BDTRUE(1,8)*BDTRUE(1,8)+BDTRUE(2,8)*BDTRUE(2,8)            &
     &    +BDTRUE(3,8)*BDTRUE(3,8)
!
! CALCULATE ANGLE BETWEEN SUN AND CENTER OF INTEGRATION FROM SATELLITE
!
!CC      ASI=DARCOS((RSTSN2+RSTCI2-RCISN2)/(TWO*RSTSN*RSTCI))
      ASI=ACOS((RSTSN2+RSTCI2-RCISN2)/(TWO*RSTSN*RSTCI))
!
! CALCULATE ANGULAR RADIUS OF CIG AND ANGULAR RADIUS OF THE SUN
!
!
!     ....FLATCI IS ALWAYS ZERO, SO SIMPLIFY CALCULATION
!
!CCC  AI = ABS(AEBOD(2)*(ONE-FLATCI*DABS(SINPSI))/RSTCI)
      AI = ABS(AEBOD(2)                          /RSTCI)
      IF( AI .GT. ONE ) THEN
         WRITE(6,*) 'SOLRAT: AI GT ONE ', AI
         WRITE(6,*) 'SOLRAT: AEBOD(2),ONE, FLATCI ',                    &
     &                       AEBOD(2),ONE, FLATCI
         WRITE(6,*) 'SOLRAT: SINPSI,  RSTCI ',                          &
     &                       SINPSI,  RSTCI
         WRITE(6,*) 'SOLRAT: AEBOD(1),RSTSN ',                          &
     &                       AEBOD(1),RSTSN
         WRITE(6,*) 'SOLRAT: CENTRAL BODY RADIUS: ', AEBOD(2)
         WRITE(6,*) '  LESS THAN DISTANCE TO SATELLITE FROM CB: ',      &
     &              RSTCI
         AI =  ONE
         WRITE(6,*) 'SOLRAT: AI SET EQUAL TO ONE '
      WRITE(6,78912) ISATIX,FSSTRT
78912 FORMAT(' PROBLEM OCCURS WITH SAT ID ',I10,' AT ',E20.11)
       stop 69
      ENDIF
!CC      AI=DARSIN( AI )
      AI=ASIN( AI )
!
      AS= ABS(AEBOD(1)/RSTSN)
      IF( AS .GT. ONE ) THEN
         WRITE(6,*) 'SOLRAT: AS GT ONE ', AS
         WRITE(6,*) 'SOLRAT: AEBOD(2),ONE, FLATCI ',                    &
     &                       AEBOD(2),ONE, FLATCI
         WRITE(6,*) 'SOLRAT: SINPSI,  RSTCI ',                          &
     &                       SINPSI,  RSTCI
         WRITE(6,*) 'SOLRAT: AEBOD(1),RSTSN ',                          &
     &                       AEBOD(1),RSTSN
         WRITE(6,*) 'SOLRAT: RADIUS OF SUN: ', AEBOD(2)
         WRITE(6,*) '  LESS THAN DISTANCE TO SATELLITE FROM SUN: ',     &
     &              RSTSN
         AS =  ONE
         WRITE(6,*) 'SOLRAT: AS SET EQUAL TO ONE '
      ENDIF
!CC      AS=DARSIN( AS )
      AS=ASIN( AS )
      AS2=AS*AS
!C    WRITE(6,*) 'SOLRAT: AI, AS ', AI, AS
!
!     IF ASI>AS+AI   NO SHADOWING FROM CENTER OF INTEGRATION
!
      LSSHD(1)=.FALSE.
      TESTR=AS+AI
      IF(TESTR.LE.ASI) GO TO 500
      LSSHD(1)=.TRUE.
      AI2=AI*AI
!
! CALCULATE FRACTION OF SUNLIGHT
!
      CALL SAREA(AI,AS,ASI,AI2,AS2,AREA,LTOT)
      RATIO=RATIO-AREA
      RATIOC = RATIO
! IF TOTAL ECLIPSE BY ANY ONE BODY RETURN
      IF(LTOT) THEN
        GOTO 2500
      ENDIF
!
!
! NOW GET THE REST OF THE BODIES
  500 CONTINUE
!C    WRITE(6,*) 'SOLRAT: NSBDY ', NSBDY
      IF(NSBDY.LE.1) THEN
       GOTO 2500
      ENDIF
!
      AX(1)=AI
      RSTCX2(1)=RSTCI2
      RSTCX(1)=RSTCI
!
!
      DO 2000 ISBDY=2,NSBDY
!C    WRITE(6,*) 'SOLRAT: ISBDY ', ISBDY
      IPTT=ISHDPT(ISBDY)
!
! GET RANGE FROM SATELLITE TO BODY
      RSTCX2(ISBDY)=(BDTRUE(1,IPTT)-XSAT(1))**2                         &
     &             +(BDTRUE(2,IPTT)-XSAT(2))**2                         &
     &             +(BDTRUE(3,IPTT)-XSAT(3))**2
      RSTCX(ISBDY)=SQRT(RSTCX2(ISBDY))
!
! GET RANGE**2  FROM BODY TO SUN
      RCXSN2=(BDTRUE(1,IPTT)-BDTRUE(1,8))**2                            &
     &      +(BDTRUE(2,IPTT)-BDTRUE(2,8))**2                            &
     &      +(BDTRUE(3,IPTT)-BDTRUE(3,8))**2
!
! CALCULATE ANGLE BETWEEN SUN AND BODY FROM SATELLITE
!
!CC   ASX=DARCOS((RSTSN2+RSTCX2(ISBDY)-RCXSN2)/(TWO*RSTSN*RSTCX(ISBDY)))
      ASX=ACOS((RSTSN2+RSTCX2(ISBDY)-RCXSN2)/(TWO*RSTSN*RSTCX(ISBDY)))
!
! CALCULATE ANGULAR RADIUS OF BODY
!
      AX(ISBDY)=   ABS(AEBOD(ISBDY+1)/RSTCX(ISBDY))
!
      IF( AX(ISBDY) .GT. ONE ) THEN
         WRITE(6,*) 'SOLRAT: AX(ISBDY) GT ONE ', AX(ISBDY)
         WRITE(6,*) 'SOLRAT: ISBDY, AEBOD(ISBDY+1) ',                   &
     &                       ISBDY, AEBOD(ISBDY+1)
         WRITE(6,*) 'SOLRAT: RSTCX(ISBDY) ',                            &
     &                       RSTCX(ISBDY)
         WRITE(6,*) 'SOLRAT: RADIUS OF SHADOWING BODY: ',               &
     &              AEBOD(ISBDY+1)
         WRITE(6,*) '  LESS THAN DISTANCE TO SATELLITE FROM BODY: ',    &
     &              RSTCX(ISBDY)
         AX(ISBDY) =  ONE
         WRITE(6,*) 'SOLRAT: AX(ISBDY) SET EQUAL TO ONE '
      ENDIF
!CC      AX(ISBDY) = DARSIN( AX(ISBDY) )
      AX(ISBDY) = ASIN( AX(ISBDY) )
!
!     IF ASX>AS+AX   NO SHADOWING FROM BODY
!
      LSSHD(ISBDY)=.FALSE.
      TESTR=AS+AX(ISBDY)
      IF(TESTR.LE.ASX) GO TO 2000
      LSSHD(ISBDY)=.TRUE.
      AX2=AX(ISBDY)*AX(ISBDY)
      CALL SAREA(AX(ISBDY),AS,ASX,AX2,AS2,AREA,LTOT)
      RATIO=RATIO-AREA
      IF(.NOT.LTOT) GO TO 1000
! IF TOTAL ECIPSE BY ANY ONE BODY RETURN
! MAKE SURE RATIO AT LEAST ZERO FIRST
      RATIO=ZERO
      GOTO 2500
!
! CHECK FOR INTERSECTIONS BETWEEN BODY AND PREVIOUS BODIES
 1000 CONTINUE
      JMBDY=ISBDY-1
      DO 1500 JSBDY=1,JMBDY
      IF(.NOT.LSSHD(JSBDY)) GO TO 1500
!
! AT THIS POINT HAVE 2 BODIES THAT INTERSECT WITH SUN
! CHECK TO SEE IF BODYES INTERSECTS WITH ONE ANOTHER
!
! RANGE**2 BETWEEN 2 BODIES
      JPTT=ISHDPT(JSBDY)
      RX1X2=(BDTRUE(1,IPTT)-BDTRUE(1,JPTT))**2                          &
     &     +(BDTRUE(2,IPTT)-BDTRUE(2,JPTT))**2                          &
     &     +(BDTRUE(3,IPTT)-BDTRUE(3,JPTT))**2
!
! ANGLE BETWEEN TWO BODIES FROM SATELLITE
!CC      AX12=DARCOS((RSTCX2(JSBDY)+RSTCX2(ISBDY)-RX1X2)
      AX12=ACOS((RSTCX2(JSBDY)+RSTCX2(ISBDY)-RX1X2)                    &
     &             /(TWO*RSTCX(JSBDY)*RSTCX(ISBDY)))
!
!     IF AX12>AX(ISBDY)+AX(JSBDY) NO INTERSECTION BETWEEN BODIES
!
      TESTR=AX(ISBDY)+AX(JSBDY)
      IF(TESTR.GT.AX12) LBSHD(ISBDY,JSBDY)=.TRUE.
      LBSHD(JSBDY,ISBDY)=.TRUE.
      LXSECT=LXSECT.OR.LBSHD(ISBDY,JSBDY)
 1500 END DO
 2000 END DO
      AREADD=ZERO
      IF(LXSECT) CALL DBAREA(NSBDY,nsbdy1,MSBDY,LBSHD,                  &
     &                       XSAT,AS,AX,AEBOD,ISHDPT,                   &
     &                       AREADD)
      RATIO=RATIO+AREADD
 2500 CONTINUE
!  COMPUTATIONS FOR TOPEX THERMAL IMBALANCE ACCELERATION MODEL
       IF(LTOPEX) THEN
!  CHECK FOR SHADOW CROSSING (50% ILLUMINATION BOUNDARY)
        DELTA1 = SUNPCT
        SUNPCT = RATIOC-HALF
        SIGN   = SUNPCT*DELTA1
! IF SATELLITE HAS PASSED A SHADOW BOUNDARY OR THIS IS THE FIRST
! CALL TO SOLRAT(IN WHICH CASE SHADOW MUST BE INITIALIZED), THEN
! COMPUTE SHADOW ENTRY AND EXIT ANGLES BASED ON CYLINDRICAL MODEL.
! THIS CALCULATION ASSUMES A CIRCULAR ORBIT AND USES CURRENT
! POSITION VECTORS(THESE WILL BE OFF BY AS MUCH AS ONE STEP OF THE
! INTEGRATOR).
        IF(SIGN .LT. ZERO .OR. SHADOW(1) .EQ. ZERO) THEN
!   COMPUTE ORBIT ANGLE AT SHADOW CROSSING
          VAL = SQRT(ONE-AEBOD(2)**2/RSTCI2)/COS(BETAP)
          IF(ABS(VAL).GT.ONE) THEN
             SHADOW(1) = THREE*PI/TWO
             SHADOW(2) = THREE*PI/TWO
             RETURN
          ELSE
             TCROSS = ACOS(VAL)
          ENDIF
!   COMPUTE SHADOW CROSSING FOR BOUNDARY JUST CROSSED (ENTRY OR EXIT)
!   OR COMPUTE BOTH IF THIS IS THE FIRST CALL
!   NOTE: COORDINATE CONVENTION IS BASED ON TOPEX SYSTEM IN WHICH
!   0 IS 90 DEG FROM SUN VECTOR.
          PITM15 = THREE*PI/TWO
          IF(SHADOW(1).EQ.ZERO.AND.SHADOW(2).EQ.ZERO) THEN
            SHADOW(1) = PITM15+TCROSS
            SHADOW(2) = PITM15-TCROSS
          ELSE IF(SUNPCT.LT.ZERO) THEN
            SHADOW(2) = PITM15-TCROSS
          ELSE
            SHADOW(1) = PITM15+TCROSS
          ENDIF
! ......SIGN ENDIF
          ENDIF
! ......LVAREA ENDIF
       ELSE
        SUNPCT=RATIO-HALF
        ENDIF
      RETURN
      END
