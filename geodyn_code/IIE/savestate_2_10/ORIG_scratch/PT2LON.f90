      SUBROUTINE PT2LON(NM,NTOLFR,COSLAT,SINLAT,COSLON,SINLON,          &
     &                  COSANG,SINANG,PXSPHL)
!********1*********2*********3*********4*********5*********6*********7**
! FUNCTION: THIS IS THE COMPANION ROUTINE TO ST2LON. IT COMPUTES
!           THE PARTIALS FOR THE PARAMETERS IN ST2LON. THESE ARE
!           THE DELTA R(IP), DELTA R(OP), DELTA T(IP) AND DELTA T(OP)
!           PARAMETERS FOR EACH OF THE LONG PERIOD FREQUENCIES
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   NM       I    S    NUMBER OF OBSERVATIONS IN THE BLOCK
!   NTOLFR   I    S    NUMBER OF FREQUENCIES
!   COSLAT   I    A    COSINES OF LATITUDES WHERE SET DESIRED
!   SINLAT   I    A    SINES OF LATITUDES WHERE SET DESIRED
!   COSLON   I    A    COSINES OF LONGITUDES WHERE SET DESIRED
!   SINLON   I    A    SINES OF LONGITUDES WHERE SET DESIRED
!   COSANG   I    A    COSINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   SINANG   I    A    SINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   PXSPHL   O    A    THE PARTIAL DERIVATIVES OF EARTH FIXED POSITION
!                      WRT DELTA R(IP), DELTA R(OP), DELTA T(IP)
!                      AND DELTA T (OP) FOR LONG PERIOD FREQUENCIES
!
! COMMENTS:  THIS SUBROUTINE WAS MODIFIED FROM SUBROUTINE STEP2LON
!            FROM DEHANT.
!
!
!********1*********2*********3*********4*********5*********6*********7**
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      DIMENSION COSLAT(NM),SINLAT(NM),COSLON(NM),SINLON(NM)
      DIMENSION COSANG(NM,NTOLFR),SINANG(NM,NTOLFR)
      DIMENSION PXSPHL(NM,3,4,NTOLFR)
!
!
!
      DO 500 K=1,NM
!
      SC2=2.D0*SINLAT(K)*COSLAT(K)
      P2=1.5D0*SINLAT(K)*SINLAT(K)-0.5D0
!
! NOTE PARMS SCALED TO M (ORIGINAL CODE MM)
!
      DO 100 J=1,NTOLFR
      DR1=P2*COSANG(K,J)
      DR2=P2*SINANG(K,J)
      DN3=SC2*COSANG(K,J)
      DN4=SC2*SINANG(K,J)
!
      PXSPHL(K,1,1,J)=DR1*COSLON(K)*COSLAT(K)
      PXSPHL(K,1,2,J)=DR2*COSLON(K)*COSLAT(K)
      PXSPHL(K,2,1,J)=DR1*SINLON(K)*COSLAT(K)
      PXSPHL(K,2,2,J)=DR2*SINLON(K)*COSLAT(K)
      PXSPHL(K,3,1,J)=DR1*SINLAT(K)
      PXSPHL(K,3,2,J)=DR2*SINLAT(K)
!
      PXSPHL(K,1,3,J)=-DN3*SINLAT(K)*COSLON(K)
      PXSPHL(K,1,4,J)=-DN4*SINLAT(K)*COSLON(K)
      PXSPHL(K,2,3,J)=-DN3*SINLAT(K)*SINLON(K)
      PXSPHL(K,2,4,J)=-DN4*SINLAT(K)*SINLON(K)
      PXSPHL(K,3,3,J)=DN3*COSLAT(K)
      PXSPHL(K,3,4,J)=DN4*COSLAT(K)
  100 END DO
!
  500 END DO
      RETURN
      END
