      SUBROUTINE SUMBRN(LSBURN,LFBURN,NEQN,NEQN3,IORDER,IOL1,IOL2,      &
     &                  IOVBAK,IBACKV,PXDDT3,SUMPX,CCP,CCV,HV,HV2,      &
     &                  VMATRX,XDDBRS,XDDBRF,JPSBRN)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  PURPOSE: INITUALIZW THE STARTING SUMS IN THE VARIATIONAL EQUATIONS
!           FOR THE TWO FINITE BURN PARMETERSTHAT DEAL WITH TIME
!
! BACKGROUND:
!          TWO OF THE BURN PARAMETERS, T0 AND TLEN (START TIME AND
!          LENGTH)  HAVE UNIQUE VARIATIONAL EQUATION INITIALIZATIONS.
!          THESE INITIALIZATIONS AFFECT THE INTEGRATION SUMS AND ARE
!          DONE IN THIS SUBROUTINE.  HERE IS A BRIEF DESCRIPTION.
!          AT THE START OF THE ARC, ALL FORCE MODEL PARAMETERS EXCEPT
!          THE SATELLITE STATE START FROM ZERO INITIAL VALUES. AT
!          THE START OF THE BURN, THE PARTIAL OF VELOCITY WRT THE T0
!          BURN PARAMETER IS SET TO THE NEGATIVE OF THE BURN
!          ACCELERATION (THE LATER THE BURN STARTS THE LESS THE
!          VELOCITY IS CHANGED BY THE STARTING ACCELERATION AND
!          dV=BURNACC*dt). AT THE END OF THE BURN,THE PARTIAL OF
!          VELOCITY WRT THE T0 BURN PARAMETER INCREMENTED BY THE
!          ENDING BURN VELOCITY (THE LATER THE BURN IS SHIFTED THE
!          MORE THE VELOCITY IS AFFECTED BY RGE ENDING ACCEL AND
!          dV=BURNACC*dt). AT THE END OF THE BURN THE PARTIAL OF
!          VELOCITY WRT THE TLEN PARAMETER IS SET TO THE ENDING
!          BURN ACCELERATION MUCH AS WAS DONE FOR T0.
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      DIMENSION  PXDDT3(NEQN,3,1),SUMPX(NEQN3,2,1)
      DIMENSION CCP(1),CCV(1)
      DIMENSION VMATRX(3,6)
      DIMENSION XDDBRS(3), XDDBRF(3)
      DIMENSION PDHOLD(3,20),PXHOLD(6),SUMPHD(6)
!!!
      IPSBRN=JPSBRN
      IF(LSBURN.AND.LFBURN) IPSBRN=JPSBRN-25
!!!
! IF LSBURN
! CALL TO UCSSS TO INITIALZE THE SUM FOR BURN T0
! THE PARTIAL OF VELOCITY IS INTIALIZED TO -XDDBRS
! USE ORBIT CORRECTOR COEFFICENTS
! NOTE THAT T0 HAS EXPLIT PARTIALS THAT NEED TO BE TAKEN
! INTO ACCOUNT
      IF(.NOT.LSBURN) GO TO 100
         DO I=1,3
           PXHOLD(I)=0.D0
           PXHOLD(I+3)=-XDDBRS(I)
         ENDDO
         DO I=1,IOl2
           PDHOLD(1,I)=PXDDT3(JPSBRN,1,IOVBAK-IOL1+I)
           PDHOLD(2,I)=PXDDT3(JPSBRN,2,IOVBAK-IOL1+I)
           PDHOLD(3,I)=PXDDT3(JPSBRN,3,IOVBAK-IOL1+I)
         ENDDO
         DO I=1,3
           PDHOLD(I,IOL1)=VMATRX(I,4)*PXHOLD(4)+VMATRX(I,5)*PXHOLD(5)   &
     &                   +VMATRX(I,6)*PXHOLD(6)
         ENDDO
         CALL UCSSS(PXHOLD,3,IORDER,IOL1,IOL2,CCP,CCV,HV,HV2,          &
     &              SUMPHD,PDHOLD)

         SUMPX(JPSBRN,1,IBACKV)=SUMPHD(1)
         SUMPX(JPSBRN,2,IBACKV)=SUMPHD(4)
         SUMPX(JPSBRN+NEQN,1,IBACKV)=SUMPHD(2)
         SUMPX(JPSBRN+NEQN,2,IBACKV)=SUMPHD(5)
         SUMPX(JPSBRN+2*NEQN,1,IBACKV)=SUMPHD(3)
         SUMPX(JPSBRN+2*NEQN,2,IBACKV)=SUMPHD(6)
  100 CONTINUE
      IF(.NOT.LFBURN) RETURN
!
!
! IF LFBURN
!
! INCREMENT THE VELOCITY PARTIAL OF T0 BY ENDING BURN ACCEL
! AND INTIALIZE THE VELOCITY PARTIAL OF TLEN BY ENDING BURN ACCEL
! SINCE THE VELOCITY PARTIAL OF T0 HAS ALREADY BEEN INITIALIZED
! AND INTEGRATED NO EXPLCIT PARTIALS ARE INVOLVED IN THIS PROCESS
! THE TLEN PARAMETER HAS NO EXPLICIT PARTIALS
!
! THIS APRROXIMATE CODE HAS BEEN UPGRADED
!!!      SUMPX(IPSBRN,2,IBACKV)=                                        &
!!!      &SUMPX(IPSBRN,2,IBACKV)+XDDBRF(1)/HV
!!!      SUMPX(IPSBRN+NEQN,2,IBACKV)=                                   &
!!!      &SUMPX(IPSBRN+NEQN,2,IBACKV)+XDDBRF(2)/HV
!!!      SUMPX(IPSBRN+2*NEQN,2,IBACKV)=                                 &
!!!     &SUMPX(IPSBRN+2*NEQN,2,IBACKV)+XDDBRF(3)/HV
!
!
         DO I=1,3
           PXHOLD(I)=0.D0
           PXHOLD(I+3)=XDDBRF(I)
         ENDDO
         DO I=1,IOL2
           PDHOLD(1,I)=0.D0
           PDHOLD(2,I)=0.D0
           PDHOLD(3,I)=0.D0
         ENDDO
         DO I=1,3
           PDHOLD(I,IOL1)=VMATRX(I,4)*PXHOLD(4)+VMATRX(I,5)*PXHOLD(5)   &
     &                   +VMATRX(I,6)*PXHOLD(6)
         ENDDO
         CALL UCSSS(PXHOLD,3,IORDER,IOL1,IOL2,CCP,CCV,HV,HV2,           &
     &              SUMPHD,PDHOLD)
         SUMPX(IPSBRN,2,IBACKV)=SUMPX(IPSBRN,2,IBACKV)+SUMPHD(4)
         SUMPX(IPSBRN+NEQN,2,IBACKV)=SUMPX(IPSBRN+NEQN,2,IBACKV)        &
     &                              +SUMPHD(5)
         SUMPX(IPSBRN+2*NEQN,2,IBACKV)=SUMPX(IPSBRN+2*NEQN,2,IBACKV)    &
     &                                +SUMPHD(6)
!
! IF LFBURN, ALSO INITIALIZE BURN TLEN. IN THIS CASE THE PARTIAL
! OF VELOCITY IS INTIALIZED TO ENDING BURN ACCELERATION, SO SUMPHD
! CAN BE REUSED, ESPECIALLY SINCE NEITHER CASE REQUIRES EXPLICIT PARTIALS
         SUMPX(IPSBRN+21,1,IBACKV)=SUMPHD(1)
         SUMPX(IPSBRN+21,2,IBACKV)=SUMPHD(4)
         SUMPX(IPSBRN+21+NEQN,1,IBACKV)=SUMPHD(2)
         SUMPX(IPSBRN+21+NEQN,2,IBACKV)=SUMPHD(5)
         SUMPX(IPSBRN+21+2*NEQN,1,IBACKV)=SUMPHD(3)
         SUMPX(IPSBRN+21+2*NEQN,2,IBACKV)=SUMPHD(6)
      RETURN
      END
