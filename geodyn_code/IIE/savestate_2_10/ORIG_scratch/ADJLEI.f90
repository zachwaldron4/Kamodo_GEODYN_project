!$ADJLEI
      SUBROUTINE ADJLEI(ISATNO,JSPACE,DELTA ,PKEPNS,SUM1  ,             &
     &    COVXYZ,COVLEI,XYZCUR,CKEPNS,AKEPNS,GMX   ,IEPYMD,IEPHM ,      &
     &    EPSEC )
!********1*********2*********3*********4*********5*********6*********7**
! ADJLEI           85/01/12            0000.0    PGMR - BILL EDDY
!
!
! FUNCTION: PRINT ADJUSTED NON-SINGULAR KEPLERIAN ELEMENTS
!           AND CORRELATIONS
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   ISATNO   I         SATELLITE NUMBER
!   JSPACE   I         COORDINATE SYSTEM THAT ELEMENTS ARE IN
!                      1-CART; 2-KEPLERIAN; 3- NON SING. KEP
!   DELTA    I         DIFFERENCES BETWEEN CKEPNS AND PKEPNS
!                      IF JSPACE=3
!   PKEPNS   I         PREVIOUS NON-SINGULAR KEPLERIAN ELEMENTS
!   SUM1     I         ATWA PART OF NORMAL MATRIX
!   COVXYZ   I         VARIANCE/COVARIANCE FOR CART.  COORDINATES
!   COVLEI   I         VARAINCE/COVARIANCE FOR N. S. KEPLER COORD.
!   XYZCUR   I         CURRENT CARTESIAN ELEMENTS(IF JSPACE NE 3)
!   CKEPNS   I         CURRENT NON SINGULAR KEPLERIAN ELEMENTS
!                      (IF JSPACE EQ 3)
!   AKEPNS   I         A PRIORI KEPLERIAN ELEMENTS
!   GM       I         GRAVITATIONAL CONSTANT TIMES MASS OF EARTH
!   IEPYMD
!   IEPHM
!   EPSEC
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CBDSTA/BDSTAT(7,999),XBDSTA
      COMMON/CESTIM/MPARM,MAPARM,MAXDIM,MAXFMG,ICLINK,IPROCS,NXCEST
      COMMON/CITER /NINNER,NARC,NGLOBL
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      COMMON/LDUAL/LASTSN,LSTSNX,LSTSNY
!
      DIMENSION ADJAPR(6)
      DIMENSION AKEPNS(6),PKEPNS(6),CKEPNS(6)
      DIMENSION COVXYZ(6,6),COVLEI(6,6),COVSCR(6,6)
      DIMENSION DELTA(1)
      DIMENSION ORBDIF(6),PK(6,6)
      DIMENSION SD(3),SDELM(6),SUM1(1)
      DIMENSION XYZCUR(6)
!
      EQUIVALENCE(ORBDIF(1),COVSCR(1,1)),(ADJAPR(1),COVSCR(1,2))
!
      CHARACTER(8)      :: APRIOR, ADJUST, SD, PREV, ELMNTS, DEL
      CHARACTER(8)      :: CURENT, TOTAL

      DATA APRIOR/'A PRIORI'/
      DATA ADJUST/'ADJUSTED'/
      DATA SD    /'STANDARD','DEVIATIO','N '  /
      DATA PREV  /'PREVIOUS'/
      DATA ELMNTS/'ELEMENTS'/
      DATA DEL   /'DELTA   '/
      DATA CURENT/' CURRENT'/
      DATA TOTAL /'   TOTAL'/

                                                ! statement function
      INDXNO(M)=MAPARM*(M-1)-(M*(M-1))/2

!********1*********2*********3*********4*********5*********6*********7**
! START OF EXECUTABLE CODE
!********1*********2*********3*********4*********5*********6*********7**
      GM=GMX
      IF(LASTSN.AND.ISATNO.EQ.1) GM=BDSTAT(7,8)
      IF(JSPACE.EQ.3) GO TO 500
!  OBTAIN UPDATED NON SINGULAR KEPLERIAN ELEMENTS FROM UPDATE CARTESIAN
      CALL ELEM(XYZCUR(1),XYZCUR(2),XYZCUR(3),XYZCUR(4),XYZCUR(5),      &
     &          XYZCUR(6),CKEPNS,10,PK,GM)
!  TRANSFORM VARCOV TO NON SINGULAR KEPLERIAN SPACE
      CALL CLEARA(COVLEI,36)
      CALL CLEARA(COVSCR,36)
! FORM PK*COVXYZ
      DO 350 I=1,6
      DO 350 K=1,6
      DO 350 J=1,6
      COVSCR(I,K)=PK(I,J)*COVXYZ(J,K)+COVSCR(I,K)
  350 CONTINUE
!  FORM PK*COVXYZ*(PK TRANSPOSE)
      DO 370 I=1,6
      DO 370 K=1,6
      DO 370 J=1,6
      COVLEI(I,K)=COVSCR(I,J)*PK(K,J)+COVLEI(I,K)
  370 CONTINUE
!  COMPUTE DIFFERENCE BETWEEN UPDATED AND PREVIOUS ELEMENTS
      DO 320 N=1,6
  320 ORBDIF(N)=CKEPNS(N)-PKEPNS(N)
      GO TO 550
! NON SINGULAR KEPLERIAN SPACE
  500 DO 510 I=1,6
  510 ORBDIF(I)=DELTA(I)
!  STORE NON SINGULAR KEPLERIAN VARCOV MATRIX
      DO 530 I=1,6
      II=INDXNO(I+6*(ISATNO-1))
      DO 530 J=I,6
      JJ=J+6*(ISATNO-1)
      COVLEI(I,J)=SUM1(II+JJ)
      COVLEI(J,I)=SUM1(II+JJ)
  530 CONTINUE
!  TAKE SQRT OF DIAGONAL ELEMENTS TO OBTAIN STANDARD DEVIATIONS
  550 DO 600 I=1,6
! CALCULATE ADJUSTED ELEMENTS - APRIORI ELEMENTS
      ADJAPR(I)=CKEPNS(I)-AKEPNS(I)
      IF(COVLEI(I,I).LT.0.0D0) WRITE(IOUT6,81004) I,COVLEI(I,I)
      SDELM(I)=SQRT(ABS(COVLEI(I,I)))
  600 END DO
      IPAGE6=IPAGE6+1
      WRITE(IOUT6,80200) IPAGE6
      WRITE(IOUT6,81000) ISATNO,NARC,NINNER,NGLOBL,IEPYMD,IEPHM,EPSEC
      WRITE(IOUT6,81001) APRIOR,ELMNTS,AKEPNS
      WRITE(IOUT6,81001)   PREV,ELMNTS,PKEPNS
      WRITE(IOUT6,81001) ADJUST,ELMNTS,CKEPNS
      WRITE(IOUT6,81001) TOTAL,DEL,ADJAPR
      WRITE(IOUT6,81001) CURENT,DEL,ORBDIF
      WRITE(IOUT6,81005) SD,SDELM
!  COMPUTE AND PRINT NON SINGULAR KEPLERIAN CORRELATIONS
      DO 620 KK=1,36
  620 COVSCR(KK,1)=COVLEI(KK,1)
      CALL CORELM(3,COVSCR)
      IF(JSPACE.NE.3) RETURN
      WRITE(IOUT6,81002) ISATNO,NARC,CKEPNS
      RETURN
81000 FORMAT('0',11X,'SATELLITE',I2,' ARC', I3,   ' NON SINGULAR KEPLE',&
     &'RIAN COORDINATE SUMMARY FOR INNER ITERATION' ,I3, ' OF GLOBAL I',&
     &'TERATION'   ,I2/ '0',22X,'EPOCH OF ELEMENTS - YEAR,MONTH,DAY',I7,&
     &3X,   'HOUR,MINUTE,SECOND',I5,F8.4/ '0',36X,'A',12X,'ECWN', 12X,  &
     &   'ESWN',12X,'SISN',12X,'SICN',12X,' MWN'/35X, 'METERS',73X,     &
     &   'DEGREES'/' ')
81001 FORMAT(8X,2(A8,1X),F17.4,4F16.12,F16.8/' ')
81002 FORMAT('0',44X,'SATELLITE',I2,' CURRENT BEST ELEMENTS FOR ARC ',  &
     &I3//42X,'A',19X,'E*COS(W+N)',14X,'E*SIN(W+N)'//30X,3D24.16        &
     &//35X,'SIN(I/2)*SIN(N)',9X,'SIN(I/2)*COS(N)',14X,                 &
     & 'M+W+N'//30X,3D24.16)
81004 FORMAT('0',10('*'),'$ NEGATIVE ARGUMENT TO DSQRT FOR NON SINGULAR'&
     &'KEPLERIAN ELEMENT',I2, '=',D24.16,'$',10('*'))
81005 FORMAT(8X,A8,1X,A8,A2,F16.4,4F16.12,F16.8/' ')
80200 FORMAT('1',34X,'NON SINGULAR KEPLERIAN SATELLITE ELEMENTS',34X,   &
     & 'UNIT  6 PAGE NO.',I6)
      END
