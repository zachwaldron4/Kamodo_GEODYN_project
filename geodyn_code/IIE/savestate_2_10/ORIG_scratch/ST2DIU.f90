      SUBROUTINE ST2DIU(NM,NTOLFR,COSLAT,SINLAT,COSLON,SINLON,          &
     &                  COSANG,SINANG,PARM,XCRSTA)
!********1*********2*********3*********4*********5*********6*********7**
! FUNCTION: THIS ONE OF THE SUBROUTINES FOR THE STEP2 OF THE SET TIDAL
!           DISPLACEMENTS. IT CALLED TO ACCOUNT FOR THE FREQUENCY
!           DEPENDENCE OF THE LOVE NUMBERS. THE RANGE OF FREQUENCIES
!           IS IN THE DIURNAL BAND
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   NM       I    S    NUMBER OF OBSERVATIONS IN THE BLOCK
!   NTOLFR   I    S    NUMBER OF FREQUENCIES
!   COSLAT   I    A    COSINES OF LATITUDES WHERE SET DESIRED
!   SINLAT   I    A    SINES OF LATITUDES WHERE SET DESIRED
!   COSLON   I    A    COSINES OF LONGITUDES WHERE SET DESIRED
!   SINLON   I    A    SINES OF LONGITUDES WHERE SET DESIRED
!   COSANG   I    A    COSINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   SINANG   I    A    SINES AT EACH OF THE TIMES FOR EACH OF THE
!                      FREQUENCIES
!   PARM     I    A    DELTA R(IP), DELTA R(OP), DELTA T(IP),DELTA T(OP)
!                      FOR DIURNAL FREQUENCIES
!   XCRSTA   O    A    TIDAL DISPLACEMENTS (AT THE NM TIMES) DUE TO
!                      DIURNAL FREQUENCY DEPENDENCE OF THE LOVE NUMBERS.
!
! COMMENTS:  THIS SUBROUTINE WAS MODIFIED FROM SUBROUTINE STEP2DIU
!            FROM DEHANT.
!
!
!********1*********2*********3*********4*********5*********6*********7**
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      DIMENSION COSLAT(NM),SINLAT(NM),COSLON(NM),SINLON(NM)
      DIMENSION COSANG(NM,NTOLFR),SINANG(NM,NTOLFR)
      DIMENSION PARM(4,NTOLFR)
      DIMENSION XCRSTA(3,NM)
!
!
!
      DO 500 K=1,NM
!
      SC2=2.D0*SINLAT(K)*COSLAT(K)
      C2MS2=COSLAT(K)*COSLAT(K)-SINLAT(K)*SINLAT(K)
      DR=0.D0
      DN=0.D0
      DE=0.D0
!
! NOTE PARMS SCALED TO M (ORIGINAL CODE MM)
!
      DO 100 J=1,NTOLFR
      COSA=COSANG(K,J)*COSLON(K)-SINANG(K,J)*SINLON(K)
      SINA=SINANG(K,J)*COSLON(K)+COSANG(K,J)*SINLON(K)
      DR=DR+SC2*(PARM(1,J)*SINA+PARM(2,J)*COSA)
      DN=DN+C2MS2*(PARM(3,J)*SINA+PARM(4,J)*COSA)
      DE=DE+SINLAT(K)*(PARM(3,J)*COSA+PARM(4,J)*SINA)
  100 END DO
      XCRSTA(1,K)=XCRSTA(1,K)+DR*COSLON(K)*COSLAT(K)-DE*SINLON(K)       &
     &                       -DN*SINLAT(K)*COSLON(K)
      XCRSTA(2,K)=XCRSTA(2,K)+DR*SINLON(K)*COSLAT(K)+DE*COSLON(K)       &
     &                       -DN*SINLAT(K)*SINLON(K)
      XCRSTA(3,K)=XCRSTA(3,K)+DR*SINLAT(K)+DN*COSLAT(K)
!
  500 END DO
      RETURN
      END
