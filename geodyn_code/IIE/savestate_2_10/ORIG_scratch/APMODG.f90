!$APMODG
      SUBROUTINE APMODG(ATPAC,ATPLC,ATPAA,ATPAAC,DXA,IAPARM,IGPARM,     &
     &                  NRMTOT,SCRTCH)
!********1*********2*********3*********4*********5*********6*********7**
! APMODG           00/00/00            0000.0    PGMR - ?
!
! FUNCTION:  MODIFY THE PARTS OF THE NORMAL EQUATIONS THAT ARE
!            PURELY GLOBAL INFORMATION TO TAKE INTO ACCOUNT
!            INFLUENCE OF ARC-GLOBAL CROSS BLOCKS.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   ATPAC   I/O        GLOBAL NORMAL MATRIX
!                      GLOBAL NORMAL MATRIX MODIFIED FOR ARC
!                      INFORMATION FOR OUTPUT
!   ATPLC   I/O        RHS OF NORMAL EQUATIONS FOR GLOBAL PARAMETERS
!                      MODIFIED FOR ARC INFORMATION FOR OUTPUT
!   ATPAA    I         ARC INVERTED  NORMAL MATRIX
!   ATPAAC   I         ARC-GLOBAL CROSS BLOCK IN NORMAL MATRIX
!   DXA      I         ARC PARAMETER CORRECTION VECTOR
!   IAPARM   I         NUMBER OF ARC PARAMETERS
!   IGPARM   I         NUMBER OF GLOBAL PARAMETERS
!   NRMTOT   I         LENGTH OF FIRST ROW OF ENTIRE NORMAL MATRIX
!   SCRTCH             SCRATCH SPACE (MUST BE AT LEAST IGPARM)
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      DIMENSION ATPAC(1),ATPLC(1),ATPAA(1),ATPAAC(1),DXA(1),SCRTCH(1)
      DATA ZERO/0.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
!   BUILD EACH COLUMN OF (CROSS T)*ARC INTO SCRTCH AND THEN ACCUMULATE
!   IT INTO THE FINAL PRODUCT
! NOTE!!!!!!!!!!!!!!!!!!!!!
!   THIS WILL PAGE THRU THE ENTIRE GLOBAL NORMAL MATRIX FOR EVERY ARC
!   PARAMETER
!
      IPTCR=-1
      ICRL=NRMTOT-1
      DO 1000 ICOL=1,IAPARM
      IPTCRT=0
      ISHORT=ICOL-1
      ISHOR1=ICOL
      ICRTL=NRMTOT-1
! QUANTITIES FROM THE ARC MATRIX WILL BE PULLED ROWISE IN THIS LOOP
! STARTING FROM THE DIAGONAL
! WHEN THERE IS A SHORTFALL,PULL COLUMN WISE UNTIL SHORTFALL MADE UP
      IF(ICOL.GT.1) GO TO 15
      DO 10 J=1,IGPARM
      SCRTCH(J)=ZERO
   10 END DO
      KBPT=1
      GO TO 50
   15 CONTINUE
      DO 20 J=1,IGPARM
      SCRTCH(J)=ATPAAC(IPTCRT+J)*ATPAA(ICOL)
   20 END DO
      IPTCRT=IPTCRT+ICRTL
      ICRTL=ICRTL-1
      KBPT=ICOL+NRMTOT-1
      IF(ISHORT.LT.2) GO TO 50
      DO 40 I=2,ISHORT
      DO 30 J=1,IGPARM
      SCRTCH(J)=SCRTCH(J)+ATPAAC(IPTCRT+J)*ATPAA(KBPT)
   30 END DO
      KBPT=KBPT+ICRTL
      IPTCRT=IPTCRT+ICRTL
      ICRTL=ICRTL-1
   40 END DO
!     FROM THIS POINT PULL ROWISE
   50 CONTINUE
      DO 70 I=ISHOR1,IAPARM
      DO 60 J=1,IGPARM
      SCRTCH(J)=SCRTCH(J)+ATPAAC(IPTCRT+J)*ATPAA(KBPT)
   60 END DO
      KBPT=KBPT+1
      IPTCRT=IPTCRT+ICRTL
      ICRTL=ICRTL-1
   70 END DO
! NOW THE ICOL COLUMN OF THE (CROSS T)*ARC MATRIX IS IN SCRTCH
!
! ACCUMULATE THE EFFECT OF THIS COLUMN ON THE PRODUCT
! (CROSS T)*ARC*(CROSS) INTO THE COMMOM NORMAL MATRIX
      IPTCNR=0
      ICNRL=IGPARM
      DO 500 II=1,IGPARM
      IPTII=IPTCR+II
      DO 400 J=1,ICNRL
      ATPAC(IPTCNR+J)=ATPAC(IPTCNR+J)-ATPAAC(IPTII+J)*SCRTCH(II)
  400 END DO
      IPTCNR=IPTCNR+ICNRL
      ICNRL=ICNRL-1
  500 END DO
      IPTCR=IPTCR+ICRL
      ICRL=ICRL-1
 1000 END DO
      IPTCRT=0
      ICRTL=NRMTOT-1
      DO 2000 I=1,IAPARM
      DO 1500 J=1,IGPARM
      ATPLC(J)=ATPLC(J)-ATPAAC(IPTCRT+J)*DXA(I)
 1500 END DO
      IPTCRT=IPTCRT+ICRTL
      ICRTL=ICRTL-1
 2000 END DO
      RETURN
      END
