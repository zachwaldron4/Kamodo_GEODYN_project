!$SUMNPG
      SUBROUTINE SUMNPG(PMPP,RESID,WT,NP,NRMTOT,NM,ATPA,ATPL,SCRTCH,    &
     &                  LAVOID)
!********1*********2*********3*********4*********5*********6*********7**
! SUMNP            85/10/08            0000.0    PGMR - TOM MARTIN
!                  84/02/27            0000.0    PGMR - D.ROWLANDS
!
! FUNCTION:  ACCUMULATE PARTIALS & RESIDUALS INTO THE
!            NORMAL MATRIX AND RIGHT HAND SIDE OF THE NORMAL EQ.
!            THERE ARE SEVERAL ROUTINES THAT PERFORM THIS FUNCTION.
!            SUMNPG IS CALLED FOR MULTI PROCESSOR APPLICATIONS
!            WHEN THE ENTIRE NORMAL MATRIX IS IN MEMORY OR FOR THE
!            FIRST (IN MEMORY) SECTION OF THE NORMAL MATRIX WHEN
!            PARTITIONING IS EMPLOYED.
!
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   PMPP     I         FULL ARRAY OF PARTIALS(INCLUDING ZEROS WHERE
!                      NECCESSARY) FOR EACH MEASUREMENT.SOMETIMES A
!                      INCLUDES ONLY ARC,SOMTIMES ARC&COMMON.
!   RESID    I         RESIDUAL ARRAY
!   WT       I         MEASUREMENT WEIGHT ARRAY
!   NP       I         NUMPER OF PARAMETERS
!   NRMTOT   I         MAXIMUM DIMENSION OF NORMAL MATRIX
!   NM       I         NUMBER OF MEASUREMENTS
!   ATPA     O         NORMAL MATRIX
!   ATPL     O         RIGHT HAND SIDE OF NORMAL EQUATIONS
!   SCRTCH       A     SCRATCH ARRAY
!
! COMMENTS:  THIS ROUTINE EXPLOITS PARALLEL PROCEESING BY SENDING
!            EACH CALL TO SUMROW TO A SEPARATE PROCESSOR. THIS
!            REQUIRES MACHINE DEPENDENT COMPILER DIRECTIVES
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/BIAOR/NAMBB,NCYCB,NAMBBH
      COMMON/CPARTI/MAXLP ,KORSIZ,MAXWRK,                               &
     &              IUNTP1,IUNTP2,IUNTPF,ISIZPF,IRECPF,NRECPF,          &
     &              NPDONE,NP1   ,NP2   ,NRECND,NPDIM ,NXPARI
      COMMON/CPARTL/LPART ,LPARTI,NXPARL
      COMMON/CGLBAR/LGDRAG,NXGLAR
      COMMON/CITERL/LSTGLB,LSTARC,LSTINR,LNADJ ,LITER1,LSTITR,          &
     &              LOBORB,LRESID,LFREEZ,LSAVEF,LHALT,LADJPI,LADJCI
      COMMON/EMAT  /EMTNUM,EMTPRT,EMTCNT,VMATRT,VMATS,VMATFR,FSCVMA,    &
     &              XEMAT
      COMMON/NPCOM /NPNAME,NPVAL(92),NPVAL0(92),IPVAL(92),IPVAL0(92),   &
     &              MPVAL(28),MPVAL0(28),NXNPCM
      COMMON/NPCOMX/IXARC ,IXSATP,IXDRAG,IXSLRD,IXACCL,IXGPCA,IXGPSA,   &
     &              IXAREA,IXSPRF,IXDFRF,IXEMIS,IXTMPA,IXTMPC,IXTIMD,   &
     &              IXTIMF,IXTHTX,IXTHDR,IXOFFS,IXBISA,IXFAGM,IXFAFM,   &
     &              IXATUD,IXRSEP,IXACCB,IXDXYZ,IXGPSBW,IXCAME,IXBURN,  &
     &              IXGLBL,IXGPC ,IXGPS, IXTGPC,IXTGPS,IXGPCT,IXGPST,   &
     &              IXTIDE,IXETDE,IXOTDE,IXOTPC,IXOTPS,IXLOCG,IXKF  ,   &
     &              IXGM  ,IXSMA ,IXFLTP,IXFLTE,IXPLTP,IXPLTV,IXPMGM,   &
     &              IXPMJ2,IXVLIT,IXEPHC,IXEPHT,IXH2LV,IXL2LV,IXOLOD,   &
     &              IXPOLX,IXPOLY,IXUT1 ,IXPXDT,IXPYDT,IXUTDT,IXVLBI,   &
     &              IXVLBV,IXXTRO,IXBISG,IXSSTF,IXFGGM,IXFGFM,IXLNTM,   &
     &              IXLNTA,IX2CCO,IX2SCO,IX2GM ,IX2BDA,IXRELP,IXJ2SN,   &
     &              IXGMSN,IXPLNF,IXPSRF,IXANTD,IXTARG,                 &
     &              IXSTAP,IXSSTC,IXSSTS,IXSTAV,IXSTL2,                 &
     &              IXSTH2,IXDPSI,IXEPST,IXCOFF,IXTOTL,NXNPCX
      COMMON/PARPRC/NPROCS,NPROCI(5,10)
!
      DIMENSION PMPP(NP,NM),RESID(NM),WT(NM),ATPA(1),ATPL(NP)
      DIMENSION SCRTCH(NP,NPROCS)
      DIMENSION IL(10),ISROW(10),IQPL(10),IQPS(10),IQPSS(10)
      DIMENSION IOBSX(10),IX(10),IIX(10)
      DIMENSION LAVOID(1)
!
      DATA ZERO/0.0D0/
!
!***********************************************************************
! STARP OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
! IF GLOBAL DRAG WAS REQUESTED AND THIS IS THE LAST INNER ITERATION
! OF AN EMAT RUN PUT THE DRAG PARTIALS AT THE END OF THE ARC PARTIALS
      IF(LGDRAG.AND.LSTINR.AND.(EMTNUM.GT.ZERO)) THEN
      IF(NPVAL0(IXDRAG).EQ.0) GOTO 80
! SET INDEX EQUAL TO THE POSITION OF THE FIRST DRAG PARAMETER
      INDEX=IPVAL0(IXDRAG)
      DO 70 INM=1,NM
! RUN THROUGH ALL OF THE DRAG PARAMETERS FOR A PARTICULAR MEASUREMENT
      DO 60 I=1,NPVAL0(IXDRAG)
! SET DRAG PARTIAL TO TEMP SPACE AND SHIFT OVER THE REST OF THE ARC
! PARTIALS.  THIS IS DONE FOR EACH DRAG PARAMETER.
      TEMP=PMPP(INDEX,INM)
      DO 50 J=INDEX,NPVAL0(IXARC)-1
      PMPP(J,INM)=PMPP(J+1,INM)
   50 END DO
! PUT TEMP (ARC DRAG PARTIAL) AT END OF ARC PARTIALS
      PMPP(NPVAL0(IXARC),INM)=TEMP
   60 END DO
   70 END DO
      ENDIF
   80 CONTINUE
!
!
!
      NA=0
      NAX=0
      DO 85 I=1,NAMBB
      IF(LAVOID(I)) GO TO 85
      NA=I
      NAX=NAX+1
   85 END DO
      IF(NAX.GT.1) THEN
         WRITE(6,6010) NAX
         WRITE(6,6011)
 6010 FORMAT(' EXECUTION TERMINATING IN SUMNPF. NUMBER OF AMBIGUIITY')
 6011 FORMAT(' PARAMETERS FOR ONE BLOCK = ',I5,' (GREATER THAN 1)')
         STOP
      ENDIF
!
!
!
!
!MIC$ PARALLEL SHARED (PMPP,ATPA,RESID,SCRTCH,NPROCI)
!MIC$*         SHARED (WT,NP,NM,ATPL,LAVOID)
!MIC$*         SHARED (IL,ISROW,IOBSX,IX,IIX)
!MIC$*         SHARED (NPROCS)
!MIC$*         SHARED (IQPS,IQPSS,IQPL)
!MIC$*         PRIVATE (IPROC)
!MIC$ DO PARALLEL
      DO 1000 IPROC=1,NPROCS
      CALL SUMROW(PMPP,RESID,WT,NP,NM,ATPA,                             &
     &            SCRTCH(1,IPROC),NPROCI(1,IPROC),                      &
     &            NPROCI(2,IPROC),NPROCI(3,IPROC),                      &
     &            NPROCI(4,IPROC),IPROC,                                &
     &            IL(IPROC),ISROW(IPROC),IOBSX(IPROC),                  &
     &            IQPL(IPROC),IQPS(IPROC),IQPSS(IPROC),                 &
     &            IX(IPROC),IIX(IPROC),LAVOID)
 1000 END DO
!MIC$ END DO
!MIC$ END PARALLEL
!
!
      IF(NAX.LT.1) GO TO 2100
      IF(NM.GT.NP) GO TO 1500
      CALL SUMGTE(PMPP,RESID,WT,NP,NRMTOT,NM,ATPA,ATPL,SCRTCH,NA,NAMBB)
      RETURN
 1500 CONTINUE
      ISROW1=(NA-1)*(NRMTOT-NAMBB+1)
      IL1=NP-NAMBB+1
      IP1=NAMBB-1
      DO 2000 IOBS=1,NM
      SCRTCH(1,1)=WT(IOBS)*PMPP(NA,IOBS)
      ATPL(NA)=ATPL(NA)+RESID(IOBS)*SCRTCH(1,1)
      ATPA(ISROW1+1)=ATPA(ISROW1+1)+SCRTCH(1,1)*PMPP(NA,IOBS)
      DO 1800 II=2,IL1
      ATPA(ISROW1+II)=ATPA(ISROW1+II)+SCRTCH(1,1)*PMPP(IP1+II,IOBS)
 1800 END DO
 2000 END DO
!
 2100 CONTINUE
!
! FINISH THE RHS PARTITION TO THE END
! SUMROW DOES NOT DEAL WITH RHS
      DO 2300 IOBS=1,NM
      IF(WT(IOBS).LE.ZERO) GO TO 2300
      DO 2200 I=1,NP
      SCRTCH(I,1)=WT(IOBS)*PMPP(I,IOBS)
      ATPL(I)=ATPL(I)+RESID(IOBS)*SCRTCH(I,1)
 2200  CONTINUE
 2300 END DO
!
! FINALLY, WRITE OUT THE PARTIALS
!
      IF(.NOT.LPARTI) RETURN
      DO 2400 IOBS=1,NM
      IF(WT(IOBS).LE.ZERO) GO TO 2400
      PMPP(NP2,IOBS)=WT(IOBS)
      CALL PARWRT(PMPP(NP2,IOBS))
 2400 END DO
      RETURN
      END
