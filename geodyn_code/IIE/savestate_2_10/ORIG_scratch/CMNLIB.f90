!$CMNLIB
      SUBROUTINE CMNLIB(MJDSEC,FSEC,XLLIB,XLLIBV)
!********1*********2*********3*********4*********5*********6*********7**
!                                      9xxx.x    PGMR - S.LUO
!
!  FUNCTION:  CALCULATE THE LUNAR LIBRATION ( RA,DEC OF LUNAR POLE AND
!             THETA ANGLE OF LUNAR ROTATION) FROM EPHEMERIS
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I    S    INTEGER SECONDS SINCE GEODYN REFERENCE TIME
!   FSEC     I    S    FRACTIONAL REMAINING SECONDS (COORDINATE ET)
!   XLLIB    O    A    THE LUNAR ORIENTATION PARAMETERS (RA,DEC&THXTGC)
!   XLLIBV   O    A    THE RATES OF LUNAR ORIENTATION PARAMETERS
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/EPHM/FSECXY(1),XP(261),YP(261),A1UT(261),EP(261),EC(261),  &
     &            FSCFLX(1),FLUXS(36),AVGFLX(36),FLUXM(288),FSECEP(1),  &
     &            EPHP(816),EPHN(96),ELIB(120),BUFT(2700),FSECNP(4)
      COMMON/EPHMPT/NTOT,NTOTS,NNPDPR,NUTORD,IBODDG(8),IGRPDG(4),       &
     &              NENPD,INRCD(2),KPLAN(11),NINEP,NEPHMR,NEPHEM,       &
     &              IBHINT,NINTBH,NXEPHP
      COMMON/EPHSET/EMFACT,DTENPD,DTINPD,FSC1EN,FSCENP(2),FSCINP(4),    &
     &   FSDINP(4),XEPHST
      DIMENSION FNSEC(4)
      DIMENSION CHEBL(10),CHEBVL(10)
      DIMENSION XLLIB(3),XLLIBV(3),FSECLB(4)
      DIMENSION CFLIBM(4,3,10)
      DATA ZERO/0.0D0/,ONE/1.0D0/,TWO/2.0D0/
      DATA FNSEC/2764800.D0,1382400.D0,691200.D0,345600.D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      IF(FSECEP(1).EQ.0.D0)GO TO 9999
!
      DO 10 I=1,4
      NL=(I-1)*30
      DO 5 JJ=1,3
      DO 5 KK=1,10
      NL=NL+1
      CFLIBM(I,JJ,KK)=ELIB(NL)
    5 CONTINUE
   10 END DO
!
      DO 15 JL1=1,3
      XLLIB(JL1)=0.D0
      XLLIBV(JL1)=0.D0
   15 END DO
!
! INITIALIZE
      VSEC=TWO/FNSEC(3)
!
! INITIALIZE FSECLB(4)
      DO 50 I=1,4
      FSECLB(I)=FSECEP(1)+FNSEC(3)*(I-1)
   50 END DO
!
      CHEBL(1)= ONE
      CHEBVL(1) = ZERO
      CHEBVL(2) = ONE
!
! MAP MJDSEC,FSEC INTO INTERVAL  -1,1  FOR EACH PERIOD GROUP
! THERE ARE 4 x 8 days libration values in 32 days
!
      RNSEC=ONE/FNSEC(3)
      TSEC=DBLE(MJDSEC)+FSEC
      IF(TSEC.LT.FSECLB(1))GO TO 9999
      IF(TSEC.GE.FSECLB(4))IGPT=4
      DO 205 J=1,3
      IF(TSEC.GE.FSECLB(J).AND.TSEC.LT.FSECLB(J+1))IGPT=J
  205 END DO
      FSECC1=FSECLB(IGPT)
      TTL=((MJDSEC-FSECC1)+FSEC)*RNSEC
      TTL=TTL*TWO-ONE
!
! COMPUTE CHEBYSHEV POLYNOMIALS AT APPROPRIATE TIMES
      CALL CHEBPV(CHEBL,CHEBVL,TTL,10,1,TWOT,.TRUE.)
      DO 400 J=1,10
      CHEBVL(J) = CHEBVL(J)*VSEC
  400 END DO
!
! COMPUTE LUNAR LIBRATION AND RATES
!
       DO  860 J=1,10
       KGP=IGPT
       XLLIB(1)=XLLIB(1)+CFLIBM(KGP,1,J)*CHEBL(J)
       XLLIB(2)=XLLIB(2)+CFLIBM(KGP,2,J)*CHEBL(J)
       XLLIB(3)=XLLIB(3)+CFLIBM(KGP,3,J)*CHEBL(J)
       XLLIBV(1)=XLLIBV(1)+CFLIBM(KGP,1,J)*CHEBVL(J)
       XLLIBV(2)=XLLIBV(2)+CFLIBM(KGP,2,J)*CHEBVL(J)
       XLLIBV(3)=XLLIBV(3)+CFLIBM(KGP,3,J)*CHEBVL(J)
  860 END DO
          XLLIB(3)=MOD(XLLIB(3),TWOPI)
          XLLIBV(3)=MOD(XLLIBV(3),TWOPI)
          XLLIB(1)=XLLIB(1)+3.D0*PI/2.D0
          XLLIB(2)=PI/2.D0-XLLIB(2)
          XLLIBV(1)=XLLIBV(1)
          XLLIBV(2)=-XLLIBV(2)
 9999 RETURN
!
      END
