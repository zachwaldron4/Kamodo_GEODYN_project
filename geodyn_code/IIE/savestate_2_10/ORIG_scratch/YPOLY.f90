      SUBROUTINE YPOLY
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      CHARACTER*96 CARD,CARD2,CARDP
      CHARACTER*80 CARDO,BLANK
      CHARACTER*2 TYPE
      COMMON/YAWNFO/YAWRP(200),YAWR(200),YAWRR(200),YAWBTP(200),        &
     &              YAWBID(200)
      DATA STEP/10.D0/
      DATA LFYIIR/.FALSE./
!
      DO 1 I=1,80
      BLANK(I:I)=' '
    1 END DO
!
      REWIND 42
      REWIND 46
      OPEN(UNIT=48,FILE='ftn48',STATUS='NEW',FORM='FORMATTED')
!
      CARDO=BLANK
      CARDO(1:6)='YAWPOL'
      WRITE(48,5000) CARDO
!
      READ(46,5000) CARD
      READ(CARD(6:17),5001) MAXSAT,MAXTIM
!
      DO 200 I=1,MAXSAT
      L1ST=.TRUE.
      LOUT48=.TRUE.
      DO 100 J=1,MAXTIM
      JMARK=J
      READ(46,5000) CARD
      LE=.TRUE.
      IF(CARD(9:9).EQ.' ') GO TO 110
!
      READ(CARD(1:51),5002) ISATID,MJDSEC,BETA
      ITQP=MOD(ISATID,10)
      IF (ITQP == 3) ITQP=2
      IF (ITQP >= 3) ITQP=3
      IF(J.EQ.1) THEN
        IF(ITQP.EQ.3) THEN
          DO JJ=2,MAXTIM
            READ(46,5000) CARD
          ENDDO
          GO TO 101
        ENDIF
      ENDIF
! FOR BLOCK IIR SATELLITES DO NOT OUTPUT THE TYPE OF POLYNOMIALS
! THAT ARE COMPUTED FOR II II/A SATELLITES
      IF(ITQP.EQ.3) LOUT48=.FALSE.
      DO 5 II=1,200
      ITEST=YAWBID(II)+.05D0
      IF(ITEST.NE.ISATID) GO TO 5
      TESTB=YAWBTP(II)
      IF(ABS(TESTB).LT..01D0) GO TO 110
      TESTC=YAWRP(II)
      IF(TESTC.LE.0.D0) GO TO 110
      YAWRX=YAWR(II)
      YAWRRX=YAWRR(II)
!
! FOR YAW BIAS NOT 0,-1,1,-2,2 THEN BIAS IS BIAS VALUE
      BIAS=TESTB
!
! BIAS OF 1 IMPLIES NORMAL BIAS, -1 IMPLIES ANTI-NORMAL BIAS
      IF(TESTB.LT.1.01D0.AND.TESTB.GT.0.99D0) BIAS=SIGN(0.5D0,BETA)
      RNBETA=-BETA
      IF(TESTB.GT.-1.01D0.AND.TESTB.LT.-0.99D0) BIAS=SIGN(0.5D0,RNBETA)
!
! BIAS OF +-1 IMPLIES BIAS OF SAME SIGN REGARDLESS OF BETA
      IF(TESTB.LT.-1.99D0.AND.TESTB.GT.-2.01D0) BIAS=-0.5D0
      IF(TESTB.GT.1.99D0.AND.TESTB.LT.2.01D0) BIAS=0.5D0
!
      GO TO 6
    5 END DO
      GO TO 110
    6 CONTINUE
      IF(J.EQ.1) THEN
   10    READ(42,5000,END=210) CARD2
         READ(CARD2(1:51),5002) JSATID
         IF(JSATID.NE.ISATID) GO TO 10
         BACKSPACE 42
      ENDIF
      IF(CARD(9:9).EQ.'X') LE=.FALSE.
      IF(L1ST.AND..NOT.LE) GO TO 100
      L1ST=.FALSE.
!
      IF(LE) THEN
         IF(J.EQ.MAXTIM) GO TO 100
         READ(46,5000) CARD2
         IF(CARD2(9:9).EQ.' ') THEN
          JMARK = JMARK + 1
          GO TO 110
         ENDIF
         READ(CARD2(10:19),5003) MJDX
         XJDX=MJDX
         BACKSPACE 46
!  DEFINE THE END TIME OF THE 1ST HALF OF THE SHADOW AND
!  THE START TIME OF THE SECOND HALF
         READ(CARD(10:19),5003) MJDE
         XJDE=MJDE
         TSTART=XJDE
         READ(CARD(55:75),5004) YAWN,YAWND
         TL=(SIGN(YAWRX,BIAS)-YAWND)/SIGN(YAWRRX,BIAS)
         TEND=TSTART+TL
         IF(TEND.GT.XJDX) TEND=XJDX
!  OUTPUT THE FIRST SHADOW POLYNOMIAL
         A0=YAWN
         A1=YAWND
         A2=.5D0*SIGN(YAWRRX,BIAS)
         TYPE='S1'
         IF(LOUT48) WRITE(48,7000) ISATID,TSTART,TEND,A0,A1,A2,TYPE
         YAWX=A0+A1*TL+A2*TL*TL
         IF(YAWX.GT.360.D0) YAWX=YAWX-360.D0
         IF(YAWX.LT.0.D0) YAWX=YAWX+360.D0
         IF(TEND.EQ.XJDX) GO TO 100
!  OUTPUT THE SECOND SHADOW POLYNOMIAL
         TSTART=TEND
         TEND=XJDX
         A0=YAWX
         A1=SIGN(YAWRX,BIAS)
         A2=0.D0
         TYPE='S2'
         IF(LOUT48) WRITE(48,7000) ISATID,TSTART,TEND,A0,A1,A2,TYPE
         TL=TEND-TSTART
         YAWX=A0+A1*TL
         IF(YAWX.GT.360.D0) YAWX=YAWX-360.D0
         IF(YAWX.LT.0.D0) YAWX=YAWX+360.D0
      ELSE
!  DEFINE THE END TIME OF THE 1ST HALF OF THE POST SHADOW AND
!  THE START TIME OF THE SECOND HALF
         READ(CARD(10:19),5003) MJDX
         XJDX=MJDX
         TSTART=XJDX
         READ(CARD(55:75),5004) YAWN,YAWND
         YAWNT=YAWN
         IF(YAWNT.GT.180.D0) YAWNT=YAWN-360.D0
         YAWXT=YAWX
         IF(YAWXT.GT.180.D0) YAWXT=YAWX-360.D0
         D=YAWNT-YAWXT-DBLE(NINT((YAWNT-YAWXT)/360.D0)*360)
         TL=(SIGN(YAWRX,D)-SIGN(YAWRX,BIAS))/SIGN(YAWRRX,D)
         TEND=TSTART+TL
         A0=YAWX
         A1=SIGN(YAWRX,BIAS)
         A2=.5D0*SIGN(YAWRRX,D)
         TYPE='P1'
         IF(LOUT48) THEN
         IF(TL.GT.0.D0) WRITE(48,7000) ISATID,TSTART,TEND,A0,A1,A2,TYPE
         ENDIF
         YAWX=A0+A1*TL+A2*TL*TL
         IF(YAWX.GT.360.D0) YAWX=YAWX-360.D0
         IF(YAWX.LT.0.D0) YAWX=YAWX+360.D0
! OUTPUT THE SECOND POST SHADOW POLYNOMIAL
         TSTART=TEND
         A0=YAWX
         A1=SIGN(YAWRX,D)
         A2=0.D0
         TYPE='P2'
! FIND END TIME
   20    READ(42,5000,END=210) CARD2
         READ(CARD2(1:19),5002) JSATID,MJDY
         XJDY=DBLE(MJDY)
         IF(JSATID.NE.ISATID) THEN
             IF(J.LT.MAXTIM) THEN
                WRITE(6,8000)
                WRITE(6,8001) I,J
             ENDIF
             BACKSPACE 42
             GO TO 100
         ENDIF
         READ(CARD2(22:30),5004) OA
         IF(OA.GT.40.D0.AND.XJDY.GT.TSTART) THEN
             WRITE(6,8000)
             WRITE(6,8001) I,J
             BACKSPACE 42
             GO TO 100
         ENDIF
         TDIF=ABS(XJDY-TSTART)
          IF(TDIF.GE.STEP) GO TO 20
         READ(CARD2(55:75),5004) YAWN,YAWND
!
         DIF=ABS(YAWN-YAWX)
         TEST1=ABS(STEP*YAWND)
         TEST2=ABS(STEP*A1)
         TEST=TEST1
         IF(TEST2.GT.TEST1) TEST=TEST2
         TEST=1.5D0*TEST
         IF(DIF.LE.TEST) GO TO 35
!
!  JUST KEEP READING TILL FIND MATCH
   30    READ(42,5000,END=210) CARD2
         READ(CARD2(1:19),5002) JSATID,MJDY
         XJDY=DBLE(MJDY)
         IF(JSATID.NE.ISATID) THEN
            IF(J.LT.MAXTIM) THEN
              WRITE(6,8000)
              WRITE(6,8001) I,J
             ENDIF
             BACKSPACE 42
             GO TO 100
         ENDIF
         READ(CARD2(22:30),5004) OA
         IF(OA.GT.40.D0.AND.XJDY.GT.TSTART) THEN
              WRITE(6,8000)
              WRITE(6,8001) I,J
             BACKSPACE 42
             GO TO 100
         ENDIF
         TDIF=XJDY-TSTART
         YAWX=A0+TDIF*A1
         IF(YAWX.GT.360.D0) YAWX=YAWX-360.D0
         IF(YAWX.LT.0.D0) YAWX=YAWX+360.D0
         READ(CARD2(55:75),5004) YAWN,YAWND
         DIF=ABS(YAWN-YAWX)
         TEST1=ABS(STEP*YAWND)
         TEST2=ABS(STEP*A1)
         TEST=TEST1
         IF(TEST2.GT.TEST1) TEST=TEST2
         TEST=1.5D0*TEST
         IF(DIF.GT.TEST) GO TO 30
   35    CONTINUE
         TEND=XJDY
         IF(LOUT48) WRITE(48,7000) ISATID,TSTART,TEND,A0,A1,A2,TYPE
!
!  NOON FLIP
         XMAX=-SIGN(YAWRX,BETA)
         LPOS=.TRUE.
         IF(XMAX.LT.0.D0) LPOS=.FALSE.
!
   40    READ(42,5000,END=210) CARD2
         READ(CARD2(1:19),5002) JSATID,MJDY
         IF(JSATID.NE.ISATID) THEN
             IF(J.LT.MAXTIM) THEN
               WRITE(6,8002)
               WRITE(6,8003) I,J
         ENDIF
             BACKSPACE 42
             GO TO 100
         ENDIF
         READ(CARD2(22:30),5004) OA
         IF(OA.LT.160.D0) GO TO 40
! READY TO START FINDING MAX RATE AND ASSOCIATED START TIME
   45    READ(42,5000,END=210) CARD2
         READ(CARD2(1:19),5002) JSATID,MJDY
         READ(CARD2(22:30),5004) OA
         IF(JSATID.NE.ISATID) THEN
             IF(J.LT.MAXTIM) THEN
               WRITE(6,8002)
               WRITE(6,8003) I,J
             ENDIF
             BACKSPACE 42
             GO TO 100
         ENDIF
         IF(OA.LT.160.D0) THEN
             BACKSPACE 42
             GO TO 100
         ENDIF
         IF(OA.GT.250.D0) THEN
             BACKSPACE 42
             GO TO 100
         ENDIF
         READ(CARD2(55:75),5004) YAWN,YAWND
!         IF(LPOS) THEN
!           IF(YAWND.LT.XMAX) GO TO 45
!         ELSE
!           IF(YAWND.GT.XMAX) GO TO 45
!         ENDIF
         IF(ABS(YAWND).LT.ABS(XMAX)) GO TO 45
! NOON MANEUVER NECESSARY AND START TIME FOUND
         TSTART=DBLE(MJDY)
         A0=YAWN
         A1=XMAX
         A2=0.D0
         TYPE='NN'
! NOW FIND END TIME
!         READ(42,5000,END=210) CARD2
!         READ(42,5000,END=210) CARD2
   50    READ(42,5000,END=210) CARD2
         READ(CARD2(1:19),5002) JSATID,MJDY
         IF(JSATID.NE.ISATID) THEN
             IF(J.LT.MAXTIM) THEN
                WRITE(6,8004)
                WRITE(6,8005) I,J,TSTART,A0,A1
             ENDIF
             BACKSPACE 42
             GO TO 100
         ENDIF
         READ(CARD2(22:30),5004) OA
         IF(OA.LT.160.D0) THEN
            WRITE(6,8004)
            WRITE(6,8005) I,J,TSTART,A0,A1
            BACKSPACE 42
            GO TO 100
         ENDIF
         IF(OA.GT.250.D0) THEN
            WRITE(6,8004)
            WRITE(6,8005) I,J,TSTART,A0,A1
            BACKSPACE 42
            GO TO 100
         ENDIF
         READ(CARD2(55:75),5004) YAWN,YAWND
         IF(ABS(YAWND).GT.ABS(XMAX)) GO TO 50
         TX=DBLE(MJDY)
         YAWX=A0+(TX-TSTART)*A1
         IF(YAWX.GT.360.D0) YAWX=YAWX-360.D0
         IF(YAWX.LT.0.D0) YAWX=YAWX+360.D0
         DIF=ABS(YAWN-YAWX)
         TEST1=ABS(STEP*YAWND)
         TEST2=ABS(STEP*A1)
         TEST=TEST1
         IF(TEST2.GT.TEST1) TEST=TEST2
         TEST=1.5D0*TEST
         IF(DIF.GT.TEST) GO TO 50
         TEND=TX
         IF(LOUT48) WRITE(48,7000) ISATID,TSTART,TEND,A0,A1,A2,TYPE
      ENDIF
  100 CONTINUE
  101 CONTINUE
      GO TO 200
  110 CONTINUE
      NX=MAXTIM-JMARK
      IF(NX.LE.0) GO TO 200
      DO 115 IX=1,NX
      READ(46,5000) CARD
  115 CONTINUE
  200 CONTINUE
  210 CONTINUE
!
! NOW DEAL WITH BLOCK IIR SATELLITES. THE ORIGINAL IGS MAIL
! ABOUT IIR SATELLITES (1653 FROM 1997) AND CONFIRMED BY
! YOAZ BAR-SEVER  STATES THAT IIR SATELLITES GO INTO FIXED
! YAW MODE (WITH YAW=0)  FROM FIRST SHADOW WITH |BETAP| < 1.6
! UNTIL FIRST EXIT WITH |BETAP| >1.6 .
!
! AS OF 12/17/2002 IT WAS DISCOVERED THAT BLOCK IIRs ARE NO
! LONGER IN FIXED YAW MODE WITH |BETAP|< 1.6 . WHEN THEIR
! MAXIMUM YAW RATE IS REACHED, THEY SIMPLY CONTINUE TO YAW
! AT THA MAXIMUM RATE UNTIL THE YAW ANGLE CATCHES UP WITH THE
! NOMINAL MODEL. THIS HAPPENS AT MIDNIGHT AND NOON WHEN |BETAP|
! IS LESS THAN SOME CRITICAL ANGLE. THAT ANGLE IS A FUNCTION
! OF THE MAXIMUM YAW RATE
!
!
      REWIND 46
      READ(46,5000) CARD
      DO 400 I=1,MAXSAT
      NXR=0
      LFIRST=.TRUE.
      T1FY=1.D25
      T2FY=2147483648.D0
      DO 300 J=1,MAXTIM
      READ(46,5000) CARD
      NXR=NXR+1
      READ(CARD(1:51),5002) ISATID,MJDSEC,BETA
      TIMEX=DBLE(MJDSEC)
      ITQP=MOD(ISATID,10)
      IF (ITQP == 3) ITQP=2
      IF (ITQP >= 3) ITQP=3
! THIS SECTION OF CODE IS FOR BLOCK IIR ONLY
      IF(ITQP.NE.3.AND.J.EQ.1) THEN
         DO JJ=2,MAXTIM
           READ(46,5000) CARD
         ENDDO
         GO TO 400
      ENDIF
!
      IF(CARD(1:7).EQ.'       ') GO TO 300
!
      DO 215 II=1,200
      ITEST=YAWBID(II)+.05D0
      IF(ITEST.NE.ISATID) GO TO 215
      YAWRM=YAWR(II)
      TESTB=YAWBTP(II)
      GO TO 220
  215 CONTINUE
      IF(.NOT.LFYIIR) THEN
         WRITE(6,6000)
         WRITE(6,6001) ISATID
         WRITE(6,6002)
         STOP
      ENDIF
  220 CONTINUE
      IF(TESTB.GT.2.001D0.OR.TESTB.LT.1.999D0) THEN
         WRITE(6,6000)
         WRITE(6,6009) ISATID,TESTB
         WRITE(6,6010)
         STOP
      ENDIF
      BIAS=.5D0
      IF(YAWRM.LT..001D0.AND..NOT.LFYIIR) THEN
        WRITE(6,6003) ISATID
        WRITE(6,6004)
        WRITE(6,6005)
        YAWRM=1.D25
        JLEFT=MAXTIM-J
        IF(JLEFT.GT.0) THEN
          DO K=1,JLEFT
           READ(46,5000) CARD
          ENDDO
        ENDIF
        GO TO 350
      ENDIF
! DETERMINE THE CRITICAL BETAP ANGLE BELOW WHICH MAXIMUM
! RATE WILL BE EXCEEDED
      IF(LFIRST) CALL YRVSBA(BIAS,YAWRM,BETM)
      LFIRST=.FALSE.
!
      IF(ABS(BETA).GT.BETM) THEN
        IF(T1FY.LT.1.D25) THEN
          XTEST=MJDSEC
          IF(XTEST.LT.T2FY.AND.CARD(9:9).EQ.'X') T2FY=XTEST
        ENDIF
        GO TO 300
      ENDIF
!
!
! |BETAP|< CRITICAL ANGLE FOR THE FIRST TIME
!
!
! INTERPOLALTE BETWEEN EPOCHS FOR INFORMATION
      JLEFT=MAXTIM-J
      IF(JLEFT.LE.0) GO TO 350
      DO 290 K=1,JLEFT
      READ(46,5000) CARD2
      NXR=NXR+1
      IF(CARD(1:7).EQ.'       ') GO TO 290
!
      READ(CARD(10:19),5003) ITM1
      TM1=DBLE(ITM1)
      READ(CARD(20:30),5005) OA1
      READ(CARD(42:52),5005) BET1
      IF(ABS(BET1).LT.BETM) THEN
        IF(CARD(9:9).EQ.'E'.AND.TM1.LE.T1FY) T1FY=TM1
      ELSE
        IF(T1FY.LT.1.D25) THEN
          IF(TM1.LT.T2FY.AND.CARD(9:9).EQ.'X') T2FY=TM1
        ENDIF
      ENDIF
!
      READ(CARD2(10:19),5003) ITM2
      TM2=DBLE(ITM2)
      READ(CARD2(20:30),5005) OA2
      READ(CARD2(42:52),5005) BET2
      IF(ABS(BET2).LT.BETM) THEN
        IF(CARD2(9:9).EQ.'E'.AND.TM2.LE.T1FY) T1FY=TM2
      ELSE
        IF(T1FY.LT.1.D25) THEN
          IF(TM2.LT.T2FY.AND.CARD2(9:9).EQ.'X') T2FY=TM2
        ENDIF
      ENDIF
!
      CARDP=CARD
      CARD=CARD2
      IF(LFYIIR) GO TO 290
!
!
      IF(OA2.LT.OA1) OA2=OA2+360.D0
      OAS=(OA2-OA1)/(TM2-TM1)
      TEST=ABS(OAS-.00833D0)/.00833D0
      IF(TEST.GT..02D0) OAS=.00833D0
      BETS=(BET2-BET1)/(TM2-TM1)
!
      CALL MXYWEX(TM1,TM2,OA1,OAS,BET1,BETS,YAWRM,BIAS,LEX,  &
     &            TEX1,TEX2,YAWM,YAWDOT,LPROB)
!
      IF(LPROB) THEN
        WRITE(6,6003) ISATID
        WRITE(6,6006)
        WRITE(6,6007)
        WRITE(6,6008) CARDP
        WRITE(6,6008) CARD2
        GO TO 280
      ENDIF
!
      IF(.NOT.LEX) GO TO 280
      IF(OA1.LT.180.D0) THEN
         TYPE='RN'
      ELSE
         TYPE='RM'
      ENDIF
      A0=YAWM
      A1=YAWDOT
      A2=0.D0
      WRITE(48,7000) ISATID,TEX1,TEX2,A0,A1,A2,TYPE
  280 CONTINUE
      CARD=CARD2
  290 CONTINUE
      IF(NXR.GE.MAXTIM) GO TO 350
  300 CONTINUE
  350 CONTINUE
      IF(LFYIIR.AND.T1FY.LT.1.D25) THEN
         TYPE='FY'
         A0=180.D0
         A1=0.D0
         A2=0.D0
         WRITE(48,7000) ISATID,T1FY,T2FY,A0,A1,A2,TYPE
      ENDIF
  400 CONTINUE
!
!
      CARDO=BLANK
      CARDO(1:6)='YPLEND'
      WRITE(48,5000) CARDO
      CLOSE(48)
      RETURN
 5000 FORMAT(A80)
 5001 FORMAT(2I6)
 5002 FORMAT(I7,2X,I10,23X,F9.5)
 5003 FORMAT(I10)
 5004 FORMAT(F9.5,2X,F10.7)
 5005 FORMAT(F11.5)
 6000 FORMAT(' EXECUTION TERMINATING IN SUBROUTINE YPOLY')
 6001 FORMAT(' NO MATCH FOR BLOCK IIR SATELLITE ',I8,' FOUND IN')
 6002 FORMAT(' IN THE YAW RATE LIST')
 6003 FORMAT(' WARNING!!!!!!!!!!!! BLOCK IIR SATELLITE ',I7)
 6004 FORMAT(' HAD A ZERO MAXIMUM YAW RATE INPUT. NO SPECIAL YAW')
 6005 FORMAT(' STEERING CAN BE COMPUTED')
 6006 FORMAT(' THERE WAS A PROBLEM DETERMINING THE END TIME OF')
 6007 FORMAT(' A WHICH SHOULD OCCUR BETWEEN THE FOLLOWING EVENTS:')
 6008 FORMAT(' ',A96)
 6009 FORMAT('  BLOCK IIR SATELLITE ',I7,' HAS A YAWBIAS =',D25.16)
 6010 FORMAT('  ONLY 2.D0 IS ACCEPTABLE')
 7000 FORMAT(I7,2X,F13.1,2X,F13.1,3(2X,F11.6),2X,A2)
 8000 FORMAT(' RAN OUT OF TIMES ON UNIT 42 FINDING END TIME FOR')
 8001 FORMAT(' POST SHADOW MANUEVER IJ = ',2I10)
 8002 FORMAT(' RAN OUT OF TIMES ON UNIT 42 FINDING START TIME FOR')
 8003 FORMAT(' NOON FLIP IJ = ',2I10)
 8004 FORMAT(' RAN OUT OF TIMES ON UNIT 42 FINDING END TIME FOR')
 8005 FORMAT(' NOON FLIP IJ = ',2I10,2X,F14.1,F10.5,2X,F10.5)
      END
