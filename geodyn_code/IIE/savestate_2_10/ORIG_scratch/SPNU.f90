!$SPNU
      SUBROUTINE SPNU(MJDS,FSECET,NM,TSI,EPST)
!     SUBROUTINE SPNU(MJDS,FSECET,FSCUTC,ETUTC,NM,TSI,EPST)
!********1*********2*********3*********4*********5*********6*********7**
! SPNU             83/06/15            8306.0    PGMR - D.ROWLANDS
!                  91/04/02            9104.0    PGMR - SBL
!
! FUNCTION:  CONVERT ET TIMES TO UT TIMES.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDS    I/O   S    ET SECONDS SINCE GEODYN REF.TIME AS INPUT
!                      UT SECONDS SINCE GEODYN REFERENCE TIME IF
!                      USED WITH FSECUT AS OUTPUT
!   FSECET   I    A    FRACTIONAL REMAINING SECONDS (ET)
!   FSCUTC   I    A    FRACTIONAL REMAINING SECONDS FROM MJDS IN UTC
!   ETUTC    I    A    ET-UTC TIME DIFFERENCES, A1-UTC RATES, TIME AT
!                      WHICH DIFF. APPLIES
!   NM       I    S    # OF TIMES FOR WHICH CONVERSION REQUESTED
!
! COMMENTS:
!
!        NOTE - NUMERICALLY MJDS REMAINS UNCHANGED ITS SENSE CHANGES
!               DEPENDING ON WHICH FSEC IT IS USED WITH.
!               BIQUADRATIC INTERPOLATION USED.
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/EPHM/FSECXY(1),XP(261),YP(261),A1UT(261),EP(261),EC(261),  &
     &            FSCFLX(1),FLUXS(36),AVGFLX(36),FLUXM(288),FSECEP(1),  &
     &            EPHP(816),EPHN(96),ELIB(120),BUFT(2700),FSECNP(4)
      COMMON/EPHMPT/NTOT,NTOTS,NNPDPR,NUTORD,IBODDG(8),IGRPDG(4),       &
     &              NENPD,INRCD(2),KPLAN(11),NINEP,NEPHMR,NEPHEM,       &
     &              IBHINT,NINTBH,NXEPHP
      COMMON/POLINF/NPOLE,NPOLEA,NUTA,MXUTC,NMODEL,NXPOLI
      DATA SIXTH/.1666666666666667D0/
      DATA ETA1/32.1496183D0/,ONE/1.D0/,THIRD/.3333333333333333D0/
      DATA FTHRD/1.333333333333333D0/
      DATA C3600/3600.0D0/
!     DIMENSION FSECET(NM),FSCUTC(NM),TSI(NM),EPST(NM)
      DIMENSION FSECET(NM),TSI(NM),EPST(NM)
!     DIMENSION ETUTC(1)
      DATA C10THR/.001D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      CONVR=SECRAD*C10THR
! CALCULATE INTERVAL IN SECONDS
      XINT=IBHINT*C3600
      XINTI=ONE/XINT
      XINTT=(IBHINT*(NINTBH-2))*C3600
      IOFST=1
      ISOFST=2
!
! GET UTC TIMES TO BE ABLE TO USE TABLES
!     CALL UTCET(.FALSE.,NM,MJDS,FSECET,FSCUTC,ETUTC)
!
!     MAKE THE APPROX THAT UTC IS ET
!
! LOOP THROUGH ALL POINTS IN BLOCK
      DO 100 II=1,NM
      IPOINT=1
! TTIME IS TIME OF FIRST POINT IN BUFFER FROM BIH TABLE
      TTIME=FSECXY(1)+XINTT
!     TIME=MJDS+FSCUTC(II)
      TIME=MJDS+FSECET(II)
! FIGURE WHICH OF 2 BUFFERS WILL BE USED IN COMPUTATIONS
      IF(TIME.GT.TTIME) IPOINT=1+NTOT
      TTIME=FSECXY(IPOINT)
! FIND TIME BETWEEN FIRST POINT IN BUFFER AND OBS TIME
      DT=TIME-TTIME
! FIND THE NUMBER OF POINTS IN BUFFER IN INTERVAL DT
      IDX=DT*XINTI
! POINTER TO FIRST BUFFERED PT TO THE LEFT OF OBS VALUE
      IDX2=IDX+IPOINT
! POINTER TWO TO THE LEFT OF OBS VALUE
      IDX1=IDX2-1
! POINTER ONE TO THE RIGHT OF OBS VALUE
      IDX3=IDX2+1
! POINTER TWO TO THE RIGHT OF OBS VALUE
      IDX4=IDX3+1
!   MAP MJDS+FSECET(II) INTO INTERVAL  0,1
      TTIME=TTIME+IDX*XINT
!     DT1=((MJDS-TTIME)+FSCUTC(II))*XINTI
      DT1=((MJDS-TTIME)+FSECET(II))*XINTI
!   FIGURE QUADRATIC COEFFCIENTS FOR EPST CORRECTION
      F2=(EP(IDX3)+EP(IDX1))*SIXTH
      Y11=FTHRD*EP(IDX2)-F2
      Y21=-THIRD*EP(IDX2)+F2
      F2=(EP(IDX4)+EP(IDX2))*SIXTH
      Y12=FTHRD*EP(IDX3)-F2
      Y22=-THIRD*EP(IDX3)+F2
      DT2=ONE-DT1
! INTERPOLATE FOR EPST CORRECTION
      EPSTC=(DT1*(Y12+DT1**2*Y22)+DT2*(Y11+DT2**2*Y21))*CONVR
!   FIGURE QUADRATIC COEFFCIENTS FOR TSI CORRECTION
      F2=(EC(IDX3)+EC(IDX1))*SIXTH
      Y11=FTHRD*EC(IDX2)-F2
      Y21=-THIRD*EC(IDX2)+F2
      F2=(EC(IDX4)+EC(IDX2))*SIXTH
      Y12=FTHRD*EC(IDX3)-F2
      Y22=-THIRD*EC(IDX3)+F2
! INTERPOLATE FOR TSI CORRECTION
      TSIC=(DT1*(Y12+DT1**2*Y22)+DT2*(Y11+DT2**2*Y21))*CONVR
!
      EPST(II)=EPST(II)+EPSTC
      TSI(II)=TSI(II)+TSIC
  100 END DO
      RETURN
      END
