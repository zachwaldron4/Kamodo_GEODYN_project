!$BWSOLR
      SUBROUTINE BWSOLR(TDNRM1,TDNRM2,TDNRM3,                           &
     &                  CTHETA,BWAREA,BWSPEC,                           &
     &                  BWDIFF,DXDD,GRPAR,JARADJ,JSPADJ,JDFADJ,IMAPAR,  &
     &                  IMAPSP,IMAPDF,NFACE,XHELIO,APGMR,APLM,          &
     &                  RS,RSSQ,FACT,VMATRX,NEQN,ISATID,ISHFLG,SPLASD)
!*******************************************************************
!  ROUTINE NAME:   BWSOLR   DATE: 08/23/90      PGMR: A. MARSHALL
!
!   FUNCTION - TO COMPUTE THE TOTAL SOLAR RADIATION PRESSURE
!              ACCELERATION FROM EACH FLATE PLATE IN THE MODEL AND
!              THE ASSOCIATED PARTIALS WITH RESPECT TO PLATE AREA,
!              SPECULAR REFLECTIVITY, AND DIFFUSE REFLECTIVITY.
!
!  I/O PARAMETERS:
!
!   NAME    A/S    I/O   DESCRIPTION OF I/O PARAMETERS IN ARGUMENT LIST
!   -----  ------ -----  -----------------------------------------------
!   TDNRM1   A      I    SPACECRAFT FLAT PLATE NORMAL VECTORS IN TRUE OF
!                        DATE SYSTEM (X COMP.)
!   TDNRM2   A      I    SPACECRAFT FLAT PLATE NORMAL VECTORS IN TRUE OF
!                        DATE SYSTEM (Y COMP.)
!   TDNRM3   A      I    SPACECRAFT FLAT PLATE NORMAL VECTORS IN TRUE OF
!                        DATE SYSTEM (Z COMP.)
!   CTHETA   A      I    COS OF ANGLE BETWEEN TOD PLATE NORMAL AND
!                        SATELLITE-SUN VECTOR
!   BWAREA   A      I    FLATE PLATE AREAS
!   BWSPEC   A      I    FLATE PLATE SPECULAR REFLECTIVITIES
!   BWDIFF   A      I    FLATE PLATE DIFFUSE  REFLECTIVITIES
!   DXDD     A      O    TRUE OF INTEGRATION ACCELERATION DUE TO SOLAR R
!   GRPAR    A      O    PARTIAL OF PLATE ACCELERATION WRT PLATE PARAM.
!   JARADJ   S      O    GRPAR POINTER TO PLATE AREA PARTIALS
!   JSPADJ   S      O    GRPAR POINTER TO PLATE SPEC. REFLEC.
!   JDFADJ   S      O    GRPAR POINTER TO PLATE DIFF. REFLEC.
!   IMAPAR   A      I    MAP FROM UNADJUSTED TO ADJUSTED POINTERS FOR
!                        PLATE AREA
!   IMAPSP   A      I    MAP FROM UNADJUSTED TO ADJUSTED POINTERS FOR
!                        PLATE SPECUALR REFLECTIVITY
!   IMAPDF   A      I    MAP FROM UNADJUSTED TO ADJUSTED POINTERS FOR
!                        PLATE DIFFUSE REFLECTIVITY
!   NFACE    S      I    NUMBER OF FLAT PLATES USED TO MODEL SATELLITE S
!   XHELIO   A      I    SUN-SATELLITE VECTOR IN TRUE OF DATE SYSTEM
!   SCAREA   S      I    SATELLITE CROSS SECTIONAL AREA AS ENTERED ON
!                        THE SATPAR CARD
!   APLM     S      I    (SAT AREA/SAT MASS)*(RADIATION PRESS. AT 1 AU)*
!                        (AU**2)
!   APGMR    S      I    APLM*(CR + CRDOT*T + CRDDOT*T**2)*RATIO
!                        THE SPACECRAFT AREA WILL BE UPDATED TO REFLECT
!                        BOX-WING MODEL PROJECTED AREA
!   RS       S      I    MAGNITUDE OF SATELLITE SUN VECTOR
!   RSSQ     S      I    RS**2
!   FACT     A      O    ARRAY USED FOR EXPLICIT PARTIAL CALCULATIONS
!                        IN 2S
!   VMATRX   A      O    PARTIAL OF ACCELERATION WRT SATELLITE STATE
!   ISHFLG   S      I    FLAG FOR SHADOWING
!                        0: SHADOWING NOT APPLIED
!                        1: CROSS SECTION GIVEN
!                        2: RATION GIVEN
!   SPLASD   A      I    INTERPOLATED SHADOWING AREA FOR
!
!************ BWSOLR LOCAL VARIABLE DEFFINITIONS************************
!   XSUN     A      W    SATELLITE-SUN VECTOR IN TRUE OF DATE SYSTEM
!   PLAREA   S      W    PROJECTED PLATE AREA
!   PART     A      W    BOX-WING PARAMETER PARTIALS IN TOD
!*******************************************************************
!
!
! NOTES:
!        1)  APLM,APGMR WILL BE MODIFIED TO REFLECT THE ACTUAL PROJECTED
!            SATELLITE AREA AS PREDICITED BY THE VARIABLE AREA MODEL
!            (BOX-WING).  THIS WILL EFFECT BOTH ACCELERATION AND PARTIAL
!            VALUES.
!        2)  VMAT CONTRIBUTION OF NORMAL VECTOR PARTIAL WRT SAT STATE
!            USES ONLY ROTATION FROM HCL TO XYZ.  ROTATION FOR SA,YAW,
!            AND GEOCENTRIC VS GEODETIC POINTING ARE CONSIDERED
!            NEGLIGBLE.
!
! REFERENCES:
!
!        "NON-GRAVITATIONAL PERTURBATIONS AND SATELLITE GEODESY,"
!         MILANI ET AL., 1987, PP. 49.
!*******************************************************************
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/BWLOGC/LVAREA,LTOPEX,LSPOT2,LGPSVA,LERS1,LMO,LMOC,LTDRSA,  &
     &              LMAGNA,LGFO,LTRMM,LEUVE,LVCL,LENVS,LCRYO,LGRAIL,    &
     &              LHY2A,LSARAL
      COMMON/BWREAL/SHADOW(2),SUNPCT,SCAREA,SCMASS,BWMEAN
      COMMON/CINTL/LORBIT,LORBVE,LNDRAG,LNSLRD,LDRADJ,LSRADJ,           &
     &             LBACKW,LSETS
      COMMON/CRMI/RMI(9)
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
!
      DIMENSION TDNRM1(NFACE),TDNRM2(NFACE),TDNRM3(NFACE)
      DIMENSION XHELIO(3),BWAREA(NFACE),BWSPEC(NFACE),                  &
     &          BWDIFF(NFACE),DXDD(3),VMATRX(3,6),CTHETA(NFACE)
      DIMENSION GRPAR(NEQN,3)
      DIMENSION IMAPAR(NFACE),IMAPSP(NFACE),IMAPDF(NFACE)
      DIMENSION XSUN(3),ACCEL(3),PART(3),FACT(3)
      DIMENSION SPLASD(NFACE)
!
      DATA ZERO/0.0D0/,ONE/1.0D0/,TWO/2.0D0/,THREE/3.0D0/
!
!********1*********2*********3*********4*********5*********6*********7**
! START OF EXECUTABLE CODE
!********1*********2*********3*********4*********5*********6*********7**
!
! INITIALIZE VARIABLES
      TOTARE = ZERO
      JCOUNT = JARADJ-1
      MCOUNT = JSPADJ-1
      NCOUNT = JDFADJ-1
!
! REMOVE SPACECRAFT AREA AS DEFINED ON THE SATPAR CARD FROM APGMR
      APGMR = APGMR/SCAREA
      APLM  = APLM /SCAREA
!
! ZERO OUT ACCELERATION ARRAY
      DXDD(1) = ZERO
      DXDD(2) = ZERO
      DXDD(3) = ZERO
! FORM UNIT SAT-SUN VECTOR
      XSUN(1) = -XHELIO(1)/RS
      XSUN(2) = -XHELIO(2)/RS
      XSUN(3) = -XHELIO(3)/RS
!
! LOOP THROUGH EACH PLATE, COMPUTING ACCELERATION AND PARTIALS
      DO 100 I=1,NFACE
!
! INITIALIZE PARTIAL POINTERS
      IF(IMAPAR(I).GT.0) JCOUNT=JCOUNT+1
      IF(IMAPSP(I).GT.0) MCOUNT=MCOUNT+1
      IF(IMAPDF(I).GT.0) NCOUNT=NCOUNT+1
!
      IF(ISHFLG.EQ.0) THEN
! SELF SHADOWING NOT APPLIED
! DETERMINE IF THIS PLATE IS ILLUMINATED
        IF(CTHETA(I).LE.ZERO) GOTO 100
        PLAREA = BWAREA(I)*CTHETA(I)
      ELSE IF(ISHFLG.EQ.1) THEN
! CROSS-SECTION
        IF(SPLASD(I).LE.ZERO) GOTO 100
        PLAREA = SPLASD(I)
      ELSE
! RATIO
        IF(SPLASD(I).LE.ZERO) GOTO 100
        PLAREA = BWAREA(I)*SPLASD(I)
      ENDIF
! SUM TOTAL ILLUMINATED SPACECRAFT CROSS-SECTIONAL AREA
      TOTARE = TOTARE + PLAREA
!
! COMPUTE SOLAR RADIATION PRESSURE ACCELERATION ON THIS PLATE IN
! X,Y,AND Z DIRECTION OF THE TRUE OF DATE SYSTEM
!
! ... If TDRSS then compute 1-transmissivity of single access antennae
! .... PANEL cards must have the single access antennae as 1st four
! .... for TDRSS macro-model
!
            OMTRNS=ONE
            IF(LTDRSA.AND.(I.LE.4).AND.ISATID.NE.9501700)               &
     &        CALL TDRSTN(I,CTHETA(I),OMTRNS)
!
      TERM1 = PLAREA*APGMR*OMTRNS/RSSQ
      TERM2 = ONE - BWSPEC(I)
      TERM3 = TWO*(BWDIFF(I)/THREE + BWSPEC(I)*CTHETA(I))
      FACT(1) = -(TERM2*XSUN(1) + TERM3*TDNRM1(I))
      FACT(2) = -(TERM2*XSUN(2) + TERM3*TDNRM2(I))
      FACT(3) = -(TERM2*XSUN(3) + TERM3*TDNRM3(I))
      ACCEL(1) = TERM1*FACT(1)
      ACCEL(2) = TERM1*FACT(2)
      ACCEL(3) = TERM1*FACT(3)
!
! ADD ACCELERATIONS TO TOTAL
      DXDD(1) = DXDD(1)+ACCEL(1)
      DXDD(2) = DXDD(2)+ACCEL(2)
      DXDD(3) = DXDD(3)+ACCEL(3)
      IF(LORBIT) GOTO 100
!
! COMPUTE PARTIAL OF ACCELERATION WRT AREA,
! ROTATE TO TRUE OF REFERENCE FRAME AND ADD THEM TO THE SAME PARTIALS
! PREVIOUSLY COMPUTED FOR THE OTHER NON-CONSERVATIVE FORCES
      IF(IMAPAR(I).GT.0) THEN
         PART(1) = ACCEL(1)/BWAREA(I)
         PART(2) = ACCEL(2)/BWAREA(I)
         PART(3) = ACCEL(3)/BWAREA(I)
         GRPAR(JCOUNT,1) = GRPAR(JCOUNT,1)+PART(1)
         GRPAR(JCOUNT,2) = GRPAR(JCOUNT,2)+PART(2)
         GRPAR(JCOUNT,3) = GRPAR(JCOUNT,3)+PART(3)
      ENDIF
!
! COMPUTE PARTIAL OF ACCELERATION WRT SPECULAR REFLECTIVITY,
! ROTATE TO TRUE OF REFERENCE FRAME AND
! ADD THEM TO THE SAME PARTIALS PREVIOUSLY COMPUTED FOR THE OTHER
! NON-CONSERVATIVE FORCES
      IF(IMAPSP(I).GT.0) THEN
        PART(1) = -TERM1*(TWO*CTHETA(I)*TDNRM1(I)-XSUN(1))
        PART(2) = -TERM1*(TWO*CTHETA(I)*TDNRM2(I)-XSUN(2))
        PART(3) = -TERM1*(TWO*CTHETA(I)*TDNRM3(I)-XSUN(3))
        GRPAR(MCOUNT,1)=GRPAR(MCOUNT,1)+PART(1)
        GRPAR(MCOUNT,2)=GRPAR(MCOUNT,2)+PART(2)
        GRPAR(MCOUNT,3)=GRPAR(MCOUNT,3)+PART(3)
      ENDIF
!
! COMPUTE PARTIAL OF ACCELERATION WRT DIFFUSE REFLECTIVITY,
! ROTATE TO TRUE OF REFERENCE FRAME AND
! ADD THEM TO THE SAME PARTIALS PREVIOUSLY COMPUTED FOR THE OTHER
! NON-CONSERVATIVE FORCES
      IF(IMAPDF(I).GT.0) THEN
        PART(1)=-TERM1*TWO/THREE*TDNRM1(I)
        PART(2)=-TERM1*TWO/THREE*TDNRM2(I)
        PART(3)=-TERM1*TWO/THREE*TDNRM3(I)
        GRPAR(NCOUNT,1)=GRPAR(NCOUNT,1)+PART(1)
        GRPAR(NCOUNT,2)=GRPAR(NCOUNT,2)+PART(2)
        GRPAR(NCOUNT,3)=GRPAR(NCOUNT,3)+PART(3)
      ENDIF
!
!
  100 END DO
! UPDATE APGMR VALUE TO REFLECT TOTAL PROJECTED AREA
      APGMR = APGMR*TOTARE
      APLM  = APLM *TOTARE
      RETURN
      END
