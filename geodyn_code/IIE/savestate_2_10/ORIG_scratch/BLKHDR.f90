!$BLKHDR
      SUBROUTINE BLKHDR(OBFOUT,INDBUF,OBSBUF,NUMOBS)
!********1*********2*********3*********4*********5*********6*********7**
! BLKHDR           86/06/04            8606.0    PGMR - TOM MARTIN
!
! FUNCTION:  LOAD OBSERVATION HEADER RECORDS FROM INTERNAL
!            OBSERVATION BUFFER INTO OUTPUT BUFFERS.
!            DUMP ANY PREVIOUSLY FILLED BUFFERS AND RESET OPEN
!            BUFFER INDICES.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   OBFOUT  I/O   A    OUTPUT BUFFERS\MODIFIED OUTPUT BUFFERS.
!   INDBUF   I    A    INDICES OF OPEN BUFFERS.
!   OBSBUF   I    A    INTERNAL OBSERVATION BUFFER.
!   NUMOBS   I    S    OBSERVATION COUNT TO BE OUTPUT
!
! COMMENTS:
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CBLOKI/MJDSBL,MTYPE ,NM    ,JSTATS,NPSEG ,JSATNO(3),ITSYS ,&
     &       NHEADB,NELEVS,ISTAEL(12),INDELV(3,4),JSTANO(3),ITARNO,     &
     &       KTARNO
      COMMON/CEARTH/AEG,AEGSQ,FGINV,FG,EGSQ,EGSQP1,C1,C2,FOURC1,TWOC2,  &
     &              XH2,XL2,WREF,RMEAN,REFC20,REFC40,REFC60,BEG,CBLTC,  &
     &              WAE2,GAMMAA,FSTAR,F4
      COMMON/CNORML/LNORM ,LNORMP
      COMMON/CONTRL/LSIMDT,LOBS  ,LORB  ,LNOADJ,LNADJL,LORFST,LORLST,   &
     &              LORALL,LORBOB,LOBFST,LOBLST,LOBALL,LPREPO,LORBVX,   &
     &              LORBVK,LACC3D,LSDATA,LCUTOT,LACCEL,LDYNAC,LFRCAT,   &
     &              LNIAU, NXCONT
      COMMON/CSIML/LSIMTI
      COMMON/CNORMI/NRECDF,NRECDH,INDREC,MPTS  ,NPTS  ,NPLEFT
      COMMON/COBBUF/IOBSBF,IBUF1 ,IBUF2 ,IWORD1,NHEADR,NWORDM
      COMMON/COBGLO/MABIAS,MCBIAS,MPVECT,MINTPL,MPARAM,MSTA,MOBBUF,     &
     &       MNPITR,MWIDTH,MSIZE ,MPART ,MBUF  ,NXCOBG
      COMMON/DTMGDN/TGTYMD,TMGDN1,TMGDN2,REPDIF,XTMGN
      COMMON/CTMGDN/LTMGDN
      COMMON/CVIEW /IOUT6 ,ILINE6,IPAGE6,MLINE6,                        &
     &              IOUT8 ,ILINE8,IPAGE8,MLINE8,                        &
     &              IOUT9 ,ILINE9,IPAGE9,MLINE9,                        &
     &              IOUT10,ILIN10,IPAG10,MLIN10,                        &
     &              IOUT15,ILIN15,IPAG15,MLIN15,                        &
     &              IOUT16,ILIN16,IPAG16,MLIN16,                        &
     &              IOUT7 ,NXCVUE
      COMMON/ION2EL/LIONC,LFRQOP,LFRQOF,NXIONL
      COMMON/KPRE/IKPRE9,IKPRE(3)
      COMMON/TROPX/LOUTRP
      COMMON/UNITS/IUNT11,IUNT12,IUNT13,IUNT19,IUNT30,IUNT71,IUNT72,    &
     &             IUNT73,IUNT05,IUNT14,IUNT65,IUNT88,IUNT21,IUNT22,    &
     &             IUNT23,IUNT24,IUNT25,IUNT26
      DIMENSION OBFOUT(MSIZE,MPART,MBUF),INDBUF(MBUF),OBSBUF(1)
      DIMENSION LHOLD(24)
      DIMENSION LIPRE9(24),LIPRE(24,3)
      DIMENSION RROTBF(200,10)
      DATA IUNT20/20/
      DATA C1D6/1.0D6/,ZERO/0.0D0/
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
      IF(MPTS.LE.0) GO TO 6000
!
!... CHECK IF THE EXTRA HEAD RECORD IS NEEDED FOR THIS RUN
!
      CHHDDT=OBFOUT(1,10,1)
      IF(CHHDDT.EQ.(-9999999.D0).OR.TMGDN1.EQ.2430000.5D0)THEN
      LTMGDN=.FALSE.
      IHO=0
      KEND=200
      ELSE
      IHO=1
      KEND=199
      ENDIF

! RESET MASTER HEADER RECORD DATA BLOCK START AND STOP TIMES
!
!.....OBSERVATION RECORD POINTER
      ND0REC=INDREC+NRECDH
!.....TIME OF FIRST OBSERVATION
      ND1REC=ND0REC+1
      NBUF1=(ND1REC-1)/MSIZE
      NDXREC=ND1REC-NBUF1*MSIZE
      NDXBUF=INDBUF(NBUF1+1)
      TIME1 =OBFOUT(NDXREC,6,NDXBUF)
!.....TIME OF LAST OBSERVATION
      NDNREC=ND0REC+MPTS
      NBUFN=(NDNREC-1)/MSIZE
      NDXREC=NDNREC-NBUFN*MSIZE
      NDXBUF=INDBUF(NBUFN+1)
      TIME2 =OBFOUT(NDXREC,6,NDXBUF)
!.....ELAPSED TIME
      ELAPSD=TIME2-TIME1
!.....STORE TIME OF FIRST OBSERVATION AND ELAPSED TIME IN MASTER HEADER
      NDXREC=INDREC+1
      IBUF=(NDXREC-1)/MSIZE
      NDXREC=NDXREC-IBUF*MSIZE
      NDXBUF=INDBUF(IBUF+1)
      OBFOUT(NDXREC,2,NDXBUF)=TIME1
      OBFOUT(NDXREC,3,NDXBUF)=ELAPSD
!
      IF(LOUTRP) THEN
         XPRE=OBFOUT(NDXREC,9,NDXBUF)+.01D0
         KHOLD=XPRE
         CALL DECOMP(KHOLD,LHOLD,24)
         LHOLD(3)=.TRUE.
         LHOLD(4)=.TRUE.
         LHOLD(6)=.TRUE.
         CALL RECOMP(LHOLD,KHOLD,24)
         OBFOUT(NDXREC,9,NDXBUF)=KHOLD
!
         NDXREC=INDREC+2
         IBUF=(NDXREC-1)/MSIZE
         NDXREC=NDXREC-IBUF*MSIZE
         NDXBUF=INDBUF(IBUF+1)
         XPRE=OBFOUT(NDXREC,9,NDXBUF)+.01D0
         KHOLD=XPRE
         CALL DECOMP(KHOLD,LHOLD,24)
         LHOLD(3)=.TRUE.
         LHOLD(4)=.TRUE.
         LHOLD(6)=.TRUE.
         CALL RECOMP(LHOLD,KHOLD,24)
         OBFOUT(NDXREC,9,NDXBUF)=KHOLD
!
         NDXREC=INDREC+3
         IBUF=(NDXREC-1)/MSIZE
         NDXREC=NDXREC-IBUF*MSIZE
         NDXBUF=INDBUF(IBUF+1)
         XPRE=OBFOUT(NDXREC,9,NDXBUF)+.01D0
         KHOLD=XPRE
         CALL DECOMP(KHOLD,LHOLD,24)
         LHOLD(3)=.TRUE.
         LHOLD(4)=.TRUE.
         LHOLD(6)=.TRUE.
         CALL RECOMP(LHOLD,KHOLD,24)
         OBFOUT(NDXREC,9,NDXBUF)=KHOLD
      ENDIF
! SUBTRACT TIME OF FIRST OBSERVATION FROM ALL OBSERVATION TIMES
      N1=ND1REC-NBUF1*MSIZE
      N2=NDNREC-NBUF1*MSIZE
!.....FIRST BUFFER WITH OBSERVATION RECORDS
      N2=MIN(N2,MSIZE)
      NDXBUF=INDBUF(NBUF1+1)
      DO 1000 N=N1,N2
      OBFOUT(N,6,NDXBUF)=OBFOUT(N,6,NDXBUF)-TIME1
 1000 END DO
      IF(NBUFN-NBUF1-1) 3500,3000,1500
!.....WHOLE BUFFERS WITH ONLY OBSERVATION RECORDS
 1500 CONTINUE
      ND1BUF=INDBUF(NBUF1+2)
      NDNBUF=INDBUF(NBUFN)
      NBUF2=NBUF1+2
      DO 2500 IBUF=NBUF2,NBUFN
      NDXBUF=INDBUF(IBUF)
      DO 2000 N=1,MSIZE
      OBFOUT(N,6,NDXBUF)=OBFOUT(N,6,NDXBUF)-TIME1
 2000 END DO
 2500 END DO
!.....FINAL BUFFER WITH OBSERVATION RECORDS
 3000 CONTINUE
      N2=NDNREC-NBUFN*MSIZE
      NDXBUF=INDBUF(NBUFN+1)
      DO 3200 N=1,N2
      OBFOUT(N,6,NDXBUF)=OBFOUT(N,6,NDXBUF)-TIME1
 3200 END DO
 3500 CONTINUE
! DUMP ANY FULL BUFFERS AND RESET POINTERS
      NDXREC=INDREC+NRECDH+NRECDD*MPTS
      NBUF=NDXREC/MSIZE
      IF(NBUF.LT.1) GO TO 4500
!.....DUMP FULL BUFFERS
      MDIMEN=MSIZE*MPART
      DO 4000 IBUF=1,NBUF
      NDXBUF=INDBUF(IBUF)
      IOBPT=MBUF+1
!  CREATE AN AXTRA HEAD RECORD INCLUDED GEODYN REFERENCE TIME (JD)
!  FOR UNIT-20
      IF(LTMGDN)THEN
       RROTBF(1,1)=TMGDN1
       RROTBF(1,10)= -9999999.D0
       DO I=2,9
       RROTBF(1,I)=0.D0
       ENDDO
       I0=IHO
       DO 35520 K2=1,KEND
       I1=I0+K2
        DO 35510 K1=1,10
       RROTBF(I1,K1)=OBFOUT(K2,K1,NDXBUF)
35510 END DO
35520 END DO
!     NDXBF1=NDXBUF
      CALL BLKDMP(RROTBF,RROTBF,MDIMEN,IUNT20)
      IF(IHO.EQ.1)THEN
        K2=200
        DO 35550 K1=1,10
        RROTBF(1,K1)=OBFOUT(K2,K1,NDXBUF)
35550 END DO
        ENDIF
       LTMGDN=.FALSE.
       ELSE
       I0=IHO
       DO 35540 K2=1,KEND
       I1=I0+K2
       DO 35530 K1=1,10
       RROTBF(I1,K1)=OBFOUT(K2,K1,NDXBUF)
35530 END DO
35540 END DO
      CALL BLKDMP(RROTBF,RROTBF,MDIMEN,IUNT20)
      IF(IHO.EQ.1)THEN
        K2=200
        DO 35560 K1=1,10
        RROTBF(1,K1)=OBFOUT(K2,K1,NDXBUF)
35560 END DO
        ENDIF
       ENDIF
 4000 END DO
      NDXREC=NDXREC-NBUF*MSIZE
 4500 CONTINUE
!.....RESET BUFFER POINTERS
      NDXBUF=INDBUF(NBUF+1)
      DO 5000 IBUF=1,MBUF
      INDBUF(IBUF)=NDXBUF
      NDXBUF=MOD(NDXBUF,MBUF)+1
 5000 END DO
!.....RESET LAST RECORD POINTER
      INDREC=NDXREC
 6000 CONTINUE
      IF(NUMOBS.GT.0) GO TO 7000
! DUMP LAST BUFFER AND ENDFILE OUTPUT UNIT
      NDXBUF=INDBUF(1)
      IF(INDREC.EQ.MSIZE) GO TO 6800
!.....ZERO FILL UNUTILIZED PORTION OF LAST BUFFER
      I1=INDREC+1
      DO 6500 IPART=1,MPART
      DO 6200 I=I1,MSIZE
      OBFOUT(I,IPART,NDXBUF)=ZERO
 6200 END DO
 6500 END DO
 6800 CONTINUE
!.....DUMP LAST BUFFER
      IF(LTMGDN)THEN
       RROTBF(1,1)=TMGDN1
       RROTBF(1,10)= -9999999.D0
       DO I=2,9
       RROTBF(1,I)=0.D0
       ENDDO
       LTMGDN=.FALSE.
       ENDIF
      MDIMEN=MSIZE*MPART
      IOBPT=MBUF+1
       I0=IHO
       DO 6850 K2=1,KEND
       I1=I0+K2
       DO 6855 K1=1,10
       RROTBF(I1,K1)=OBFOUT(K2,K1,NDXBUF)
 6855 END DO
 6850 END DO
      CALL BLKDMP(RROTBF,RROTBF,MDIMEN,IUNT20)
!     CALL BLKDMP(OBFOUT(1,1,NDXBUF),OBFOUT(1,1,IOBPT),MDIMEN,IUNT20)
!.....END FILE OUTPUT UNIT
      CLOSE(IUNT20)
!.....NOTIFY USER THAT OUTPUT FILE HAS BEEN CLOSED.
      WRITE(IOUT6,68000) IUNT20
      IF(MPTS.GT.0) RETURN
      WRITE(IOUT6,69000)
      WRITE(IUNT88,69000)
      RETURN
 7000 CONTINUE
! INITIALIZE NEW OBSERVATION BLOCK
!
!.....NUMBER OF OUTPUT DATA POINTS IN THIS PASS
      NPLEFT=NUMOBS
!.....NUMBER OF HEADER RECORDS REQUIRED
      NRECDH=NHEADR
      IF(MTYPE.EQ.99.OR.MTYPE.EQ.100.OR.MTYPE.EQ.199) NRECDH=3
!.....NUMBER OF DATA RECORDS PER OBSERVATION
      NRECDD=NHEADR
      IF(MTYPE.EQ.99.OR.MTYPE.EQ.100.OR.MTYPE.EQ.199) NRECDD=4
!.....NUMBER OF FREE RECORDS IN BUFFERS
      NRECDF=MBUF*MSIZE-INDREC
!.....NUMBER OF OBSERVATIONS FOR WHICH FREE SPACE IS AVAILABLE
      NXFREE=(NRECDF-NRECDH)/NRECDD
!.....NUMBER OF OBSERVATIONS IN NEXT OUTPUT BLOCK
      MPTS  =MIN(NXFREE,NPLEFT)
      NPTS  =0
! CLEAR PORTION OF OBSERVATION BUFFER(S) TO BE FILLED
      MCLEAR=(MPTS*NRECDD+NRECDH)
      I0=INDREC
      I1=INDREC+1
      I2=INDREC+MCLEAR
      NBUF=(I2-1)/MSIZE
      I2=I2-NBUF*MSIZE
      IF(NBUF.EQ.0) GO TO 7800
!.....CLEAR STARTING AND WHOLE BUFFERS
      DO 7500 IBUF=1,NBUF
      NDXBUF=INDBUF(IBUF)
      NCLEAR=MSIZE-I0
      DO 7200 IPART=1,MPART
      CALL CLEARA(OBFOUT(I1,IPART,NDXBUF),NCLEAR)
 7200 END DO
      I0=0
      I1=1
 7500 END DO
!.....CLEAR ENDING BUFFER
 7800 CONTINUE
      IBUF=NBUF
      NDXBUF=INDBUF(IBUF+1)
      NCLEAR=I2-I0
      DO 8000 IPART=1,MPART
      CALL CLEARA(OBFOUT(I1,IPART,NDXBUF),NCLEAR)
 8000 END DO
! LOAD HEADER RECORDS INTO OUTPUT BLOCK
      INDEX0=IWORD1-1
      DO 8500 IRECDH=1,NHEADR
      NDXREC=INDREC+IRECDH
      IBUF=(NDXREC-1)/MSIZE
      NDXREC=NDXREC-IBUF*MSIZE
      NDXBUF=INDBUF(IBUF+1)
      DO 8200 IPART=1,MPART
      I=INDEX0+IPART
      OBFOUT(NDXREC,IPART,NDXBUF)=OBSBUF(I)
 8200 END DO
      INDEX0=INDEX0+MPART
      IF(.NOT.LNORM.AND..NOT.LSIMTI) GO TO 8500
!.....ALTER PREPRO WORDS TO REFLECT CORRECTIONS APPLIED BY GEODYN-IIE
      IPRE =OBFOUT(NDXREC,9,NDXBUF)
      IF(LNORM) THEN
      IPRE=NORMSW(IPRE,MTYPE,IRECHD)
      ENDIF
! IF SIMULATED DATA CARDS ARE PRESENT AND THE IONOSPHERIC OPTION IS
! SELECTED OVERWRITE PREPRO WORDS WITH INITIAL VALUES FROM THE DATA
! WITH BITS 6 AND 7 MODIFIED TO INDICATE IONOSPHERIC CORRECTIONS
! ARE PROVIDED AND INCLUDED IN THE SUM OF OBSERVATION CORRECTIONS.
      IF(LIONC.AND.LSIMDT)THEN
        IF(IRECDH.EQ.1)THEN
          CALL DECOMP(IKPRE9,LIPRE9,24)
! MODIFY BITS 6 AND 7 TO INDICATE IONOSPHERIC CORRECTIONS
! ARE PROVIDED.
          LIPRE9(6)=.TRUE.
          LIPRE9(7)=.TRUE.
          CALL RECOMP(LIPRE9,IPRE,24)
        ELSE
          CALL DECOMP(IKPRE(IRECDH-1),LIPRE(1,IRECDH-1),24)
! MODIFY BITS 6 AND 7 TO INDICATE IONOSPHERIC CORRECTIONS
! ARE INCLUDED IN THE SUM OF OBSERVATION CORRECTIONS.
          LIPRE(6,IRECDH-1)=.TRUE.
          LIPRE(7,IRECDH-1)=.TRUE.
          CALL RECOMP(LIPRE(1,IRECDH-1),IPRE,24)
        ENDIF
      ENDIF
      OBFOUT(NDXREC,9,NDXBUF)=IPRE
!     OBFOUT(NDXREC,9,NDXBUF)=NORMSW(IPRE,MTYPE,IRECHD)
 8500 END DO
! LOAD RECORD TYPE INDICATORS INTO OBSERVATION AND CORRECTION RECORDS
      ND0REC=INDREC+NRECDH
      DO 9000 IRECDH=1,NHEADR
      RECTYP=(IRECDH-1)*C1D6
      CALL BLKTYP(OBFOUT,INDBUF,MSIZE,MPART,MBUF,MPTS,RECTYP,ND0REC)
      ND0REC=ND0REC+MPTS
 9000 END DO
! OVERWRITE OBSERVATION COUNT
      NDXREC=INDREC+1
      IBUF=(NDXREC-1)/MSIZE
      NDXREC=NDXREC-IBUF*MSIZE
      NDXBUF=INDBUF(IBUF+1)
      OBFOUT(NDXREC,7,NDXBUF)=MPTS
      IF(MTYPE.LT.99) RETURN
!
! ALTIMETER DATA PROCESSING FROM THIS POINT DOWN
!
! LOAD REC. TYPE IND. INTO OCEAN PARAMETERS AND LOCATION DATA RECORDS
      DO 9500 IRECDH=4,7,3
      RECTYP=IRECDH*C1D6
      CALL BLKTYP(OBFOUT,INDBUF,MSIZE,MPART,MBUF,MPTS,RECTYP,ND0REC)
      ND0REC=ND0REC+MPTS
 9500 END DO
! LOAD EARTH SEMI-MAJOR AXIS AND FLATTENING INTO ALTIMETER
!      LOCATION DATA DESCRIPTOR RECORD
      NDXREC=INDREC+NRECDH
      IBUF=(NDXREC-1)/MSIZE
      NDXREC=NDXREC-IBUF*MSIZE
      NDXBUF=INDBUF(IBUF+1)
      OBFOUT(NDXREC,1,NDXBUF)=AEG
      OBFOUT(NDXREC,2,NDXBUF)=FGINV
      OBFOUT(NDXREC,10,NDXBUF)=-2.D0*C1D6
      RETURN
68000 FORMAT('0** BLKHDR **  OBSERVATION OUTPUT UNIT(',I2,') HAS BEEN', &
     &   ' CLOSED.'/1H )
69000 FORMAT('0** BLKHDR **  WARNING!  EITHER NO OBSERVATION BLOCKS ',  &
     &   'HAVE BEEN OUTPUT, OR'/25X,'LAST OBSERVATION BLOCK WAS NULL.'/ &
     &   1H )
      END
