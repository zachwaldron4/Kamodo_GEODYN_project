!$OCCULT
      SUBROUTINE OCCULT(MJDSEC,FSEC,NM,AEBOD,XSAT,XSATBC,URXX,RSS,      &
     &                  LOCCLT,LMSHD,LSHD,SCRTCH,AA,II)
!********1*********2*********3*********4*********5*********6*********7**
! OCCULT           88/07/21            8805.0    PGMR - D. ROWLANDS
!
! FUNCTION:  IN THE INTERPLANETARY CASE, DETERMINE IF THE CENTER
!            OF INTEGRATION BODY OBSCURES THE SATELLITE FROM THE
!            TRACKING STATION. THIS IS ONLY USEFUL WHEN GENERATING
!            SIMULATED DATA. ALSO SEE IF SATELLITE IS SHADOWED.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I    S    SATELLITE INTEGER TIME
!   FSEC     I    A    SATELLITE REMAINING SECONDS TIME
!   NM       I    S    NUMBER OF MESUREMENTS.
!   AEBOD    I    A    ARRAY CONTAINING RADIUS OF THE SUN FOLLOWED
!                      BY RADII OF BODIES THAT MIGHT OBSCURE THE
!                      SUN FROM THE SATELLITE. THE CIG BODY IS IN
!                      THE SECOND LOCATION.
!   XSAT     I    A    TRUE OF REF CIG SAT COORDINATES
!   XSATBC   I    A    TRUE OF REF BARYCENTRIC SAT COORDINATES
!   URXX     I    A    UNIT VECTOR POINTING FROM STATION TO
!                      SAT (TRUE OF REF)
!   RSS      I    A    ONE WAY RANGE BETWEEN SAT AND SUN
!   LOCCLT   O    A    TRUE IF PLANET OBSCURES THE SAT FROM STATION
!   LMSHD    O    A    TRUE IF SAT IS SHADOWED
!   LSHD     I    S    TRUE IF WANT TO TEST ON SATELLITE IN SHADOW.
!   SCRTCH  I/O   A    SCRATCH ARRAY.  SCRTCH(NM,6)
!
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/CBARYC/CBODY(6),TBODY(6),SUNRF(6)
      COMMON/CNIGLO/MINTIM,MSATG3,MEQNG ,MEQNG3,MSATG ,MSATOB,MSATA ,   &
     &              MSATA3,MSETA ,MINTVL,MSORDR,MSORDV,NMXORD,          &
     &       MCIPV ,MXBACK,MXI   ,MPXPF ,MAXAB ,MSETDG,MXSATD,          &
     &       MXDEGS,MXDRP ,MXDRPA,MXSRP ,MXSRPA,MXGAP ,MSATDR,          &
     &       MSATSR,MSATGA,MXDRPD,MXSRPD,MXGAPD,MXBCKP,MXTPMS,          &
     &       NSTAIN,NXCNIG
      DIMENSION AEBOD(2),RSS(NM),URXX(NM,3),XSAT(MINTIM,3),LOCCLT(NM)
      DIMENSION FSEC(NM),XSATBC(MINTIM,3),LMSHD(NM)
      DIMENSION SCRTCH(NM,6)
      DIMENSION AA(1),II(1)
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
! CALULATE SATELLITE RADIUS VECTOR FROM CIG
!
!
      DO 100 I=1,NM
      SCRTCH(I,2)=XSAT(I,1)*XSAT(I,1)+XSAT(I,2)*XSAT(I,2)               &
     &           +XSAT(I,3)*XSAT(I,3)
      SCRTCH(I,3)=SQRT(SCRTCH(I,2))
  100 END DO
! CALCULATE ANGLE BETWEEEN CIG AND STATION FROM SATELLITE
      DO 200 I=1,NM
      SCRTCH(I,2)=XSAT(I,1)*URXX(I,1)+XSAT(I,2)*URXX(I,2)               &
     &           +XSAT(I,3)*URXX(I,3)
      SCRTCH(I,2)=SCRTCH(I,2)/SCRTCH(I,3)
!CC      SCRTCH(I,1)=DARCOS(SCRTCH(I,2))
      SCRTCH(I,1)=ACOS(SCRTCH(I,2))
  200 END DO
! CALCULATE  APPARENT RADIUS OF CIG
      DO 300 I=1,NM
      SCRTCH(I,4)=AEBOD(2)/SCRTCH(I,3)
!CC      SCRTCH(I,2)=DARSIN(SCRTCH(I,4))
      SCRTCH(I,2)=ASIN(SCRTCH(I,4))
  300 END DO
!
! IF ANGLE BETEEN CIG AND STATION SMALLER THAN APPARENT RADIUS,
! THEN LOCCLT =.TRUE.
      DO 400 I=1,NM
      LOCCLT(I)=LOCCLT(I).OR.(SCRTCH(I,1).LT.SCRTCH(I,2))
  400 END DO
      IF(.NOT.LSHD) RETURN
!
!  FIGURE IF SATELLITE IS SHADOWED
      DO 500 I=1,NM
      CALL PLANPO(MJDSEC,FSEC(I),.FALSE.,.TRUE.,AA,II)
      SCRTCH(I,4)=SUNRF(1)
      SCRTCH(I,5)=SUNRF(2)
      SCRTCH(I,6)=SUNRF(3)
      SCRTCH(I,4)=-SCRTCH(I,4)+XSATBC(I,1)
      SCRTCH(I,5)=-SCRTCH(I,5)+XSATBC(I,2)
      SCRTCH(I,6)=-SCRTCH(I,6)+XSATBC(I,3)
      SCRTCH(I,1)=XSAT(I,1)*SCRTCH(I,4)+XSAT(I,2)*SCRTCH(I,5)           &
     &           +XSAT(I,3)*SCRTCH(I,6)
      SCRTCH(I,4)=SCRTCH(I,1)/(RSS(I)*SCRTCH(I,3))
!CC      SCRTCH(I,1)=DARCOS(SCRTCH(I,4))
      SCRTCH(I,1)=ACOS(SCRTCH(I,4))
!CC      SCRTCH(I,4)=DARSIN(AEBOD(1)/RSS(I))+SCRTCH(I,2)
      SCRTCH(I,4)=ASIN(AEBOD(1)/RSS(I))+SCRTCH(I,2)
  500 END DO
      DO 1000 I=1,NM
      LMSHD(I)=LMSHD(I).OR.(SCRTCH(I,1).LT.SCRTCH(I,4))
 1000 END DO
      RETURN
      END
