!$OLTIME
      SUBROUTINE OLTIME(MJDSC0,FSEC,NM,ANGFAC,SPEED,TRIGOL,SCTOL,IPTOLS,&
     &                  ANGSAT,CONNAM,SCRTCH,GRDANG,UANG,FAMP,IDOODN)
!********1*********2*********3*********4*********5*********6*********7**
! OLTIME            00/00/00            8604.0    PGMR - ?
!
! FUNCTION:  COMPUTES OCEAN LOADING TRIG INFORMATION
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSC0   I    S    START BLOCK SATELLITE INTEGER TIME
!   FSEC     I    A    FRACTIONAL SECONDS OF BLOCK MEASUREMENT TIMES
!   NM       I    S    NUMBER OF OBSERVATIONS IN THE BLOCK
!   ANGFAC   I    A    FACTORS TO MULTIPLY EACH OF THE 5 ARGUMENTS BY
!                      PLUS A -/+ PHASE FOR CERTAIN DOODSON NUMBERS
!   SPEED    I    A    RATE OF ANGULAR ARGUMENT FOR EACH FREQUENCY
!   TRIGOL   O    A    COSINES AND SINES OF OCEAN LOADING FREQURNCIES
!                      AT BLOCK START TIME (IN SOME CASES MODIFIED
!                      FOR SIDE BANDS). ALSO DERIVATIVES WRT OF
!                      THE COSINE AND SINES.
!   SCTOL    O    A    SCRATCH SPACE FOR OCEAN LOADING
!   IPTOLS   I    A    POINTER FROM OCEAN LOADING FREQUENCIES TO
!                      SATELLITE TIDE SIDE BAND INFORMATION
!   ANGSAT   I    A    SAME AS ANGFAC EXCEPT FOR SAT TIDES
!   CONNAM   I    A    CONSITUENT NAME ARRAY FOR SAT TIDES
!   SCRTCH  I/O   A    SCRATCH ARRAY FOR INTERMEDIATE CALCULATIONS
!                      FOR SATELLITE TIDES
!   GRDANG   O    A    ANGLE COMPUTED FROM THE LINEAR COMBINATION
!                      OF ASTRONOMICAL ANGLES (GIVEN AT GRID TIMES)
!                      FOR SAT TIDES
!   UANG     O    A    ANGLE TO BE ADDED TO GRDANG TO COMPENSATE
!                      FOR SIDE BANDS (GIVEN AT GRID TIMES)
!   FAMP     O    A    FACTOR TO MULTIPLLT AMPLITUDE OF THE TIDE TO
!                      COMPENSATE FOR SIDE BANDS (GIVEN AT GRID TIMES)
!                      UNIQUE DOODSON NUMBERS (FOR SAT TIDES)
!                      SIDEBANDS OR NOT FOR SAT TIDES
! COMMENTS:
!
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
!
      COMMON/CTIDES/NTIDE,NSTADJ,NET,NETADJ,NETUN,NOT,NOTADJ,           &
     &   NOTUN ,NETUNU,NETADU,NOTUNU,NOTADU,NBSTEP,KTIDA(3),            &
     &   ILLMAX,NUNPLY,NNMPLY,NDOODN,MXTMRT,NRESP ,NXCTID
      COMMON/EPHM/FSECXY(1),XP(261),YP(261),A1UT(261),EP(261),EC(261),  &
     &            FSCFLX(1),FLUXS(36),AVGFLX(36),FLUXM(288),FSECEP(1),  &
     &            EPHP(816),EPHN(96),ELIB(120),BUFT(2700),FSECNP(4)
      COMMON/OLDMOD/NTOLFR,NTOLF2,NTOLMD,NSTAOL,NSITOL,NTOLAJ,MXOLSA,   &
     &              NPOLSH,                                             &
     &              NXOLMD
      COMMON/DTMGDN/TGTYMD,TMGDN1,TMGDN2,REPDIF,XTMGN
      COMMON/LOLOAD/LOLMD,LOLAJ,L2FOLT,LAPLOD
      COMMON/TIDALC/LRAY,LOLDST,NXTIDL
      COMMON/TIDE2I/ISCRT(5),INTRT,NTMRT,JMP2RS(10)
      DIMENSION ANGFAC(NTOLFR,6),SPEED(NTOLFR)
      DIMENSION IDOODN(NDOODN,3)
      DIMENSION TRIGOL(NTOLFR,4),SCTOL(NTOLFR)
      DIMENSION IPTOLS(NTOLFR)
      DIMENSION FSEC(NM)
      DIMENSION ANGSAT(NDOODN,6)
      DIMENSION CONNAM(NDOODN)
      DIMENSION SCRTCH(1)
      DIMENSION GRDANG(NDOODN,MXTMRT)
      DIMENSION UANG(NDOODN,MXTMRT,3)
      DIMENSION FAMP(NDOODN,MXTMRT,3)
!
      DATA CAPTA/0.4101437397284052D+00/,CAPTB/0.3168808892311202D-09/
      DATA H0A/0.4881627972896973D+01/,H0B/0.6283319510866408D+03/,     &
     &     H0C/0.5288347633542818D-05/
      DATA S0A/0.3316125578789226D-07/,S0B/0.1977458042509575D-04/,     &
     &     S0C/0.8399709144920216D+04/,S0D/0.4719969957617067D+01/
      DATA P0A/-0.2094395102393195D-06/,P0B/0.1802052452684145D-03/,    &
     &     P0C/0.7101804125081532D+02/,P0D/0.5835153231900137D+01/
      DATA XNA/-0.3490658503988659D-07/,XNB/-0.3630284844148205D-04/,   &
     &     XNC/ 0.3375714627564109D+02/,XND/-0.4523601602118369D+01/
      DATA PSA/0.5235987755982988D-07/,PSB/0.7853981633974482D-05/,     &
     &     PSC/0.3000255890763292D-01/,PSD/0.4908229408691244D+01/
      DATA NSDAY/86400/,PI2/1.570796326795D0/,ONE/1.D0/,ZERO/0.D0/
      DATA ETA1/32.1496183D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
       I0OFF=ABS(FSEC(1))
       IF(FSEC(1).LT.0.D0) I0OFF=-I0OFF
       MJDSEC=MJDSC0+I0OFF
       NTDDOO=NTIDE
       IF(NDOODN.GT.NTIDE) NTDDOO=NDOODN
       CALL RAYGRD (MJDSC0,FSEC(1),CONNAM,ANGSAT,SCRTCH,GRDANG,         &
     &              UANG,FAMP,IDOODN,ISLP,ISRP,NTDDOO)
!
!  OFFU IS AN APPRX ET-UT1
      OFFU=A1UT(32)+ETA1
!
!  GET INTEGER SECONDS AT BEGINNING OF DAY
      MM=MJDSEC+INT(REPDIF)
!     ISSBD=MOD(MJDSEC,NSDAY)
      ISSBD=MOD(MM,NSDAY)
!     ISABD=MJDSEC-ISSBD
      ISABD=MM-ISSBD
      XSABD=DBLE(ISABD)
      XSSBD=DBLE(ISSBD)-OFFU
!
!
      CAPT=CAPTA+CAPTB*XSABD
!  MEAN LONGITUDE OF SUN AT BEGINNING OF DAY
      H0=H0A+(H0B+H0C*CAPT)*CAPT
!   MEAN LONGITUDE OF  MOON AT BEGINNING OF DAY
      S0=((S0A*CAPT-S0B)*CAPT+S0C)*CAPT+S0D
!   MEAN LONGITUDE OF LUNAR PRIGEE AT BEGINNING OF DAY
      P0=((P0A*CAPT-P0B)*CAPT+P0C)*CAPT+P0D
!
      XN0=((XNA*CAPT+XNB)*CAPT+XNC)*CAPT+XND
!
      PS0=((PSA*CAPT-PSB)*CAPT+PSC)*CAPT+PSD
!
!    GET ANGLE FOR EACH FREQ AT START OF BLOCK
      DO 20 N=1,NTOLFR
      TRIGOL(N,2)=ANGFAC(N,1)*H0+ANGFAC(N,6)
      TRIGOL(N,1)=TRIGOL(N,2)+ANGFAC(N,2)*S0
      TRIGOL(N,2)=TRIGOL(N,1)+ANGFAC(N,3)*P0
   20 END DO
          IF(.NOT.L2FOLT) GO TO 100
      DO 60 N=1,NTOLFR
      TRIGOL(N,1)=TRIGOL(N,2)+ANGFAC(N,4)*XN0
      TRIGOL(N,2)=TRIGOL(N,1)+ANGFAC(N,5)*PS0
   60 END DO
  100     CONTINUE
      DO 110 N=1,NTOLFR
      TRIGOL(N,1)=TRIGOL(N,2)+SPEED(N)*XSSBD
  110 END DO
!
! COMPUTE SIDE BAND INFORMATION
!
      IMID=(1+NM)/2
      DIST=DBLE(MJDSC0-ISCRT(ISLP))+FSEC(IMID)
      FRAC2=DIST/DBLE(INTRT)
      FRAC1=1.D0-FRAC2
      DO 150 N=1,NTOLFR
      SCTOL(N)=1.D0
      IF(IPTOLS(N).LE.0) GO TO 150
      I=IPTOLS(N)
!
! ALL OCEAN LOADING GET TYPE 1 SIDEBANDS
!
      SFRAC1=FRAC1
      SFRAC2=FRAC2
      IF(SPEED(N).LT.0) THEN
! RETROGRADE PHASE IS SHIFTED IN OPOSITE DIRECTION
! THIS IS ALSO TRUE FOR DYNMIC TIDES BUT
! HANDLED DIFFERENTLY
        SFRAC1=-FRAC1
        SFRAC2=-FRAC2
      ENDIF
      TRIGOL(N,1)=TRIGOL(N,1)+SFRAC1*UANG(I,ISLP,2)                     &
    &                        +SFRAC2*UANG(I,ISRP,2)
      SCTOL(N)=FRAC1*FAMP(I,ISLP,2)+FRAC2*FAMP(I,ISRP,2)
  150 END DO
! ALTER ANGLE TO GET SINES FROM COSINE FUNCTION
      DO 220 N=1,NTOLFR
      TRIGOL(N,2)=PI2-TRIGOL(N,1)
  220 END DO
! GET COS AND SIN FOR EACH FREQ AT START OF BLOCK
       DO 225 N=1,NTOLF2
       TRIGOL(N,3)=COS(TRIGOL(N,1))
  225  CONTINUE
       DO 230 N=1,NTOLFR
       TRIGOL(N,3)=SCTOL(N)*TRIGOL(N,3)
       TRIGOL(N,4)=SCTOL(N)*TRIGOL(N,4)
  230  CONTINUE
!
!   GET THE DERIVATIVE OF DELTS WRT TIME IN SECONDS
!   FIGURE TERM FOR HT AT OTHER TIMES
      DO 250 N=1,NTOLFR
      TRIGOL(N,1)=-TRIGOL(N,4)*SPEED(N)
      TRIGOL(N,2)=TRIGOL(N,3)*SPEED(N)
  250 END DO
      RETURN
      END
