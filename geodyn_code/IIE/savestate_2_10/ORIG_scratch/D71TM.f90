!$D71TM
      SUBROUTINE D71TM(MJDSEC,FSEC)
!********1*********2*********3*********4*********5*********6*********7**
! D71TM            83/08/01            8308.0    PGMR - D. ROWLANDS
!                                                (TAKEN FROM GEODYN)
!
! FUNCTION:  CALCULATE VARIABLES NEEDED FOR THE JACCHIA 71 ATMOSPHERIC
!            DENSITY MODEL.THE VARIABLES CALCULATED BY THIS ROUTINE
!            DEPEND ONLY ON TIME.THEY ARE INDEPENDENT OF SAT POSITION.
!
! I/O PARAMETERS:
!
!   NAME    I/O  A/S   DESCRIPTION OF PARAMETERS
!   ------  ---  ---   ------------------------------------------------
!   MJDSEC   I    S    TIME IN INTEGRAL SECONDS FROM GEODYN REF. TIME
!   FSEC     I    S    FRACTIONAL REMAINING SECONDS.
!
! COMMENTS:  OUTPUT    THROUGH COMMON BLOCK D71CUR
!
! RESTRICTIONS:  BUFPOS MUST BE CALLED FIRST WITH LDRAG=.TRUE.
!
!********1*********2*********3*********4*********5*********6*********7**
      IMPLICIT DOUBLE PRECISION (A-H,O-Z),LOGICAL(L)
      SAVE
      COMMON/EPHSET/EMFACT,DTENPD,DTINPD,FSC1EN,FSCENP(2),FSCINP(4),    &
     &   FSDINP(4),XEPHST
      COMMON/CONSTR/PI,TWOPI,DEGRAD,SECRAD,SECDAY
      COMMON/DTMGDN/TGTYMD,TMGDN1,TMGDN2,REPDIF,XTMGN
      COMMON/D71BUF/DECSUN(4),RASUN(4),TCD71(3),XKPC(3),FSCTC,FSCKP
      COMMON/D71CUR/DECS,RAS,T3,PHASE,XKP3,EXPKP3
      DATA XKPLG/67319.99999D0/
!     DATA PEPCH/536025600.D0/,YRSC/31556926.08D0/
      DATA YRSC/31556926.08D0/
!CC      DATA SECDAY/86400.D0/
!
!
!***********************************************************************
! START OF EXECUTABLE CODE *********************************************
!***********************************************************************
!
!
      PEPCH = (2436204.5D0 - TMGDN1)/365.2422D0
!
      IND=((MJDSEC-FSCINP(1))+FSEC)/DTINPD+1
      FDAY=((MJDSEC-FSCINP(IND))+FSEC)/DTINPD
      DECS=DECSUN(IND)+FDAY*(DECSUN(IND+1)-DECSUN(IND))
!
! THE FOLLOWING CODE IS NECESSARY TO REMOVE THE SINGULARITY
! ABOUT AUTUMNAL EQUINOX WHEN INTERPOLATING FOR THE RIGHT ASC.
! OF THE SUN. (SBL)
!
      IF(RASUN(IND+1).GT.RASUN(IND)) THEN
         RAS=RASUN(IND)+FDAY*(RASUN(IND+1)-RASUN(IND))
      ELSE
!
! IF THE INTERPOLATION INTERVAL ENDPOINTS SPAN THE AUTUMNAL EQUINOX
! THEN INTERPOLATE FOR 91 TO 269 DEGREES AND CONVERT BACK TO
! 0 TO PI AND -PI TO 0. (SBL)
!
         RSTMP2=RASUN(IND+1) + TWOPI
         RAS=RASUN(IND)+FDAY*(RSTMP2-RASUN(IND))
         IF(RAS.GT.PI) RAS=RAS-TWOPI
      ENDIF
      IND=1
      TTEST=MJDSEC+FSEC-FSCTC
      IF(TTEST.LE.SECDAY) GO TO 10
      IND=2
      TTEST=TTEST-SECDAY
   10 FDAY=TTEST/SECDAY
      T3=TCD71(IND)+FDAY*(TCD71(IND+1)-TCD71(IND))
      IND=1
      TTEST=MJDSEC+FSEC-XKPLG-FSCKP
      IF(TTEST.LE.SECDAY) GO TO 20
      IND=2
      TTEST=TTEST-SECDAY
   20 FDAY=TTEST/SECDAY
      XKP3=XKPC(IND)+FDAY*(XKPC(IND+1)-XKPC(IND))
      EXPKP3=EXP(XKP3)
      PHASE=(MJDSEC+FSEC)/YRSC - PEPCH
!     PHASE=((MJDSEC-PEPCH)+FSEC)/YRSC
      RETURN
      END
